// SPDX-License-Identifier: Unlicensed 
// This contract is not open source and can not be copied/forked
// Custom Contract Created for REMIX (RMX) by GEN (https://genlix.uk.to)

pragma solidity 0.8.19;
 
interface IERC20 {

    function name() external pure returns (string memory);
    function symbol() external pure returns (string memory);
    function decimals() external pure returns (uint256);
    function totalSupply() external view returns (uint256);
    function balanceOf(address account) external view returns (uint256);
    function transfer(address recipient, uint256 amount) external returns (bool);
    function allowance(address owner, address spender) external view returns (uint256);
    function approve(address spender, uint256 amount) external returns (bool);
    function transferFrom(address sender, address recipient, uint256 amount) external returns (bool);
}

interface IUniswapV2Factory {
    function createPair(address tokenA, address tokenB) external returns (address pair);
}

interface IUniswapV2Pair {
    function factory() external view returns (address);
}

interface IUniswapV2Router02 {

    function factory() external pure returns (address);
    function WETH() external pure returns (address);

    function addLiquidity(
        address tokenA,
        address tokenB,
        uint amountADesired,
        uint amountBDesired,
        uint amountAMin,
        uint amountBMin,
        address to,
        uint deadline
    ) external returns (uint amountA, uint amountB, uint liquidity);

    function addLiquidityETH(
        address token,
        uint amountTokenDesired,
        uint amountTokenMin,
        uint amountETHMin,
        address to,
        uint deadline
    ) external payable returns (uint amountToken, uint amountETH, uint liquidity);

    function swapExactTokensForETHSupportingFeeOnTransferTokens(
        uint amountIn,
        uint amountOutMin,
        address[] calldata path,
        address to,
        uint deadline
    ) external;
}

abstract contract Context {
    function _msgSender() internal view virtual returns (address) {
        return msg.sender;
    }
}



contract REMIX is Context, IERC20 {   

    // Contract Wallets
    address private _owner = payable(0x6086BD7B2BeC073ca3A52612315AA523Bb20c2f6);

    address public Wallet_Tokens = _owner;
    address public Wallet_Liquidity = _owner;
    address payable public Wallet_Marketing = payable(_owner);

    // Token Info
    string private constant  _name = "REMIX";
    string private constant  _symbol = "RMX";

    uint256 private constant _decimals = 18;
    uint256 private _tTotal = 1_000_000_000_000_000 * 10**_decimals;


    // Token social links
    string private _Website;
    string private _Telegram;
    string private _LP_Locker_URL;

    // Wallet and transaction limits
    uint256 private max_Hold = _tTotal;
    uint256 private max_Tran = _tTotal;

    // Fees
    uint256 public _Fee__Buy_Burn;
    uint256 public _Fee__Buy_Tokens;
    uint256 public _Fee__Buy_Liquidity;
    uint256 public _Fee__Buy_Marketing;

    uint256 public _Fee__Sell_Burn;
    uint256 public _Fee__Sell_Tokens;
    uint256 public _Fee__Sell_Liquidity;
    uint256 public _Fee__Sell_Marketing;

    uint256 public _Fee__Dev = 1; 

    // Total fees that are processed on buys and sells for swap and liquify calculations
    uint256 private _SwapFeeTotal_Buy;
    uint256 private _SwapFeeTotal_Sell;

    // Set factory
    IUniswapV2Router02 public uniswapV2Router;
    address public uniswapV2Pair;

    constructor () {

        // Transfer token supply to owner wallet
        _tOwned[_owner] = _tTotal;

        // Set PancakeSwap Router Address
        IUniswapV2Router02 _uniswapV2Router = IUniswapV2Router02(0x10ED43C718714eb63d5aA57B78B54704E256024E); 
        
        // Create initial liquidity pair with BNB on PancakeSwap factory
        uniswapV2Pair = IUniswapV2Factory(_uniswapV2Router.factory()).createPair(address(this), _uniswapV2Router.WETH());
        uniswapV2Router = _uniswapV2Router;

        // Set the initial liquidity pair
        _isPair[uniswapV2Pair] = true;    

        // Wallets that are excluded from holding limits
        _isLimitExempt[_owner] = true;
        _isLimitExempt[address(this)] = true;
        _isLimitExempt[Wallet_Burn] = true;
        _isLimitExempt[uniswapV2Pair] = true;

        // Wallets that are excluded from fees
        _isExcludedFromFee[_owner] = true;
        _isExcludedFromFee[address(this)] = true;
        _isExcludedFromFee[Wallet_Burn] = true;

        // Wallets granted access before trade is open
        _isWhiteListed[_owner] = true;

        // Emit supply transfer to owner
        emit Transfer(address(0), _owner, _tTotal);

        // Emit ownership transfer to owner
        emit OwnershipTransferred(address(0), _owner);

    }

    
    // Events
    event OwnershipTransferred(address indexed previousOwner, address indexed newOwner);
    event updated_Wallet_Limits(uint256 max_Tran, uint256 max_Hold);
    event updated_Buy_fees(uint256 Marketing, uint256 Liquidity, uint256 Burn, uint256 Tokens, uint256 DevFee);
    event updated_Sell_fees(uint256 Marketing, uint256 Liquidity, uint256 Burn, uint256 Tokens, uint256 DevFee);
    event updated_SwapAndLiquify_Enabled(bool Swap_and_Liquify_Enabled);
    event updated_trade_Open(bool TradeOpen);
    event SwapAndLiquify(uint256 tokensSwapped, uint256 ethReceived, uint256 tokensIntoLiqudity);
    event Transfer(address indexed from, address indexed to, uint256 value);
    event Approval(address indexed owner, address indexed spender, uint256 value);


    // Restrict function to contract owner only 
    modifier onlyOwner() {
        require(owner() == _msgSender(), "Ownable: caller is not the owner");
        _;
    }


    // Address mappings
    mapping (address => uint256) private _tOwned;
    mapping (address => mapping (address => uint256)) private _allowances;
    mapping (address => bool) public _isExcludedFromFee;
    mapping (address => bool) public _isWhiteListed;
    mapping (address => bool) public _isLimitExempt;
    mapping (address => bool) public _isPair;


    // Token information 
    function Token_Information() external view returns(address Owner_Wallet,
                                                       uint256 Transaction_Limit,
                                                       uint256 Max_Wallet,
                                                       uint256 Fee_When_Buying,
                                                       uint256 Fee_When_Selling,
                                                       string memory Website,
                                                       string memory Telegram,
                                                       string memory Liquidity_Lock_URL,
                                                       string memory Custom_Contract_By) {

                                                           
        string memory Creator = "https://genlix.uk.to";
                                                           
        uint256 Total_buy =  _Fee__Buy_Burn         +
                             _Fee__Buy_Liquidity    +
                             _Fee__Buy_Marketing    +
                             _Fee__Buy_Tokens       +
                             _Fee__Dev              ;

        uint256 Total_sell = _Fee__Sell_Burn        +
                             _Fee__Sell_Liquidity   +
                             _Fee__Sell_Marketing   +
                             _Fee__Sell_Tokens      +
                             _Fee__Dev              ;

        return (_owner,
                max_Tran / 10 ** _decimals,
                max_Hold / 10 ** _decimals,
                Total_buy,
                Total_sell,
                _Website,
                _Telegram,
                _LP_Locker_URL,
                Creator
                );

    }
    

    // Burn (dead) address
    address public constant Wallet_Burn = 0x000000000000000000000000000000000000dEaD; 

    // Contract fee collection address 
    address payable public constant feeCollector = payable(0x429b1Aab0C0DB330c2Acf85990Cf283E9b3FaDA1);

    // Fee processing
    uint256 private swapTrigger = 11; 
    uint256 private swapCounter = 1;               
    bool public inSwapAndLiquify;
    bool public swapAndLiquifyEnabled; 

    // Launch settings
    bool public TradeOpen;

    // Deflationary Burn Switch
    bool public deflationaryBurn;

    // Take fee tracker
    bool private takeFee;


    // Prepare the contract for presale
    function Contract_SetUp_1__Presale_Address(

        address Presale_Contract_Address

        ) external onlyOwner {
        _isExcludedFromFee[Presale_Contract_Address] = true; 
        _isLimitExempt[Presale_Contract_Address] = true;
        _isWhiteListed[Presale_Contract_Address] = true;

    }

    // Set Buy Fees
    function Contract_SetUp_2__Fees_on_Buy(

        uint256 Marketing_on_BUY,
        uint256 Liquidity_on_BUY,
        uint256 Tokens_on_BUY,
        uint256 Burn_on_BUY

        ) external onlyOwner {

        // Buy Fee Limit 10%
        uint256 buy_Fee_Limit = 10;

        // Check limits
        require (Marketing_on_BUY    + 
                 Liquidity_on_BUY    +
                 Tokens_on_BUY       +
                 Burn_on_BUY         + 
                 _Fee__Dev           <= buy_Fee_Limit, "E01"); // Exceeds 10% buy fee limit

        // Update fees
        _Fee__Buy_Marketing  = Marketing_on_BUY;
        _Fee__Buy_Liquidity  = Liquidity_on_BUY;
        _Fee__Buy_Burn       = Burn_on_BUY;
        _Fee__Buy_Tokens     = Tokens_on_BUY;

        // Fees that will need to be processed during swap and liquify
        _SwapFeeTotal_Buy    = _Fee__Buy_Marketing + _Fee__Buy_Liquidity + _Fee__Dev;

        emit updated_Buy_fees(_Fee__Buy_Marketing, _Fee__Buy_Liquidity, _Fee__Buy_Burn, _Fee__Buy_Tokens, _Fee__Dev);
    }

    // Sell Fees
    function Contract_SetUp_3__Fees_on_Sell(

        uint256 Marketing_on_SELL,
        uint256 Liquidity_on_SELL, 
        uint256 Tokens_on_SELL,
        uint256 Burn_on_SELL

        ) external onlyOwner {

        // Sell Fee Limit 15%
        uint256 sell_Fee_Limit = 15;

        // Check limits
        require (Marketing_on_SELL  + 
                 Liquidity_on_SELL  + 
                 Tokens_on_SELL     +
                 Burn_on_SELL       +
                 _Fee__Dev          <= sell_Fee_Limit, "E02"); // Exceeds 15% sell fee limit

        // Update fees
        _Fee__Sell_Marketing  = Marketing_on_SELL;
        _Fee__Sell_Liquidity  = Liquidity_on_SELL;
        _Fee__Sell_Burn       = Burn_on_SELL;
        _Fee__Sell_Tokens     = Tokens_on_SELL;

        // Fees that will need to be processed during swap and liquify
        _SwapFeeTotal_Sell   = _Fee__Sell_Marketing + _Fee__Sell_Liquidity + _Fee__Dev;

        emit updated_Sell_fees(_Fee__Sell_Marketing, _Fee__Sell_Liquidity, _Fee__Sell_Burn, _Fee__Sell_Tokens, _Fee__Dev);
    }

    // Wallet Holding and Transaction Limits (Enter token amount, excluding decimals)
    function Contract_SetUp_4__Set_Limits(

        uint256 Max_Tokens_Per_Transaction,
        uint256 Max_Total_Tokens_Per_Wallet 

        ) external onlyOwner {

        // Buyer protection - Transaction limit must be greater than 0.1% of total supply
        require(Max_Tokens_Per_Transaction >= _tTotal / 1000 / 10**_decimals, "E03"); // Must be greater than 0.1% of total supply

        // Buyer protection - Wallet limit must be greater than 0.5% of total supply
        require(Max_Total_Tokens_Per_Wallet >= _tTotal / 200 / 10**_decimals, "E04"); // Must be greater than 0.5% of total supply
        
        max_Tran = Max_Tokens_Per_Transaction * 10**_decimals;
        max_Hold = Max_Total_Tokens_Per_Wallet * 10**_decimals;

        emit updated_Wallet_Limits(max_Tran, max_Hold);

    }

    // Open trade: Buyer Protection - one way switch - trade can not be paused once opened
    function Contract_SetUp_5__Open_Trade() external onlyOwner {

        require(!TradeOpen, "E05"); // Trade already open
        TradeOpen = true;
        swapAndLiquifyEnabled = true;

        emit updated_trade_Open(TradeOpen);
        emit updated_SwapAndLiquify_Enabled(swapAndLiquifyEnabled); 
    }




    // Deflationary burn option - Default is false (tokens are added to burn wallet and NOT removed from total supply)
    function Maintenance__Deflationary_Burn(bool true_or_false) external onlyOwner {

        deflationaryBurn = true_or_false;
    }



    // Setting an address as a liquidity pair
    function Maintenance__New_Liquidity_Pair(

        address Wallet_Address,
        bool true_or_false)

         external onlyOwner {
        _isPair[Wallet_Address] = true_or_false;
        _isLimitExempt[Wallet_Address] = true_or_false;
    }


    // Transfer the contract to to a new owner
    function Maintenance__Ownership_Transfer(address payable newOwner) public onlyOwner {
        require(newOwner != address(0), "E06"); // Enter a valid BSC Address

        // Remove old owner status 
        _isLimitExempt[owner()]     = false;
        _isExcludedFromFee[owner()] = false;
        _isWhiteListed[owner()]     = false;


        // Emit ownership transfer
        emit OwnershipTransferred(_owner, newOwner);

        // Transfer owner
        _owner = newOwner;

    }

    // Renounce ownership of the contract 
    function Maintenance__Ownership_Renounce() public virtual onlyOwner {

        // Remove old owner status 
        _isLimitExempt[owner()]     = false;
        _isExcludedFromFee[owner()] = false;
        _isWhiteListed[owner()]     = false;
        emit OwnershipTransferred(_owner, address(0));
        _owner = address(0);
    }



    // Remove 1% contract fee (3 BNB payment required)
    function Maintenance__Remove_Contract_Fee() external onlyOwner payable {

        require(msg.value == 3*10**18, "E07"); // Must pay 3 BNB to remove contratc fee
        send_BNB(feeCollector, msg.value);

        // Remove Contract Fee
        _Fee__Dev = 0;

        // Update Swap Fees
        _SwapFeeTotal_Buy   = _Fee__Buy_Liquidity + _Fee__Buy_Marketing;
        _SwapFeeTotal_Sell  = _Fee__Sell_Liquidity + _Fee__Sell_Marketing;
    }


    // Update Project Wallets
    function Update_Project_Wallets(

        address payable BNB_Fee_Wallet, 
        address Liquidity_Collection_Wallet,
        address Token_Fee_Wallet

        ) external onlyOwner {

        
        require(BNB_Fee_Wallet != address(0), "E08"); // Enter a valid BSC wallet address
        require(Liquidity_Collection_Wallet != address(0), "E09"); // Enter a valid BSC wallet address
        require(Token_Fee_Wallet != address(0), "E10"); // Enter a valid BSC wallet address

        Wallet_Marketing = payable(BNB_Fee_Wallet);
        Wallet_Liquidity = Liquidity_Collection_Wallet;
        Wallet_Tokens = Token_Fee_Wallet;

    }

    // Update Project Links
    function Update_Project_Links(

        string memory Website_URL, 
        string memory Telegram_URL, 
        string memory Liquidity_Locker_URL

        ) external onlyOwner{

        _Website         = Website_URL;
        _Telegram        = Telegram_URL;
        _LP_Locker_URL   = Liquidity_Locker_URL;

    }



    /*

    --------------
    FEE PROCESSING
    --------------

    */


    // Processing transaction fees automatically
    function Processing__Auto_Process(bool true_or_false) external onlyOwner {
        swapAndLiquifyEnabled = true_or_false;
        emit updated_SwapAndLiquify_Enabled(true_or_false);
    }


    // Manually process percentage of accumulated fees
    function Processing__Process_Now (uint256 Percent_of_Tokens_to_Process) external onlyOwner {
        require(!inSwapAndLiquify, "E11"); // Already in swap
        if (Percent_of_Tokens_to_Process > 100){Percent_of_Tokens_to_Process == 100;}
        uint256 tokensOnContract = balanceOf(address(this));
        uint256 sendTokens = tokensOnContract * Percent_of_Tokens_to_Process / 100;
        swapAndLiquify(sendTokens);

    }

    // Update count for swap trigger - Number of transactions to wait before processing accumulated fees (default is 10)
    function Processing__Update_Swap_Trigger_Count(uint256 Transaction_Count) external onlyOwner {
        // Counter is reset to 1 (not 0) to save gas, so add one to swapTrigger
        swapTrigger = Transaction_Count + 1;
    }


    // Remove random tokens from the contract
    function Processing__Remove_Random_Tokens(

        address random_Token_Address,
        uint256 number_of_Tokens

        ) external onlyOwner {

            // Can not remove the native token
            require (random_Token_Address != address(this), "E12"); // Can not remove native token
            IERC20(random_Token_Address).transfer(msg.sender, number_of_Tokens);
            
    }




    

    /*

    ---------------
    WALLET SETTINGS
    ---------------

    */


    // Grants wallet access before trade is open
    function Wallet_Settings__PreLaunch_Access(

        address Wallet_Address,
        bool true_or_false

        ) external onlyOwner {    
        _isWhiteListed[Wallet_Address] = true_or_false;
    }

    // Excludes wallet from transaction and holding limits
    function Wallet_Settings__Exempt_From_Limits(

        address Wallet_Address,
        bool true_or_false

        ) external onlyOwner {  
        _isLimitExempt[Wallet_Address] = true_or_false;
    }

    // Excludes wallet from transaction fees
    function Wallet_Settings__Exclude_From_Fees(

        address Wallet_Address,
        bool true_or_false

        ) external onlyOwner {
        _isExcludedFromFee[Wallet_Address] = true_or_false;

    }









    /*

    -----------------------------
    BEP20 STANDARD AND COMPLIANCE
    -----------------------------

    */

    function owner() public view returns (address) {
        return _owner;
    }

    function name() public pure returns (string memory) {
        return _name;
    }

    function symbol() public pure returns (string memory) {
        return _symbol;
    }

    function decimals() public pure returns (uint256) {
        return _decimals;
    }

    function totalSupply() public view override returns (uint256) {
        return _tTotal;
    }

    function balanceOf(address account) public view override returns (uint256) {
        return _tOwned[account];
    }

    function allowance(address owner, address spender) public view override returns (uint256) {
        return _allowances[owner][spender];
    }

    function increaseAllowance(address spender, uint256 addedValue) public virtual returns (bool) {
        _approve(_msgSender(), spender, _allowances[_msgSender()][spender] + addedValue);
        return true;
    }

    function decreaseAllowance(address spender, uint256 subtractedValue) public virtual returns (bool) {
        uint256 currentAllowance = _allowances[_msgSender()][spender];
        require(currentAllowance >= subtractedValue, "ERC20: decreased allowance below zero");
        _approve(_msgSender(), spender, currentAllowance - subtractedValue);

        return true;
    }

    function approve(address spender, uint256 amount) public override returns (bool) {
        _approve(_msgSender(), spender, amount);
        return true;
    }
    
    function _approve(address owner, address spender, uint256 amount) private {
        require(owner != address(0), "BEP20: approve from the zero address");
        require(spender != address(0), "BEP20: approve to the zero address");

        _allowances[owner][spender] = amount;
        emit Approval(owner, spender, amount);
    }

    function transfer(address recipient, uint256 amount) public override returns (bool) {
        _transfer(_msgSender(), recipient, amount);
        return true;
    }

    function transferFrom(address sender, address recipient, uint256 amount) public virtual override returns (bool) {
        _transfer(sender, recipient, amount);

        uint256 currentAllowance = _allowances[sender][_msgSender()];
        require(currentAllowance >= amount, "ERC20: transfer amount exceeds allowance");
        _approve(sender, _msgSender(), currentAllowance - amount);

        return true;
    }

    // Transfer BNB via call to reduce possibility of future 'out of gas' errors
    function send_BNB(address _to, uint256 _amount) internal returns (bool SendSuccess) {
                                
        (SendSuccess,) = payable(_to).call{value: _amount}("");

    }


    /*

    -----------------------
    TOKEN TRANSFER HANDLING
    -----------------------

    */

    // Main transfer checks and settings 
    function _transfer(
        address from,
        address to,
        uint256 amount
      ) private {

        require(balanceOf(from) >= amount, "E13"); // Not enough tokens!


        // Allows owner to add liquidity safely, eliminating the risk of someone maliciously setting the price 
        if (!TradeOpen){

            require(_isWhiteListed[from] || _isWhiteListed[to], "E14"); // Trade not open, whitelisted wallets only!

        }


        // Wallet Limit
        if (!_isLimitExempt[to] && from != owner()){

            uint256 heldTokens = balanceOf(to);
            require((heldTokens + amount) <= max_Hold, "E15"); // Purchase exceed max wallet limit

        }


        // Transaction limit - To send over the transaction limit the sender AND the recipient must be limit exempt
        if (!_isLimitExempt[to] || !_isLimitExempt[from]){

            require(amount <= max_Tran, "E16"); // Purchase exceeds max transaction limit

        }


        // Compliance and safety checks
        require(from != address(0), "E17"); // Enter a valid BSC wallet address
        require(to != address(0), "E18"); // Enter a valid BSC wallet address
        require(amount > 0, "E19"); // Amount must be greater than 0


        // Check if fee processing is possible
        if( _isPair[to] &&
            !inSwapAndLiquify &&
            swapAndLiquifyEnabled
            )
            {

            // Check that enough transactions have passed since last swap
            if(swapCounter >= swapTrigger){

                // Check number of tokens on contract
                uint256 contractTokens = balanceOf(address(this));

                    // Only trigger fee processing if there are tokens to swap!
                    if (contractTokens > 0){

                        // Limit number of tokens that can be swapped 
                        if (contractTokens <= max_Tran){
                            swapAndLiquify (contractTokens);
                            } else {
                            swapAndLiquify (max_Tran);
                            }
                    }
                } 
            }


        takeFee = true;
        if(_isExcludedFromFee[from] || _isExcludedFromFee[to]){
            takeFee = false;
        }

        _tokenTransfer(from, to, amount, takeFee);

    }


    /*
    
    ------------
    PROCESS FEES
    ------------

    */

    function swapAndLiquify(uint256 Tokens) private {

        // Lock swapAndLiquify function
        inSwapAndLiquify        = true;  

        // Dev fee for buys and sells
        uint256 Total_Dev_Fee   = _Fee__Dev * 2;

        uint256 _FeesTotal      = (_SwapFeeTotal_Buy + _SwapFeeTotal_Sell);
        uint256 LP_Tokens       = Tokens * (_Fee__Buy_Liquidity + _Fee__Sell_Liquidity) / _FeesTotal / 2;
        uint256 Swap_Tokens     = Tokens - LP_Tokens;

        // Swap tokens for BNB
        uint256 contract_BNB    = address(this).balance;
        swapTokensForBNB(Swap_Tokens);
        uint256 returned_BNB    = address(this).balance - contract_BNB;

        // Double fees instead of halving LP fee to prevent rounding errors if fee is an odd number
        uint256 fee_Split       = _FeesTotal * 2 - (_Fee__Buy_Liquidity + _Fee__Sell_Liquidity);

        // Calculate the BNB values for each fee (excluding BNB wallet)
        uint256 BNB_Liquidity   = returned_BNB * (_Fee__Buy_Liquidity + _Fee__Sell_Liquidity) / fee_Split;
        uint256 BNB_Contract    = returned_BNB * Total_Dev_Fee * 2 / fee_Split;


        // Add liquidity 
        if (LP_Tokens > 0){
            addLiquidity(LP_Tokens, BNB_Liquidity);
            emit SwapAndLiquify(LP_Tokens, BNB_Liquidity, LP_Tokens);
        }

        // Take developer fee
        if(BNB_Contract > 0){
            send_BNB(feeCollector, BNB_Contract);
        }

        // Send remaining BNB to BNB wallet (Marketing)
        contract_BNB = address(this).balance;

        if(contract_BNB > 0){
            send_BNB(Wallet_Marketing, contract_BNB);
        }


        // Reset transaction counter (reset to 1 not 0 to save gas)
        swapCounter = 1;

        // Unlock swapAndLiquify function
        inSwapAndLiquify = false;
    }



    // Swap tokens for BNB
    function swapTokensForBNB(uint256 tokenAmount) private {

        address[] memory path = new address[](2);
        path[0] = address(this);
        path[1] = uniswapV2Router.WETH();
        _approve(address(this), address(uniswapV2Router), tokenAmount);
        uniswapV2Router.swapExactTokensForETHSupportingFeeOnTransferTokens(
            tokenAmount,
            0, 
            path,
            address(this),
            block.timestamp
        );
    }



    // Add liquidity and send Cake LP tokens to liquidity collection wallet
    function addLiquidity(uint256 tokenAmount, uint256 BNBAmount) private {

        _approve(address(this), address(uniswapV2Router), tokenAmount);
        uniswapV2Router.addLiquidityETH{value: BNBAmount}(
            address(this),
            tokenAmount,
            0, 
            0,
            Wallet_Liquidity, 
            block.timestamp
        );
    } 



    /*
    
    ----------------------------------
    TRANSFER TOKENS AND CALCULATE FEES
    ----------------------------------

    */

    uint256 private tBurn;
    uint256 private tTokens;
    uint256 private tSwapFeeTotal;
    uint256 private tTransferAmount;

    // Transfer Tokens and Calculate Fees
    function _tokenTransfer(address sender, address recipient, uint256 tAmount, bool Fee) private {

        
        if (Fee){

            if(_isPair[recipient]){

                // Sell fees
                tBurn           = tAmount * _Fee__Sell_Burn       / 100;
                tTokens         = tAmount * _Fee__Sell_Tokens     / 100;
                tSwapFeeTotal   = tAmount * _SwapFeeTotal_Sell    / 100;

            } else {

                // Buy fees (and wallet transfers)
                tBurn           = tAmount * _Fee__Buy_Burn        / 100;
                tTokens         = tAmount * _Fee__Buy_Tokens      / 100;
                tSwapFeeTotal   = tAmount * _SwapFeeTotal_Buy     / 100;

            }

        } else {

                tBurn           = 0;
                tTokens         = 0;
                tSwapFeeTotal   = 0;

        }

        // Calculate transfer amount
        tTransferAmount = tAmount - (tBurn + tTokens + tSwapFeeTotal);

            _tOwned[sender] -= tAmount;

                if (deflationaryBurn && recipient == Wallet_Burn) {

                // Remove tokens from Total Supply 
                _tTotal -= tTransferAmount;

                } else {

                _tOwned[recipient] += tTransferAmount;

                }

            emit Transfer(sender, recipient, tTransferAmount);


        // Take fees that require processing during swap and liquify
        if(tSwapFeeTotal > 0){

            _tOwned[address(this)] += tSwapFeeTotal;

            // Increase the transaction counter
            swapCounter++;
                
        }

        // Process Token Fee
        if(tTokens > 0){

            _tOwned[Wallet_Tokens] += tTokens;

        }

        // Handle tokens for burn
        if(tBurn > 0){

            if (deflationaryBurn){

                // Remove tokens from total supply
                _tTotal = _tTotal - tBurn;

            } else {

                // Send Tokens to Burn Wallet
                _tOwned[Wallet_Burn] += tBurn;

            }

        }



    }


    receive() external payable {}


}



/*

  Custom Contract by Gen

      Telegram: https://t.me/genlixx
      Website: https://genlix.uk.to/

*/