// SPDX-License-Identifier: MIT

pragma solidity >=0.6.0 <0.8.0;

/*
 * @dev Provides information about the current execution context, including the
 * sender of the transaction and its data. While these are generally available
 * via msg.sender and msg.data, they should not be accessed in such a direct
 * manner, since when dealing with GSN meta-transactions the account sending and
 * paying for execution may not be the actual sender (as far as an application
 * is concerned).
 *
 * This contract is only required for intermediate, library-like contracts.
 */
abstract contract Context {
    function _msgSender() internal view virtual returns (address payable) {
        return msg.sender;
    }

    function _msgData() internal view virtual returns (bytes memory) {
        this; // silence state mutability warning without generating bytecode - see https://github.com/ethereum/solidity/issues/2691
        return msg.data;
    }
}


pragma solidity >=0.6.0 <0.8.0;

/**
 * @dev Contract module which provides a basic access control mechanism, where
 * there is an account (an owner) that can be granted exclusive access to
 * specific functions.
 *
 * By default, the owner account will be the one that deploys the contract. This
 * can later be changed with {transferOwnership}.
 *
 * This module is used through inheritance. It will make available the modifier
 * `onlyOwner`, which can be applied to your functions to restrict their use to
 * the owner.
 */
abstract contract Ownable is Context {
    address private _owner;

    event OwnershipTransferred(address indexed previousOwner, address indexed newOwner);

    /**
     * @dev Initializes the contract setting the deployer as the initial owner.
     */
    constructor () internal {
        address msgSender = _msgSender();
        _owner = msgSender;
        emit OwnershipTransferred(address(0), msgSender);
    }

    /**
     * @dev Returns the address of the current owner.
     */
    function owner() public view returns (address) {
        return _owner;
    }

    /**
     * @dev Throws if called by any account other than the owner.
     */
    modifier onlyOwner() {
        require(_owner == _msgSender(), "Ownable: caller is not the owner");
        _;
    }

    /**
     * @dev Leaves the contract without owner. It will not be possible to call
     * `onlyOwner` functions anymore. Can only be called by the current owner.
     *
     * NOTE: Renouncing ownership will leave the contract without an owner,
     * thereby removing any functionality that is only available to the owner.
     */
    function renounceOwnership() public virtual onlyOwner {
        emit OwnershipTransferred(_owner, address(0));
        _owner = address(0);
    }

    /**
     * @dev Transfers ownership of the contract to a new account (`newOwner`).
     * Can only be called by the current owner.
     */
    function transferOwnership(address newOwner) public virtual onlyOwner {
        require(newOwner != address(0), "Ownable: new owner is the zero address");
        emit OwnershipTransferred(_owner, newOwner);
        _owner = newOwner;
    }
}


pragma solidity >=0.6.0 <0.8.0;

/**
 * @dev Library for managing
 * https://en.wikipedia.org/wiki/Set_(abstract_data_type)[sets] of primitive
 * types.
 *
 * Sets have the following properties:
 *
 * - Elements are added, removed, and checked for existence in constant time
 * (O(1)).
 * - Elements are enumerated in O(n). No guarantees are made on the ordering.
 *
 * ```
 * contract Example {
 *     // Add the library methods
 *     using EnumerableSet for EnumerableSet.AddressSet;
 *
 *     // Declare a set state variable
 *     EnumerableSet.AddressSet private mySet;
 * }
 * ```
 *
 * As of v3.3.0, sets of type `bytes32` (`Bytes32Set`), `address` (`AddressSet`)
 * and `uint256` (`UintSet`) are supported.
 */
library EnumerableSet {
    // To implement this library for multiple types with as little code
    // repetition as possible, we write it in terms of a generic Set type with
    // bytes32 values.
    // The Set implementation uses private functions, and user-facing
    // implementations (such as AddressSet) are just wrappers around the
    // underlying Set.
    // This means that we can only create new EnumerableSets for types that fit
    // in bytes32.

    struct Set {
        // Storage of set values
        bytes32[] _values;

        // Position of the value in the `values` array, plus 1 because index 0
        // means a value is not in the set.
        mapping (bytes32 => uint256) _indexes;
    }

    /**
     * @dev Add a value to a set. O(1).
     *
     * Returns true if the value was added to the set, that is if it was not
     * already present.
     */
    function _add(Set storage set, bytes32 value) private returns (bool) {
        if (!_contains(set, value)) {
            set._values.push(value);
            // The value is stored at length-1, but we add 1 to all indexes
            // and use 0 as a sentinel value
            set._indexes[value] = set._values.length;
            return true;
        } else {
            return false;
        }
    }

    /**
     * @dev Removes a value from a set. O(1).
     *
     * Returns true if the value was removed from the set, that is if it was
     * present.
     */
    function _remove(Set storage set, bytes32 value) private returns (bool) {
        // We read and store the value's index to prevent multiple reads from the same storage slot
        uint256 valueIndex = set._indexes[value];

        if (valueIndex != 0) { // Equivalent to contains(set, value)
            // To delete an element from the _values array in O(1), we swap the element to delete with the last one in
            // the array, and then remove the last element (sometimes called as 'swap and pop').
            // This modifies the order of the array, as noted in {at}.

            uint256 toDeleteIndex = valueIndex - 1;
            uint256 lastIndex = set._values.length - 1;

            // When the value to delete is the last one, the swap operation is unnecessary. However, since this occurs
            // so rarely, we still do the swap anyway to avoid the gas cost of adding an 'if' statement.

            bytes32 lastvalue = set._values[lastIndex];

            // Move the last value to the index where the value to delete is
            set._values[toDeleteIndex] = lastvalue;
            // Update the index for the moved value
            set._indexes[lastvalue] = toDeleteIndex + 1; // All indexes are 1-based

            // Delete the slot where the moved value was stored
            set._values.pop();

            // Delete the index for the deleted slot
            delete set._indexes[value];

            return true;
        } else {
            return false;
        }
    }

    /**
     * @dev Returns true if the value is in the set. O(1).
     */
    function _contains(Set storage set, bytes32 value) private view returns (bool) {
        return set._indexes[value] != 0;
    }

    /**
     * @dev Returns the number of values on the set. O(1).
     */
    function _length(Set storage set) private view returns (uint256) {
        return set._values.length;
    }

   /**
    * @dev Returns the value stored at position `index` in the set. O(1).
    *
    * Note that there are no guarantees on the ordering of values inside the
    * array, and it may change when more values are added or removed.
    *
    * Requirements:
    *
    * - `index` must be strictly less than {length}.
    */
    function _at(Set storage set, uint256 index) private view returns (bytes32) {
        require(set._values.length > index, "EnumerableSet: index out of bounds");
        return set._values[index];
    }

    // Bytes32Set

    struct Bytes32Set {
        Set _inner;
    }

    /**
     * @dev Add a value to a set. O(1).
     *
     * Returns true if the value was added to the set, that is if it was not
     * already present.
     */
    function add(Bytes32Set storage set, bytes32 value) internal returns (bool) {
        return _add(set._inner, value);
    }

    /**
     * @dev Removes a value from a set. O(1).
     *
     * Returns true if the value was removed from the set, that is if it was
     * present.
     */
    function remove(Bytes32Set storage set, bytes32 value) internal returns (bool) {
        return _remove(set._inner, value);
    }

    /**
     * @dev Returns true if the value is in the set. O(1).
     */
    function contains(Bytes32Set storage set, bytes32 value) internal view returns (bool) {
        return _contains(set._inner, value);
    }

    /**
     * @dev Returns the number of values in the set. O(1).
     */
    function length(Bytes32Set storage set) internal view returns (uint256) {
        return _length(set._inner);
    }

   /**
    * @dev Returns the value stored at position `index` in the set. O(1).
    *
    * Note that there are no guarantees on the ordering of values inside the
    * array, and it may change when more values are added or removed.
    *
    * Requirements:
    *
    * - `index` must be strictly less than {length}.
    */
    function at(Bytes32Set storage set, uint256 index) internal view returns (bytes32) {
        return _at(set._inner, index);
    }

    // AddressSet

    struct AddressSet {
        Set _inner;
    }

    /**
     * @dev Add a value to a set. O(1).
     *
     * Returns true if the value was added to the set, that is if it was not
     * already present.
     */
    function add(AddressSet storage set, address value) internal returns (bool) {
        return _add(set._inner, bytes32(uint256(value)));
    }

    /**
     * @dev Removes a value from a set. O(1).
     *
     * Returns true if the value was removed from the set, that is if it was
     * present.
     */
    function remove(AddressSet storage set, address value) internal returns (bool) {
        return _remove(set._inner, bytes32(uint256(value)));
    }

    /**
     * @dev Returns true if the value is in the set. O(1).
     */
    function contains(AddressSet storage set, address value) internal view returns (bool) {
        return _contains(set._inner, bytes32(uint256(value)));
    }

    /**
     * @dev Returns the number of values in the set. O(1).
     */
    function length(AddressSet storage set) internal view returns (uint256) {
        return _length(set._inner);
    }

   /**
    * @dev Returns the value stored at position `index` in the set. O(1).
    *
    * Note that there are no guarantees on the ordering of values inside the
    * array, and it may change when more values are added or removed.
    *
    * Requirements:
    *
    * - `index` must be strictly less than {length}.
    */
    function at(AddressSet storage set, uint256 index) internal view returns (address) {
        return address(uint256(_at(set._inner, index)));
    }


    // UintSet

    struct UintSet {
        Set _inner;
    }

    /**
     * @dev Add a value to a set. O(1).
     *
     * Returns true if the value was added to the set, that is if it was not
     * already present.
     */
    function add(UintSet storage set, uint256 value) internal returns (bool) {
        return _add(set._inner, bytes32(value));
    }

    /**
     * @dev Removes a value from a set. O(1).
     *
     * Returns true if the value was removed from the set, that is if it was
     * present.
     */
    function remove(UintSet storage set, uint256 value) internal returns (bool) {
        return _remove(set._inner, bytes32(value));
    }

    /**
     * @dev Returns true if the value is in the set. O(1).
     */
    function contains(UintSet storage set, uint256 value) internal view returns (bool) {
        return _contains(set._inner, bytes32(value));
    }

    /**
     * @dev Returns the number of values on the set. O(1).
     */
    function length(UintSet storage set) internal view returns (uint256) {
        return _length(set._inner);
    }

   /**
    * @dev Returns the value stored at position `index` in the set. O(1).
    *
    * Note that there are no guarantees on the ordering of values inside the
    * array, and it may change when more values are added or removed.
    *
    * Requirements:
    *
    * - `index` must be strictly less than {length}.
    */
    function at(UintSet storage set, uint256 index) internal view returns (uint256) {
        return uint256(_at(set._inner, index));
    }
}


pragma solidity >=0.4.0;

/**
 * @dev Wrappers over Solidity's arithmetic operations with added overflow
 * checks.
 *
 * Arithmetic operations in Solidity wrap on overflow. This can easily result
 * in bugs, because programmers usually assume that an overflow raises an
 * error, which is the standard behavior in high level programming languages.
 * `SafeMath` restores this intuition by reverting the transaction when an
 * operation overflows.
 *
 * Using this library instead of the unchecked operations eliminates an entire
 * class of bugs, so it's recommended to use it always.
 */
library SafeMath {
    /**
     * @dev Returns the addition of two unsigned integers, reverting on
     * overflow.
     *
     * Counterpart to Solidity's `+` operator.
     *
     * Requirements:
     *
     * - Addition cannot overflow.
     */
    function add(uint256 a, uint256 b) internal pure returns (uint256) {
        uint256 c = a + b;
        require(c >= a, 'SafeMath: addition overflow');

        return c;
    }

    /**
     * @dev Returns the subtraction of two unsigned integers, reverting on
     * overflow (when the result is negative).
     *
     * Counterpart to Solidity's `-` operator.
     *
     * Requirements:
     *
     * - Subtraction cannot overflow.
     */
    function sub(uint256 a, uint256 b) internal pure returns (uint256) {
        return sub(a, b, 'SafeMath: subtraction overflow');
    }

    /**
     * @dev Returns the subtraction of two unsigned integers, reverting with custom message on
     * overflow (when the result is negative).
     *
     * Counterpart to Solidity's `-` operator.
     *
     * Requirements:
     *
     * - Subtraction cannot overflow.
     */
    function sub(
        uint256 a,
        uint256 b,
        string memory errorMessage
    ) internal pure returns (uint256) {
        require(b <= a, errorMessage);
        uint256 c = a - b;

        return c;
    }

    /**
     * @dev Returns the multiplication of two unsigned integers, reverting on
     * overflow.
     *
     * Counterpart to Solidity's `*` operator.
     *
     * Requirements:
     *
     * - Multiplication cannot overflow.
     */
    function mul(uint256 a, uint256 b) internal pure returns (uint256) {
        // Gas optimization: this is cheaper than requiring 'a' not being zero, but the
        // benefit is lost if 'b' is also tested.
        // See: https://github.com/OpenZeppelin/openzeppelin-contracts/pull/522
        if (a == 0) {
            return 0;
        }

        uint256 c = a * b;
        require(c / a == b, 'SafeMath: multiplication overflow');

        return c;
    }

    /**
     * @dev Returns the integer division of two unsigned integers. Reverts on
     * division by zero. The result is rounded towards zero.
     *
     * Counterpart to Solidity's `/` operator. Note: this function uses a
     * `revert` opcode (which leaves remaining gas untouched) while Solidity
     * uses an invalid opcode to revert (consuming all remaining gas).
     *
     * Requirements:
     *
     * - The divisor cannot be zero.
     */
    function div(uint256 a, uint256 b) internal pure returns (uint256) {
        return div(a, b, 'SafeMath: division by zero');
    }

    /**
     * @dev Returns the integer division of two unsigned integers. Reverts with custom message on
     * division by zero. The result is rounded towards zero.
     *
     * Counterpart to Solidity's `/` operator. Note: this function uses a
     * `revert` opcode (which leaves remaining gas untouched) while Solidity
     * uses an invalid opcode to revert (consuming all remaining gas).
     *
     * Requirements:
     *
     * - The divisor cannot be zero.
     */
    function div(
        uint256 a,
        uint256 b,
        string memory errorMessage
    ) internal pure returns (uint256) {
        require(b > 0, errorMessage);
        uint256 c = a / b;
        // assert(a == b * c + a % b); // There is no case in which this doesn't hold

        return c;
    }

    /**
     * @dev Returns the remainder of dividing two unsigned integers. (unsigned integer modulo),
     * Reverts when dividing by zero.
     *
     * Counterpart to Solidity's `%` operator. This function uses a `revert`
     * opcode (which leaves remaining gas untouched) while Solidity uses an
     * invalid opcode to revert (consuming all remaining gas).
     *
     * Requirements:
     *
     * - The divisor cannot be zero.
     */
    function mod(uint256 a, uint256 b) internal pure returns (uint256) {
        return mod(a, b, 'SafeMath: modulo by zero');
    }

    /**
     * @dev Returns the remainder of dividing two unsigned integers. (unsigned integer modulo),
     * Reverts with custom message when dividing by zero.
     *
     * Counterpart to Solidity's `%` operator. This function uses a `revert`
     * opcode (which leaves remaining gas untouched) while Solidity uses an
     * invalid opcode to revert (consuming all remaining gas).
     *
     * Requirements:
     *
     * - The divisor cannot be zero.
     */
    function mod(
        uint256 a,
        uint256 b,
        string memory errorMessage
    ) internal pure returns (uint256) {
        require(b != 0, errorMessage);
        return a % b;
    }

    function min(uint256 x, uint256 y) internal pure returns (uint256 z) {
        z = x < y ? x : y;
    }

    // babylonian method (https://en.wikipedia.org/wiki/Methods_of_computing_square_roots#Babylonian_method)
    function sqrt(uint256 y) internal pure returns (uint256 z) {
        if (y > 3) {
            z = y;
            uint256 x = y / 2 + 1;
            while (x < z) {
                z = x;
                x = (y / x + x) / 2;
            }
        } else if (y != 0) {
            z = 1;
        }
    }
}


pragma solidity >=0.5.0;

library RobustSwapLibrary {
    using SafeMath for uint;

    // returns sorted token addresses, used to handle return values from pairs sorted in this order
    function sortTokens(address tokenA, address tokenB) internal pure returns (address token0, address token1) {
        require(tokenA != tokenB, 'RobustSwapLibrary: IDENTICAL_ADDRESSES');
        (token0, token1) = tokenA < tokenB ? (tokenA, tokenB) : (tokenB, tokenA);
        require(token0 != address(0), 'RobustSwapLibrary: ZERO_ADDRESS');
    }

    // calculates the CREATE2 address for a pair without making any external calls
    function pairFor(address factory, address tokenA, address tokenB) internal pure returns (address pair) {
        (address token0, address token1) = sortTokens(tokenA, tokenB);
        pair = address(uint(keccak256(abi.encodePacked(
                hex'ff',
                factory,
                keccak256(abi.encodePacked(token0, token1)),
                hex'edd465f2d11f2d3f39833ead7e83c2f5b667edf263d3cf1413dac6f7c78adbb9' // init code hash
            ))));
    }

    // fetches and sorts the reserves for a pair
    function getReserves(address factory, address tokenA, address tokenB) internal view returns (uint reserveA, uint reserveB) {
        (address token0,) = sortTokens(tokenA, tokenB);
        (uint reserve0, uint reserve1,) = IRobustSwapPair(pairFor(factory, tokenA, tokenB)).getReserves();
        (reserveA, reserveB) = tokenA == token0 ? (reserve0, reserve1) : (reserve1, reserve0);
    }

    // given some amount of an asset and pair reserves, returns an equivalent amount of the other asset
    function quote(uint amountA, uint reserveA, uint reserveB) internal pure returns (uint amountB) {
        require(amountA > 0, 'RobustSwapLibrary: INSUFFICIENT_AMOUNT');
        require(reserveA > 0 && reserveB > 0, 'RobustSwapLibrary: INSUFFICIENT_LIQUIDITY');
        amountB = amountA.mul(reserveB) / reserveA;
    }

    // given an input amount of an asset and pair reserves, returns the maximum output amount of the other asset
    function getAmountOut(uint amountIn, uint reserveIn, uint reserveOut) internal pure returns (uint amountOut) {
        require(amountIn > 0, 'RobustSwapLibrary: INSUFFICIENT_INPUT_AMOUNT');
        require(reserveIn > 0 && reserveOut > 0, 'RobustSwapLibrary: INSUFFICIENT_LIQUIDITY');
        uint amountInWithFee = amountIn.mul(998);
        uint numerator = amountInWithFee.mul(reserveOut);
        uint denominator = reserveIn.mul(1000).add(amountInWithFee);
        amountOut = numerator / denominator;
    }

    // given an output amount of an asset and pair reserves, returns a required input amount of the other asset
    function getAmountIn(uint amountOut, uint reserveIn, uint reserveOut) internal pure returns (uint amountIn) {
        require(amountOut > 0, 'RobustSwapLibrary: INSUFFICIENT_OUTPUT_AMOUNT');
        require(reserveIn > 0 && reserveOut > 0, 'RobustSwapLibrary: INSUFFICIENT_LIQUIDITY');
        uint numerator = reserveIn.mul(amountOut).mul(1000);
        uint denominator = reserveOut.sub(amountOut).mul(998);
        amountIn = (numerator / denominator).add(1);
    }

    // performs chained getAmountOut calculations on any number of pairs
    function getAmountsOut(address factory, uint amountIn, address[] memory path) internal view returns (uint[] memory amounts) {
        require(path.length >= 2, 'RobustSwapLibrary: INVALID_PATH');
        amounts = new uint[](path.length);
        amounts[0] = amountIn;
        for (uint i; i < path.length - 1; i++) {
            (uint reserveIn, uint reserveOut) = getReserves(factory, path[i], path[i + 1]);
            amounts[i + 1] = getAmountOut(amounts[i], reserveIn, reserveOut);
        }
    }

    // performs chained getAmountIn calculations on any number of pairs
    function getAmountsIn(address factory, uint amountOut, address[] memory path) internal view returns (uint[] memory amounts) {
        require(path.length >= 2, 'RobustSwapLibrary: INVALID_PATH');
        amounts = new uint[](path.length);
        amounts[amounts.length - 1] = amountOut;
        for (uint i = path.length - 1; i > 0; i--) {
            (uint reserveIn, uint reserveOut) = getReserves(factory, path[i - 1], path[i]);
            amounts[i - 1] = getAmountIn(amounts[i], reserveIn, reserveOut);
        }
    }
}


pragma solidity >=0.5.0;

interface IERC20 {
    event Approval(address indexed owner, address indexed spender, uint value);
    event Transfer(address indexed from, address indexed to, uint value);

    function name() external view returns (string memory);
    function symbol() external view returns (string memory);
    function decimals() external view returns (uint8);
    function totalSupply() external view returns (uint);
    function balanceOf(address owner) external view returns (uint);
    function allowance(address owner, address spender) external view returns (uint);

    function approve(address spender, uint value) external returns (bool);
    function transfer(address to, uint value) external returns (bool);
    function transferFrom(address from, address to, uint value) external returns (bool);
}


pragma solidity >=0.5.0;

interface IRobustSwapFactory {
    event PairCreated(address indexed token0, address indexed token1, address pair, uint);

    function feeTo() external view returns (address);
    function feeToSetter() external view returns (address);

    function getPair(address tokenA, address tokenB) external view returns (address pair);
    function allPairs(uint) external view returns (address pair);
    function allPairsLength() external view returns (uint);

    function createPair(address tokenA, address tokenB) external returns (address pair);

    function setFeeTo(address) external;
    function setFeeToSetter(address) external;
}


pragma solidity >=0.5.0;

interface IRobustSwapERC20 {
    event Approval(address indexed owner, address indexed spender, uint value);
    event Transfer(address indexed from, address indexed to, uint value);

    function name() external pure returns (string memory);
    function symbol() external pure returns (string memory);
    function decimals() external pure returns (uint8);
    function totalSupply() external view returns (uint);
    function balanceOf(address owner) external view returns (uint);
    function allowance(address owner, address spender) external view returns (uint);

    function approve(address spender, uint value) external returns (bool);
    function transfer(address to, uint value) external returns (bool);
    function transferFrom(address from, address to, uint value) external returns (bool);

    function DOMAIN_SEPARATOR() external view returns (bytes32);
    function PERMIT_TYPEHASH() external pure returns (bytes32);
    function nonces(address owner) external view returns (uint);

    function permit(address owner, address spender, uint value, uint deadline, uint8 v, bytes32 r, bytes32 s) external;
}


pragma solidity ^0.6.6;

interface IMasterchef {
  function pendingRBS(uint256 _pid, address _user) external view returns (uint256);
  function withdraw(uint256 _pid, uint256 _amount) external ;
  function deposit(uint256 _pid, uint256 _amount, address _referrer) external;
}

pragma solidity >=0.5.0;

interface IRobustSwapPair {
    event Approval(address indexed owner, address indexed spender, uint value);
    event Transfer(address indexed from, address indexed to, uint value);

    function name() external pure returns (string memory);
    function symbol() external pure returns (string memory);
    function decimals() external pure returns (uint8);
    function totalSupply() external view returns (uint);
    function balanceOf(address owner) external view returns (uint);
    function allowance(address owner, address spender) external view returns (uint);

    function approve(address spender, uint value) external returns (bool);
    function transfer(address to, uint value) external returns (bool);
    function transferFrom(address from, address to, uint value) external returns (bool);

    function DOMAIN_SEPARATOR() external view returns (bytes32);
    function PERMIT_TYPEHASH() external pure returns (bytes32);
    function nonces(address owner) external view returns (uint);

    function permit(address owner, address spender, uint value, uint deadline, uint8 v, bytes32 r, bytes32 s) external;

    event Mint(address indexed sender, uint amount0, uint amount1);
    event Burn(address indexed sender, uint amount0, uint amount1, address indexed to);
    event Swap(
        address indexed sender,
        uint amount0In,
        uint amount1In,
        uint amount0Out,
        uint amount1Out,
        address indexed to
    );
    event Sync(uint112 reserve0, uint112 reserve1);

    function MINIMUM_LIQUIDITY() external pure returns (uint);
    function factory() external view returns (address);
    function token0() external view returns (address);
    function token1() external view returns (address);
    function getReserves() external view returns (uint112 reserve0, uint112 reserve1, uint32 blockTimestampLast);
    function price0CumulativeLast() external view returns (uint);
    function price1CumulativeLast() external view returns (uint);
    function kLast() external view returns (uint);

    function mint(address to) external returns (uint liquidity);
    function burn(address to) external returns (uint amount0, uint amount1);
    function swap(uint amount0Out, uint amount1Out, address to, bytes calldata data) external;
    function skim(address to) external;
    function sync() external;

    function initialize(address, address) external;
}


pragma solidity >=0.6.0 <0.8.0;

interface IOracle {

    function update(address tokenA, address tokenB) external;

    function consult(address tokenIn, uint256 amountIn, address tokenOut) external view returns (uint256 amountOut);
}

// RobustSwap Trade Mining Contract
// Users earn RBS as reward when trading between specific pairs on the RobustSwap DEX.

contract RobustSwapMining is Ownable {

	using SafeMath for uint256;
	using EnumerableSet for EnumerableSet.AddressSet;

	EnumerableSet.AddressSet private _outputlist;

	// The block number when RBS mining starts
	uint256 public startBlock;
	// Total allocation points
	uint256 public totalAllocPoint = 0;
	IOracle public oracle;
	// rbsrouter address
	address public router;
	// rbsmasterchef address
	address public masterchef;
	// rbsfactory address
	IRobustSwapFactory public factory;
	// rbstoken address
	IRobustSwapERC20 public rbsToken;
	// BNB Address
	address public constant addressBNB = 0xbb4CdB9CBd36B01bD1cBaEBF2De08d9173bc095c;
	// pair corresponding pid
	mapping(address => uint256) public pairOfPid;
	// pid of SwapMiningPool in the MasterChef
	uint256 public miningPID;
	// Required Quantity
	uint256 public constant MAX_REQUIRED_QUANTITY = 1 ether;
	uint256 public minRequiredQuantity = 0.1 ether;
	

	constructor(address _rbsToken, address _factory, address _oracle, address _router, uint256 _startBlock) public {
		require(_rbsToken != address(0), "illegal address");
		rbsToken = IRobustSwapERC20(_rbsToken);
		require(_factory != address(0), "illegal address");
		factory = IRobustSwapFactory(_factory);
		require(_oracle != address(0), "illegal address");
		oracle = IOracle(_oracle);
		require(_router != address(0), "illegal address");
		router = _router;
		startBlock = _startBlock;
	}

	struct UserInfo {
		uint256 reward; // Users reward every transaction
		uint256 totalQuantity; // User total BNB traded
		uint256 totalReward; // User total received RBS reward
	}

	struct PoolInfo {
		address pair; // Trading pairs that can be mined
		uint256 totalQuantity; // Pool total traded BNB
		uint256 allocPoint; // How many allocation points assigned to this pool
		uint256 allocRBSAmount; // How many RBSs
		uint256 lastRewardBlock; // Last transaction block
	}

	PoolInfo[] public poolInfo;

	mapping(uint256 => mapping(address => UserInfo)) public userInfo;

	function poolLength() public view returns (uint256) {

		return poolInfo.length;
	}
	
	// Add a trading pair
	function addPair(uint256 _allocPoint, address _pair, bool _withUpdate) external onlyOwner {

		require(_pair != address(0), "_pair is the zero address");

		if (_withUpdate) {

			massMintPools();
		}

		uint256 lastRewardBlock = block.number > startBlock ? block.number : startBlock;

		totalAllocPoint = totalAllocPoint.add(_allocPoint);

		poolInfo.push(
			PoolInfo({
				pair: _pair,
				totalQuantity: 0,
				allocPoint: _allocPoint,
				allocRBSAmount: 0,
				lastRewardBlock: lastRewardBlock
			})
		);

		pairOfPid[_pair] = poolLength() - 1;
	}

	// Update the allocPoint of the pool
	function setPair(uint256 _pid, uint256 _allocPoint, bool _withUpdate) external onlyOwner {

		require(_pid < poolLength(), "SwapMining: Pool not found");

		if (_withUpdate) {

			massMintPools();
		}

		totalAllocPoint = totalAllocPoint.sub(poolInfo[_pid].allocPoint).add(_allocPoint);
		poolInfo[_pid].allocPoint = _allocPoint;
	}
	
	// Add token to output list
	function addOutputlist(address _addToken) external onlyOwner returns (bool) {

		require(_addToken != address(0), "SwapMining: Token is the zero address");		
		return EnumerableSet.add(_outputlist, _addToken);
	}
	
	// Delete token from output list
	function delOutputlist(address _delToken) external onlyOwner returns (bool) {

		require(_delToken != address(0), "SwapMining: Token is the zero address");		
		return EnumerableSet.remove(_outputlist, _delToken);
	}

	// Check if token is in output list
	function isOutputlist(address _token) public view returns (bool) {

		return EnumerableSet.contains(_outputlist, _token);
	}
	
	// Get output list length
    function getOutputlistLength() public view returns (uint256) {
	
        return EnumerableSet.length(_outputlist);
    }
	
	// Get output list address
    function getOutputlist(uint256 _index) external view returns (address) {
	
        require(_index <= getOutputlistLength() - 1, "SwapMining: Index out of bounds");		
        return EnumerableSet.at(_outputlist, _index);
    }
	
	//Set RobustSwap Router 
	function setRouter(address newRouter) external onlyOwner {

		require(newRouter != address(0), "SwapMining: New router is the zero address");		
		router = newRouter;
	}
	
	// Set RBS MasterChef contract
	function setMasterChef(address newMasterchef) external onlyOwner {

		require(newMasterchef != address(0), "SwapMining: New masterchef is the zero address");		
		masterchef = newMasterchef;
	}
	
	// Set price oracle for target token quantity check
	function setOracle(IOracle _oracle) external onlyOwner {

		require(address(_oracle) != address(0), "SwapMining: New oracle is the zero address");
		oracle = _oracle;
	}

	// RBS rewards for the current block
	function getRbsReward() public view returns (uint256) {

		return IMasterchef(masterchef).pendingRBS(miningPID, address(this));
	}

	// Approve the swapMining to transfer 1 LP token to MasterChef
	function masterChefApprove(address lptoken) external onlyOwner {

		IERC20(lptoken).approve(masterchef, 1 ether);
	}

	// Owner calls this function to add 1 LP token to MasterChef
	function masterChefDeposit() external onlyOwner {

		IMasterchef(masterchef).deposit(miningPID, 1 ether, address(0));
	}
	
	// Owner calls this function halt rewards
	function masterChefRemove() external onlyOwner {

		uint256 length = poolInfo.length;

		for (uint256 pid = 0; pid < length; ++pid) {

			mint(pid);
		}

		// Remove 1 LP token from MasterChef
		IMasterchef(masterchef).withdraw(miningPID, 1 ether);		
	}
	
	// Withdraw 1 LP or any other token from contract
	function withdrawContractLP (IERC20 _tokenAddress, address _recipient, uint256 _amount) external onlyOwner {

		require(address(_tokenAddress) != address(0),"SwapMining: Token address cannot be zero address");		
		require(_recipient != address(0),"SwapMining: Recipient cannot be zero address");

		uint256 balanceToken = _tokenAddress.balanceOf(address(this));

		require(balanceToken > 0,"SwapMining: Token has no balance to withdraw");
		require(_amount <= balanceToken,"SwapMining: Insufficient token balance to withdraw");

		balanceToken = _amount > 0 ? _amount : balanceToken;
		_tokenAddress.transfer(_recipient, balanceToken);
	}
	
	
	// Update all pools
	function massMintPools() public {

		uint256 length = poolInfo.length;

		for (uint256 pid = 0; pid < length; ++pid) {

			mint(pid);
		}

		//Withdraw RBS from the RBS MasterChef contract
		IMasterchef(masterchef).withdraw(miningPID, 0);
	}
	
	// Allocate RBS to each pool called by massMintPools()
	function mint(uint256 _pid) public returns (bool) {

		PoolInfo storage pool = poolInfo[_pid];

		if (block.number <= pool.lastRewardBlock) {

			return false;
		}

		uint256 blockReward = getRbsReward();

		if (blockReward <= 0) {

			return false;

		}
		// Calculate the rewards obtained by the pool based on the allocPoint
		uint256 rbsReward = blockReward.mul(pool.allocPoint).div(totalAllocPoint);

		// Increase the number of RBS tokens in the current pool
		pool.allocRBSAmount = pool.allocRBSAmount.add(rbsReward);

		// Assign block number
		pool.lastRewardBlock = block.number;

		return true;
	}

	// Only router address can initiate swap function
	modifier onlyRouter() {

		require(msg.sender == router, "SwapMining: Caller is not the router");
		_;
	}

	// Swap can only be initiated by router address
	function swap(address account, address input, address output, uint256 amount) public onlyRouter returns (bool) {

		require(account != address(0), "SwapMining: Taker swap account is the zero address");
		require(input != address(0), "SwapMining: Taker swap input is the zero address");
		require(output != address(0), "SwapMining: Taker swap output is the zero address");

		// Check if pairs exists for reward
		if (poolLength() <= 0) {

			return false;
		}
		
		// Mint(pairOfPid[pair]);
		massMintPools();

		// Only transaction between BNB and output token gets reward
		if ( input != addressBNB && !isOutputlist(output)) {

			return false;
		}

		//Get BNB and output pair
		address pair = RobustSwapLibrary.pairFor(address(factory), input, output);

		// Get address for pair
		PoolInfo storage pool = poolInfo[pairOfPid[pair]];

		// Check for BNB pair and available allocation
		if (pool.pair != pair || pool.allocPoint <= 0 || pool.allocRBSAmount <= 0) {

			return false;
		}

		//Get the input BNB quantity for transaction
		uint256 quantity = getQuantity(output, amount, input);

		// Update Oracle
		IOracle(oracle).update(input, output);

		// Add input BNB quantity to total pool quantity
		pool.totalQuantity = pool.totalQuantity.add(quantity);

		// Get user info
		UserInfo storage user = userInfo[pairOfPid[pair]][account];

		// Add input BNB quantity to user total quantity
		user.totalQuantity = user.totalQuantity.add(quantity);

		// Calculate how much reward user should receive
		if (quantity >= MAX_REQUIRED_QUANTITY) {

			// User receives total pool reward if trading maximum quantity or more
			user.reward = user.reward.add(pool.allocRBSAmount);
			pool.allocRBSAmount = 0;

		} else {

			// User receives a percentage of the pool based on input BNB quantity
			user.reward = user.reward.add((pool.allocRBSAmount).mul(quantity).div(MAX_REQUIRED_QUANTITY));
			pool.allocRBSAmount = pool.allocRBSAmount.sub(user.reward);
		}

		return true;
	}
	
	// Calculate the input quantity of BNB from transaction
	function getQuantity(address outputToken, uint256 outputAmount, address inputToken) internal view returns (uint256) {

		uint256 quantity = 0;

		if (IRobustSwapFactory(factory).getPair(outputToken, inputToken) != address(0)) {

			quantity = IOracle(oracle).consult(outputToken, outputAmount, inputToken);
		}

		return quantity;
	}

	// User claims RBS reward
	function takerWithdraw() external {

		uint256 userSub;
		uint256 length = poolInfo.length;

		massMintPools();
		
		UserInfo memory user;

		for (uint256 pid = 0; pid < length; ++pid) {
		
			user = userInfo[pid][msg.sender];

			if (user.reward > 0) {

				// Get all rewards held by the user in each pool
				userSub = userSub.add(user.reward);

				// User total claimed RBS reward
				user.totalReward = user.totalReward.add(userSub);

				// Reset user reward
				user.reward = 0;

                // Reset the userInfo
                userInfo[pid][msg.sender] = user;
			}
		}

		if (userSub <= 0) {
			return;
		}

		// Transfer RBS reward to user
		safeRBSTransfer(msg.sender, userSub);
	}

	// Get reward for user from a specific pool
	function getUserReward(uint256 _pid, address _user) external view returns (uint256) {

		//Ensure pool exists
		require(_pid <= poolInfo.length - 1, "SwapMining: Pool not found");

		// User info from particular pool
		UserInfo memory user = userInfo[_pid][_user];

		//RBS available to users and user total transactions
		return (user.reward);
	}

	// Get reward for user from all pools
	function getReward(address _user) external view returns (uint256) {

		uint256 length = poolInfo.length;
		uint256 userSub;

		UserInfo memory user;

		for (uint256 pid = 0; pid < length; ++pid) {
		
			user = userInfo[pid][_user];

			if (user.reward > 0) {

				userSub = userSub.add(user.reward);
			}
		}

		//RBS available to users and user total transactions
		return (userSub);
	}

	// Get details of the pool
	function getPoolInfo(uint256 _pid) external view returns (address, address, uint256, uint256) {

		require(_pid <= poolInfo.length - 1, "SwapMining: Pool not found");
		PoolInfo memory pool = poolInfo[_pid];

		address token0 = IRobustSwapPair(pool.pair).token0();
		address token1 = IRobustSwapPair(pool.pair).token1();

		uint256 rbsAmount = pool.allocRBSAmount;
		uint256 blockReward = getRbsReward();
		uint256 rbsReward = blockReward.mul(pool.allocPoint).div(totalAllocPoint);

		rbsAmount = rbsAmount.add(rbsReward);

		return (token0, token1, rbsAmount, pool.allocPoint);
	}
	
	// OnlyOwner: Allows transfer of RBS from contract
	function ownerWithdraw(address _to, uint256 _amount) external onlyOwner {

		safeRBSTransfer(_to, _amount);
	}

	// Allow transfers even if rounding error causes pool to not have enough RBS

	function safeRBSTransfer(address _to, uint256 _amount) internal {

		uint256 balance = rbsToken.balanceOf(address(this));

		if (_amount > balance) {

			_amount = balance;
		}

		rbsToken.transfer(_to, _amount);
	}
	
	// Set minimum quantity required to earn reward
	function setMinRequiredQuantity(uint256 _minRequiredQuantity) external onlyOwner {

		require(_minRequiredQuantity <= MAX_REQUIRED_QUANTITY, "SwapMining: Value should be less than MAX");
		minRequiredQuantity = _minRequiredQuantity;
	}
	
	// Set mining for the RobustSwap MasterChef
	function setMiningPID(uint256 _miningPID) external onlyOwner {

		miningPID = _miningPID;
	}
}