/*
 * 
 * Written by: MrGreenCrypto
 * Co-Founder of CodeCraftrs.com
 * 
 * SPDX-License-Identifier: None
 */

pragma solidity 0.8.17;

interface IBEP20 {
    function totalSupply() external view returns (uint256);
    function decimals() external view returns (uint8);
    function symbol() external view returns (string memory);
    function name() external view returns (string memory);
    function balanceOf(address account) external view returns (uint256);
    function transfer(address recipient, uint256 amount) external returns (bool);
    function allowance(address _owner, address spender) external view returns (uint256);
    function approve(address spender, uint256 amount) external returns (bool);
    function transferFrom(address sender, address recipient, uint256 amount ) external returns (bool);
    event Transfer(address indexed from, address indexed to, uint256 value);
    event Approval(address indexed owner, address indexed spender, uint256 value);
}

interface IDEXFactory {function createPair(address tokenA, address tokenB) external returns (address pair);}
interface IDEXPair {function sync() external;}
interface IHelper {function giveMeMyMoneyBack(address currency) external returns (bool);}

interface IDEXRouter {
    function factory() external pure returns (address);    
    function WETH() external pure returns (address);
    function addLiquidity(address tokenA, address tokenB, uint amountADesired, uint amountBDesired, uint amountAMin, uint amountBMin, address to, uint deadline) external returns (uint amountA, uint amountB, uint liquidity);
    function swapExactTokensForTokensSupportingFeeOnTransferTokens(uint amountIn, uint amountOutMin, address[] calldata path, address to, uint deadline) external;
}

contract Whim is IBEP20 {
    string private _name = "Whim";
    string private _symbol = "Whim";
    uint8 constant _decimals = 18;
    uint256 _totalSupply = 420 * (10**_decimals);

    address public constant CEO = 0x9ADc8DABBa8B7F1176f9EFE37d7062B04a85420d;
    address private constant rewardAddress = 0x55d398326f99059fF775485246999027B3197955;
    IBEP20 public constant rewardToken = IBEP20(0x55d398326f99059fF775485246999027B3197955);

    mapping(address => uint256) private _balances;
    mapping(address => mapping(address => uint256)) private _allowances;
    mapping(address => bool) public limitless;
    mapping(address => bool) public isExludedFromMaxWallet;

    uint256 public tax = 10;
    uint256 public Royalties = 3;
    uint256 public liq = 1;
    uint256 public Tesla = 1;
    uint256 public Expansion = 3;
    uint256 public Roulette = 2;
    uint256 public RouletteBalance;
    uint256 public buyCounter;
    uint256 public enough = 1000000000000000000;
    uint256 private swapAt = _totalSupply / 10_000;
    uint256 public maxWalletInPermille = 5;
    uint256 private maxTx = 50;

    IDEXRouter public constant ROUTER = IDEXRouter(0x10ED43C718714eb63d5aA57B78B54704E256024E);
    IHelper private constant helper = IHelper(0xd85FbeFc217ce783A54bC25A0Aea971F0B1d7D5b);
    address private constant DEAD = 0x000000000000000000000000000000000000dEaD;

    address public immutable pcsPair;
    
    struct Share {
        uint256 amount;
        uint256 totalExcluded;
        uint256 totalRealised; 
    }

    mapping (address => uint256) public shareholderIndexes;
    mapping (address => uint256) public lastClaim;
    mapping (address => Share) public shares;
    mapping (address => bool) public addressNotGettingRoyalties;

    uint256 public totalShares;
    uint256 public totalDistributed;
    uint256 public RoyaltiesPerShare;
    uint256 private veryLargeNumber = 10 ** 36;
    uint256 private rewardTokenBalanceBefore;

    uint256 public minTokensForRoyalties;
    address[] private shareholders;
    
    modifier onlyCEO(){
        require (msg.sender == CEO, "Only the CEO can do that");
        _;
    }

    constructor() {
        pcsPair = IDEXFactory(IDEXRouter(ROUTER).factory()).createPair(rewardAddress, address(this));
        _allowances[address(this)][address(ROUTER)] = type(uint256).max;

        isExludedFromMaxWallet[pcsPair] = true;
        isExludedFromMaxWallet[address(this)] = true;

        addressNotGettingRoyalties[pcsPair] = true;
        addressNotGettingRoyalties[address(this)] = true;

        limitless[CEO] = true;
        limitless[address(this)] = true;

        _balances[address(this)] = _totalSupply;
        emit Transfer(address(0), address(this), _totalSupply);
    }

    receive() external payable {}
    function name() public view override returns (string memory) {return _name;}
    function totalSupply() public view override returns (uint256) {return _totalSupply - _balances[DEAD];}
    function decimals() public pure override returns (uint8) {return _decimals;}
    function symbol() public view override returns (string memory) {return _symbol;}
    function balanceOf(address account) public view override returns (uint256) {return _balances[account];}
    
    function allowance(address holder, address spender) public view override returns (uint256) {return _allowances[holder][spender];}
    function transfer(address recipient, uint256 amount) external override returns (bool) {return _transferFrom(msg.sender, recipient, amount);}
    function approveMax(address spender) external returns (bool) {return approve(spender, type(uint256).max);}
    
    function approve(address spender, uint256 amount) public override returns (bool) {
        require(spender != address(0), "Can't use zero address here");
        _allowances[msg.sender][spender] = amount;
        emit Approval(msg.sender, spender, amount);
        return true;
    }

    function increaseAllowance(address spender, uint256 addedValue) public returns (bool) {
        require(spender != address(0), "Can't use zero address here");
        _allowances[msg.sender][spender]  = allowance(msg.sender, spender) + addedValue;
        emit Approval(msg.sender, spender, _allowances[msg.sender][spender]);
        return true;
    }

    function decreaseAllowance(address spender, uint256 subtractedValue) public returns (bool) {
        require(spender != address(0), "Can't use zero address here");
        require(allowance(msg.sender, spender) >= subtractedValue, "Can't subtract more than current allowance");
        _allowances[msg.sender][spender]  = allowance(msg.sender, spender) - subtractedValue;
        emit Approval(msg.sender, spender, _allowances[msg.sender][spender]);
        return true;
    }
    

    function transferFrom(address sender, address recipient, uint256 amount ) external override returns (bool) {
        if (_allowances[sender][msg.sender] != type(uint256).max) {
            require(_allowances[sender][msg.sender] >= amount, "Insufficient Allowance");
            _allowances[sender][msg.sender] -= amount;
            emit Approval(sender, msg.sender, _allowances[sender][msg.sender]);
        }
        return _transferFrom(sender, recipient, amount);
    }

    bool private launched;
    bool private live;

    function rescueBeforeLaunch() external onlyCEO {
        require(!launched);
        rewardToken.transfer(CEO, rewardToken.balanceOf(address(this)));
    }

    function launch() external onlyCEO {
        require(!launched);
        rewardToken.approve(address(ROUTER), type(uint256).max);
        
        ROUTER.addLiquidity(
            address(this),
            rewardAddress,
            _balances[address(this)] / 10,
            rewardToken.balanceOf(address(this)),
            0,
            0,
            CEO,
            block.timestamp
        );
        Royalties = 14;
        liq = 7;
        Tesla = 7;
        Expansion = 7;
        Roulette = 7; 
        tax = Royalties + liq + Tesla + Expansion + Roulette;
        launched = true;
    }

    function Leggo() external onlyCEO {
        require(launched && !live);        
        _lowGasTransfer(address(this), pcsPair, _balances[address(this)]);
        IDEXPair(pcsPair).sync();
        Royalties = 3;
        liq = 1;
        Tesla = 1;
        Expansion = 3;
        Roulette = 2; 
        tax = Royalties + liq + Tesla + Expansion + Roulette;
        live = true;
    }

    function setTaxes(uint256 RoyaltiesTax, uint256 liqTax, uint256 TeslaTax, uint256 ExpansionTax, uint256 RouletteTax) external onlyCEO {
        Royalties = RoyaltiesTax;
        liq = liqTax;
        Tesla = TeslaTax;
        Expansion = ExpansionTax;
        Roulette = RouletteTax; 
        tax = Royalties + liq + Tesla + Expansion + Roulette;
        require(tax < 50, "Tax safety limit");     
    }
    
    function setMaxWalletInPermille(uint256 permille) external onlyCEO {
        maxWalletInPermille = permille;
        require(maxWalletInPermille >= 50, "MaxWallet safety limit");
    }

    function setMaxTxInPercentOfMaxWallet(uint256 percent) external onlyCEO {
        maxTx = percent;
        require(maxTx >= 50, "MaxTx safety limit");
    }
    
    function setNameAndSymbol(string memory newName, string memory newSymbol) external onlyCEO {
        _name = newName;
        _symbol = newSymbol;
    }

    function setMinBuy(uint256 btcAmount) external onlyCEO {
        enough = btcAmount;
    }

    function _transferFrom(address sender, address recipient, uint256 amount) internal returns (bool) {
        if (limitless[sender] || limitless[recipient]) return _lowGasTransfer(sender, recipient, amount);
        amount = takeTax(sender, recipient, amount);
        _lowGasTransfer(sender, recipient, amount);
        if(!addressNotGettingRoyalties[sender]) setShare(sender);
        if(!addressNotGettingRoyalties[recipient]) setShare(recipient);
        return true;
    }

    function takeTax(address sender, address recipient, uint256 amount) internal returns (uint256) {
        if(!isExludedFromMaxWallet[recipient]) require(_balances[recipient] + amount <= _totalSupply * maxWalletInPermille / 1000, "MaxWallet");
        if(!isExludedFromMaxWallet[recipient]) require(amount <= _totalSupply * maxWalletInPermille * maxTx / 1000 / 100, "MaxTx");
        
        uint256 totalTax = tax;
        if(totalTax == 0) return amount;
        uint256 taxAmount = amount * totalTax / 100;
        uint256 taxToSwap = amount * (Royalties + Tesla + Expansion) / 100;
        uint256 RouletteTax = amount * Roulette / 100;
        uint256 liqTax = amount * liq / 100;
        if(taxToSwap > 0) _lowGasTransfer(sender, address(this), taxToSwap);
        
        if(RouletteTax > 0) {
            _lowGasTransfer(sender, address(this), RouletteTax);
            RouletteBalance += RouletteTax;
        }

        if(isPair(sender) && isEnough(amount)) {
            buyCounter++;
            uint256 totalBuys = buyCounter;
            if(totalBuys == 7 || totalBuys == 14 || totalBuys == 21) {
                _lowGasTransfer(address(this), recipient, RouletteBalance);
                RouletteBalance = 0;
                if(totalBuys >= 21) buyCounter = 0;
            }
        }

        if(liqTax > 0) _lowGasTransfer(sender, pcsPair, liqTax);
        if(!isPair(sender)) {
            swapForRoyalties();
            IDEXPair(pcsPair).sync();
        }
        return amount - taxAmount;
    }

    function isEnough(uint256 amount) public view returns (bool isIt) {
        uint256 equivalent = rewardToken.balanceOf(pcsPair) * amount / _balances[pcsPair];
        if(equivalent >= enough) return true;
        return false;
    }

    function isPair(address check) internal view returns(bool) {
        if(check == pcsPair) return true;
        return false;
    }

    function _lowGasTransfer(address sender, address recipient, uint256 amount) internal returns (bool) {
        require(sender != address(0) && recipient != address(0), "Can't use zero addresses here");
        require(amount <= _balances[sender], "Can't transfer more than you own");
        if(amount == 0) return true;
        _balances[sender] -= amount;
        _balances[recipient] += amount;
        emit Transfer(sender, recipient, amount);
        return true;
    }

    function swapForRoyalties() internal {
        if(_balances[address(this)] - RouletteBalance < swapAt) return;
        rewardTokenBalanceBefore = rewardToken.balanceOf(address(this));

        address[] memory pathForSelling = new address[](2);
        pathForSelling[0] = address(this);
        pathForSelling[1] = address(rewardToken);

        ROUTER.swapExactTokensForTokensSupportingFeeOnTransferTokens(
            _balances[address(this)] - RouletteBalance,
            0,
            pathForSelling,
            address(helper),
            block.timestamp
        );
        require(helper.giveMeMyMoneyBack(rewardAddress),"Something went wrong");
        uint256 newRewardTokenBalance = rewardToken.balanceOf(address(this));
        if(newRewardTokenBalance <= rewardTokenBalanceBefore) return;
        uint256 amount = newRewardTokenBalance - rewardTokenBalanceBefore;
        RoyaltiesPerShare = RoyaltiesPerShare + (veryLargeNumber * amount / totalShares);
    }

    function setShare(address shareholder) internal {
        if(shares[shareholder].amount >= minTokensForRoyalties) distributeRoyalties(shareholder);
        if(shares[shareholder].amount == 0 && _balances[shareholder] >= minTokensForRoyalties) addShareholder(shareholder);
        
        if(shares[shareholder].amount >= minTokensForRoyalties && _balances[shareholder] < minTokensForRoyalties){
            totalShares = totalShares - shares[shareholder].amount;
            shares[shareholder].amount = 0;
            removeShareholder(shareholder);
            return;
        }

        if(_balances[shareholder] >= minTokensForRoyalties){
            totalShares = totalShares - shares[shareholder].amount + _balances[shareholder];
            shares[shareholder].amount = _balances[shareholder];
            shares[shareholder].totalExcluded = getTotalRoyaltiesOf(shares[shareholder].amount);
        }
    }

    function claimNoSwap() external {if(getUnpaidEarnings(msg.sender) > 0) distributeRoyalties(msg.sender);}
    function claimBnbPaired(address desiredRewardToken) external {if(getUnpaidEarnings(msg.sender) > 0) distributeRoyaltiesSplit(msg.sender, desiredRewardToken);}
    function claimExpert(address[] memory path) external {if(getUnpaidEarnings(msg.sender) > 0) distributeRoyaltiesExpert(msg.sender, path);}

    function distributeRoyalties(address shareholder) internal {
        uint256 amount = getUnpaidEarnings(shareholder);
        if(amount == 0) return;
        rewardToken.transfer(shareholder, amount);
        totalDistributed = totalDistributed + amount;
        shares[shareholder].totalRealised = shares[shareholder].totalRealised + amount;
        shares[shareholder].totalExcluded = getTotalRoyaltiesOf(shares[shareholder].amount);
    }

    function distributeRoyaltiesSplit(address shareholder, address userReward) internal {
        uint256 amount = getUnpaidEarnings(shareholder);
        if(amount == 0) return;

        rewardToken.transfer(shareholder,amount * 2 / 3);

        address[] memory path = new address[](3);
        path[0] = rewardAddress;
        path[1] = ROUTER.WETH();
        path[2] = userReward;

        ROUTER.swapExactTokensForTokensSupportingFeeOnTransferTokens(
            amount / 3,
            0,
            path,
            shareholder,
            block.timestamp
        );

        totalDistributed = totalDistributed + amount;
        shares[shareholder].totalRealised = shares[shareholder].totalRealised + amount;
        shares[shareholder].totalExcluded = getTotalRoyaltiesOf(shares[shareholder].amount);
    }

    function distributeRoyaltiesExpert(address shareholder, address[] memory userPath) internal {
        uint256 amount = getUnpaidEarnings(shareholder);
        if(amount == 0) return;

        rewardToken.transfer(shareholder,amount * 2 / 3);

        ROUTER.swapExactTokensForTokensSupportingFeeOnTransferTokens(
            amount / 3,
            0,
            userPath,
            shareholder,
            block.timestamp
        );
        
        totalDistributed = totalDistributed + amount;
        shares[shareholder].totalRealised = shares[shareholder].totalRealised + amount;
        shares[shareholder].totalExcluded = getTotalRoyaltiesOf(shares[shareholder].amount);
    }

    function getUnpaidEarnings(address shareholder) public view returns (uint256) {
        uint256 shareholderTotalRoyalties = getTotalRoyaltiesOf(shares[shareholder].amount);
        uint256 shareholderTotalExcluded = shares[shareholder].totalExcluded;
        if(shareholderTotalRoyalties <= shareholderTotalExcluded) return 0;
        return shareholderTotalRoyalties - shareholderTotalExcluded;
    }

    function getTotalRoyaltiesOf(uint256 share) internal view returns (uint256) {
        return share * RoyaltiesPerShare / veryLargeNumber;
    }
   
    function addShareholder(address shareholder) internal {
        shareholderIndexes[shareholder] = shareholders.length;
        shareholders.push(shareholder);
    }

    function removeShareholder(address shareholder) internal {
        shareholders[shareholderIndexes[shareholder]] = shareholders[shareholders.length-1];
        shareholderIndexes[shareholders[shareholders.length-1]] = shareholderIndexes[shareholder];
        shareholders.pop();
    }
}