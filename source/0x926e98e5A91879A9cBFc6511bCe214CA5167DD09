
// File: contracts/DogeChampionsNFT.sol
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.4;

import "@openzeppelin/contracts/utils/Counters.sol";
import "@openzeppelin/contracts/token/ERC721/extensions/ERC721URIStorage.sol";
import "@openzeppelin/contracts/token/ERC721/extensions/ERC721Enumerable.sol";
import "@openzeppelin/contracts/token/ERC721/ERC721.sol";
import "@openzeppelin/contracts/utils/Strings.sol";

import "contracts/ITournament.sol";
import "contracts/DogeChampionsConsumable.sol";

contract DogeChampionsNFT is ERC721URIStorage, ERC721Enumerable
{
    event Mint(address minter, uint256 tokenId);
    event Enhance(uint256 tokenId, uint256 consumableId, int256 valueChange);

    using Counters for Counters.Counter;
    Counters.Counter public _tokenIds;
    Counters.Counter private randomizationNonce;
    Counters.Counter public numberOfNormalMints;
    Counters.Counter public numberOfDiscountMints;
    Counters.Counter public numberOfGiveawayMints;
    Counters.Counter public numberOfTournamentMints;
    Counters.Counter public mintPhase;

    uint[] balance = new uint[](2);

    mapping(address => uint256) public initialMintDiscountWhitelist;
    mapping(address => uint256) public initialMintGiveawayWhitelist;
    mapping(address => uint256) public finalMintDiscountWhitelist;
    mapping(address => uint256) public finalMintGiveawayWhitelist;

    mapping(address => uint256) public collaboratorWhitelist;

    mapping(uint256 => uint256) public tokenIdToAttackPower;
    mapping(uint256 => uint256) public tokenIdToDefensePower;
    mapping(uint256 => uint256) public tokenIdToCRTRate;
    mapping(uint256 => uint256) public tokenIdToPassive;
    mapping(uint256 => uint256) public tokenIdToElement;
    mapping(uint256 => uint256) public tokenIdToRarity;
    mapping(uint256 => address) public tokenIdToMinter;

    mapping(address => uint256[]) public walletToPlayableTokenIds;
    mapping(uint256 => bool) public dividendIgnoredNFTs;

    mapping(uint256 => address) public playableTokenIdToUser;

    mapping(uint256 => uint256[]) public tokenIdToUsedConsumableHistory;
    mapping(uint256 => int256[]) public tokenIdToPropertyValueChangeHistory;

    mapping(address => uint256) public userToNumberOfFirstPhaseMints;
    mapping(address => uint256) public userToNumberOfSecondPhaseMints;

    uint256 public discountedMintPrice = 0.11 ether;
    uint256 public initialMintPrice = 0.22 ether;
    uint256 public finalMintPrice = 0.55 ether;

    uint256 constant normalMintLimit = 9000;
    uint256 constant discountMintLimit = 1000;
    uint256 constant giveawayMintLimit = 400;
    uint256 constant finalGiveawayMintLimit = 400;
    uint256 constant tournamentMintLimit = 9200;

    address private marketplaceAddress;
    address payable public owner;

    bool private isMintAvailable = false;
    bool private isPresaleAvailable = false;

    bool private ignoreDiscountWhitelist = false;

    ITournament private dogeChampionsTournament;
    DogeChampionsConsumable private dogeChampionsConsumable;

    // percentage rates of property determination. These will be used to set Boost Events
    uint256 private firstChancePercent;
    uint256 private secondChancePercent;
    uint256 private thirdChancePercent;
    uint256 private forthChancePercent;

    string private baseURIOverride = "ipfs://CustomCIDHashToBeSet/"; // this will be set after deployment

    /*
     * @author required override for ERC721Enumerable
     */
    function _beforeTokenTransfer(address from, address to, uint256 tokenId) internal override(ERC721, ERC721Enumerable)
    {
        super._beforeTokenTransfer(from, to, tokenId);
    }

    /*
     * @author required override for ERC721Enumerable
     */
    function _burn(uint256 tokenId) internal override(ERC721, ERC721URIStorage)
    {
        super._burn(tokenId);
    }

    /*
     * @author required override for ERC721Enumerable
     */
    function tokenURI(uint256 tokenId) public view override(ERC721, ERC721URIStorage) returns (string memory)
    {
        return super.tokenURI(tokenId);
    }

    /*
     * @author required override for ERC721Enumerable
     */
    function supportsInterface(bytes4 interfaceId) public view override(ERC721, ERC721Enumerable) returns (bool)
    {
        return super.supportsInterface(interfaceId);
    }

    /*
     * @author optional override for baseURI.
     */
    function _baseURI() internal view override returns (string memory)
    {
        return baseURIOverride;
    }

    /*
     * @author modifier that prevents calls from addresses rather than owner
     */
    modifier onlyOwner
    {
        require(msg.sender == owner, "Only owner can access this function.");
        _;
    }

    /*
     * @author modifier that prevents calls from addresses rather than DogeChampionsMarketplace contract
     */
    modifier onlyMarketplace
    {
        require(msg.sender == marketplaceAddress, "Only DogeChampionsMarketplace contract can access this function.");
        _;
    }

    /*
     * @author plain good old constructor
     */
    constructor() ERC721("DogeChampionsNFT", "DOGECHMP")
    {
        owner = payable(msg.sender);
        configureMint();

        firstChancePercent = 60;
        secondChancePercent = 90;
        thirdChancePercent = 98;
        forthChancePercent = 100;
    }

    /*
     * @author configures the mint phase. No more than 2 mint phases can be established
     */
    function configureMint() public onlyOwner
    {
        require(mintPhase.current() < 2, "No more mint phase can be started. We already ran 2 mint phases.");
        mintPhase.increment();

        numberOfNormalMints.reset();
        numberOfDiscountMints.reset();
        numberOfGiveawayMints.reset();
    }

    /*
     * @author checks if system should ignore given tokenId for dividend yield algorithm.
     * As we will be giving out free NFTs for tournament winners, our dividend calculation
     * would be broken as total supply would increase slowly. At some point, it is inevitable
     * that dividend algorithm would break. So, we are ignoring reward NFTs and don't count
     * them while reflecting dividend profits.
     */
    function isNFTDividendIgnored(uint256 tokenId) public view returns(bool)
    {
        return dividendIgnoredNFTs[tokenId];
    }

    
    /*
     * @author calculates random number full on-chain using randomized nonce and keccak
     */
    function calculateRandomness(uint256 modulus) internal returns(uint256)
    {
        randomizationNonce.increment(); 
        return uint256(keccak256(abi.encodePacked(block.timestamp, msg.sender, randomizationNonce.current()))) % modulus;
    }

    /*
     * @author validates if given tokenIds are playable by user
     */
    function validatePlayability(address walletAddress, uint256[4] memory tokenIds) public view returns(bool)
    {
        bool zeroValidation = false;
        bool containsNonPlayableTokenId = false;
        for(uint256 i = 0; i < 4; i++)
        {
            if(tokenIds[i] != 0)
            {
                zeroValidation = true;
                if(playableTokenIdToUser[tokenIds[i]] != walletAddress)
                {
                    containsNonPlayableTokenId = true;
                    break;
                }
            }
        }

        return zeroValidation && !containsNonPlayableTokenId;
    }

    /*
     * @author re-evaluates given tokenId's property depending on consumableTokenId
     */
    function enhanceProperty(uint256 tokenId, uint256 consumableTokenId) public
    {
        require(tokenId <= _tokenIds.current(), "There is no such NFT.");
        require(consumableTokenId >= 0 && consumableTokenId < 6, "There is no such consumable type.");
        require(dogeChampionsConsumable.balanceOf(msg.sender, consumableTokenId) > 0, "You don't have enough consumable.");
        require(ownerOf(tokenId) == msg.sender, "Can't enhance properties of not owned NFT.");

        dogeChampionsConsumable.burn(consumableTokenId, msg.sender);

        if(consumableTokenId < 3)
        {
            normalEnhance(tokenId, consumableTokenId);
        }
        else
        {
            premiumEnhance(tokenId, consumableTokenId);
        }
    }

    /*
     * @author helper internal function for enhancing properties using normal consumable
     */
    function normalEnhance(uint256 tokenId, uint256 consumableTokenId) internal
    {
        int256 valueChange;
        if(consumableTokenId == 0)
        {
            valueChange = determineAttackPower(tokenId, false);
        }
        else if(consumableTokenId == 1)
        {
            valueChange = determineDefensePower(tokenId, false);
        }
        else if(consumableTokenId == 2)
        {
            valueChange = determineCRTRate(tokenId, false);
        }

        tokenIdToUsedConsumableHistory[tokenId].push(consumableTokenId); 
        tokenIdToPropertyValueChangeHistory[tokenId].push(valueChange);

        emit Enhance(tokenId, consumableTokenId, valueChange);
    }

    /*
     * @author helper internal function for enhancing properties using premium consumable
     */
    function premiumEnhance(uint256 tokenId, uint256 consumableTokenId) internal
    {
        int256 valueChange;
        if(consumableTokenId == 3)
        {
            valueChange = determineAttackPower(tokenId, true);
        }
        else if(consumableTokenId == 4)
        {
            valueChange = determineDefensePower(tokenId, true);
        }
        else if(consumableTokenId == 5)
        {
            valueChange = determineCRTRate(tokenId, true);
        }

        tokenIdToUsedConsumableHistory[tokenId].push(consumableTokenId); 
        tokenIdToPropertyValueChangeHistory[tokenId].push(valueChange);

        emit Enhance(tokenId, consumableTokenId, valueChange);
    }

    // @author following region is for mint related functions

    /*
     * @author mints a DogeChampionsNFT for regular mint price on current mint phase
     */
    function normalMint(uint256 amount) public payable returns (uint256)
    {
        if(mintPhase.current() < 2)
        {
            require(msg.value == initialMintPrice * amount, "Please submit the exact price to mint DogeChampionsNFT.");
            require(userToNumberOfFirstPhaseMints[msg.sender] + amount <= 20, "You hit mint limit.");
        }
        else
        {
            require(msg.value == finalMintPrice * amount, "Please submit the exact price to mint DogeChampionsNFT.");
            require(userToNumberOfSecondPhaseMints[msg.sender] + amount  <= 20, "You hit mint limit.");
        }
        require(isMintAvailable == true, "Minting is not started yet.");

        if(ignoreDiscountWhitelist)
        {
            require(numberOfNormalMints.current() + numberOfDiscountMints.current() + amount < normalMintLimit + discountMintLimit, "Decrease amount of mint, not enough supply left.");
        }
        else
        {
            require(numberOfNormalMints.current() + amount <= normalMintLimit, "Decrease amount of mint, not enough supply left.");
        }
        
        owner.transfer(msg.value);

        for(uint256 i = 0; i < amount; i++)
        {
            _tokenIds.increment();
            numberOfNormalMints.increment();
            mint(false, 0);
        }

        if(mintPhase.current() < 2)
        {
            userToNumberOfFirstPhaseMints[msg.sender] = userToNumberOfFirstPhaseMints[msg.sender] + amount;
        }
        else
        {
            userToNumberOfSecondPhaseMints[msg.sender] = userToNumberOfSecondPhaseMints[msg.sender] + amount;
        }

        return _tokenIds.current();
    }

    /*
     * @author mints a DogeChampionsNFT for discounted price - only for presale whitelist users
     */
    function discountMint(uint256 amount) public payable returns (uint256)
    {
        require(!ignoreDiscountWhitelist, "Discount period ended.");
        if(mintPhase.current() < 2)
        {
            require(initialMintDiscountWhitelist[msg.sender] >= amount, "You don't have enough WL credit.");
            initialMintDiscountWhitelist[msg.sender] = initialMintDiscountWhitelist[msg.sender] - amount;
            require(userToNumberOfFirstPhaseMints[msg.sender] + amount <= 20, "You hit mint limit.");
        }
        else
        {
            require(finalMintDiscountWhitelist[msg.sender] >= amount, "You don't have enough WL credit.");
            finalMintDiscountWhitelist[msg.sender] = finalMintDiscountWhitelist[msg.sender] - amount;
            require(userToNumberOfSecondPhaseMints[msg.sender] + amount  <= 20, "You hit mint limit.");
        }
        require(isPresaleAvailable == true, "Presale is not started yet.");
        require(msg.value == discountedMintPrice * amount, "Please submit the exact price to mint a Doge Champion.");
        require(numberOfDiscountMints.current() + amount <= discountMintLimit, "We are out of stock!");

        owner.transfer(msg.value);

        for(uint256 i = 0; i < amount; i++)
        {
            _tokenIds.increment();
            numberOfDiscountMints.increment();
            mint(false, 0);
        }

        if(mintPhase.current() < 2)
        {
            userToNumberOfFirstPhaseMints[msg.sender] = userToNumberOfFirstPhaseMints[msg.sender] + amount;
        }
        else
        {
            userToNumberOfSecondPhaseMints[msg.sender] = userToNumberOfSecondPhaseMints[msg.sender] + amount;
        }

        return _tokenIds.current();
    }

    /*
     * @author mints a DogeChampionsNFT for free (users will only pay gas fees) - only for giveaway whitelist users
     */
    function giveawayMint() public returns (uint256)
    {
        if(mintPhase.current() < 2)
        {
            require(initialMintGiveawayWhitelist[msg.sender] > 0, "You are not in whitelist.");
            initialMintGiveawayWhitelist[msg.sender] = initialMintGiveawayWhitelist[msg.sender] - 1;
        }  
        else
        {
            require(finalMintGiveawayWhitelist[msg.sender] > 0, "You are not in whitelist.");
            finalMintGiveawayWhitelist[msg.sender] = finalMintGiveawayWhitelist[msg.sender] - 1;
        }
        require(isMintAvailable == true, "Minting is not started yet.");
        require(numberOfGiveawayMints.current() < giveawayMintLimit, "We are out of stock!");

        _tokenIds.increment();
        numberOfGiveawayMints.increment();
        mint(false, 0);

        return _tokenIds.current();
    }

    /*
     * @author mints a DogeChampionsNFT from tournament rewards - these are not included to Diamond Hands program
     */
    function tournamentMint(uint256 rarity) public
    {
        require(numberOfTournamentMints.current() < tournamentMintLimit, "We hit tournament mint limit.");
        uint256[] memory rewards = dogeChampionsTournament.getRewards(msg.sender);
        require(rarity < 5, "There is no such NFT rarity.");
        require(rewards[rarity] > 0, "You don't have NFT rewards.");

        _tokenIds.increment();
        numberOfTournamentMints.increment();
        mint(true, rarity);

        dogeChampionsTournament.decreaseReward(rarity, msg.sender);
        dividendIgnoredNFTs[_tokenIds.current()] = true;
    }

    /*
     * @author mints a DogeChampionsNFT for free. This method is created for collaborators.
     */
    function collaborationMint(uint256 amount) public returns (uint256)
    {
        require(isMintAvailable == true, "Minting is not started yet.");
        require(collaboratorWhitelist[msg.sender] >= amount, "You don't have enough credit.");

        if(ignoreDiscountWhitelist)
        {
            require(numberOfNormalMints.current() + numberOfDiscountMints.current() + amount <= normalMintLimit + discountMintLimit, "Decrease amount of mint, not enough supply left.");
        }
        else
        {
            require(numberOfNormalMints.current() + amount <= normalMintLimit, "Decrease amount of mint, not enough supply left.");
        }

        for(uint256 i = 0; i < amount; i++)
        {
            _tokenIds.increment();
            numberOfNormalMints.increment();
            mint(false, 0);
        }

        collaboratorWhitelist[msg.sender] = collaboratorWhitelist[msg.sender] - amount;

        return _tokenIds.current();
    }

    /*
     * @author generic mint function for different price mints functions to consume
     */
    function mint(bool isTournamentMint, uint256 tournamentMintRarity) internal 
    {
        uint256 newItemId = _tokenIds.current();

        determineRarity(newItemId, isTournamentMint, tournamentMintRarity);
        determineElement(newItemId);
        determineAttackPower(newItemId, false);
        determineDefensePower(newItemId, false);
        determineCRTRate(newItemId, false);
        determinePassive(newItemId);

        string memory firstPart = string(abi.encodePacked(Strings.toString(determineBackgroundLayer(newItemId)), "-", Strings.toString(determineWeaponLayer()), "-", Strings.toString(determineSkinLayer(newItemId)), "-"));
        string memory secondPart = string(abi.encodePacked(Strings.toString(determineFaceLayer()), "-", Strings.toString(determineEyeLayer()), "-", Strings.toString(determineMouthLayer()), "-"));
        string memory thirdPart = string(abi.encodePacked(Strings.toString(determineGlassesLayer()), "-", Strings.toString(determineOutfitLayer()), "-", Strings.toString(determineHeadLayer()), ".json"));

        _mint(msg.sender, newItemId);
        _setTokenURI(newItemId, string(abi.encodePacked(firstPart, secondPart, thirdPart)));
        setApprovalForAll(marketplaceAddress, true);

        pushToPlayableIdsOnMint(msg.sender, newItemId);

        tokenIdToMinter[_tokenIds.current()] = msg.sender;

        emit Mint(msg.sender, _tokenIds.current());
    }

    // @author following region is for determining a DogeChampionsNFT's properties during mint

    /*
     * @author determines rarity of user's DogeChampionsNFT randomly on-chain during mint
     */
    function determineRarity(uint256 tokenId, bool isTournamentMint, uint256 tournamentRarity) internal
    {
        if(isTournamentMint)
        {
            tokenIdToRarity[tokenId] = tournamentRarity;
            return;
        }

        uint256 randomNumber = calculateRandomness(200); // using 200 as limit to be more precise on floating points

        if(randomNumber >= 0 && randomNumber < 80) // 40% chance
            tokenIdToRarity[tokenId] = 0; // un-common
        else if(randomNumber >= 80 && randomNumber < 140) // 30% chance
            tokenIdToRarity[tokenId] = 1; // common
        else if(randomNumber >= 140 && randomNumber < 180) // 20% chance
            tokenIdToRarity[tokenId] = 2; // rare
        else if(randomNumber >= 180 && randomNumber < 199) // 9.5% chance
            tokenIdToRarity[tokenId] = 3; // epic
        else if(randomNumber >= 199 && randomNumber < 200) // 0.5% chance
            tokenIdToRarity[tokenId] = 4; // legendary
    }

    /*
     * @author determines element of user's DogeChampionsNFT randomly on-chain during mint
     */
    function determineElement(uint256 tokenId) internal
    {
        uint256 randomNumber = calculateRandomness(40);

        if(randomNumber >= 0 && randomNumber < 9)
            tokenIdToElement[tokenId] = 0;
        else if(randomNumber >= 9 && randomNumber < 19)
            tokenIdToElement[tokenId] = 1;
        else if(randomNumber >= 19 && randomNumber < 29)
            tokenIdToElement[tokenId] = 2;
        else if(randomNumber >= 29 && randomNumber < 39)
            tokenIdToElement[tokenId] = 3;
    }

    /*
     * @author determines background layer of user's DogeChampionsNFT randomly on-chain during mint
     */
    function determineBackgroundLayer(uint256 tokenId) internal returns (uint256)
    {
        uint256 rarityIndicator = tokenIdToRarity[tokenId];
        uint256 randomNumber = calculateRandomness(100);
        uint256 candidateBackgroundLayer;


        if(rarityIndicator == 4) // legendary card
        {
            if(randomNumber >= 0 && randomNumber < 33)
                candidateBackgroundLayer = 10;
            else if(randomNumber >= 33 && randomNumber < 66)
                candidateBackgroundLayer = 11;
            else if(randomNumber >= 66 && randomNumber < 100)
                candidateBackgroundLayer = 12;
        }
        else if(rarityIndicator == 3) // epic card
        {
            if(randomNumber >= 0 && randomNumber < 50)
                candidateBackgroundLayer = 8;
            else if(randomNumber >= 50 && randomNumber < 100)
                candidateBackgroundLayer = 9;
        }
        else if(rarityIndicator == 2) // rare card
        {
            if(randomNumber >= 0 && randomNumber < 50)
                candidateBackgroundLayer = 6;
            else if(randomNumber >= 50 && randomNumber < 100)
                candidateBackgroundLayer = 7;
        }
        else if(rarityIndicator == 1) // uncommon card
        {
            if(randomNumber >= 0 && randomNumber < 50)
                candidateBackgroundLayer = 4;
            else if(randomNumber >= 50 && randomNumber < 100)
                candidateBackgroundLayer = 5;
        }
        else // common card
        {
            if(randomNumber >= 0 && randomNumber < 25)
                candidateBackgroundLayer = 0;
            else if(randomNumber >= 25 && randomNumber < 50)
                candidateBackgroundLayer = 1;
            else if(randomNumber >= 50 && randomNumber < 75)
                candidateBackgroundLayer = 2;
            else if(randomNumber >= 75 && randomNumber < 100)
                candidateBackgroundLayer = 3;
        }

        return candidateBackgroundLayer;
    }

    /*
     * @author determines body layer of user's DogeChampionsNFT randomly on-chain during mint
     */
    function determineSkinLayer(uint256 tokenId) internal view returns (uint256)
    {
        return tokenIdToElement[tokenId];
    }

    /*
     * @author determines back attachment layer of user's DogeChampionsNFT randomly on-chain during mint
     */
    function determineWeaponLayer() internal returns (uint256)
    {
        return calculateRandomness(18);
    }

    /*
     * @author determines eye layer of user's DogeChampionsNFT randomly on-chain during mint
     */
    function determineEyeLayer() internal returns (uint256)
    {
        return calculateRandomness(8);
    }

    /*
     * @author determines face layer of user's DogeChampionsNFT randomly on-chain during mint
     */
    function determineFaceLayer() internal returns (uint256)
    {
        return calculateRandomness(16);
    }

    /*
     * @author determines face attachment layer of user's DogeChampionsNFT randomly on-chain during mint
     */
    function determineGlassesLayer() internal returns (uint256)
    {
        return calculateRandomness(23);
    }

    /*
     * @author determines head layer of user's DogeChampionsNFT randomly on-chain during mint
     */
    function determineHeadLayer() internal returns (uint256)
    {
        return calculateRandomness(93);
    }

    /*
     * @author determines mouth layer of user's DogeChampionsNFT randomly on-chain during mint
     */
    function determineMouthLayer() internal returns (uint256)
    {
        return calculateRandomness(12);
    }

    /*
     * @author determines outfit layer of user's DogeChampionsNFT randomly on-chain during mint
     */
    function determineOutfitLayer() internal returns (uint256)
    {
        return calculateRandomness(116);
    }

    /*
     * @author determines attack power peoperty of user's DogeChampionsNFT randomly on-chain during mint
     */
    function determineAttackPower(uint256 tokenId, bool isPremiumEnhance) internal returns (int256)
    {
        uint256 rarityIndicator = tokenIdToRarity[tokenId];
        uint256 attackPowerCandidate;

        uint256 percent = calculateRandomness(100);

        if(percent < firstChancePercent) // 60% chance
        {
            attackPowerCandidate = calculateRandomness(20) + 50; // 50 - 69 power
        }
        else if(percent < secondChancePercent) // 30% chance
        {
            attackPowerCandidate = calculateRandomness(10) + 70; // 70 - 79 power
        }
        else if(percent < thirdChancePercent) // 8% chance
        {
            attackPowerCandidate = calculateRandomness(10) + 80; // 80 - 89 power
        }
        else if(percent < forthChancePercent) // 2% chance
        {
            attackPowerCandidate = calculateRandomness(10) + 90; // 90 - 99 power
        }

        if(attackPowerCandidate == 99)
            attackPowerCandidate = 100;

        for(uint256 i = 0; i < rarityIndicator; i++)
            attackPowerCandidate *= 2;

        if(isPremiumEnhance)
        {
            if(attackPowerCandidate > tokenIdToAttackPower[tokenId])
            {
                uint256 oldValue = tokenIdToAttackPower[tokenId];
                tokenIdToAttackPower[tokenId] = attackPowerCandidate;
                return int256(attackPowerCandidate) - int256(oldValue);
            }
            return 0;
        }
        else
        {
            uint256 oldValue = tokenIdToAttackPower[tokenId];
            tokenIdToAttackPower[tokenId] = attackPowerCandidate;
            return int256(attackPowerCandidate) - int256(oldValue);
        }
    }

    /*
     * @author determines defense power peoperty of user's DogeChampionsNFT randomly on-chain during mint
     */
    function determineDefensePower(uint256 tokenId, bool isPremiumEnhance) internal returns(int256)
    {
        uint256 rarityIndicator = tokenIdToRarity[tokenId];
        uint256 defensePowerCandidate;

        uint256 percent = calculateRandomness(100);

        if(percent < firstChancePercent) // 60% chance
        {
            defensePowerCandidate = calculateRandomness(20) + 50; // 50 - 69 power
        }
        else if(percent < secondChancePercent) // 30% chance
        {
            defensePowerCandidate = calculateRandomness(10) + 70; // 70 - 79 power
        }
        else if(percent < thirdChancePercent) // 8% chance
        {
            defensePowerCandidate = calculateRandomness(10) + 80; // 80 - 89 power
        }
        else if(percent < forthChancePercent) // 2% chance
        {
            defensePowerCandidate = calculateRandomness(10) + 90; // 90 - 99 power
        }

        if(defensePowerCandidate == 99)
            defensePowerCandidate = 100;

        for(uint256 i = 0; i < rarityIndicator; i++)
            defensePowerCandidate *= 2;

        if(isPremiumEnhance)
        {
            if(defensePowerCandidate > tokenIdToDefensePower[tokenId])
            {
                uint256 oldValue = tokenIdToDefensePower[tokenId];
                tokenIdToDefensePower[tokenId] = defensePowerCandidate;
                return int256(defensePowerCandidate) - int256(oldValue);
            }
            return 0;
        }
        else
        {
            uint256 oldValue = tokenIdToDefensePower[tokenId];
            tokenIdToDefensePower[tokenId] = defensePowerCandidate;
            return int256(defensePowerCandidate) - int256(oldValue);
        }
    }

    /*
     * @author determines CRT rate peoperty of user's DogeChampionsNFT randomly on-chain during mint
     */
    function determineCRTRate(uint256 tokenId, bool isPremiumEnhance) internal returns(int256)
    {
        uint256 CRTRateCandidate;

        uint256 percent = calculateRandomness(100);

        if(percent < 60) // 60% chance
        {
            CRTRateCandidate = calculateRandomness(4) + 2; // 2 - 5 CRT rate
        }
        else if(percent < 90) // 30% chance
        {
            CRTRateCandidate = calculateRandomness(3) + 6; // 6 - 8 CRT rate
        }
        else if(percent < 98) // 8% chance
        {
            CRTRateCandidate = calculateRandomness(2) + 9; // 9 - 10 CRT rate
        }
        else if(percent < 100) // 2% chance
        {
            CRTRateCandidate = calculateRandomness(2) + 11; // 11 - 12 CRT rate
        }

        if(isPremiumEnhance)
        {
            if(CRTRateCandidate > tokenIdToCRTRate[tokenId])
            {
                uint256 oldValue = tokenIdToCRTRate[tokenId];
                tokenIdToCRTRate[tokenId] = CRTRateCandidate;
                return int256(CRTRateCandidate) - int256(oldValue);
            }
            return 0;
        }
        else
        {
            uint256 oldValue = tokenIdToCRTRate[tokenId];
            tokenIdToCRTRate[tokenId] = CRTRateCandidate;
            return int256(CRTRateCandidate) - int256(oldValue);
        }
    }

    /*
     * @author determines passive skill of user's DogeChampionsNFT randomly on-chain during mint
     */
    function determinePassive(uint256 tokenId) internal
    {
        uint256 randomNumber = calculateRandomness(100);

        if(randomNumber >= 0 && randomNumber < 25) // 25% chance
            tokenIdToPassive[tokenId] = 1; // additional 3 normal turns for each Doge Champion
        else if(randomNumber >= 25 && randomNumber < 40) // 15% chance
            tokenIdToPassive[tokenId] = 2; // additional 1 CRT turn for each Doge Champion
        else if(randomNumber >= 40 && randomNumber < 65) // 25% chance
            tokenIdToPassive[tokenId] = 3; // everyone 5% more attack power
        else if(randomNumber >= 65 && randomNumber < 80) // 15% chance
            tokenIdToPassive[tokenId] = 4; // everyone 10% more attack power
        else if(randomNumber >= 80 && randomNumber < 95) // 15% chance
            tokenIdToPassive[tokenId] = 5; // same element 10% more attack power
        else if(randomNumber >= 95 && randomNumber < 100) // 5% chance
            tokenIdToPassive[tokenId] = 6; // same element 20% more attack power

    }

    // @author following region is for altering playable tokenIds of given user

    /*
     * @author adds a new tokenId for given user into playable ids map
     */
    function pushToPlayableIds(address walletAddress, uint256 tokenId) public onlyMarketplace
    {
        walletToPlayableTokenIds[walletAddress].push(tokenId);
        playableTokenIdToUser[tokenId] = walletAddress;
    }

    /*
     * @author adds a new tokenId for given user into playable ids map internally
     */
    function pushToPlayableIdsOnMint(address walletAddress, uint256 tokenId) internal
    {
        walletToPlayableTokenIds[walletAddress].push(tokenId);
        playableTokenIdToUser[tokenId] = walletAddress;
    }

    /*
     * @author removes given tokenId for given user from playable ids map
     */
    function removeFromPlayableIds(address walletAddress, uint256 tokenId) public onlyMarketplace
    {
        uint256[] memory playableIdArray = walletToPlayableTokenIds[walletAddress];

        for(uint256 i = 0; i < playableIdArray.length; i++)
        {
            if(playableIdArray[i] == tokenId)
            {
                delete walletToPlayableTokenIds[walletAddress][i];
                break;
            }
        }
    }

    // @author Following region is for getter functions

    /*
     * @author returns current mint phase's number of normal mints
     */
    function getCurrentMintCount() public view returns (uint256)
    {
        return numberOfNormalMints.current() + numberOfDiscountMints.current();
    }

    /*
     * @author returns property determination rates.
     */
    function getPropertyDeterminationRates() public view returns (uint256, uint256, uint256, uint256)
    {
        return (firstChancePercent, secondChancePercent, thirdChancePercent, forthChancePercent);
    }

    /*
     * @author returns total supply
     */
     function totalSupply() public override view returns (uint256)
     {
         return _tokenIds.current();
     }

    /*
     * @author returns attack powers of given tokenIds. This is consumed by tournament contract
     */
    function getAttackPowers(uint256[4] memory tokenIds) public view returns(uint256[4] memory result)
    {
        result = [tokenIdToAttackPower[tokenIds[0]],
                  tokenIdToAttackPower[tokenIds[1]],
                  tokenIdToAttackPower[tokenIds[2]],
                  tokenIdToAttackPower[tokenIds[3]]];
        
        return result;
    }

    /*
     * @author returns elements and rarities for given tokenIds. This is consumed by tournament contract
     */
    function getElementsAndRarities(uint256[4] memory tokenIds) public view returns(uint256[4] memory elements, uint256[4] memory rarities)
    {
        elements = [tokenIdToElement[tokenIds[0]],
                    tokenIdToElement[tokenIds[1]],
                    tokenIdToElement[tokenIds[2]],
                    tokenIdToElement[tokenIds[3]]];

        rarities = [tokenIdToRarity[tokenIds[0]],
                  tokenIdToRarity[tokenIds[1]],
                  tokenIdToRarity[tokenIds[2]],
                  tokenIdToRarity[tokenIds[3]]];
        
        return (elements, rarities);
    }

    /*
     * @author returns attack, defense, CRT and passive skills for given tokenIds. This is consumed by tournament contract
     */
    function getTournamentValues(uint256[4] memory tokenIds) public view returns(uint256[4] memory attackPowers, uint256[4] memory defensePowers, uint256[4] memory crtRates, uint256[4] memory passiveSkills)
    {
        attackPowers = [tokenIdToAttackPower[tokenIds[0]],
                    tokenIdToAttackPower[tokenIds[1]],
                    tokenIdToAttackPower[tokenIds[2]],
                    tokenIdToAttackPower[tokenIds[3]]];

        defensePowers = [tokenIdToDefensePower[tokenIds[0]],
                  tokenIdToDefensePower[tokenIds[1]],
                  tokenIdToDefensePower[tokenIds[2]],
                  tokenIdToDefensePower[tokenIds[3]]];

        crtRates = [tokenIdToCRTRate[tokenIds[0]],
                  tokenIdToCRTRate[tokenIds[1]],
                  tokenIdToCRTRate[tokenIds[2]],
                  tokenIdToCRTRate[tokenIds[3]]];

        passiveSkills = [tokenIdToPassive[tokenIds[0]],
                  tokenIdToPassive[tokenIds[1]],
                  tokenIdToPassive[tokenIds[2]],
                  tokenIdToPassive[tokenIds[3]]];
        
        return (attackPowers, defensePowers, crtRates, passiveSkills);
    }

    /*
     * @author returns all values for given tokenId.
     */
    function getAllValues(uint256[] memory tokenIds) public view returns(uint256[] memory attacks,
                                                               uint256[] memory defenses,
                                                               uint256[] memory crts,
                                                               uint256[] memory passives,
                                                               uint256[] memory rarities,
                                                               uint256[] memory elements,
                                                               string[] memory uris)
    {
        attacks = new uint[](tokenIds.length);
        defenses = new uint[](tokenIds.length);
        crts = new uint[](tokenIds.length);
        passives = new uint[](tokenIds.length);
        rarities = new uint[](tokenIds.length);
        elements = new uint[](tokenIds.length);
        uris = new string[](tokenIds.length);

        for(uint256 i = 0; i < tokenIds.length; i++)
        {
            attacks[i] = tokenIdToAttackPower[tokenIds[i]];
            defenses[i] = tokenIdToDefensePower[tokenIds[i]];
            crts[i] = tokenIdToCRTRate[tokenIds[i]];
            passives[i] = tokenIdToPassive[tokenIds[i]];
            rarities[i] = tokenIdToRarity[tokenIds[i]];
            elements[i] = tokenIdToElement[tokenIds[i]];
            uris[i] = tokenURI(tokenIds[i]);
        }

        return (attacks,
                defenses,
                crts,
                passives,
                rarities,
                elements,
                uris);
    }

    /*
     * @author returns history of used consumables on given tokenId
     */
    function getConsumableHistory(uint256 tokenId) public view returns(uint256[] memory, int256[] memory)
    {
        uint256[] memory usedConsumableHistoryArray = tokenIdToUsedConsumableHistory[tokenId];
        int256[] memory valueChangeHistoryArray = tokenIdToPropertyValueChangeHistory[tokenId];
        uint256 length = usedConsumableHistoryArray.length;

        if(length < 10)
        {
            uint256[] memory history = new uint256[](length);
            int256[] memory valueChange = new int256[](length);
            for(uint256 i = 0; i < length; i++)
            {
                history[i] = usedConsumableHistoryArray[i];
                valueChange[i] = valueChangeHistoryArray[i];
            }
            return (history, valueChange);
        }
        else
        {
            uint256[] memory history = new uint256[](length);
            int256[] memory valueChange = new int256[](length);
            uint256 historyIndex = length - 10;
            for(uint256 i = 0; i < 10; i++)
            {
                history[i] = usedConsumableHistoryArray[historyIndex];
                valueChange[i] = valueChangeHistoryArray[historyIndex];
                historyIndex++;
            }
            return (history, valueChange);
        }
    }

    /*
     * @author returns minter of given tokenId
     */
    function getMinterOfToken(uint256 tokenId) public view returns(address)
    {
        return tokenIdToMinter[tokenId];
    }

    /*
     * @author returns playable user of given tokenId
     */
    function getPlayableUserOfToken(uint256 tokenId) public view returns(address)
    {
        return playableTokenIdToUser[tokenId];
    }

    /*
     * @author returns mint price for normal minters. This should only called after
     * checking wallet address if it is in whitelist or not
     */
    function getMintPrice() public view returns(uint256)
    {
        if(mintPhase.current() < 2)
            return initialMintPrice;
        else
            return finalMintPrice;
    }

    /*
     * @author returns discounted mint price for whitelist users
     */
    function getDiscountedMintPrice() public view returns(uint256)
    {
        return discountedMintPrice;
    }

    /*
     * @author returns playable tokenIds of given user. After users mint / purchase
     * DogeChampionsNFT, they will be able to see minted / purchased tokenId even
     * if they start selling their NFTs on marketplace. This is to prevent users
     * stop their sales on our marketplace to enter tournaments.
     */
    function getPlayableIds(address walletAddress) public view returns(uint256[] memory)
    {
        return walletToPlayableTokenIds[walletAddress];
    }

    // @author Following region is for setter functions

    /*
     * @author sets property determination rates. We are going to use this method for setting Boost Events
     */
    function setPropertyDeterminationRates(uint256 first, uint256 second, uint256 third) public onlyOwner
    {
        require(first > 0, "Minimum percent should be 1.");
        require(first < second && second < third && third < 100, "Percentages should be in order and less than 100.");
        require(isMintAvailable == false, "This can't be changed during the mint.");

        firstChancePercent = first;
        secondChancePercent = second;
        thirdChancePercent = third;
    }

    /*
     * @author resets property determination rates to default. Should be called to end current Boost Event
     */
    function resetPropertyDeterminationRates() public onlyOwner
    {
        firstChancePercent = 60;
        secondChancePercent = 90;
        thirdChancePercent = 98;
    }

    /*
     * @author sets mint price. Send x1000 of the value for price
     */
    function setMintPrice(uint256 price) public onlyOwner
    {
        require(price >= 0, "Price should be greater than or equal to 0.");
        if(mintPhase.current() < 2)
            initialMintPrice = price * (0.001 ether);
        else
            finalMintPrice = price * (0.001 ether);

    }

    /*
     * @author sets discounted mint price. Send x1000 of the value for price
     */
    function setDiscountMintPrice(uint256 price) public onlyOwner
    {
        require(price >= 0, "Price should be greater than or equal to 0.");
        discountedMintPrice = price * (0.001 ether);
        
    }

    /*
     * @author sets baseURIOverride with given uri
     */
    function setBaseURI(string memory uri) public onlyOwner
    {
        baseURIOverride = uri;
    }

    /*
     * @author sets ignoreDiscountWhitelist flag
     */
    function setIgnoreDiscountFlag(bool shouldIgnore) public onlyOwner
    {
        ignoreDiscountWhitelist = shouldIgnore;
    }

    /*
     * @author sets tournament contract related fields
     */
    function setTournamentContract(address contractAddress) public onlyOwner
    {
        dogeChampionsTournament = ITournament(contractAddress);
    }

    /*
     * @author sets consumable contract related fields
     */
    function setConsumableContract(address contractAddress) public onlyOwner
    {
        dogeChampionsConsumable = DogeChampionsConsumable(contractAddress);
    }

    /*
     * @author sets marketplace contract related fields
     */
    function setMarketplaceContract(address contractAddress) public onlyOwner
    {
        marketplaceAddress = contractAddress;
    }

    /*
     * @author sets availability flag of public mint. If set to false, nobody can mint NFTs
     */
    function setMintAvailability(bool isAvailable) public onlyOwner
    {
        isMintAvailable = isAvailable;
    }

    /*
     * @author sets availability flag of presale mint. If set to false, whitelisted users can't mint NFTs
     */
    function setPresaleAvailability(bool isAvailable) public onlyOwner
    {
        isPresaleAvailable = isAvailable;
    }

    /*
     * @author sets number of free mints for given user into collaborator whitelist map
     */
    function setAddressToCollaboratorWhitelist(address walletAddress, uint256 amount) public onlyOwner
    {
        collaboratorWhitelist[walletAddress] = amount;
    }

    /*
     * @author puts given user into presale whitelist map according to mint phase
     */
    function setAddressToDiscountWhitelist(address walletAddress, uint256 amount) public onlyOwner
    {
        if(mintPhase.current() < 2){
            initialMintDiscountWhitelist[walletAddress] = amount;
        }else {
            finalMintDiscountWhitelist[walletAddress] = amount;
        }
    }
    
    /*
     * @author puts given user into giveaway whitelist map according to mint phase
     */
    function setAddressToGiveawayWhitelist(address walletAddress, uint256 amount) public onlyOwner
    {
        if(mintPhase.current() < 2){
            initialMintGiveawayWhitelist[walletAddress] = amount;
        }else {
            finalMintGiveawayWhitelist[walletAddress] = amount;
        }
    }
}

// File: @openzeppelin/contracts/utils/Counters.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (utils/Counters.sol)

pragma solidity ^0.8.0;

/**
 * @title Counters
 * @author Matt Condon (@shrugs)
 * @dev Provides counters that can only be incremented, decremented or reset. This can be used e.g. to track the number
 * of elements in a mapping, issuing ERC721 ids, or counting request ids.
 *
 * Include with `using Counters for Counters.Counter;`
 */
library Counters {
    struct Counter {
        // This variable should never be directly accessed by users of the library: interactions must be restricted to
        // the library's function. As of Solidity v0.5.2, this cannot be enforced, though there is a proposal to add
        // this feature: see https://github.com/ethereum/solidity/issues/4637
        uint256 _value; // default: 0
    }

    function current(Counter storage counter) internal view returns (uint256) {
        return counter._value;
    }

    function increment(Counter storage counter) internal {
        unchecked {
            counter._value += 1;
        }
    }

    function decrement(Counter storage counter) internal {
        uint256 value = counter._value;
        require(value > 0, "Counter: decrement overflow");
        unchecked {
            counter._value = value - 1;
        }
    }

    function reset(Counter storage counter) internal {
        counter._value = 0;
    }
}


// File: @openzeppelin/contracts/token/ERC721/extensions/ERC721URIStorage.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (token/ERC721/extensions/ERC721URIStorage.sol)

pragma solidity ^0.8.0;

import "../ERC721.sol";

/**
 * @dev ERC721 token with storage based token URI management.
 */
abstract contract ERC721URIStorage is ERC721 {
    using Strings for uint256;

    // Optional mapping for token URIs
    mapping(uint256 => string) private _tokenURIs;

    /**
     * @dev See {IERC721Metadata-tokenURI}.
     */
    function tokenURI(uint256 tokenId) public view virtual override returns (string memory) {
        require(_exists(tokenId), "ERC721URIStorage: URI query for nonexistent token");

        string memory _tokenURI = _tokenURIs[tokenId];
        string memory base = _baseURI();

        // If there is no base URI, return the token URI.
        if (bytes(base).length == 0) {
            return _tokenURI;
        }
        // If both are set, concatenate the baseURI and tokenURI (via abi.encodePacked).
        if (bytes(_tokenURI).length > 0) {
            return string(abi.encodePacked(base, _tokenURI));
        }

        return super.tokenURI(tokenId);
    }

    /**
     * @dev Sets `_tokenURI` as the tokenURI of `tokenId`.
     *
     * Requirements:
     *
     * - `tokenId` must exist.
     */
    function _setTokenURI(uint256 tokenId, string memory _tokenURI) internal virtual {
        require(_exists(tokenId), "ERC721URIStorage: URI set of nonexistent token");
        _tokenURIs[tokenId] = _tokenURI;
    }

    /**
     * @dev Destroys `tokenId`.
     * The approval is cleared when the token is burned.
     *
     * Requirements:
     *
     * - `tokenId` must exist.
     *
     * Emits a {Transfer} event.
     */
    function _burn(uint256 tokenId) internal virtual override {
        super._burn(tokenId);

        if (bytes(_tokenURIs[tokenId]).length != 0) {
            delete _tokenURIs[tokenId];
        }
    }
}


// File: @openzeppelin/contracts/token/ERC721/extensions/ERC721Enumerable.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (token/ERC721/extensions/ERC721Enumerable.sol)

pragma solidity ^0.8.0;

import "../ERC721.sol";
import "./IERC721Enumerable.sol";

/**
 * @dev This implements an optional extension of {ERC721} defined in the EIP that adds
 * enumerability of all the token ids in the contract as well as all token ids owned by each
 * account.
 */
abstract contract ERC721Enumerable is ERC721, IERC721Enumerable {
    // Mapping from owner to list of owned token IDs
    mapping(address => mapping(uint256 => uint256)) private _ownedTokens;

    // Mapping from token ID to index of the owner tokens list
    mapping(uint256 => uint256) private _ownedTokensIndex;

    // Array with all token ids, used for enumeration
    uint256[] private _allTokens;

    // Mapping from token id to position in the allTokens array
    mapping(uint256 => uint256) private _allTokensIndex;

    /**
     * @dev See {IERC165-supportsInterface}.
     */
    function supportsInterface(bytes4 interfaceId) public view virtual override(IERC165, ERC721) returns (bool) {
        return interfaceId == type(IERC721Enumerable).interfaceId || super.supportsInterface(interfaceId);
    }

    /**
     * @dev See {IERC721Enumerable-tokenOfOwnerByIndex}.
     */
    function tokenOfOwnerByIndex(address owner, uint256 index) public view virtual override returns (uint256) {
        require(index < ERC721.balanceOf(owner), "ERC721Enumerable: owner index out of bounds");
        return _ownedTokens[owner][index];
    }

    /**
     * @dev See {IERC721Enumerable-totalSupply}.
     */
    function totalSupply() public view virtual override returns (uint256) {
        return _allTokens.length;
    }

    /**
     * @dev See {IERC721Enumerable-tokenByIndex}.
     */
    function tokenByIndex(uint256 index) public view virtual override returns (uint256) {
        require(index < ERC721Enumerable.totalSupply(), "ERC721Enumerable: global index out of bounds");
        return _allTokens[index];
    }

    /**
     * @dev Hook that is called before any token transfer. This includes minting
     * and burning.
     *
     * Calling conditions:
     *
     * - When `from` and `to` are both non-zero, ``from``'s `tokenId` will be
     * transferred to `to`.
     * - When `from` is zero, `tokenId` will be minted for `to`.
     * - When `to` is zero, ``from``'s `tokenId` will be burned.
     * - `from` cannot be the zero address.
     * - `to` cannot be the zero address.
     *
     * To learn more about hooks, head to xref:ROOT:extending-contracts.adoc#using-hooks[Using Hooks].
     */
    function _beforeTokenTransfer(
        address from,
        address to,
        uint256 tokenId
    ) internal virtual override {
        super._beforeTokenTransfer(from, to, tokenId);

        if (from == address(0)) {
            _addTokenToAllTokensEnumeration(tokenId);
        } else if (from != to) {
            _removeTokenFromOwnerEnumeration(from, tokenId);
        }
        if (to == address(0)) {
            _removeTokenFromAllTokensEnumeration(tokenId);
        } else if (to != from) {
            _addTokenToOwnerEnumeration(to, tokenId);
        }
    }

    /**
     * @dev Private function to add a token to this extension's ownership-tracking data structures.
     * @param to address representing the new owner of the given token ID
     * @param tokenId uint256 ID of the token to be added to the tokens list of the given address
     */
    function _addTokenToOwnerEnumeration(address to, uint256 tokenId) private {
        uint256 length = ERC721.balanceOf(to);
        _ownedTokens[to][length] = tokenId;
        _ownedTokensIndex[tokenId] = length;
    }

    /**
     * @dev Private function to add a token to this extension's token tracking data structures.
     * @param tokenId uint256 ID of the token to be added to the tokens list
     */
    function _addTokenToAllTokensEnumeration(uint256 tokenId) private {
        _allTokensIndex[tokenId] = _allTokens.length;
        _allTokens.push(tokenId);
    }

    /**
     * @dev Private function to remove a token from this extension's ownership-tracking data structures. Note that
     * while the token is not assigned a new owner, the `_ownedTokensIndex` mapping is _not_ updated: this allows for
     * gas optimizations e.g. when performing a transfer operation (avoiding double writes).
     * This has O(1) time complexity, but alters the order of the _ownedTokens array.
     * @param from address representing the previous owner of the given token ID
     * @param tokenId uint256 ID of the token to be removed from the tokens list of the given address
     */
    function _removeTokenFromOwnerEnumeration(address from, uint256 tokenId) private {
        // To prevent a gap in from's tokens array, we store the last token in the index of the token to delete, and
        // then delete the last slot (swap and pop).

        uint256 lastTokenIndex = ERC721.balanceOf(from) - 1;
        uint256 tokenIndex = _ownedTokensIndex[tokenId];

        // When the token to delete is the last token, the swap operation is unnecessary
        if (tokenIndex != lastTokenIndex) {
            uint256 lastTokenId = _ownedTokens[from][lastTokenIndex];

            _ownedTokens[from][tokenIndex] = lastTokenId; // Move the last token to the slot of the to-delete token
            _ownedTokensIndex[lastTokenId] = tokenIndex; // Update the moved token's index
        }

        // This also deletes the contents at the last position of the array
        delete _ownedTokensIndex[tokenId];
        delete _ownedTokens[from][lastTokenIndex];
    }

    /**
     * @dev Private function to remove a token from this extension's token tracking data structures.
     * This has O(1) time complexity, but alters the order of the _allTokens array.
     * @param tokenId uint256 ID of the token to be removed from the tokens list
     */
    function _removeTokenFromAllTokensEnumeration(uint256 tokenId) private {
        // To prevent a gap in the tokens array, we store the last token in the index of the token to delete, and
        // then delete the last slot (swap and pop).

        uint256 lastTokenIndex = _allTokens.length - 1;
        uint256 tokenIndex = _allTokensIndex[tokenId];

        // When the token to delete is the last token, the swap operation is unnecessary. However, since this occurs so
        // rarely (when the last minted token is burnt) that we still do the swap here to avoid the gas cost of adding
        // an 'if' statement (like in _removeTokenFromOwnerEnumeration)
        uint256 lastTokenId = _allTokens[lastTokenIndex];

        _allTokens[tokenIndex] = lastTokenId; // Move the last token to the slot of the to-delete token
        _allTokensIndex[lastTokenId] = tokenIndex; // Update the moved token's index

        // This also deletes the contents at the last position of the array
        delete _allTokensIndex[tokenId];
        _allTokens.pop();
    }
}


// File: @openzeppelin/contracts/token/ERC721/ERC721.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (token/ERC721/ERC721.sol)

pragma solidity ^0.8.0;

import "./IERC721.sol";
import "./IERC721Receiver.sol";
import "./extensions/IERC721Metadata.sol";
import "../../utils/Address.sol";
import "../../utils/Context.sol";
import "../../utils/Strings.sol";
import "../../utils/introspection/ERC165.sol";

/**
 * @dev Implementation of https://eips.ethereum.org/EIPS/eip-721[ERC721] Non-Fungible Token Standard, including
 * the Metadata extension, but not including the Enumerable extension, which is available separately as
 * {ERC721Enumerable}.
 */
contract ERC721 is Context, ERC165, IERC721, IERC721Metadata {
    using Address for address;
    using Strings for uint256;

    // Token name
    string private _name;

    // Token symbol
    string private _symbol;

    // Mapping from token ID to owner address
    mapping(uint256 => address) private _owners;

    // Mapping owner address to token count
    mapping(address => uint256) private _balances;

    // Mapping from token ID to approved address
    mapping(uint256 => address) private _tokenApprovals;

    // Mapping from owner to operator approvals
    mapping(address => mapping(address => bool)) private _operatorApprovals;

    /**
     * @dev Initializes the contract by setting a `name` and a `symbol` to the token collection.
     */
    constructor(string memory name_, string memory symbol_) {
        _name = name_;
        _symbol = symbol_;
    }

    /**
     * @dev See {IERC165-supportsInterface}.
     */
    function supportsInterface(bytes4 interfaceId) public view virtual override(ERC165, IERC165) returns (bool) {
        return
            interfaceId == type(IERC721).interfaceId ||
            interfaceId == type(IERC721Metadata).interfaceId ||
            super.supportsInterface(interfaceId);
    }

    /**
     * @dev See {IERC721-balanceOf}.
     */
    function balanceOf(address owner) public view virtual override returns (uint256) {
        require(owner != address(0), "ERC721: balance query for the zero address");
        return _balances[owner];
    }

    /**
     * @dev See {IERC721-ownerOf}.
     */
    function ownerOf(uint256 tokenId) public view virtual override returns (address) {
        address owner = _owners[tokenId];
        require(owner != address(0), "ERC721: owner query for nonexistent token");
        return owner;
    }

    /**
     * @dev See {IERC721Metadata-name}.
     */
    function name() public view virtual override returns (string memory) {
        return _name;
    }

    /**
     * @dev See {IERC721Metadata-symbol}.
     */
    function symbol() public view virtual override returns (string memory) {
        return _symbol;
    }

    /**
     * @dev See {IERC721Metadata-tokenURI}.
     */
    function tokenURI(uint256 tokenId) public view virtual override returns (string memory) {
        require(_exists(tokenId), "ERC721Metadata: URI query for nonexistent token");

        string memory baseURI = _baseURI();
        return bytes(baseURI).length > 0 ? string(abi.encodePacked(baseURI, tokenId.toString())) : "";
    }

    /**
     * @dev Base URI for computing {tokenURI}. If set, the resulting URI for each
     * token will be the concatenation of the `baseURI` and the `tokenId`. Empty
     * by default, can be overriden in child contracts.
     */
    function _baseURI() internal view virtual returns (string memory) {
        return "";
    }

    /**
     * @dev See {IERC721-approve}.
     */
    function approve(address to, uint256 tokenId) public virtual override {
        address owner = ERC721.ownerOf(tokenId);
        require(to != owner, "ERC721: approval to current owner");

        require(
            _msgSender() == owner || isApprovedForAll(owner, _msgSender()),
            "ERC721: approve caller is not owner nor approved for all"
        );

        _approve(to, tokenId);
    }

    /**
     * @dev See {IERC721-getApproved}.
     */
    function getApproved(uint256 tokenId) public view virtual override returns (address) {
        require(_exists(tokenId), "ERC721: approved query for nonexistent token");

        return _tokenApprovals[tokenId];
    }

    /**
     * @dev See {IERC721-setApprovalForAll}.
     */
    function setApprovalForAll(address operator, bool approved) public virtual override {
        _setApprovalForAll(_msgSender(), operator, approved);
    }

    /**
     * @dev See {IERC721-isApprovedForAll}.
     */
    function isApprovedForAll(address owner, address operator) public view virtual override returns (bool) {
        return _operatorApprovals[owner][operator];
    }

    /**
     * @dev See {IERC721-transferFrom}.
     */
    function transferFrom(
        address from,
        address to,
        uint256 tokenId
    ) public virtual override {
        //solhint-disable-next-line max-line-length
        require(_isApprovedOrOwner(_msgSender(), tokenId), "ERC721: transfer caller is not owner nor approved");

        _transfer(from, to, tokenId);
    }

    /**
     * @dev See {IERC721-safeTransferFrom}.
     */
    function safeTransferFrom(
        address from,
        address to,
        uint256 tokenId
    ) public virtual override {
        safeTransferFrom(from, to, tokenId, "");
    }

    /**
     * @dev See {IERC721-safeTransferFrom}.
     */
    function safeTransferFrom(
        address from,
        address to,
        uint256 tokenId,
        bytes memory _data
    ) public virtual override {
        require(_isApprovedOrOwner(_msgSender(), tokenId), "ERC721: transfer caller is not owner nor approved");
        _safeTransfer(from, to, tokenId, _data);
    }

    /**
     * @dev Safely transfers `tokenId` token from `from` to `to`, checking first that contract recipients
     * are aware of the ERC721 protocol to prevent tokens from being forever locked.
     *
     * `_data` is additional data, it has no specified format and it is sent in call to `to`.
     *
     * This internal function is equivalent to {safeTransferFrom}, and can be used to e.g.
     * implement alternative mechanisms to perform token transfer, such as signature-based.
     *
     * Requirements:
     *
     * - `from` cannot be the zero address.
     * - `to` cannot be the zero address.
     * - `tokenId` token must exist and be owned by `from`.
     * - If `to` refers to a smart contract, it must implement {IERC721Receiver-onERC721Received}, which is called upon a safe transfer.
     *
     * Emits a {Transfer} event.
     */
    function _safeTransfer(
        address from,
        address to,
        uint256 tokenId,
        bytes memory _data
    ) internal virtual {
        _transfer(from, to, tokenId);
        require(_checkOnERC721Received(from, to, tokenId, _data), "ERC721: transfer to non ERC721Receiver implementer");
    }

    /**
     * @dev Returns whether `tokenId` exists.
     *
     * Tokens can be managed by their owner or approved accounts via {approve} or {setApprovalForAll}.
     *
     * Tokens start existing when they are minted (`_mint`),
     * and stop existing when they are burned (`_burn`).
     */
    function _exists(uint256 tokenId) internal view virtual returns (bool) {
        return _owners[tokenId] != address(0);
    }

    /**
     * @dev Returns whether `spender` is allowed to manage `tokenId`.
     *
     * Requirements:
     *
     * - `tokenId` must exist.
     */
    function _isApprovedOrOwner(address spender, uint256 tokenId) internal view virtual returns (bool) {
        require(_exists(tokenId), "ERC721: operator query for nonexistent token");
        address owner = ERC721.ownerOf(tokenId);
        return (spender == owner || getApproved(tokenId) == spender || isApprovedForAll(owner, spender));
    }

    /**
     * @dev Safely mints `tokenId` and transfers it to `to`.
     *
     * Requirements:
     *
     * - `tokenId` must not exist.
     * - If `to` refers to a smart contract, it must implement {IERC721Receiver-onERC721Received}, which is called upon a safe transfer.
     *
     * Emits a {Transfer} event.
     */
    function _safeMint(address to, uint256 tokenId) internal virtual {
        _safeMint(to, tokenId, "");
    }

    /**
     * @dev Same as {xref-ERC721-_safeMint-address-uint256-}[`_safeMint`], with an additional `data` parameter which is
     * forwarded in {IERC721Receiver-onERC721Received} to contract recipients.
     */
    function _safeMint(
        address to,
        uint256 tokenId,
        bytes memory _data
    ) internal virtual {
        _mint(to, tokenId);
        require(
            _checkOnERC721Received(address(0), to, tokenId, _data),
            "ERC721: transfer to non ERC721Receiver implementer"
        );
    }

    /**
     * @dev Mints `tokenId` and transfers it to `to`.
     *
     * WARNING: Usage of this method is discouraged, use {_safeMint} whenever possible
     *
     * Requirements:
     *
     * - `tokenId` must not exist.
     * - `to` cannot be the zero address.
     *
     * Emits a {Transfer} event.
     */
    function _mint(address to, uint256 tokenId) internal virtual {
        require(to != address(0), "ERC721: mint to the zero address");
        require(!_exists(tokenId), "ERC721: token already minted");

        _beforeTokenTransfer(address(0), to, tokenId);

        _balances[to] += 1;
        _owners[tokenId] = to;

        emit Transfer(address(0), to, tokenId);
    }

    /**
     * @dev Destroys `tokenId`.
     * The approval is cleared when the token is burned.
     *
     * Requirements:
     *
     * - `tokenId` must exist.
     *
     * Emits a {Transfer} event.
     */
    function _burn(uint256 tokenId) internal virtual {
        address owner = ERC721.ownerOf(tokenId);

        _beforeTokenTransfer(owner, address(0), tokenId);

        // Clear approvals
        _approve(address(0), tokenId);

        _balances[owner] -= 1;
        delete _owners[tokenId];

        emit Transfer(owner, address(0), tokenId);
    }

    /**
     * @dev Transfers `tokenId` from `from` to `to`.
     *  As opposed to {transferFrom}, this imposes no restrictions on msg.sender.
     *
     * Requirements:
     *
     * - `to` cannot be the zero address.
     * - `tokenId` token must be owned by `from`.
     *
     * Emits a {Transfer} event.
     */
    function _transfer(
        address from,
        address to,
        uint256 tokenId
    ) internal virtual {
        require(ERC721.ownerOf(tokenId) == from, "ERC721: transfer of token that is not own");
        require(to != address(0), "ERC721: transfer to the zero address");

        _beforeTokenTransfer(from, to, tokenId);

        // Clear approvals from the previous owner
        _approve(address(0), tokenId);

        _balances[from] -= 1;
        _balances[to] += 1;
        _owners[tokenId] = to;

        emit Transfer(from, to, tokenId);
    }

    /**
     * @dev Approve `to` to operate on `tokenId`
     *
     * Emits a {Approval} event.
     */
    function _approve(address to, uint256 tokenId) internal virtual {
        _tokenApprovals[tokenId] = to;
        emit Approval(ERC721.ownerOf(tokenId), to, tokenId);
    }

    /**
     * @dev Approve `operator` to operate on all of `owner` tokens
     *
     * Emits a {ApprovalForAll} event.
     */
    function _setApprovalForAll(
        address owner,
        address operator,
        bool approved
    ) internal virtual {
        require(owner != operator, "ERC721: approve to caller");
        _operatorApprovals[owner][operator] = approved;
        emit ApprovalForAll(owner, operator, approved);
    }

    /**
     * @dev Internal function to invoke {IERC721Receiver-onERC721Received} on a target address.
     * The call is not executed if the target address is not a contract.
     *
     * @param from address representing the previous owner of the given token ID
     * @param to target address that will receive the tokens
     * @param tokenId uint256 ID of the token to be transferred
     * @param _data bytes optional data to send along with the call
     * @return bool whether the call correctly returned the expected magic value
     */
    function _checkOnERC721Received(
        address from,
        address to,
        uint256 tokenId,
        bytes memory _data
    ) private returns (bool) {
        if (to.isContract()) {
            try IERC721Receiver(to).onERC721Received(_msgSender(), from, tokenId, _data) returns (bytes4 retval) {
                return retval == IERC721Receiver.onERC721Received.selector;
            } catch (bytes memory reason) {
                if (reason.length == 0) {
                    revert("ERC721: transfer to non ERC721Receiver implementer");
                } else {
                    assembly {
                        revert(add(32, reason), mload(reason))
                    }
                }
            }
        } else {
            return true;
        }
    }

    /**
     * @dev Hook that is called before any token transfer. This includes minting
     * and burning.
     *
     * Calling conditions:
     *
     * - When `from` and `to` are both non-zero, ``from``'s `tokenId` will be
     * transferred to `to`.
     * - When `from` is zero, `tokenId` will be minted for `to`.
     * - When `to` is zero, ``from``'s `tokenId` will be burned.
     * - `from` and `to` are never both zero.
     *
     * To learn more about hooks, head to xref:ROOT:extending-contracts.adoc#using-hooks[Using Hooks].
     */
    function _beforeTokenTransfer(
        address from,
        address to,
        uint256 tokenId
    ) internal virtual {}
}


// File: @openzeppelin/contracts/utils/Strings.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (utils/Strings.sol)

pragma solidity ^0.8.0;

/**
 * @dev String operations.
 */
library Strings {
    bytes16 private constant _HEX_SYMBOLS = "0123456789abcdef";

    /**
     * @dev Converts a `uint256` to its ASCII `string` decimal representation.
     */
    function toString(uint256 value) internal pure returns (string memory) {
        // Inspired by OraclizeAPI's implementation - MIT licence
        // https://github.com/oraclize/ethereum-api/blob/b42146b063c7d6ee1358846c198246239e9360e8/oraclizeAPI_0.4.25.sol

        if (value == 0) {
            return "0";
        }
        uint256 temp = value;
        uint256 digits;
        while (temp != 0) {
            digits++;
            temp /= 10;
        }
        bytes memory buffer = new bytes(digits);
        while (value != 0) {
            digits -= 1;
            buffer[digits] = bytes1(uint8(48 + uint256(value % 10)));
            value /= 10;
        }
        return string(buffer);
    }

    /**
     * @dev Converts a `uint256` to its ASCII `string` hexadecimal representation.
     */
    function toHexString(uint256 value) internal pure returns (string memory) {
        if (value == 0) {
            return "0x00";
        }
        uint256 temp = value;
        uint256 length = 0;
        while (temp != 0) {
            length++;
            temp >>= 8;
        }
        return toHexString(value, length);
    }

    /**
     * @dev Converts a `uint256` to its ASCII `string` hexadecimal representation with fixed length.
     */
    function toHexString(uint256 value, uint256 length) internal pure returns (string memory) {
        bytes memory buffer = new bytes(2 * length + 2);
        buffer[0] = "0";
        buffer[1] = "x";
        for (uint256 i = 2 * length + 1; i > 1; --i) {
            buffer[i] = _HEX_SYMBOLS[value & 0xf];
            value >>= 4;
        }
        require(value == 0, "Strings: hex length insufficient");
        return string(buffer);
    }
}


// File: contracts/ITournament.sol
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.4;

interface ITournament
{
    /*
     * @author data model for tournaments
     */
    struct Tournament
    {
        uint256 tournamentId;
        uint256 endTimestamp;
        uint256 tournamentType;
        uint256[4] elements;
        bool isPaid;
        bool isConsumableOnly;
    }

    /*
     * @author sets reward count for winner
     */
    function setWinnerRewardCount(uint256 count) external;

    /*
     * @author sets reward count for participation
     */
    function setParticipationRewardCount(uint256 count) external;

    /*
     * @author sets target NFT contract address
     */
    function setNFTContract(address contractAddress) external;

    /*
     * @author sets target consumable / collectible contract address
     */
    function setConsumableContract(address contractAddress) external;

    /*
     * @author sets entrance fee for paid BNB tournaments
     */
    function setPaidTournamentEntranceFee(uint256 fee) external;

    /*
     * @author registers given tokenIds to given tournament
     */
    function register(uint256 tournamentId, uint256[4] memory tokenIds) external payable;

    /*
     * @author sets new tournament
     */
    function setTournament(uint256 endTimestamp, uint256 tournamentType, uint256[4] memory elements, bool isPaid, bool isConsumableOnly) external;

    /*
     * @author finalizes latest ended tournament
     */
    function endCurrentTournament() external;

    /*
     * @author returns contract balance
     */
    function getContractBalance() external view returns(uint256);

    /*
     * @author returns active tournaments
     */
    function getActiveTournaments() external view returns(Tournament[] memory);

    /*
     * @author returns latest n ended tournaments
     */
    function getLatestEndedTournaments() external view returns(Tournament[] memory, address[] memory);

    /*
     * @author returns tournament of given tournamentId
     */
    function getTournament(uint256 tournamentId) external view returns(Tournament memory);

    /*
     * @author returns tournament winner of given tournamentId
     */
    function getTournamentWinner(uint256 tournamentId) external view returns(address);

    /*
     * @author returns current tournament
     */
    function getCurrentTournament() external view returns(Tournament memory);

    /*
     * @author returns paid tournament entrance fee
     */
    function getPaidTournamentEntryFee() external view returns(uint256);

    /*
     * @author returns participant count of given tournamentId
     */
    function getParticipantCount(uint256 tournamentId) external view returns(uint256);

    /*
     * @author returns rewards for given wallet address
     */
    function getRewards(address walletAddress) external view returns(uint256[] memory);

    /*
     * @author returns if user is joined to given tournamentId or not
     */
    function getIsJoined(uint256 tournamentId, address walletAddress) external view returns(bool);

    /*
     * @author decreases rewards for given wallet address
     */
    function decreaseReward(uint256 tournamentType, address walletAddress) external;

    /*
     * @author decreases consumable rewards for given wallet address
     */
    function decreaseConsumableReward(uint256 amount, address walletAddress) external;
}

// File: contracts/DogeChampionsConsumable.sol
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.4;

import "@openzeppelin/contracts/token/ERC1155/ERC1155.sol";

import "contracts/ITournament.sol";

contract DogeChampionsConsumable is ERC1155
{
    uint256 private constant ATTACK_NORMAL = 0;
    uint256 private constant DEFENSE_NORMAL = 1;
    uint256 private constant CRT_NORMAL = 2;
    uint256 private constant ATTACK_PREMIUM = 3;
    uint256 private constant DEFENSE_PREMIUM = 4;
    uint256 private constant CRT_PREMIUM = 5;

    mapping(uint256 => uint256) private bundleIdToBundle;
    mapping(uint256 => uint256) private bundleIdToPrice;

    uint256 private premiumPrice = 0.1 ether;

    address private marketplaceAddress;
    address payable private owner;

    bool private areBundlesAvailable;

    ITournament private dogeChampionsTournament;
    address private dogeChampionsNFTContractAddress;

    // events related fields

    bool isConsumeFrenzyEventActive;
    uint256 activeConsumeFrenzyEventId;
    uint256 consumeFrenzyEventEntranceCount;
    uint256 consumeFrenzyEventRewardCount;
    mapping(uint256 => mapping(address => uint256)) userToUsedConsumable;

    bool isPremiumEventActive;
    uint256 premiumEventEntranceCount;
    uint256 premiumEventRewardCount;

    /*
     * @author modifier that prevents calls from addresses rather than owner
     */
    modifier onlyOwner
    {
        require(msg.sender == owner, "Only owner can access this function.");
        _;
    }

    /*
     * @author modifier that prevents calls from addresses rather than DogeChampionsNFT contract
     */
    modifier onlyDogeChampionsNFT
    {
        require(msg.sender == dogeChampionsNFTContractAddress, "Only DogeChampionsNFT contract can access this function.");
        _;
    }

    /*
     * @author plain good old constructor
     */
    constructor() ERC1155("ipfs://QmesZgjyaa4R1Jm88XtBzXB7BE77zQVc4bMrT8mHbqYj1D/{id}.json")
    {
        owner = payable(msg.sender);

        // mint 20 unit from each consumable type to deployer for testing purposes on production
        _mint(msg.sender, ATTACK_NORMAL, 20, "");
        _mint(msg.sender, DEFENSE_NORMAL, 20, "");
        _mint(msg.sender, CRT_NORMAL, 20, "");
        _mint(msg.sender, ATTACK_PREMIUM, 20, "");
        _mint(msg.sender, DEFENSE_PREMIUM, 20, "");
        _mint(msg.sender, CRT_PREMIUM, 20, "");

        bundleIdToBundle[0] = 5;
        bundleIdToPrice[0] = 0.25 ether;

        bundleIdToBundle[1] = 10;
        bundleIdToPrice[1] = 2 ether;

        bundleIdToBundle[2] = 20;
        bundleIdToPrice[2] = 3 ether;

        bundleIdToBundle[3] = 30;
        bundleIdToPrice[3] = 4 ether;

        bundleIdToBundle[4] = 40;
        bundleIdToPrice[4] = 5 ether;

        areBundlesAvailable = false;

        isConsumeFrenzyEventActive = false;
        isPremiumEventActive = false;

        activeConsumeFrenzyEventId = 0;
    }

    /*
     * @author sets marketplace contract related fields
     */
    function setMarketplaceContractAddress(address contractAddress) public onlyOwner
    {
        marketplaceAddress = contractAddress;
    }

    /*
     * @author sets tournament contract related fields
     */
    function setTournamentContract(address contractAddress) public onlyOwner
    {
        dogeChampionsTournament = ITournament(contractAddress);
    }

    /*
     * @author sets DogeChampionsNFT contract related fields
     */
    function setNFTContract(address contractAddress) public onlyOwner
    {
        dogeChampionsNFTContractAddress = contractAddress;
    }

    /*
     * @author sets premium consumable price. Send x1000 higher value for price in parameter.
     */
    function setPremiumPrice(uint256 price) public onlyOwner
    {
        require(price >= 0, "Price should be greater than or equal to 0.");
        premiumPrice = price * (0.001 ether);
    }

    /*
     * @author mints random consumables to user if he / she has enough consumable reward
     */
    function mint(uint256 amount) public 
    {
        uint256[] memory rewards = dogeChampionsTournament.getRewards(msg.sender);
        require(rewards[5] >= amount, "You don't have enough Consumable rewards.");

        for(uint256 i = 0; i < amount; i++)
        {
            uint256 tokenId = calculateConsumableType(i + 1 + amount);
            _mint(msg.sender, tokenId, 1, "");
        }

        dogeChampionsTournament.decreaseConsumableReward(amount, msg.sender);

        setApprovalForAll(marketplaceAddress, true);
    }

    /*
     * @author mints given premium consumable to user for a price
     */
    function premiumMint(uint256 tokenId, uint256 amount) public payable
    {
        require(tokenId > 2 && tokenId < 6, "There is no such premium consumable type.");
        require(msg.value == premiumPrice * amount, "Please send exact price.");

        _mint(msg.sender, tokenId, amount, "");

        owner.transfer(msg.value);

        setApprovalForAll(marketplaceAddress, true);
    }

    /*
     * @author mints given bundle
     */
    function bundleMint(uint256 tokenId, uint256 bundleId) public payable
    {
        require(areBundlesAvailable == true, "Bundle sales are closed.");
        require(tokenId > 2 && tokenId < 6, "There is no such premium consumable type.");
        require(bundleId >= 0 && bundleId < 5, "There are only 5 bundles.");
        require(msg.value == bundleIdToPrice[bundleId], "Please send exact price.");

        _mint(msg.sender, tokenId, bundleIdToBundle[bundleId], "");

        owner.transfer(msg.value);

        setApprovalForAll(marketplaceAddress, true);
    }

    /*
     * @author mints consume frenzy event reward
     */
    function consumeFrenzyEventMint(uint256 premiumTokenId) public
    {
        require(isConsumeFrenzyEventActive == true, "Consume frenzy event is not active.");
        require(premiumTokenId >= 3 && premiumTokenId < 6, "There is no such premium type.");
        require(userToUsedConsumable[activeConsumeFrenzyEventId][msg.sender] >= consumeFrenzyEventEntranceCount, "Not enough consumables.");

        _mint(msg.sender, premiumTokenId, consumeFrenzyEventRewardCount, "");
        userToUsedConsumable[activeConsumeFrenzyEventId][msg.sender] = userToUsedConsumable[activeConsumeFrenzyEventId][msg.sender] - consumeFrenzyEventEntranceCount;
    }

    /*
     * @author mints premium consumable from premium event
     */
    function premiumEventMint(uint256 consumableId) public
    {
        require(isPremiumEventActive == true, "Premium event is not active.");
        require(consumableId >= 0 && consumableId < 3, "There is no such consumable type.");
        require(balanceOf(msg.sender, consumableId) >= premiumEventEntranceCount, "Not enough consumables.");

        _burn(msg.sender, consumableId, premiumEventEntranceCount);
        _mint(msg.sender, consumableId + 3, premiumEventRewardCount, "");
    }

    /*
     * @author burns consumable after user uses it onto a Doge Champion
     */
    function burn(uint256 tokenId, address walletAddress) public onlyDogeChampionsNFT
    {
        if(isConsumeFrenzyEventActive)
        {
            userToUsedConsumable[activeConsumeFrenzyEventId][walletAddress] = userToUsedConsumable[activeConsumeFrenzyEventId][walletAddress] + 1;
        }

        _burn(walletAddress, tokenId, 1);
    }

    /*
     * @author sets bundle availability
     */
    function setBundlesAvailability(bool isAvailable) public onlyOwner
    {
        areBundlesAvailable = isAvailable;
    }

    /*
     * @author sets consume frenzy event state
     */
    function setConsumeFrenzyEventState(bool isActive) public onlyOwner
    {
        require(isConsumeFrenzyEventActive != isActive, "State is the same.");
        isConsumeFrenzyEventActive = isActive;
        if(!isActive)
        {
            activeConsumeFrenzyEventId = activeConsumeFrenzyEventId + 1;
        }
    }

    /*
     * @author sets premium event state
     */
    function setPremiumEventState(bool isActive) public onlyOwner
    {
        isPremiumEventActive = isActive;
    }

    /*
     * @author sets consume frenzy event data
     */
    function setConsumeFrenzyEventData(uint256 entranceCount, uint256 rewardCount) public onlyOwner
    {
        consumeFrenzyEventEntranceCount = entranceCount;
        consumeFrenzyEventRewardCount = rewardCount;
    }

    /*
     * @author sets premium event data
     */
    function setPremiumEventData(uint256 entranceCount, uint256 rewardCount) public onlyOwner
    {
        premiumEventEntranceCount = entranceCount;
        premiumEventRewardCount = rewardCount;
    }

    /*
     * @author sets bundle amount and price of given bundle Id. Send bundle price x1000 of the value.
     */
    function setBundleDetails(uint256 bundleId, uint256 bundleAmount, uint256 bundlePrice) public onlyOwner
    {
        require(bundleId >= 0 && bundleId < 5, "There are only 5 bundles.");
        bundleIdToBundle[bundleId] = bundleAmount;
        bundleIdToBundle[bundleId] = bundlePrice * (0.001 ether);
    }

    /*
     * @author returns user progress for consume frenzy event
     */
    function getConsumeFrenzyEventProgress() public view returns(uint256 progress, uint256)
    {
        return (userToUsedConsumable[activeConsumeFrenzyEventId][msg.sender], consumeFrenzyEventEntranceCount);
    }

    /*
     * @author helper function to determine consumable type randomly
     */
    function calculateConsumableType(uint256 nonce) internal view returns(uint256)
    {
        return uint256(keccak256(abi.encodePacked(block.timestamp, msg.sender, nonce))) % 3;
    }
}

// File: @openzeppelin/contracts/token/ERC721/IERC721.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (token/ERC721/IERC721.sol)

pragma solidity ^0.8.0;

import "../../utils/introspection/IERC165.sol";

/**
 * @dev Required interface of an ERC721 compliant contract.
 */
interface IERC721 is IERC165 {
    /**
     * @dev Emitted when `tokenId` token is transferred from `from` to `to`.
     */
    event Transfer(address indexed from, address indexed to, uint256 indexed tokenId);

    /**
     * @dev Emitted when `owner` enables `approved` to manage the `tokenId` token.
     */
    event Approval(address indexed owner, address indexed approved, uint256 indexed tokenId);

    /**
     * @dev Emitted when `owner` enables or disables (`approved`) `operator` to manage all of its assets.
     */
    event ApprovalForAll(address indexed owner, address indexed operator, bool approved);

    /**
     * @dev Returns the number of tokens in ``owner``'s account.
     */
    function balanceOf(address owner) external view returns (uint256 balance);

    /**
     * @dev Returns the owner of the `tokenId` token.
     *
     * Requirements:
     *
     * - `tokenId` must exist.
     */
    function ownerOf(uint256 tokenId) external view returns (address owner);

    /**
     * @dev Safely transfers `tokenId` token from `from` to `to`, checking first that contract recipients
     * are aware of the ERC721 protocol to prevent tokens from being forever locked.
     *
     * Requirements:
     *
     * - `from` cannot be the zero address.
     * - `to` cannot be the zero address.
     * - `tokenId` token must exist and be owned by `from`.
     * - If the caller is not `from`, it must be have been allowed to move this token by either {approve} or {setApprovalForAll}.
     * - If `to` refers to a smart contract, it must implement {IERC721Receiver-onERC721Received}, which is called upon a safe transfer.
     *
     * Emits a {Transfer} event.
     */
    function safeTransferFrom(
        address from,
        address to,
        uint256 tokenId
    ) external;

    /**
     * @dev Transfers `tokenId` token from `from` to `to`.
     *
     * WARNING: Usage of this method is discouraged, use {safeTransferFrom} whenever possible.
     *
     * Requirements:
     *
     * - `from` cannot be the zero address.
     * - `to` cannot be the zero address.
     * - `tokenId` token must be owned by `from`.
     * - If the caller is not `from`, it must be approved to move this token by either {approve} or {setApprovalForAll}.
     *
     * Emits a {Transfer} event.
     */
    function transferFrom(
        address from,
        address to,
        uint256 tokenId
    ) external;

    /**
     * @dev Gives permission to `to` to transfer `tokenId` token to another account.
     * The approval is cleared when the token is transferred.
     *
     * Only a single account can be approved at a time, so approving the zero address clears previous approvals.
     *
     * Requirements:
     *
     * - The caller must own the token or be an approved operator.
     * - `tokenId` must exist.
     *
     * Emits an {Approval} event.
     */
    function approve(address to, uint256 tokenId) external;

    /**
     * @dev Returns the account approved for `tokenId` token.
     *
     * Requirements:
     *
     * - `tokenId` must exist.
     */
    function getApproved(uint256 tokenId) external view returns (address operator);

    /**
     * @dev Approve or remove `operator` as an operator for the caller.
     * Operators can call {transferFrom} or {safeTransferFrom} for any token owned by the caller.
     *
     * Requirements:
     *
     * - The `operator` cannot be the caller.
     *
     * Emits an {ApprovalForAll} event.
     */
    function setApprovalForAll(address operator, bool _approved) external;

    /**
     * @dev Returns if the `operator` is allowed to manage all of the assets of `owner`.
     *
     * See {setApprovalForAll}
     */
    function isApprovedForAll(address owner, address operator) external view returns (bool);

    /**
     * @dev Safely transfers `tokenId` token from `from` to `to`.
     *
     * Requirements:
     *
     * - `from` cannot be the zero address.
     * - `to` cannot be the zero address.
     * - `tokenId` token must exist and be owned by `from`.
     * - If the caller is not `from`, it must be approved to move this token by either {approve} or {setApprovalForAll}.
     * - If `to` refers to a smart contract, it must implement {IERC721Receiver-onERC721Received}, which is called upon a safe transfer.
     *
     * Emits a {Transfer} event.
     */
    function safeTransferFrom(
        address from,
        address to,
        uint256 tokenId,
        bytes calldata data
    ) external;
}


// File: @openzeppelin/contracts/token/ERC721/IERC721Receiver.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (token/ERC721/IERC721Receiver.sol)

pragma solidity ^0.8.0;

/**
 * @title ERC721 token receiver interface
 * @dev Interface for any contract that wants to support safeTransfers
 * from ERC721 asset contracts.
 */
interface IERC721Receiver {
    /**
     * @dev Whenever an {IERC721} `tokenId` token is transferred to this contract via {IERC721-safeTransferFrom}
     * by `operator` from `from`, this function is called.
     *
     * It must return its Solidity selector to confirm the token transfer.
     * If any other value is returned or the interface is not implemented by the recipient, the transfer will be reverted.
     *
     * The selector can be obtained in Solidity with `IERC721.onERC721Received.selector`.
     */
    function onERC721Received(
        address operator,
        address from,
        uint256 tokenId,
        bytes calldata data
    ) external returns (bytes4);
}


// File: @openzeppelin/contracts/token/ERC721/extensions/IERC721Metadata.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (token/ERC721/extensions/IERC721Metadata.sol)

pragma solidity ^0.8.0;

import "../IERC721.sol";

/**
 * @title ERC-721 Non-Fungible Token Standard, optional metadata extension
 * @dev See https://eips.ethereum.org/EIPS/eip-721
 */
interface IERC721Metadata is IERC721 {
    /**
     * @dev Returns the token collection name.
     */
    function name() external view returns (string memory);

    /**
     * @dev Returns the token collection symbol.
     */
    function symbol() external view returns (string memory);

    /**
     * @dev Returns the Uniform Resource Identifier (URI) for `tokenId` token.
     */
    function tokenURI(uint256 tokenId) external view returns (string memory);
}


// File: @openzeppelin/contracts/utils/Address.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (utils/Address.sol)

pragma solidity ^0.8.0;

/**
 * @dev Collection of functions related to the address type
 */
library Address {
    /**
     * @dev Returns true if `account` is a contract.
     *
     * [IMPORTANT]
     * ====
     * It is unsafe to assume that an address for which this function returns
     * false is an externally-owned account (EOA) and not a contract.
     *
     * Among others, `isContract` will return false for the following
     * types of addresses:
     *
     *  - an externally-owned account
     *  - a contract in construction
     *  - an address where a contract will be created
     *  - an address where a contract lived, but was destroyed
     * ====
     */
    function isContract(address account) internal view returns (bool) {
        // This method relies on extcodesize, which returns 0 for contracts in
        // construction, since the code is only stored at the end of the
        // constructor execution.

        uint256 size;
        assembly {
            size := extcodesize(account)
        }
        return size > 0;
    }

    /**
     * @dev Replacement for Solidity's `transfer`: sends `amount` wei to
     * `recipient`, forwarding all available gas and reverting on errors.
     *
     * https://eips.ethereum.org/EIPS/eip-1884[EIP1884] increases the gas cost
     * of certain opcodes, possibly making contracts go over the 2300 gas limit
     * imposed by `transfer`, making them unable to receive funds via
     * `transfer`. {sendValue} removes this limitation.
     *
     * https://diligence.consensys.net/posts/2019/09/stop-using-soliditys-transfer-now/[Learn more].
     *
     * IMPORTANT: because control is transferred to `recipient`, care must be
     * taken to not create reentrancy vulnerabilities. Consider using
     * {ReentrancyGuard} or the
     * https://solidity.readthedocs.io/en/v0.5.11/security-considerations.html#use-the-checks-effects-interactions-pattern[checks-effects-interactions pattern].
     */
    function sendValue(address payable recipient, uint256 amount) internal {
        require(address(this).balance >= amount, "Address: insufficient balance");

        (bool success, ) = recipient.call{value: amount}("");
        require(success, "Address: unable to send value, recipient may have reverted");
    }

    /**
     * @dev Performs a Solidity function call using a low level `call`. A
     * plain `call` is an unsafe replacement for a function call: use this
     * function instead.
     *
     * If `target` reverts with a revert reason, it is bubbled up by this
     * function (like regular Solidity function calls).
     *
     * Returns the raw returned data. To convert to the expected return value,
     * use https://solidity.readthedocs.io/en/latest/units-and-global-variables.html?highlight=abi.decode#abi-encoding-and-decoding-functions[`abi.decode`].
     *
     * Requirements:
     *
     * - `target` must be a contract.
     * - calling `target` with `data` must not revert.
     *
     * _Available since v3.1._
     */
    function functionCall(address target, bytes memory data) internal returns (bytes memory) {
        return functionCall(target, data, "Address: low-level call failed");
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`], but with
     * `errorMessage` as a fallback revert reason when `target` reverts.
     *
     * _Available since v3.1._
     */
    function functionCall(
        address target,
        bytes memory data,
        string memory errorMessage
    ) internal returns (bytes memory) {
        return functionCallWithValue(target, data, 0, errorMessage);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but also transferring `value` wei to `target`.
     *
     * Requirements:
     *
     * - the calling contract must have an ETH balance of at least `value`.
     * - the called Solidity function must be `payable`.
     *
     * _Available since v3.1._
     */
    function functionCallWithValue(
        address target,
        bytes memory data,
        uint256 value
    ) internal returns (bytes memory) {
        return functionCallWithValue(target, data, value, "Address: low-level call with value failed");
    }

    /**
     * @dev Same as {xref-Address-functionCallWithValue-address-bytes-uint256-}[`functionCallWithValue`], but
     * with `errorMessage` as a fallback revert reason when `target` reverts.
     *
     * _Available since v3.1._
     */
    function functionCallWithValue(
        address target,
        bytes memory data,
        uint256 value,
        string memory errorMessage
    ) internal returns (bytes memory) {
        require(address(this).balance >= value, "Address: insufficient balance for call");
        require(isContract(target), "Address: call to non-contract");

        (bool success, bytes memory returndata) = target.call{value: value}(data);
        return verifyCallResult(success, returndata, errorMessage);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but performing a static call.
     *
     * _Available since v3.3._
     */
    function functionStaticCall(address target, bytes memory data) internal view returns (bytes memory) {
        return functionStaticCall(target, data, "Address: low-level static call failed");
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-string-}[`functionCall`],
     * but performing a static call.
     *
     * _Available since v3.3._
     */
    function functionStaticCall(
        address target,
        bytes memory data,
        string memory errorMessage
    ) internal view returns (bytes memory) {
        require(isContract(target), "Address: static call to non-contract");

        (bool success, bytes memory returndata) = target.staticcall(data);
        return verifyCallResult(success, returndata, errorMessage);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but performing a delegate call.
     *
     * _Available since v3.4._
     */
    function functionDelegateCall(address target, bytes memory data) internal returns (bytes memory) {
        return functionDelegateCall(target, data, "Address: low-level delegate call failed");
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-string-}[`functionCall`],
     * but performing a delegate call.
     *
     * _Available since v3.4._
     */
    function functionDelegateCall(
        address target,
        bytes memory data,
        string memory errorMessage
    ) internal returns (bytes memory) {
        require(isContract(target), "Address: delegate call to non-contract");

        (bool success, bytes memory returndata) = target.delegatecall(data);
        return verifyCallResult(success, returndata, errorMessage);
    }

    /**
     * @dev Tool to verifies that a low level call was successful, and revert if it wasn't, either by bubbling the
     * revert reason using the provided one.
     *
     * _Available since v4.3._
     */
    function verifyCallResult(
        bool success,
        bytes memory returndata,
        string memory errorMessage
    ) internal pure returns (bytes memory) {
        if (success) {
            return returndata;
        } else {
            // Look for revert reason and bubble it up if present
            if (returndata.length > 0) {
                // The easiest way to bubble the revert reason is using memory via assembly

                assembly {
                    let returndata_size := mload(returndata)
                    revert(add(32, returndata), returndata_size)
                }
            } else {
                revert(errorMessage);
            }
        }
    }
}


// File: @openzeppelin/contracts/utils/Context.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (utils/Context.sol)

pragma solidity ^0.8.0;

/**
 * @dev Provides information about the current execution context, including the
 * sender of the transaction and its data. While these are generally available
 * via msg.sender and msg.data, they should not be accessed in such a direct
 * manner, since when dealing with meta-transactions the account sending and
 * paying for execution may not be the actual sender (as far as an application
 * is concerned).
 *
 * This contract is only required for intermediate, library-like contracts.
 */
abstract contract Context {
    function _msgSender() internal view virtual returns (address) {
        return msg.sender;
    }

    function _msgData() internal view virtual returns (bytes calldata) {
        return msg.data;
    }
}


// File: @openzeppelin/contracts/utils/introspection/ERC165.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (utils/introspection/ERC165.sol)

pragma solidity ^0.8.0;

import "./IERC165.sol";

/**
 * @dev Implementation of the {IERC165} interface.
 *
 * Contracts that want to implement ERC165 should inherit from this contract and override {supportsInterface} to check
 * for the additional interface id that will be supported. For example:
 *
 * ```solidity
 * function supportsInterface(bytes4 interfaceId) public view virtual override returns (bool) {
 *     return interfaceId == type(MyInterface).interfaceId || super.supportsInterface(interfaceId);
 * }
 * ```
 *
 * Alternatively, {ERC165Storage} provides an easier to use but more expensive implementation.
 */
abstract contract ERC165 is IERC165 {
    /**
     * @dev See {IERC165-supportsInterface}.
     */
    function supportsInterface(bytes4 interfaceId) public view virtual override returns (bool) {
        return interfaceId == type(IERC165).interfaceId;
    }
}


// File: @openzeppelin/contracts/utils/introspection/IERC165.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (utils/introspection/IERC165.sol)

pragma solidity ^0.8.0;

/**
 * @dev Interface of the ERC165 standard, as defined in the
 * https://eips.ethereum.org/EIPS/eip-165[EIP].
 *
 * Implementers can declare support of contract interfaces, which can then be
 * queried by others ({ERC165Checker}).
 *
 * For an implementation, see {ERC165}.
 */
interface IERC165 {
    /**
     * @dev Returns true if this contract implements the interface defined by
     * `interfaceId`. See the corresponding
     * https://eips.ethereum.org/EIPS/eip-165#how-interfaces-are-identified[EIP section]
     * to learn more about how these ids are created.
     *
     * This function call must use less than 30 000 gas.
     */
    function supportsInterface(bytes4 interfaceId) external view returns (bool);
}


// File: @openzeppelin/contracts/token/ERC721/extensions/IERC721Enumerable.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (token/ERC721/extensions/IERC721Enumerable.sol)

pragma solidity ^0.8.0;

import "../IERC721.sol";

/**
 * @title ERC-721 Non-Fungible Token Standard, optional enumeration extension
 * @dev See https://eips.ethereum.org/EIPS/eip-721
 */
interface IERC721Enumerable is IERC721 {
    /**
     * @dev Returns the total amount of tokens stored by the contract.
     */
    function totalSupply() external view returns (uint256);

    /**
     * @dev Returns a token ID owned by `owner` at a given `index` of its token list.
     * Use along with {balanceOf} to enumerate all of ``owner``'s tokens.
     */
    function tokenOfOwnerByIndex(address owner, uint256 index) external view returns (uint256 tokenId);

    /**
     * @dev Returns a token ID at a given `index` of all the tokens stored by the contract.
     * Use along with {totalSupply} to enumerate all tokens.
     */
    function tokenByIndex(uint256 index) external view returns (uint256);
}


// File: @openzeppelin/contracts/token/ERC1155/ERC1155.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (token/ERC1155/ERC1155.sol)

pragma solidity ^0.8.0;

import "./IERC1155.sol";
import "./IERC1155Receiver.sol";
import "./extensions/IERC1155MetadataURI.sol";
import "../../utils/Address.sol";
import "../../utils/Context.sol";
import "../../utils/introspection/ERC165.sol";

/**
 * @dev Implementation of the basic standard multi-token.
 * See https://eips.ethereum.org/EIPS/eip-1155
 * Originally based on code by Enjin: https://github.com/enjin/erc-1155
 *
 * _Available since v3.1._
 */
contract ERC1155 is Context, ERC165, IERC1155, IERC1155MetadataURI {
    using Address for address;

    // Mapping from token ID to account balances
    mapping(uint256 => mapping(address => uint256)) private _balances;

    // Mapping from account to operator approvals
    mapping(address => mapping(address => bool)) private _operatorApprovals;

    // Used as the URI for all token types by relying on ID substitution, e.g. https://token-cdn-domain/{id}.json
    string private _uri;

    /**
     * @dev See {_setURI}.
     */
    constructor(string memory uri_) {
        _setURI(uri_);
    }

    /**
     * @dev See {IERC165-supportsInterface}.
     */
    function supportsInterface(bytes4 interfaceId) public view virtual override(ERC165, IERC165) returns (bool) {
        return
            interfaceId == type(IERC1155).interfaceId ||
            interfaceId == type(IERC1155MetadataURI).interfaceId ||
            super.supportsInterface(interfaceId);
    }

    /**
     * @dev See {IERC1155MetadataURI-uri}.
     *
     * This implementation returns the same URI for *all* token types. It relies
     * on the token type ID substitution mechanism
     * https://eips.ethereum.org/EIPS/eip-1155#metadata[defined in the EIP].
     *
     * Clients calling this function must replace the `\{id\}` substring with the
     * actual token type ID.
     */
    function uri(uint256) public view virtual override returns (string memory) {
        return _uri;
    }

    /**
     * @dev See {IERC1155-balanceOf}.
     *
     * Requirements:
     *
     * - `account` cannot be the zero address.
     */
    function balanceOf(address account, uint256 id) public view virtual override returns (uint256) {
        require(account != address(0), "ERC1155: balance query for the zero address");
        return _balances[id][account];
    }

    /**
     * @dev See {IERC1155-balanceOfBatch}.
     *
     * Requirements:
     *
     * - `accounts` and `ids` must have the same length.
     */
    function balanceOfBatch(address[] memory accounts, uint256[] memory ids)
        public
        view
        virtual
        override
        returns (uint256[] memory)
    {
        require(accounts.length == ids.length, "ERC1155: accounts and ids length mismatch");

        uint256[] memory batchBalances = new uint256[](accounts.length);

        for (uint256 i = 0; i < accounts.length; ++i) {
            batchBalances[i] = balanceOf(accounts[i], ids[i]);
        }

        return batchBalances;
    }

    /**
     * @dev See {IERC1155-setApprovalForAll}.
     */
    function setApprovalForAll(address operator, bool approved) public virtual override {
        _setApprovalForAll(_msgSender(), operator, approved);
    }

    /**
     * @dev See {IERC1155-isApprovedForAll}.
     */
    function isApprovedForAll(address account, address operator) public view virtual override returns (bool) {
        return _operatorApprovals[account][operator];
    }

    /**
     * @dev See {IERC1155-safeTransferFrom}.
     */
    function safeTransferFrom(
        address from,
        address to,
        uint256 id,
        uint256 amount,
        bytes memory data
    ) public virtual override {
        require(
            from == _msgSender() || isApprovedForAll(from, _msgSender()),
            "ERC1155: caller is not owner nor approved"
        );
        _safeTransferFrom(from, to, id, amount, data);
    }

    /**
     * @dev See {IERC1155-safeBatchTransferFrom}.
     */
    function safeBatchTransferFrom(
        address from,
        address to,
        uint256[] memory ids,
        uint256[] memory amounts,
        bytes memory data
    ) public virtual override {
        require(
            from == _msgSender() || isApprovedForAll(from, _msgSender()),
            "ERC1155: transfer caller is not owner nor approved"
        );
        _safeBatchTransferFrom(from, to, ids, amounts, data);
    }

    /**
     * @dev Transfers `amount` tokens of token type `id` from `from` to `to`.
     *
     * Emits a {TransferSingle} event.
     *
     * Requirements:
     *
     * - `to` cannot be the zero address.
     * - `from` must have a balance of tokens of type `id` of at least `amount`.
     * - If `to` refers to a smart contract, it must implement {IERC1155Receiver-onERC1155Received} and return the
     * acceptance magic value.
     */
    function _safeTransferFrom(
        address from,
        address to,
        uint256 id,
        uint256 amount,
        bytes memory data
    ) internal virtual {
        require(to != address(0), "ERC1155: transfer to the zero address");

        address operator = _msgSender();

        _beforeTokenTransfer(operator, from, to, _asSingletonArray(id), _asSingletonArray(amount), data);

        uint256 fromBalance = _balances[id][from];
        require(fromBalance >= amount, "ERC1155: insufficient balance for transfer");
        unchecked {
            _balances[id][from] = fromBalance - amount;
        }
        _balances[id][to] += amount;

        emit TransferSingle(operator, from, to, id, amount);

        _doSafeTransferAcceptanceCheck(operator, from, to, id, amount, data);
    }

    /**
     * @dev xref:ROOT:erc1155.adoc#batch-operations[Batched] version of {_safeTransferFrom}.
     *
     * Emits a {TransferBatch} event.
     *
     * Requirements:
     *
     * - If `to` refers to a smart contract, it must implement {IERC1155Receiver-onERC1155BatchReceived} and return the
     * acceptance magic value.
     */
    function _safeBatchTransferFrom(
        address from,
        address to,
        uint256[] memory ids,
        uint256[] memory amounts,
        bytes memory data
    ) internal virtual {
        require(ids.length == amounts.length, "ERC1155: ids and amounts length mismatch");
        require(to != address(0), "ERC1155: transfer to the zero address");

        address operator = _msgSender();

        _beforeTokenTransfer(operator, from, to, ids, amounts, data);

        for (uint256 i = 0; i < ids.length; ++i) {
            uint256 id = ids[i];
            uint256 amount = amounts[i];

            uint256 fromBalance = _balances[id][from];
            require(fromBalance >= amount, "ERC1155: insufficient balance for transfer");
            unchecked {
                _balances[id][from] = fromBalance - amount;
            }
            _balances[id][to] += amount;
        }

        emit TransferBatch(operator, from, to, ids, amounts);

        _doSafeBatchTransferAcceptanceCheck(operator, from, to, ids, amounts, data);
    }

    /**
     * @dev Sets a new URI for all token types, by relying on the token type ID
     * substitution mechanism
     * https://eips.ethereum.org/EIPS/eip-1155#metadata[defined in the EIP].
     *
     * By this mechanism, any occurrence of the `\{id\}` substring in either the
     * URI or any of the amounts in the JSON file at said URI will be replaced by
     * clients with the token type ID.
     *
     * For example, the `https://token-cdn-domain/\{id\}.json` URI would be
     * interpreted by clients as
     * `https://token-cdn-domain/000000000000000000000000000000000000000000000000000000000004cce0.json`
     * for token type ID 0x4cce0.
     *
     * See {uri}.
     *
     * Because these URIs cannot be meaningfully represented by the {URI} event,
     * this function emits no events.
     */
    function _setURI(string memory newuri) internal virtual {
        _uri = newuri;
    }

    /**
     * @dev Creates `amount` tokens of token type `id`, and assigns them to `to`.
     *
     * Emits a {TransferSingle} event.
     *
     * Requirements:
     *
     * - `to` cannot be the zero address.
     * - If `to` refers to a smart contract, it must implement {IERC1155Receiver-onERC1155Received} and return the
     * acceptance magic value.
     */
    function _mint(
        address to,
        uint256 id,
        uint256 amount,
        bytes memory data
    ) internal virtual {
        require(to != address(0), "ERC1155: mint to the zero address");

        address operator = _msgSender();

        _beforeTokenTransfer(operator, address(0), to, _asSingletonArray(id), _asSingletonArray(amount), data);

        _balances[id][to] += amount;
        emit TransferSingle(operator, address(0), to, id, amount);

        _doSafeTransferAcceptanceCheck(operator, address(0), to, id, amount, data);
    }

    /**
     * @dev xref:ROOT:erc1155.adoc#batch-operations[Batched] version of {_mint}.
     *
     * Requirements:
     *
     * - `ids` and `amounts` must have the same length.
     * - If `to` refers to a smart contract, it must implement {IERC1155Receiver-onERC1155BatchReceived} and return the
     * acceptance magic value.
     */
    function _mintBatch(
        address to,
        uint256[] memory ids,
        uint256[] memory amounts,
        bytes memory data
    ) internal virtual {
        require(to != address(0), "ERC1155: mint to the zero address");
        require(ids.length == amounts.length, "ERC1155: ids and amounts length mismatch");

        address operator = _msgSender();

        _beforeTokenTransfer(operator, address(0), to, ids, amounts, data);

        for (uint256 i = 0; i < ids.length; i++) {
            _balances[ids[i]][to] += amounts[i];
        }

        emit TransferBatch(operator, address(0), to, ids, amounts);

        _doSafeBatchTransferAcceptanceCheck(operator, address(0), to, ids, amounts, data);
    }

    /**
     * @dev Destroys `amount` tokens of token type `id` from `from`
     *
     * Requirements:
     *
     * - `from` cannot be the zero address.
     * - `from` must have at least `amount` tokens of token type `id`.
     */
    function _burn(
        address from,
        uint256 id,
        uint256 amount
    ) internal virtual {
        require(from != address(0), "ERC1155: burn from the zero address");

        address operator = _msgSender();

        _beforeTokenTransfer(operator, from, address(0), _asSingletonArray(id), _asSingletonArray(amount), "");

        uint256 fromBalance = _balances[id][from];
        require(fromBalance >= amount, "ERC1155: burn amount exceeds balance");
        unchecked {
            _balances[id][from] = fromBalance - amount;
        }

        emit TransferSingle(operator, from, address(0), id, amount);
    }

    /**
     * @dev xref:ROOT:erc1155.adoc#batch-operations[Batched] version of {_burn}.
     *
     * Requirements:
     *
     * - `ids` and `amounts` must have the same length.
     */
    function _burnBatch(
        address from,
        uint256[] memory ids,
        uint256[] memory amounts
    ) internal virtual {
        require(from != address(0), "ERC1155: burn from the zero address");
        require(ids.length == amounts.length, "ERC1155: ids and amounts length mismatch");

        address operator = _msgSender();

        _beforeTokenTransfer(operator, from, address(0), ids, amounts, "");

        for (uint256 i = 0; i < ids.length; i++) {
            uint256 id = ids[i];
            uint256 amount = amounts[i];

            uint256 fromBalance = _balances[id][from];
            require(fromBalance >= amount, "ERC1155: burn amount exceeds balance");
            unchecked {
                _balances[id][from] = fromBalance - amount;
            }
        }

        emit TransferBatch(operator, from, address(0), ids, amounts);
    }

    /**
     * @dev Approve `operator` to operate on all of `owner` tokens
     *
     * Emits a {ApprovalForAll} event.
     */
    function _setApprovalForAll(
        address owner,
        address operator,
        bool approved
    ) internal virtual {
        require(owner != operator, "ERC1155: setting approval status for self");
        _operatorApprovals[owner][operator] = approved;
        emit ApprovalForAll(owner, operator, approved);
    }

    /**
     * @dev Hook that is called before any token transfer. This includes minting
     * and burning, as well as batched variants.
     *
     * The same hook is called on both single and batched variants. For single
     * transfers, the length of the `id` and `amount` arrays will be 1.
     *
     * Calling conditions (for each `id` and `amount` pair):
     *
     * - When `from` and `to` are both non-zero, `amount` of ``from``'s tokens
     * of token type `id` will be  transferred to `to`.
     * - When `from` is zero, `amount` tokens of token type `id` will be minted
     * for `to`.
     * - when `to` is zero, `amount` of ``from``'s tokens of token type `id`
     * will be burned.
     * - `from` and `to` are never both zero.
     * - `ids` and `amounts` have the same, non-zero length.
     *
     * To learn more about hooks, head to xref:ROOT:extending-contracts.adoc#using-hooks[Using Hooks].
     */
    function _beforeTokenTransfer(
        address operator,
        address from,
        address to,
        uint256[] memory ids,
        uint256[] memory amounts,
        bytes memory data
    ) internal virtual {}

    function _doSafeTransferAcceptanceCheck(
        address operator,
        address from,
        address to,
        uint256 id,
        uint256 amount,
        bytes memory data
    ) private {
        if (to.isContract()) {
            try IERC1155Receiver(to).onERC1155Received(operator, from, id, amount, data) returns (bytes4 response) {
                if (response != IERC1155Receiver.onERC1155Received.selector) {
                    revert("ERC1155: ERC1155Receiver rejected tokens");
                }
            } catch Error(string memory reason) {
                revert(reason);
            } catch {
                revert("ERC1155: transfer to non ERC1155Receiver implementer");
            }
        }
    }

    function _doSafeBatchTransferAcceptanceCheck(
        address operator,
        address from,
        address to,
        uint256[] memory ids,
        uint256[] memory amounts,
        bytes memory data
    ) private {
        if (to.isContract()) {
            try IERC1155Receiver(to).onERC1155BatchReceived(operator, from, ids, amounts, data) returns (
                bytes4 response
            ) {
                if (response != IERC1155Receiver.onERC1155BatchReceived.selector) {
                    revert("ERC1155: ERC1155Receiver rejected tokens");
                }
            } catch Error(string memory reason) {
                revert(reason);
            } catch {
                revert("ERC1155: transfer to non ERC1155Receiver implementer");
            }
        }
    }

    function _asSingletonArray(uint256 element) private pure returns (uint256[] memory) {
        uint256[] memory array = new uint256[](1);
        array[0] = element;

        return array;
    }
}


// File: @openzeppelin/contracts/token/ERC1155/IERC1155.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (token/ERC1155/IERC1155.sol)

pragma solidity ^0.8.0;

import "../../utils/introspection/IERC165.sol";

/**
 * @dev Required interface of an ERC1155 compliant contract, as defined in the
 * https://eips.ethereum.org/EIPS/eip-1155[EIP].
 *
 * _Available since v3.1._
 */
interface IERC1155 is IERC165 {
    /**
     * @dev Emitted when `value` tokens of token type `id` are transferred from `from` to `to` by `operator`.
     */
    event TransferSingle(address indexed operator, address indexed from, address indexed to, uint256 id, uint256 value);

    /**
     * @dev Equivalent to multiple {TransferSingle} events, where `operator`, `from` and `to` are the same for all
     * transfers.
     */
    event TransferBatch(
        address indexed operator,
        address indexed from,
        address indexed to,
        uint256[] ids,
        uint256[] values
    );

    /**
     * @dev Emitted when `account` grants or revokes permission to `operator` to transfer their tokens, according to
     * `approved`.
     */
    event ApprovalForAll(address indexed account, address indexed operator, bool approved);

    /**
     * @dev Emitted when the URI for token type `id` changes to `value`, if it is a non-programmatic URI.
     *
     * If an {URI} event was emitted for `id`, the standard
     * https://eips.ethereum.org/EIPS/eip-1155#metadata-extensions[guarantees] that `value` will equal the value
     * returned by {IERC1155MetadataURI-uri}.
     */
    event URI(string value, uint256 indexed id);

    /**
     * @dev Returns the amount of tokens of token type `id` owned by `account`.
     *
     * Requirements:
     *
     * - `account` cannot be the zero address.
     */
    function balanceOf(address account, uint256 id) external view returns (uint256);

    /**
     * @dev xref:ROOT:erc1155.adoc#batch-operations[Batched] version of {balanceOf}.
     *
     * Requirements:
     *
     * - `accounts` and `ids` must have the same length.
     */
    function balanceOfBatch(address[] calldata accounts, uint256[] calldata ids)
        external
        view
        returns (uint256[] memory);

    /**
     * @dev Grants or revokes permission to `operator` to transfer the caller's tokens, according to `approved`,
     *
     * Emits an {ApprovalForAll} event.
     *
     * Requirements:
     *
     * - `operator` cannot be the caller.
     */
    function setApprovalForAll(address operator, bool approved) external;

    /**
     * @dev Returns true if `operator` is approved to transfer ``account``'s tokens.
     *
     * See {setApprovalForAll}.
     */
    function isApprovedForAll(address account, address operator) external view returns (bool);

    /**
     * @dev Transfers `amount` tokens of token type `id` from `from` to `to`.
     *
     * Emits a {TransferSingle} event.
     *
     * Requirements:
     *
     * - `to` cannot be the zero address.
     * - If the caller is not `from`, it must be have been approved to spend ``from``'s tokens via {setApprovalForAll}.
     * - `from` must have a balance of tokens of type `id` of at least `amount`.
     * - If `to` refers to a smart contract, it must implement {IERC1155Receiver-onERC1155Received} and return the
     * acceptance magic value.
     */
    function safeTransferFrom(
        address from,
        address to,
        uint256 id,
        uint256 amount,
        bytes calldata data
    ) external;

    /**
     * @dev xref:ROOT:erc1155.adoc#batch-operations[Batched] version of {safeTransferFrom}.
     *
     * Emits a {TransferBatch} event.
     *
     * Requirements:
     *
     * - `ids` and `amounts` must have the same length.
     * - If `to` refers to a smart contract, it must implement {IERC1155Receiver-onERC1155BatchReceived} and return the
     * acceptance magic value.
     */
    function safeBatchTransferFrom(
        address from,
        address to,
        uint256[] calldata ids,
        uint256[] calldata amounts,
        bytes calldata data
    ) external;
}


// File: @openzeppelin/contracts/token/ERC1155/IERC1155Receiver.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (token/ERC1155/IERC1155Receiver.sol)

pragma solidity ^0.8.0;

import "../../utils/introspection/IERC165.sol";

/**
 * @dev _Available since v3.1._
 */
interface IERC1155Receiver is IERC165 {
    /**
        @dev Handles the receipt of a single ERC1155 token type. This function is
        called at the end of a `safeTransferFrom` after the balance has been updated.
        To accept the transfer, this must return
        `bytes4(keccak256("onERC1155Received(address,address,uint256,uint256,bytes)"))`
        (i.e. 0xf23a6e61, or its own function selector).
        @param operator The address which initiated the transfer (i.e. msg.sender)
        @param from The address which previously owned the token
        @param id The ID of the token being transferred
        @param value The amount of tokens being transferred
        @param data Additional data with no specified format
        @return `bytes4(keccak256("onERC1155Received(address,address,uint256,uint256,bytes)"))` if transfer is allowed
    */
    function onERC1155Received(
        address operator,
        address from,
        uint256 id,
        uint256 value,
        bytes calldata data
    ) external returns (bytes4);

    /**
        @dev Handles the receipt of a multiple ERC1155 token types. This function
        is called at the end of a `safeBatchTransferFrom` after the balances have
        been updated. To accept the transfer(s), this must return
        `bytes4(keccak256("onERC1155BatchReceived(address,address,uint256[],uint256[],bytes)"))`
        (i.e. 0xbc197c81, or its own function selector).
        @param operator The address which initiated the batch transfer (i.e. msg.sender)
        @param from The address which previously owned the token
        @param ids An array containing ids of each token being transferred (order and length must match values array)
        @param values An array containing amounts of each token being transferred (order and length must match ids array)
        @param data Additional data with no specified format
        @return `bytes4(keccak256("onERC1155BatchReceived(address,address,uint256[],uint256[],bytes)"))` if transfer is allowed
    */
    function onERC1155BatchReceived(
        address operator,
        address from,
        uint256[] calldata ids,
        uint256[] calldata values,
        bytes calldata data
    ) external returns (bytes4);
}


// File: @openzeppelin/contracts/token/ERC1155/extensions/IERC1155MetadataURI.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (token/ERC1155/extensions/IERC1155MetadataURI.sol)

pragma solidity ^0.8.0;

import "../IERC1155.sol";

/**
 * @dev Interface of the optional ERC1155MetadataExtension interface, as defined
 * in the https://eips.ethereum.org/EIPS/eip-1155#metadata-extensions[EIP].
 *
 * _Available since v3.1._
 */
interface IERC1155MetadataURI is IERC1155 {
    /**
     * @dev Returns the URI for token type `id`.
     *
     * If the `\{id\}` substring is present in the URI, it must be replaced by
     * clients with the actual token type ID.
     */
    function uri(uint256 id) external view returns (string memory);
}

