
// File: contracts/lib/IERC20.sol
//SPDX-License-Identifier: MIT
pragma solidity 0.8.14;

interface IERC20 {

    function totalSupply() external view returns (uint256);
    
    function symbol() external view returns(string memory);
    
    function name() external view returns(string memory);

    /**
     * @dev Returns the amount of tokens owned by `account`.
     */
    function balanceOf(address account) external view returns (uint256);
    
    /**
     * @dev Returns the number of decimal places
     */
    function decimals() external view returns (uint8);

    /**
     * @dev Moves `amount` tokens from the caller's account to `recipient`.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * Emits a {Transfer} event.
     */
    function transfer(address recipient, uint256 amount) external returns (bool);

    /**
     * @dev Returns the remaining number of tokens that `spender` will be
     * allowed to spend on behalf of `owner` through {transferFrom}. This is
     * zero by default.
     *
     * This value changes when {approve} or {transferFrom} are called.
     */
    function allowance(address owner, address spender) external view returns (uint256);

    /**
     * @dev Sets `amount` as the allowance of `spender` over the caller's tokens.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * IMPORTANT: Beware that changing an allowance with this method brings the risk
     * that someone may use both the old and the new allowance by unfortunate
     * transaction ordering. One possible solution to mitigate this race
     * condition is to first reduce the spender's allowance to 0 and set the
     * desired value afterwards:
     * https://github.com/ethereum/EIPs/issues/20#issuecomment-263524729
     *
     * Emits an {Approval} event.
     */
    function approve(address spender, uint256 amount) external returns (bool);

    /**
     * @dev Moves `amount` tokens from `sender` to `recipient` using the
     * allowance mechanism. `amount` is then deducted from the caller's
     * allowance.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * Emits a {Transfer} event.
     */
    function transferFrom(address sender, address recipient, uint256 amount) external returns (bool);

    /**
     * @dev Emitted when `value` tokens are moved from one account (`from`) to
     * another (`to`).
     *
     * Note that `value` may be zero.
     */
    event Transfer(address indexed from, address indexed to, uint256 value);

    /**
     * @dev Emitted when the allowance of a `spender` for an `owner` is set by
     * a call to {approve}. `value` is the new allowance.
     */
    event Approval(address indexed owner, address indexed spender, uint256 value);
}

// File: contracts/lib/Ownable.sol
// SPDX-License-Identifier: GPL-3.0

pragma solidity 0.8.14;

/**
 * @title Owner
 * @dev Set & change owner
 */
contract Ownable {

    address private owner;
    
    // event for EVM logging
    event OwnerSet(address indexed oldOwner, address indexed newOwner);
    
    // modifier to check if caller is owner
    modifier onlyOwner() {
        // If the first argument of 'require' evaluates to 'false', execution terminates and all
        // changes to the state and to Ether balances are reverted.
        // This used to consume all gas in old EVM versions, but not anymore.
        // It is often a good idea to use 'require' to check if functions are called correctly.
        // As a second argument, you can also provide an explanation about what went wrong.
        require(msg.sender == owner, "Caller is not owner");
        _;
    }
    
    /**
     * @dev Set contract deployer as owner
     */
    constructor() {
        owner = msg.sender; // 'msg.sender' is sender of current call, contract deployer for a constructor
        emit OwnerSet(address(0), owner);
    }

    /**
     * @dev Change owner
     * @param newOwner address of new owner
     */
    function changeOwner(address newOwner) public onlyOwner {
        emit OwnerSet(owner, newOwner);
        owner = newOwner;
    }

    /**
     * @dev Return owner address 
     * @return address of owner
     */
    function getOwner() external view returns (address) {
        return owner;
    }
}

// File: contracts/lib/SafeMath.sol
//SPDX-License-Identifier: MIT
pragma solidity 0.8.14;

library SafeMath {
    /**
     * @dev Returns the addition of two unsigned integers, reverting on
     * overflow.
     *
     * Counterpart to Solidity's `+` operator.
     *
     * Requirements:
     *
     * - Addition cannot overflow.
     */
    function add(uint256 a, uint256 b) internal pure returns (uint256) {
        uint256 c = a + b;
        require(c >= a, "SafeMath: addition overflow");

        return c;
    }

    /**
     * @dev Returns the subtraction of two unsigned integers, reverting on
     * overflow (when the result is negative).
     *
     * Counterpart to Solidity's `-` operator.
     *
     * Requirements:
     *
     * - Subtraction cannot overflow.
     */
    function sub(uint256 a, uint256 b) internal pure returns (uint256) {
        return sub(a, b, "SafeMath: subtraction overflow");
    }

    /**
     * @dev Returns the subtraction of two unsigned integers, reverting with custom message on
     * overflow (when the result is negative).
     *
     * Counterpart to Solidity's `-` operator.
     *
     * Requirements:
     *
     * - Subtraction cannot overflow.
     */
    function sub(uint256 a, uint256 b, string memory errorMessage) internal pure returns (uint256) {
        require(b <= a, errorMessage);
        uint256 c = a - b;

        return c;
    }

    /**
     * @dev Returns the multiplication of two unsigned integers, reverting on
     * overflow.
     *
     * Counterpart to Solidity's `*` operator.
     *
     * Requirements:
     *
     * - Multiplication cannot overflow.
     */
    function mul(uint256 a, uint256 b) internal pure returns (uint256) {
        // Gas optimization: this is cheaper than requiring 'a' not being zero, but the
        // benefit is lost if 'b' is also tested.
        // See: https://github.com/OpenZeppelin/openzeppelin-contracts/pull/522
        if (a == 0) {
            return 0;
        }

        uint256 c = a * b;
        require(c / a == b, "SafeMath: multiplication overflow");

        return c;
    }

    /**
     * @dev Returns the integer division of two unsigned integers. Reverts on
     * division by zero. The result is rounded towards zero.
     *
     * Counterpart to Solidity's `/` operator. Note: this function uses a
     * `revert` opcode (which leaves remaining gas untouched) while Solidity
     * uses an invalid opcode to revert (consuming all remaining gas).
     *
     * Requirements:
     *
     * - The divisor cannot be zero.
     */
    function div(uint256 a, uint256 b) internal pure returns (uint256) {
        return div(a, b, "SafeMath: division by zero");
    }

    /**
     * @dev Returns the integer division of two unsigned integers. Reverts with custom message on
     * division by zero. The result is rounded towards zero.
     *
     * Counterpart to Solidity's `/` operator. Note: this function uses a
     * `revert` opcode (which leaves remaining gas untouched) while Solidity
     * uses an invalid opcode to revert (consuming all remaining gas).
     *
     * Requirements:
     *
     * - The divisor cannot be zero.
     */
    function div(uint256 a, uint256 b, string memory errorMessage) internal pure returns (uint256) {
        require(b > 0, errorMessage);
        uint256 c = a / b;
        // assert(a == b * c + a % b); // There is no case in which this doesn't hold

        return c;
    }

    /**
     * @dev Returns the remainder of dividing two unsigned integers. (unsigned integer modulo),
     * Reverts when dividing by zero.
     *
     * Counterpart to Solidity's `%` operator. This function uses a `revert`
     * opcode (which leaves remaining gas untouched) while Solidity uses an
     * invalid opcode to revert (consuming all remaining gas).
     *
     * Requirements:
     *
     * - The divisor cannot be zero.
     */
    function mod(uint256 a, uint256 b) internal pure returns (uint256) {
        return mod(a, b, "SafeMath: modulo by zero");
    }

    /**
     * @dev Returns the remainder of dividing two unsigned integers. (unsigned integer modulo),
     * Reverts with custom message when dividing by zero.
     *
     * Counterpart to Solidity's `%` operator. This function uses a `revert`
     * opcode (which leaves remaining gas untouched) while Solidity uses an
     * invalid opcode to revert (consuming all remaining gas).
     *
     * Requirements:
     *
     * - The divisor cannot be zero.
     */
    function mod(uint256 a, uint256 b, string memory errorMessage) internal pure returns (uint256) {
        require(b != 0, errorMessage);
        return a % b;
    }
}

// File: contracts/Staking/MaxiStaking.sol
//SPDX-License-Identifier: MIT
pragma solidity 0.8.14;

import "../lib/SafeMath.sol";
import "../lib/Ownable.sol";
import "../lib/IERC20.sol";

contract FLMCStaking is Ownable, IERC20 {

    using SafeMath for uint256;

    // Staking Token
    IERC20 public immutable token;

    // Staking Protocol Token Info
    string private _name;
    string private _symbol;
    uint8 private immutable _decimals;

    // Trackable User Info
    struct UserInfo {
        uint256 balance;
        uint256 totalStaked;
        uint256 totalWithdrawn;
    }
    // User -> UserInfo
    mapping ( address => UserInfo ) public userInfo;

    // Timer Information
    uint256 public lockTime;
    uint256 public lockStartTime;

    // Max Amount of tokens needed in contract for lock to begin
    uint256 public Max_Amount_Before_Lock_Begins;

    // Minimum stake amount
    uint256 public minimumStakeAmount;

    // total number of tokens deposited, regardless of rewards
    uint256 public totalDeposited;

    // total supply of MAXI
    uint256 private _totalSupply;

    // precision factor
    uint256 private constant precision = 10**18;

    // Reentrancy Guard
    uint256 private constant _NOT_ENTERED = 1;
    uint256 private constant _ENTERED = 2;
    uint256 private _status;
    modifier nonReentrant() {
        require(_status != _ENTERED, "Reentrancy Guard call");
        _status = _ENTERED;
        _;
        _status = _NOT_ENTERED;
    }

    // Events
    event Deposit(address depositor, uint256 amountToken);
    event Withdraw(address withdrawer, uint256 amountToken);
    event FeeTaken(uint256 fee);

    constructor(
        address _token, 
        string memory name_, 
        string memory symbol_,
        uint256 _lockTime,
        uint256 _Max_Amount_Before_Lock_Begins,
        uint256 _minimumStakeAmount
    ) {

        require(_token != address(0), 'Zero Address');
        require(_lockTime <= 10**8, 'Fee Timer Too Long');

        // pair token data
        _name = name_;
        _symbol = symbol_;
        _decimals = IERC20(_token).decimals();

        // staking lock time
        lockTime = _lockTime;
        Max_Amount_Before_Lock_Begins = _Max_Amount_Before_Lock_Begins;
        minimumStakeAmount = _minimumStakeAmount;

        // pair staking token
        token = IERC20(_token);

        // set reentrancy
        _status = _NOT_ENTERED;
        
        // emit transfer so bscscan registers contract as token
        emit Transfer(address(0), msg.sender, 0);
    }

    /**
        name of Staking Pool's token representation
    */
    function name() external view override returns (string memory) {
        return _name;
    }

    /**
        symbol of Staking Pool's token representation
    */
    function symbol() external view override returns (string memory) {
        return _symbol;
    }

    /**
        decimals for Staking Pool's token representation
        Should always match underlying token
    */
    function decimals() external view override returns (uint8) {
        return _decimals;
    }

    /**
        total supply of Staking Pool's token representation
        Should match the current supply of `token` in this contract
        Since that is the correct sum of balanceOf([...allHolders])
    */
    function totalSupply() external view override returns (uint256) {
        return token.balanceOf(address(this));
    }

    /** Shows The Value Of Users' Staked Token */
    function balanceOf(address account) public view override returns (uint256) {
        return ReflectionsFromContractBalance(userInfo[account].balance);
    }

    /**
        This token is not transferrable, this function exists to properly inherit IERC20
    */
    function transfer(address, uint256) external pure override returns (bool) {
        return true;
    }
    function transferFrom(address, address, uint256) external pure override returns (bool) {
        return true;
    }

    /**
        Sets the lock time, evaluated in seconds
     */
    function setLockTime(uint256 newLockTime) external onlyOwner {
        require(
            newLockTime <= 10**8,
            'Lock Time Too High'
        );
        lockTime = newLockTime;
    }

    /**
        Sets the maximum amount of tokens staked before the lock time begins
     */
    function setMaxAmountBeforeLockBegins(uint newMaxAmount) external onlyOwner {
        require(newMaxAmount > 0, 'New Max Cannot Be Zero');
        Max_Amount_Before_Lock_Begins = newMaxAmount;
    }

    /**
        Sets the minimum stake amount per user
        Can be between 0 if no minimum is enforced
     */
    function setMinimumStakeAmount(uint minAmount) external onlyOwner {
        minimumStakeAmount = minAmount;
    }

    /**
        After the 5 day grace period, owner can call reLock, to roll over all the users that have not unstaked their tokens
        Allowing them to keep earning.
        During this 5 day period, no new users can stake, only existing users can choose to stay if they want to
     */
    function reLock() external onlyOwner {
        require(
            block.timestamp >= (lockStartTime + lockTime + 5 days),
            'Too Soon To Relock'
        );
        _lock();
    }

    /**
        Withdraws any BNB sent to contract
     */
    function withdrawBNB() external onlyOwner {
        (bool s,) = payable(msg.sender).call{value: address(this).balance}("");
        require(s, 'Error On BNB Withdrawal');
    }

    /**
        Recover any token that is not FLMC from the contract
     */
    function recoverForeignToken(IERC20 _token) external onlyOwner {
        require(
            address(_token) != address(token),
            'Cannot Withdraw Staking Tokens'
        );
        require(
            _token.transfer(msg.sender, _token.balanceOf(address(this))),
            'Error Withdrawing Foreign Token'
        );
    }

    receive() external payable {}

    /**
        Transfers in `amount` of Token From Sender
        And Locks In Contract, Minting MAXI Tokens
     */
    function deposit(uint256 amount) external nonReentrant {

        // ensure locking has not begun
        require(
            lockStartTime == 0,
            'No More Depositing'
        );

        // check this here, so if there is an wei left over preventing the pool from beginning, the user can fill the remainder 
        require(
            amount >= minimumStakeAmount,
            'Minimum Stake Amount Not Met'
        );

        // fetch the amount of tokens needed before lock begins
        uint256 nTokensRemaining = numTokensUntilLockBegins();

        // if the number of tokens remaining is less than supplied amount, only transfer in what is needed
        if (nTokensRemaining < amount) {
            amount = nTokensRemaining;
        }

        // update total deposited
        unchecked {
            totalDeposited += amount;
        }

        // Track Balance Before Deposit
        uint previousBalance = token.balanceOf(address(this));

        // Transfer In Token
        uint received = _transferIn(amount);

        // register internal math
        if (_totalSupply == 0 || previousBalance == 0 || totalDeposited == 0) {
            _registerFirstPurchase(received);
        } else {
            _mintTo(msg.sender, received, previousBalance);
        }

        // lock the contract if the total deposited hits desired threshold
        if (totalDeposited >= Max_Amount_Before_Lock_Begins) {
            _lock();
        }
    }
    
    /**
        Redeems all available tokens for user
     */
    function withdrawAll() external returns (uint256) {
        return withdraw(balanceOf(msg.sender));
    }

    /**
        Redeems `amount` of Underlying Tokens, As Seen From BalanceOf()
     */
    function withdraw(uint256 amount) public nonReentrant returns (uint256) {

        // ensure the remaining lock time is zero
        require(
            remainingLockTime() == 0,
            'Not Time To Unlock'
        );

        // Token Amount Into Contract Balance Amount
        uint MAXI_Amount = amount == balanceOf(msg.sender) ? userInfo[msg.sender].balance : TokenToContractBalance(amount);

        require(
            userInfo[msg.sender].balance > 0 &&
            userInfo[msg.sender].balance >= MAXI_Amount &&
            balanceOf(msg.sender) >= amount &&
            amount > 0 &&
            MAXI_Amount > 0,
            'Insufficient Funds'
        );

        // burn MAXI Tokens From Sender
        _burn(msg.sender, MAXI_Amount, amount);

        // increment total withdrawn
        unchecked {
            userInfo[msg.sender].totalWithdrawn += amount;
        }

        // send amount less fee
        uint256 balance = token.balanceOf(address(this));
        if (amount > balance) {
            amount = balance;
        }
        
        // transfer token to sender
        require(
            token.transfer(msg.sender, amount),
            'Error On Token Transfer'
        );

        emit Withdraw(msg.sender, amount);
        return amount;
    }


    /**
        Starts the timer for the user lock
     */
    function _lock() internal {
        lockStartTime = block.timestamp;
    }

    /**
        Registers the First Stake
     */
    function _registerFirstPurchase(uint received) internal {
        
        // increment total staked
        unchecked {
            userInfo[msg.sender].totalStaked += received;
        }

        // mint MAXI Tokens To Sender
        _mint(msg.sender, received, received);

        emit Deposit(msg.sender, received);
    }

    /**
        Handles the minting of MAXI tokens to sender
     */
    function _mintTo(address sender, uint256 received, uint256 previousBalance) internal {
        // Number Of Maxi Tokens To Mint
        uint nToMint = (_totalSupply * received) / previousBalance;
        require(
            nToMint > 0,
            'Zero To Mint'
        );

        // increment total staked
        unchecked {
            userInfo[sender].totalStaked += received;
        }
        
        // mint MAXI Tokens To Sender
        _mint(sender, nToMint, received);

        emit Deposit(sender, received);
    }

    /**
        Transfers in `amount` of `token`, returning the amount received
        The redundancy is to ensure tokens with transfer fees are accounted for
     */
    function _transferIn(uint256 amount) internal returns (uint256) {
        uint before = token.balanceOf(address(this));
        require(
            token.transferFrom(msg.sender, address(this), amount),
            'Failure On TransferFrom'
        );
        uint After = token.balanceOf(address(this));
        require(
            After > before,
            'Error On Transfer In'
        );
        return After - before;
    }

    /**
     * Burns `amount` of Contract Balance Token
     */
    function _burn(address from, uint256 amount, uint256 amountToken) private {
        userInfo[from].balance = userInfo[from].balance.sub(amount);
        _totalSupply = _totalSupply.sub(amount);
        emit Transfer(from, address(0), amountToken);
    }

    /**
     * Mints `amount` of Contract Balance Token
     */
    function _mint(address to, uint256 amount, uint256 stablesWorth) private {
        // allocate
        userInfo[to].balance = userInfo[to].balance.add(amount);
        _totalSupply = _totalSupply.add(amount);
        emit Transfer(address(0), to, stablesWorth);
    }


    /**
        Converts A Staking Token Amount Into A MAXI Amount
     */
    function TokenToContractBalance(uint256 amount) public view returns (uint256) {
        return amount.mul(precision).div(_getConversionRate());
    }

    /**
        Converts A MAXI Amount Into An Token Amount
     */
    function ReflectionsFromContractBalance(uint256 amount) public view returns (uint256) {
        return amount.mul(_getConversionRate()).div(precision);
    }

    /**
        Calculates the number of tokens allowed before staking is filled and the lock begins
     */
    function numTokensUntilLockBegins() public view returns (uint256) {
        return totalDeposited >= Max_Amount_Before_Lock_Begins ? 0 : Max_Amount_Before_Lock_Begins - totalDeposited;
    }

    /** Conversion Ratio For MAXI -> Token */
    function getConversionRate() external view returns (uint256) {
        return _getConversionRate();
    }

    /**
        Lock Time Remaining
     */
    function remainingLockTime() public view returns (uint256) {
        return unlocksAt() < block.timestamp ? 0 : unlocksAt() - block.timestamp;
    }

    /**
        Unix Timestamp at which staking unlocks
     */
    function unlocksAt() public view returns (uint256) {
        return lockStartTime + lockTime;
    }

    /** Returns Total Profit for User In Token From MAXI */
    function getTotalProfits(address user) external view returns (uint256) {
        uint top = balanceOf(user) + userInfo[user].totalWithdrawn;
        return top <= userInfo[user].totalStaked ? 0 : top - userInfo[user].totalStaked;
    }
    
    /** Conversion Ratio For MAXI -> Token */
    function _getConversionRate() internal view returns (uint256) {
        if (_totalSupply == 0) {
            return precision;
        }
        uint256 backingValue = token.balanceOf(address(this));
        return (backingValue.mul(precision)).div(_totalSupply);
    }

    /** function has no use in contract */
    function allowance(address, address) external pure override returns (uint256) { 
        return 0;
    }
    /** function has no use in contract */
    function approve(address spender, uint256) public override returns (bool) {
        emit Approval(msg.sender, spender, 0);
        return true;
    }
}
