/**
 *Submitted for verification at BscScan.com on 2023-02-28
*/

/**
 *Submitted for verification at BscScan.com on 2023-01-19
*/

/*
 * Written by Sam, Guru Network.
 * (C) Sam, 543#3017
 * (C) FTM.Guru
 * (C) Guru Network
 * (C) Eliteness.Network
 * For help or queries, visit us on https://discord.gg/QpyfMarNrV.
 * Based on FTM.Guru/LF, Curve.fi, Synthetix.io, & Yearn.finance.
 *
 * * * * * * * * * * * * * * * * * * * * * * * * * * * *
 * * * * * Guru Network: Multi-Reward Farmland * * * * *
 * * * * * * * * * * * * * * * * * * * * * * * * * * * *
 *
 *
 *
 */
pragma solidity 0.8.9;
///pragma experimental ABIEncoderV2;

//ftm.guru's Universal On-chain TVL Calculator
//Source: https://ftm.guru/rawdata/tvl
interface ITVL {
	//Using Version = v7
	function p_lpt_coin_usd(address lp) external view returns(uint256);
	function p_lpt_usd(address u,address lp) external view returns(uint256);
	function p_t_coin_usd(address lp) external view returns(uint256);
}

interface IGauge {
	function depositAll() external;
	function deposit(uint256 amount) external;
	function withdrawAll() external;
	function withdraw(uint256 amount) external;
	function emergencyWithdraw() external;
	function emergencyWithdrawAmount(uint256 _amount) external;
	function withdrawAllAndHarvest() external;
	function getReward(address _user) external;
	function getReward() external;
	function claimFees() external;
	function earned(address account) external view returns (uint256);
	function balanceOf(address account) external view returns (uint256);
}

library Address {
	function isContract(address account) internal view returns (bool) {
		// This method relies in extcodesize, which returns 0 for contracts in
		// construction, since the code is only stored at the end of the
		// constructor execution.

		uint256 size;
		// solhint-disable-next-line no-inline-assembly
		assembly { size := extcodesize(account) }
		return size > 0;
	}
}

interface IERC20 {
	/**
	 * @dev Returns the amount of tokens in existence.
	 */
	function totalSupply() external view returns (uint256);

	/**
	 * @dev Returns the amount of tokens owned by `account`.
	 */
	function balanceOf(address account) external view returns (uint256);

	/**
	 * @dev Moves `amount` tokens from the caller's account to `recipient`.
	 *
	 * Returns a boolean value indicating whether the operation succeeded.
	 *
	 * Emits a `Transfer` event.
	 */
	function transfer(address recipient, uint256 amount) external returns (bool);

	/**
	 * @dev Returns the remaining number of tokens that `spender` will be
	 * allowed to spend on behalf of `owner` through `transferFrom`. This is
	 * zero by default.
	 *
	 * This value changes when `approve` or `transferFrom` are called.
	 */
	function allowance(address owner, address spender) external view returns (uint256);

	/**
	 * @dev Sets `amount` as the allowance of `spender` over the caller's tokens.
	 *
	 * Returns a boolean value indicating whether the operation succeeded.
	 *
	 * > Beware that changing an allowance with this method brings the risk
	 * that someone may use both the old and the new allowance by unfortunate
	 * transaction ordering. One possible solution to mitigate this race
	 * condition is to first reduce the spender's allowance to 0 and set the
	 * desired value afterwards:
	 * https://github.com/ethereum/EIPs/issues/20#issuecomment-263524729
	 *
	 * Emits an `Approval` event.
	 */
	function approve(address spender, uint256 amount) external returns (bool);

	/**
	 * @dev Moves `amount` tokens from `sender` to `recipient` using the
	 * allowance mechanism. `amount` is then deducted from the caller's
	 * allowance.
	 *
	 * Returns a boolean value indicating whether the operation succeeded.
	 *
	 * Emits a `Transfer` event.
	 */
	function transferFrom(address sender, address recipient, uint256 amount) external returns (bool);

	/**
	 * @dev Emitted when `value` tokens are moved from one account (`from`) to
	 * another (`to`).
	 *
	 * Note that `value` may be zero.
	 */
	event Transfer(address indexed from, address indexed to, uint256 value);

	/**
	 * @dev Emitted when the allowance of a `spender` for an `owner` is set by
	 * a call to `approve`. `value` is the new allowance.
	 */
	event Approval(address indexed owner, address indexed spender, uint256 value);
}


library Math {
	/**
	 * @dev Returns the largest of two numbers.
	 */
	function max(uint256 a, uint256 b) internal pure returns (uint256) {
		return a >= b ? a : b;
	}

	/**
	 * @dev Returns the smallest of two numbers.
	 */
	function min(uint256 a, uint256 b) internal pure returns (uint256) {
		return a < b ? a : b;
	}

	/**
	 * @dev Returns the average of two numbers. The result is rounded towards
	 * zero.
	 */
	function average(uint256 a, uint256 b) internal pure returns (uint256) {
		// (a + b) / 2 can overflow, so we distribute
		return (a / 2) + (b / 2) + ((a % 2 + b % 2) / 2);
	}
}

contract Owned {
	address public owner;
	address public nominatedOwner;

	constructor(address _owner) {
		require(_owner != address(0), "Owner address cannot be 0");
		owner = _owner;
		emit OwnerChanged(address(0), _owner);
	}

	function nominateNewOwner(address _owner) external onlyOwner {
		nominatedOwner = _owner;
		emit OwnerNominated(_owner);
	}

	function acceptOwnership() external {
		require(msg.sender == nominatedOwner, "You must be nominated before you can accept ownership");
		emit OwnerChanged(owner, nominatedOwner);
		owner = nominatedOwner;
		nominatedOwner = address(0);
	}

	modifier onlyOwner {
		_onlyOwner();
		_;
	}

	function _onlyOwner() private view {
		require(msg.sender == owner, "Only the contract owner may perform this action");
	}

	event OwnerNominated(address newOwner);
	event OwnerChanged(address oldOwner, address newOwner);

	uint public lastPauseTime;
	bool public paused;


	/**
	 * @notice Change the paused state of the contract
	 * @dev Only the contract owner may call this.
	 */
	function setPaused(bool _paused) external onlyOwner {
		// Ensure we're actually changing the state before we do anything
		if (_paused == paused) {
			return;
		}

		// Set our paused state.
		paused = _paused;

		// If applicable, set the last pause time.
		if (paused) {
			lastPauseTime = block.timestamp;
		}

		// Let everyone know that our pause state has changed.
		emit PauseChanged(paused);
	}

	event PauseChanged(bool isPaused);

	modifier notPaused {
		require(!paused, "This action cannot be performed while the contract is paused");
		_;
	}
}

contract ReentrancyGuard {
	/// @dev counter to allow mutex lock with only one SSTORE operation
	uint256 private _guardCounter;

	constructor () {
		// The counter starts at one to prevent changing it from zero to a non-zero
		// value, which is a more expensive operation.
		_guardCounter = 1;
	}

	/**
	 * @dev Prevents a contract from calling itself, directly or indirectly.
	 * Calling a `nonReentrant` function from another `nonReentrant`
	 * function is not supported. It is possible to prevent this from happening
	 * by making the `nonReentrant` function external, and make it call a
	 * `private` function that does the actual work.
	 */
	modifier nonReentrant() {
		_guardCounter += 1;
		uint256 localCounter = _guardCounter;
		_;
		require(localCounter == _guardCounter, "ReentrancyGuard: reentrant call");
	}
}


library SafeERC20 {
	using SafeMath for uint256;
	using Address for address;

	function safeTransfer(IERC20 token, address to, uint256 value) internal {
		callOptionalReturn(token, abi.encodeWithSelector(token.transfer.selector, to, value));
	}

	function safeTransferFrom(IERC20 token, address from, address to, uint256 value) internal {
		callOptionalReturn(token, abi.encodeWithSelector(token.transferFrom.selector, from, to, value));
	}

	function safeApprove(IERC20 token, address spender, uint256 value) internal {
		// safeApprove should only be called when setting an initial allowance,
		// or when resetting it to zero. To increase and decrease it, use
		// 'safeIncreaseAllowance' and 'safeDecreaseAllowance'
		// solhint-disable-next-line max-line-length
		require((value == 0) || (token.allowance(address(this), spender) == 0),
			"SafeERC20: approve from non-zero to non-zero allowance"
		);
		callOptionalReturn(token, abi.encodeWithSelector(token.approve.selector, spender, value));
	}

	function safeIncreaseAllowance(IERC20 token, address spender, uint256 value) internal {
		uint256 newAllowance = token.allowance(address(this), spender).add(value);
		callOptionalReturn(token, abi.encodeWithSelector(token.approve.selector, spender, newAllowance));
	}

	function safeDecreaseAllowance(IERC20 token, address spender, uint256 value) internal {
		uint256 newAllowance = token.allowance(address(this), spender).sub(value);
		callOptionalReturn(token, abi.encodeWithSelector(token.approve.selector, spender, newAllowance));
	}

	/**
	 * @dev Imitates a Solidity high-level call (i.e. a regular function call to a contract), relaxing the requirement
	 * on the return value: the return value is optional (but if data is returned, it must not be false).
	 * @param token The token targeted by the call.
	 * @param data The call data (encoded using abi.encode or one of its variants).
	 */
	function callOptionalReturn(IERC20 token, bytes memory data) private {
		// We need to perform a low level call here, to bypass Solidity's return data size checking mechanism, since
		// we're implementing it ourselves.

		// A Solidity high level call has three parts:
		//  1. The target address is checked to verify it contains contract code
		//  2. The call itself is made, and success asserted
		//  3. The return value is decoded, which in turn checks the size of the returned data.
		// solhint-disable-next-line max-line-length
		require(address(token).isContract(), "SafeERC20: call to non-contract");

		// solhint-disable-next-line avoid-low-level-calls
		(bool success, bytes memory returndata) = address(token).call(data);
		require(success, "SafeERC20: low-level call failed");

		if (returndata.length > 0) { // Return data is optional
			// solhint-disable-next-line max-line-length
			require(abi.decode(returndata, (bool)), "SafeERC20: ERC20 operation did not succeed");
		}
	}
}

library SafeMath {
	/**
	 * @dev Returns the addition of two unsigned integers, reverting on
	 * overflow.
	 *
	 * Counterpart to Solidity's `+` operator.
	 *
	 * Requirements:
	 * - Addition cannot overflow.
	 */
	function add(uint256 a, uint256 b) internal pure returns (uint256) {
		uint256 c = a + b;
		require(c >= a, "SafeMath: addition overflow");

		return c;
	}

	/**
	 * @dev Returns the subtraction of two unsigned integers, reverting on
	 * overflow (when the result is negative).
	 *
	 * Counterpart to Solidity's `-` operator.
	 *
	 * Requirements:
	 * - Subtraction cannot overflow.
	 */
	function sub(uint256 a, uint256 b) internal pure returns (uint256) {
		require(b <= a, "SafeMath: subtraction overflow");
		uint256 c = a - b;

		return c;
	}

	/**
	 * @dev Returns the multiplication of two unsigned integers, reverting on
	 * overflow.
	 *
	 * Counterpart to Solidity's `*` operator.
	 *
	 * Requirements:
	 * - Multiplication cannot overflow.
	 */
	function mul(uint256 a, uint256 b) internal pure returns (uint256) {
		// Gas optimization: this is cheaper than requiring 'a' not being zero, but the
		// benefit is lost if 'b' is also tested.
		// See: https://github.com/OpenZeppelin/openzeppelin-solidity/pull/522
		if (a == 0) {
			return 0;
		}

		uint256 c = a * b;
		require(c / a == b, "SafeMath: multiplication overflow");

		return c;
	}

	/**
	 * @dev Returns the integer division of two unsigned integers. Reverts on
	 * division by zero. The result is rounded towards zero.
	 *
	 * Counterpart to Solidity's `/` operator. Note: this function uses a
	 * `revert` opcode (which leaves remaining gas untouched) while Solidity
	 * uses an invalid opcode to revert (consuming all remaining gas).
	 *
	 * Requirements:
	 * - The divisor cannot be zero.
	 */
	function div(uint256 a, uint256 b) internal pure returns (uint256) {
		// Solidity only automatically asserts when dividing by 0
		require(b > 0, "SafeMath: division by zero");
		uint256 c = a / b;
		// assert(a == b * c + a % b); // There is no case in which this doesn't hold

		return c;
	}

	/**
	 * @dev Returns the remainder of dividing two unsigned integers. (unsigned integer modulo),
	 * Reverts when dividing by zero.
	 *
	 * Counterpart to Solidity's `%` operator. This function uses a `revert`
	 * opcode (which leaves remaining gas untouched) while Solidity uses an
	 * invalid opcode to revert (consuming all remaining gas).
	 *
	 * Requirements:
	 * - The divisor cannot be zero.
	 */
	function mod(uint256 a, uint256 b) internal pure returns (uint256) {
		require(b != 0, "SafeMath: modulo by zero");
		return a % b;
	}
}

contract GuruMultiRewardFarmland is ReentrancyGuard, Owned {
	using SafeMath for uint256;
	using SafeERC20 for IERC20;

	/* ========== STATE VARIABLES ========== */

	struct Reward {
		address rewardsDistributor;
		uint256 rewardsDuration;
		uint256 periodFinish;
		uint256 rewardRate;
		uint256 lastUpdateTime;
		uint256 rewardPerTokenStored;
	}
	IERC20 public stakingToken;
	IGauge public gauge;
	mapping(address => Reward) public rewardData;
	address[] public rewardTokens;
	address[] public gaugeRewardTokens;

	// user -> reward token -> amount
	mapping(address => mapping(address => uint256)) public userRewardPerTokenPaid;
	mapping(address => mapping(address => uint256)) public rewards;

	bool public emergency;
	address public feeTaker;
	uint256 public feePerMillion;

	address public TvlGuru;
	mapping(address => bytes) public TvlPriceFeed;

	uint256 private _totalSupply;
	mapping(address => uint256) private _balances;
	mapping(address => mapping(address => uint256)) private _earnings;

	/* ========== CONSTRUCTOR ========== */

	constructor(
		address _stakingToken,
		address _gauge,
		address[] memory _gaugeRewardTokens,
		address _tg,
		bytes memory _tf
	) public Owned(msg.sender) {
		gauge = IGauge(_gauge);
		stakingToken = IERC20(_stakingToken);
		for(uint i; i<_gaugeRewardTokens.length;i++) {
			gaugeRewardTokens.push(_gaugeRewardTokens[i]);
		}
		TvlGuru = _tg;
		TvlPriceFeed[_stakingToken] = _tf;
		stakingToken.approve(_gauge,type(uint256).max);
	}

	function addReward(
		address _rewardsToken,
		address _rewardsDistributor,
		uint256 _rewardsDuration,
		bytes memory _TvlPriceFeed
	)
		public
		onlyOwner
	{
		require(rewardData[_rewardsToken].rewardsDuration == 0);
		rewardTokens.push(_rewardsToken);
		rewardData[_rewardsToken].rewardsDistributor = _rewardsDistributor;
		rewardData[_rewardsToken].rewardsDuration = _rewardsDuration;
		TvlPriceFeed[_rewardsToken] = _TvlPriceFeed;
	}

	/* ========== VIEWS ========== */

	function totalSupply() external view returns (uint256) {
		return _totalSupply;
	}

	function balanceOf(address account) external view returns (uint256) {
		return _balances[account];
	}

	function lastTimeRewardApplicable(address _rewardsToken) public view returns (uint256) {
		return Math.min(block.timestamp, rewardData[_rewardsToken].periodFinish);
	}

	function rewardPerToken(address _rewardsToken) public view returns (uint256) {
		if (_totalSupply == 0) {
			return rewardData[_rewardsToken].rewardPerTokenStored;
		}
		return
			rewardData[_rewardsToken].rewardPerTokenStored.add(
				lastTimeRewardApplicable(_rewardsToken).sub(rewardData[_rewardsToken].lastUpdateTime).mul(rewardData[_rewardsToken].rewardRate).mul(1e18).div(_totalSupply)
			);
	}

	function earned(address account, address _rewardsToken) public view returns (uint256) {
		return _balances[account].mul(rewardPerToken(_rewardsToken).sub(userRewardPerTokenPaid[account][_rewardsToken])).div(1e18).add(rewards[account][_rewardsToken]);
	}

	function earnings(address account, address _rewardsToken) public view returns (uint256) {
		return _earnings[account][_rewardsToken];
	}

	function earningsList(address _user, address[] memory _tokens) public view returns (uint[] memory) {
		uint[] memory _e = new uint[](rewardTokens.length);
		for(uint i; i<_e.length; i++) {
			_e[i] = _earnings[_user][rewardTokens[i]];
		}
		return _e;
	}

	function getRewardForDuration(address _rewardsToken) external view returns (uint256) {
		return rewardData[_rewardsToken].rewardRate.mul(rewardData[_rewardsToken].rewardsDuration);
	}

	function rewardTokensLength() public view returns(uint) {
		return rewardTokens.length;
	}

	function rewardTokensList() public view returns(address[] memory) {
		return rewardTokens;
	}

    function gaugeRewardListLength() external view returns (uint) {
        return gaugeRewardTokens.length;
    }

    function gaugeEarnings() external view returns (uint) {
    	return gauge.earned(address(this));
    }

	/* ========== MUTATIVE FUNCTIONS ========== */

	function setRewardsDistributor(address _rewardsToken, address _rewardsDistributor) external onlyOwner {
		rewardData[_rewardsToken].rewardsDistributor = _rewardsDistributor;
	}

	function _stake(uint256 samount, address _ben) internal {
		require(samount > 0, "Cannot stake 0");
		uint _fee = samount.mul(feePerMillion).div(1e6);
		uint amount = samount.sub(_fee);
		_totalSupply = _totalSupply.add(amount);
		_balances[_ben] = _balances[_ben].add(amount);
		stakingToken.safeTransferFrom(msg.sender, address(this), amount);
		if(_fee > 0) {
			stakingToken.safeTransferFrom(msg.sender, feeTaker, _fee);
		}
		emit Staked(_ben, amount);
		if(!emergency) gauge.deposit(amount);
	}

	// @notice Cannot deposit if paused
	function stake(uint256 amount) external nonReentrant notPaused updateReward(msg.sender) {
		_stake(amount,msg.sender);
	}

	function stakeAll() external nonReentrant notPaused updateReward(msg.sender) {
		_stake(stakingToken.balanceOf(msg.sender),msg.sender);
	}

	// @notice Can withhdraw funds even if paused
	function _withdraw(uint256 amount) internal {
		require(amount > 0, "Cannot withdraw 0");
		require(_balances[msg.sender] >= amount, "Overdrawing not allowed.");
		_totalSupply = _totalSupply.sub(amount);
		_balances[msg.sender] = _balances[msg.sender].sub(amount);
		if(!emergency) {
			uint _pre = gauge.balanceOf(address(this));
			gauge.withdraw(amount);
			require( gauge.balanceOf(address(this)) - _pre == amount, "Dirty draw!");
		}
		stakingToken.safeTransfer(msg.sender, amount);
		emit Withdrawn(msg.sender, amount);
	}

	function withdraw(uint256 amount) public nonReentrant notPaused updateReward(msg.sender) {
		_withdraw(amount);
	}

	function withdrawAll() external nonReentrant notPaused updateReward(msg.sender) {
		_withdraw(_balances[msg.sender]);
	}

	function exit() external {
		withdraw(_balances[msg.sender]);
		getReward();
	}

	function claimGaugeRewards() public {
		uint _gl = gaugeRewardTokens.length;
		uint[] memory _pgb = new uint[](_gl);
		for(uint i; i<_gl; i++) {
			_pgb[i] = IERC20(gaugeRewardTokens[i]).balanceOf(address(this));
		}
		gauge.getReward();
		for(uint i; i<_gl; i++) {
			address _grt = gaugeRewardTokens[i];
			uint _gd = IERC20(_grt).balanceOf(address(this)) - _pgb[i];
			if(_gd > 0) {
				IERC20(_grt).transfer(feeTaker, _gd);
				emit GaugeEarnings(feeTaker, _grt, _gd);
			}
		}
	}

	// Claim all rewards in one go.
	// @notice Can claim earnings even if paused
	// @notice Claims would fail if rewards have been recovered by Owner
	function getReward() public nonReentrant updateReward(msg.sender) {
		if(!emergency) claimGaugeRewards();
		for (uint i; i < rewardTokens.length; i++) {
			address _rewardsToken = rewardTokens[i];
			uint256 _reward = rewards[msg.sender][_rewardsToken];
			if (_reward > 0) {
				rewards[msg.sender][_rewardsToken] = 0;
				IERC20(_rewardsToken).safeTransfer(msg.sender, _reward);
				emit RewardPaid(msg.sender, _rewardsToken, _reward);
				_earnings[msg.sender][_rewardsToken] += _reward;
			}
		}
	}

	// Claim a specific set of rewards, in one go.
	function getReward(address[] calldata _rewardsTokens) external nonReentrant updateReward(msg.sender) {
		if(!emergency) claimGaugeRewards();
		for (uint i; i < _rewardsTokens.length; i++) {
			address _rewardsToken = _rewardsTokens[i];
			uint256 _reward = rewards[msg.sender][_rewardsToken];
			if (_reward > 0) {
				rewards[msg.sender][_rewardsToken] = 0;
				IERC20(_rewardsToken).safeTransfer(msg.sender, _reward);
				emit RewardPaid(msg.sender, _rewardsToken, _reward);
				_earnings[msg.sender][_rewardsToken] += _reward;
			}
		}
	}

	//Claim a specific Reward
	function getReward(address _rewardsToken) external nonReentrant updateReward(msg.sender) {
		if(!emergency) claimGaugeRewards();
		uint256 _reward = rewards[msg.sender][_rewardsToken];
		if (_reward > 0) {
			rewards[msg.sender][_rewardsToken] = 0;
			IERC20(_rewardsToken).safeTransfer(msg.sender, _reward);
			emit RewardPaid(msg.sender, _rewardsToken, _reward);
			_earnings[msg.sender][_rewardsToken] += _reward;
		}
	}

	/* ========== RESTRICTED FUNCTIONS ========== */

	function notifyRewardAmount(address _rewardsToken, uint256 reward) external updateReward(address(0)) {
		require(rewardData[_rewardsToken].rewardsDistributor == msg.sender);
		// handle the transfer of reward tokens via `transferFrom` to reduce the number
		// of transactions required and ensure correctness of the reward amount
		IERC20(_rewardsToken).safeTransferFrom(msg.sender, address(this), reward);

		if (block.timestamp >= rewardData[_rewardsToken].periodFinish) {
			rewardData[_rewardsToken].rewardRate = reward.div(rewardData[_rewardsToken].rewardsDuration);
		} else {
			uint256 remaining = rewardData[_rewardsToken].periodFinish.sub(block.timestamp);
			uint256 leftover = remaining.mul(rewardData[_rewardsToken].rewardRate);
			rewardData[_rewardsToken].rewardRate = reward.add(leftover).div(rewardData[_rewardsToken].rewardsDuration);
		}

		rewardData[_rewardsToken].lastUpdateTime = block.timestamp;
		rewardData[_rewardsToken].periodFinish = block.timestamp.add(rewardData[_rewardsToken].rewardsDuration);
		emit RewardAdded(_rewardsToken, reward);
	}

	// Recovering ERC20s
	// @notice Can be used break a specific reward's program.
	// @notice Depositors can still withdraw, but without any rewards.
	function recoverERC20(address tokenAddress, uint256 tokenAmount) external onlyOwner {
		require(tokenAddress != address(stakingToken), "Cannot touch user deposits");
		IERC20(tokenAddress).safeTransfer(owner, tokenAmount);
		emit Recovered(tokenAddress, tokenAmount);
	}

	function setRewardsDuration(address _rewardsToken, uint256 _rewardsDuration) external {
		require(
			block.timestamp > rewardData[_rewardsToken].periodFinish,
			"Reward period still active"
		);
		require(rewardData[_rewardsToken].rewardsDistributor == msg.sender || msg.sender == owner);
		require(_rewardsDuration > 0, "Reward duration must be non-zero");
		rewardData[_rewardsToken].rewardsDuration = _rewardsDuration;
		emit RewardsDurationUpdated(_rewardsToken, rewardData[_rewardsToken].rewardsDuration);
	}

	function setFeeTaker(address _f) external onlyOwner {
		require(_f != address(0) , "Invalid Fee Taker!");
		feeTaker = _f;
	}

	function setFee(uint _f) external onlyOwner {
		require(_f <= 1e5, "Deposit fee is capped at 10%.");
		feePerMillion = _f;
	}



	/* ========== TVL GURU ========== */

	// Getters

	function tvl() public view returns (uint _tvl) {
		( , bytes memory _tt) = address(TvlGuru).staticcall(TvlPriceFeed[address(stakingToken)]);
		(uint _pt) = abi.decode(_tt, (uint));
		_tvl = (_totalSupply * _pt) / 1e18;

		for(uint i; i<rewardTokens.length; i++) {
			( , _tt) = address(TvlGuru).staticcall( TvlPriceFeed[ rewardTokens[i] ] );
			_pt = abi.decode(_tt, (uint));
			_tvl += ( IERC20(rewardTokens[i]).balanceOf(address(this)) * _pt) / 1e18;
		}
	}

	function apr() public view returns(uint _apr) {
		( , bytes memory _tt) = address(TvlGuru).staticcall(TvlPriceFeed[address(stakingToken)]);
		(uint _pt) = abi.decode(_tt, (uint));
		uint _tvl = (_totalSupply * _pt) / 1e18;

		uint _roi;

		for(uint i; i<rewardTokens.length; i++) {
			Reward memory _rdt = rewardData[ rewardTokens[i] ];
			uint _pf = _rdt.periodFinish;
			if(_pf > block.timestamp) {
				uint _rr = _rdt.rewardRate;
				( , bytes memory _tt) = address(TvlGuru).staticcall( TvlPriceFeed[ rewardTokens[i] ] );
				uint _pt = abi.decode(_tt, (uint));
				_roi += (_rr * _pt * 365 * 24 * 60 * 60) / 1e18;
			}
		}

		_apr = (_roi * 1e18 * 100) / _tvl;
	}

	function apr(uint i) public view returns(uint _apr) {
		( , bytes memory _tt) = address(TvlGuru).staticcall(TvlPriceFeed[address(stakingToken)]);
		(uint _pt) = abi.decode(_tt, (uint));
		uint _tvl = (_totalSupply * _pt) / 1e18;
		Reward memory _rdt = rewardData[ rewardTokens[i] ];
		uint _pf = _rdt.periodFinish;
		uint _roi;
		if(_pf > block.timestamp) {
			uint _rr = _rdt.rewardRate;
			( , bytes memory _tt) = address(TvlGuru).staticcall( TvlPriceFeed[ rewardTokens[i] ] );
			uint _pt = abi.decode(_tt, (uint));
			_roi = _rr * _pt * 365 * 86400 / 1e18;
		}
		_apr = (_roi * 1e18 * 100) / _tvl;
	}

	function aprs() public view returns(uint[] memory) {
		( , bytes memory _tt) = address(TvlGuru).staticcall(TvlPriceFeed[address(stakingToken)]);
		(uint _pt) = abi.decode(_tt, (uint));
		uint _tvl = (_totalSupply * _pt) / 1e18;
		uint[] memory _aprs = new uint[](rewardTokens.length);
		for(uint i; i<rewardTokens.length; i++) {
			Reward memory _rdt = rewardData[ rewardTokens[i] ];
			uint _pf = _rdt.periodFinish;
			uint _roi;
			if(_pf > block.timestamp) {
				uint _rr = _rdt.rewardRate;
				( , bytes memory _tt) = address(TvlGuru).staticcall( TvlPriceFeed[ rewardTokens[i] ] );
				uint _pt = abi.decode(_tt, (uint));
				_roi = _rr * _pt * 365 * 86400 / 1e18;
			}
			_aprs[i] = (_roi * 1e18 * 100) / _tvl;
		}
		return _aprs;
	}

	// Setters

	function setEmergency(bool _e) external onlyOwner {
		emergency = _e;
		if(_e) {
			uint _gl = gaugeRewardTokens.length;
			uint[] memory _pgb = new uint[](_gl);
			for(uint i; i<_gl; i++) {
				_pgb[i] = IERC20(gaugeRewardTokens[i]).balanceOf(address(this));
			}
			try gauge.withdrawAllAndHarvest() {
				for(uint i; i<_gl; i++) {
					address _grt = gaugeRewardTokens[i];
					uint _gd = IERC20(_grt).balanceOf(address(this)) - _pgb[i];
					if(_gd > 0) {
						IERC20(_grt).transfer(feeTaker, _gd);
						emit GaugeEarnings(feeTaker, _grt, _gd);
					}
				}
				return;
			}
			catch {
				try gauge.withdrawAll() {
					return;
				}
				catch {
					try gauge.emergencyWithdraw() {
						return;
					}
					catch {
						return;
					}
				}
			}
		}
		else {
			if(stakingToken.balanceOf(address(this)) > 0) {
				gauge.depositAll();
			}
		}
	}

	function emergencyAction(uint _n, uint _a) external onlyOwner {
		if(_n==1) gauge.withdrawAll();
		if(_n==2) gauge.withdraw(_a);
		if(_n==3) gauge.emergencyWithdraw();
		if(_n==4) gauge.emergencyWithdrawAmount(_a);
		if(_n==5) gauge.withdrawAllAndHarvest();
		if(_n==6) gauge.getReward();
	}

	function setTvlGuru(address _tg) external onlyOwner {
		TvlGuru = _tg;
	}

	function setTvlFeed(address _t, bytes calldata _d) external onlyOwner {
		TvlPriceFeed[_t] = _d;
	}

	function setTvlFeed(address[] calldata _t, bytes[] calldata _d) external onlyOwner {
		for(uint i; i<rewardTokens.length; i++) {
			TvlPriceFeed[_t[i]] = _d[i];
		}
	}

    function addGaugeRewardTokens(address[] memory _tks) external onlyOwner {
        for(uint _j; _j < _tks.length; _j++) {
        	gaugeRewardTokens.push(_tks[_j]);
        }
    }

    function removeGaugeRewardTokens(address[] memory _tks) external onlyOwner {
        for(uint j; j < _tks.length; j++) {
        	uint _l = gaugeRewardTokens.length;
        	if(gaugeRewardTokens[_l-1]==_tks[j]) {
            	gaugeRewardTokens.pop();
        	}
        	else {
            	for(uint i; i < _l - 1; i++) {
                	if(gaugeRewardTokens[i]==_tks[j]) {
                    	gaugeRewardTokens[i] = gaugeRewardTokens[_l-1];
                    	gaugeRewardTokens.pop();
                	}
            	}
        	}
        }
    }


	/* ========== MODIFIERS ========== */

	modifier updateReward(address account) {
		for (uint i; i < rewardTokens.length; i++) {
			address token = rewardTokens[i];
			rewardData[token].rewardPerTokenStored = rewardPerToken(token);
			rewardData[token].lastUpdateTime = lastTimeRewardApplicable(token);
			if (account != address(0)) {
				rewards[account][token] = earned(account, token);
				userRewardPerTokenPaid[account][token] = rewardData[token].rewardPerTokenStored;
			}
		}
		_;
	}

	/* ========== EVENTS ========== */

	event RewardAdded(address indexed token, uint256 reward);
	event Staked(address indexed user, uint256 amount);
	event Withdrawn(address indexed user, uint256 amount);
	event RewardPaid(address indexed user, address indexed rewardsToken, uint256 reward);
	event GaugeEarnings(address indexed user, address indexed rewardsToken, uint256 reward);
	event RewardsDurationUpdated(address indexed token, uint256 newDuration);
	event Recovered(address indexed token, uint256 amount);
}
// Need help?
// Join our Discord: https://discord.gg/QpyfMarNrV
// Powered by Guru Network
// (🦾,🚀)