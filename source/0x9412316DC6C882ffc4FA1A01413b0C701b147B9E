
// File: @openzeppelin/contracts/access/Ownable.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.9.0) (access/Ownable.sol)

pragma solidity ^0.8.0;

import "../utils/Context.sol";

/**
 * @dev Contract module which provides a basic access control mechanism, where
 * there is an account (an owner) that can be granted exclusive access to
 * specific functions.
 *
 * By default, the owner account will be the one that deploys the contract. This
 * can later be changed with {transferOwnership}.
 *
 * This module is used through inheritance. It will make available the modifier
 * `onlyOwner`, which can be applied to your functions to restrict their use to
 * the owner.
 */
abstract contract Ownable is Context {
    address private _owner;

    event OwnershipTransferred(address indexed previousOwner, address indexed newOwner);

    /**
     * @dev Initializes the contract setting the deployer as the initial owner.
     */
    constructor() {
        _transferOwnership(_msgSender());
    }

    /**
     * @dev Throws if called by any account other than the owner.
     */
    modifier onlyOwner() {
        _checkOwner();
        _;
    }

    /**
     * @dev Returns the address of the current owner.
     */
    function owner() public view virtual returns (address) {
        return _owner;
    }

    /**
     * @dev Throws if the sender is not the owner.
     */
    function _checkOwner() internal view virtual {
        require(owner() == _msgSender(), "Ownable: caller is not the owner");
    }

    /**
     * @dev Leaves the contract without owner. It will not be possible to call
     * `onlyOwner` functions. Can only be called by the current owner.
     *
     * NOTE: Renouncing ownership will leave the contract without an owner,
     * thereby disabling any functionality that is only available to the owner.
     */
    function renounceOwnership() public virtual onlyOwner {
        _transferOwnership(address(0));
    }

    /**
     * @dev Transfers ownership of the contract to a new account (`newOwner`).
     * Can only be called by the current owner.
     */
    function transferOwnership(address newOwner) public virtual onlyOwner {
        require(newOwner != address(0), "Ownable: new owner is the zero address");
        _transferOwnership(newOwner);
    }

    /**
     * @dev Transfers ownership of the contract to a new account (`newOwner`).
     * Internal function without access restriction.
     */
    function _transferOwnership(address newOwner) internal virtual {
        address oldOwner = _owner;
        _owner = newOwner;
        emit OwnershipTransferred(oldOwner, newOwner);
    }
}


// File: @openzeppelin/contracts/access/Ownable2Step.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.9.0) (access/Ownable2Step.sol)

pragma solidity ^0.8.0;

import "./Ownable.sol";

/**
 * @dev Contract module which provides access control mechanism, where
 * there is an account (an owner) that can be granted exclusive access to
 * specific functions.
 *
 * By default, the owner account will be the one that deploys the contract. This
 * can later be changed with {transferOwnership} and {acceptOwnership}.
 *
 * This module is used through inheritance. It will make available all functions
 * from parent (Ownable).
 */
abstract contract Ownable2Step is Ownable {
    address private _pendingOwner;

    event OwnershipTransferStarted(address indexed previousOwner, address indexed newOwner);

    /**
     * @dev Returns the address of the pending owner.
     */
    function pendingOwner() public view virtual returns (address) {
        return _pendingOwner;
    }

    /**
     * @dev Starts the ownership transfer of the contract to a new account. Replaces the pending transfer if there is one.
     * Can only be called by the current owner.
     */
    function transferOwnership(address newOwner) public virtual override onlyOwner {
        _pendingOwner = newOwner;
        emit OwnershipTransferStarted(owner(), newOwner);
    }

    /**
     * @dev Transfers ownership of the contract to a new account (`newOwner`) and deletes any pending owner.
     * Internal function without access restriction.
     */
    function _transferOwnership(address newOwner) internal virtual override {
        delete _pendingOwner;
        super._transferOwnership(newOwner);
    }

    /**
     * @dev The new owner accepts the ownership transfer.
     */
    function acceptOwnership() public virtual {
        address sender = _msgSender();
        require(pendingOwner() == sender, "Ownable2Step: caller is not the new owner");
        _transferOwnership(sender);
    }
}


// File: @openzeppelin/contracts/security/Pausable.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.7.0) (security/Pausable.sol)

pragma solidity ^0.8.0;

import "../utils/Context.sol";

/**
 * @dev Contract module which allows children to implement an emergency stop
 * mechanism that can be triggered by an authorized account.
 *
 * This module is used through inheritance. It will make available the
 * modifiers `whenNotPaused` and `whenPaused`, which can be applied to
 * the functions of your contract. Note that they will not be pausable by
 * simply including this module, only once the modifiers are put in place.
 */
abstract contract Pausable is Context {
    /**
     * @dev Emitted when the pause is triggered by `account`.
     */
    event Paused(address account);

    /**
     * @dev Emitted when the pause is lifted by `account`.
     */
    event Unpaused(address account);

    bool private _paused;

    /**
     * @dev Initializes the contract in unpaused state.
     */
    constructor() {
        _paused = false;
    }

    /**
     * @dev Modifier to make a function callable only when the contract is not paused.
     *
     * Requirements:
     *
     * - The contract must not be paused.
     */
    modifier whenNotPaused() {
        _requireNotPaused();
        _;
    }

    /**
     * @dev Modifier to make a function callable only when the contract is paused.
     *
     * Requirements:
     *
     * - The contract must be paused.
     */
    modifier whenPaused() {
        _requirePaused();
        _;
    }

    /**
     * @dev Returns true if the contract is paused, and false otherwise.
     */
    function paused() public view virtual returns (bool) {
        return _paused;
    }

    /**
     * @dev Throws if the contract is paused.
     */
    function _requireNotPaused() internal view virtual {
        require(!paused(), "Pausable: paused");
    }

    /**
     * @dev Throws if the contract is not paused.
     */
    function _requirePaused() internal view virtual {
        require(paused(), "Pausable: not paused");
    }

    /**
     * @dev Triggers stopped state.
     *
     * Requirements:
     *
     * - The contract must not be paused.
     */
    function _pause() internal virtual whenNotPaused {
        _paused = true;
        emit Paused(_msgSender());
    }

    /**
     * @dev Returns to normal state.
     *
     * Requirements:
     *
     * - The contract must be paused.
     */
    function _unpause() internal virtual whenPaused {
        _paused = false;
        emit Unpaused(_msgSender());
    }
}


// File: @openzeppelin/contracts/security/ReentrancyGuard.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.9.0) (security/ReentrancyGuard.sol)

pragma solidity ^0.8.0;

/**
 * @dev Contract module that helps prevent reentrant calls to a function.
 *
 * Inheriting from `ReentrancyGuard` will make the {nonReentrant} modifier
 * available, which can be applied to functions to make sure there are no nested
 * (reentrant) calls to them.
 *
 * Note that because there is a single `nonReentrant` guard, functions marked as
 * `nonReentrant` may not call one another. This can be worked around by making
 * those functions `private`, and then adding `external` `nonReentrant` entry
 * points to them.
 *
 * TIP: If you would like to learn more about reentrancy and alternative ways
 * to protect against it, check out our blog post
 * https://blog.openzeppelin.com/reentrancy-after-istanbul/[Reentrancy After Istanbul].
 */
abstract contract ReentrancyGuard {
    // Booleans are more expensive than uint256 or any type that takes up a full
    // word because each write operation emits an extra SLOAD to first read the
    // slot's contents, replace the bits taken up by the boolean, and then write
    // back. This is the compiler's defense against contract upgrades and
    // pointer aliasing, and it cannot be disabled.

    // The values being non-zero value makes deployment a bit more expensive,
    // but in exchange the refund on every call to nonReentrant will be lower in
    // amount. Since refunds are capped to a percentage of the total
    // transaction's gas, it is best to keep them low in cases like this one, to
    // increase the likelihood of the full refund coming into effect.
    uint256 private constant _NOT_ENTERED = 1;
    uint256 private constant _ENTERED = 2;

    uint256 private _status;

    constructor() {
        _status = _NOT_ENTERED;
    }

    /**
     * @dev Prevents a contract from calling itself, directly or indirectly.
     * Calling a `nonReentrant` function from another `nonReentrant`
     * function is not supported. It is possible to prevent this from happening
     * by making the `nonReentrant` function external, and making it call a
     * `private` function that does the actual work.
     */
    modifier nonReentrant() {
        _nonReentrantBefore();
        _;
        _nonReentrantAfter();
    }

    function _nonReentrantBefore() private {
        // On the first call to nonReentrant, _status will be _NOT_ENTERED
        require(_status != _ENTERED, "ReentrancyGuard: reentrant call");

        // Any calls to nonReentrant after this point will fail
        _status = _ENTERED;
    }

    function _nonReentrantAfter() private {
        // By storing the original value once again, a refund is triggered (see
        // https://eips.ethereum.org/EIPS/eip-2200)
        _status = _NOT_ENTERED;
    }

    /**
     * @dev Returns true if the reentrancy guard is currently set to "entered", which indicates there is a
     * `nonReentrant` function in the call stack.
     */
    function _reentrancyGuardEntered() internal view returns (bool) {
        return _status == _ENTERED;
    }
}


// File: @openzeppelin/contracts/token/ERC20/extensions/IERC20Metadata.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (token/ERC20/extensions/IERC20Metadata.sol)

pragma solidity ^0.8.0;

import "../IERC20.sol";

/**
 * @dev Interface for the optional metadata functions from the ERC20 standard.
 *
 * _Available since v4.1._
 */
interface IERC20Metadata is IERC20 {
    /**
     * @dev Returns the name of the token.
     */
    function name() external view returns (string memory);

    /**
     * @dev Returns the symbol of the token.
     */
    function symbol() external view returns (string memory);

    /**
     * @dev Returns the decimals places of the token.
     */
    function decimals() external view returns (uint8);
}


// File: @openzeppelin/contracts/token/ERC20/IERC20.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.9.0) (token/ERC20/IERC20.sol)

pragma solidity ^0.8.0;

/**
 * @dev Interface of the ERC20 standard as defined in the EIP.
 */
interface IERC20 {
    /**
     * @dev Emitted when `value` tokens are moved from one account (`from`) to
     * another (`to`).
     *
     * Note that `value` may be zero.
     */
    event Transfer(address indexed from, address indexed to, uint256 value);

    /**
     * @dev Emitted when the allowance of a `spender` for an `owner` is set by
     * a call to {approve}. `value` is the new allowance.
     */
    event Approval(address indexed owner, address indexed spender, uint256 value);

    /**
     * @dev Returns the amount of tokens in existence.
     */
    function totalSupply() external view returns (uint256);

    /**
     * @dev Returns the amount of tokens owned by `account`.
     */
    function balanceOf(address account) external view returns (uint256);

    /**
     * @dev Moves `amount` tokens from the caller's account to `to`.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * Emits a {Transfer} event.
     */
    function transfer(address to, uint256 amount) external returns (bool);

    /**
     * @dev Returns the remaining number of tokens that `spender` will be
     * allowed to spend on behalf of `owner` through {transferFrom}. This is
     * zero by default.
     *
     * This value changes when {approve} or {transferFrom} are called.
     */
    function allowance(address owner, address spender) external view returns (uint256);

    /**
     * @dev Sets `amount` as the allowance of `spender` over the caller's tokens.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * IMPORTANT: Beware that changing an allowance with this method brings the risk
     * that someone may use both the old and the new allowance by unfortunate
     * transaction ordering. One possible solution to mitigate this race
     * condition is to first reduce the spender's allowance to 0 and set the
     * desired value afterwards:
     * https://github.com/ethereum/EIPs/issues/20#issuecomment-263524729
     *
     * Emits an {Approval} event.
     */
    function approve(address spender, uint256 amount) external returns (bool);

    /**
     * @dev Moves `amount` tokens from `from` to `to` using the
     * allowance mechanism. `amount` is then deducted from the caller's
     * allowance.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * Emits a {Transfer} event.
     */
    function transferFrom(address from, address to, uint256 amount) external returns (bool);
}


// File: @openzeppelin/contracts/utils/Context.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (utils/Context.sol)

pragma solidity ^0.8.0;

/**
 * @dev Provides information about the current execution context, including the
 * sender of the transaction and its data. While these are generally available
 * via msg.sender and msg.data, they should not be accessed in such a direct
 * manner, since when dealing with meta-transactions the account sending and
 * paying for execution may not be the actual sender (as far as an application
 * is concerned).
 *
 * This contract is only required for intermediate, library-like contracts.
 */
abstract contract Context {
    function _msgSender() internal view virtual returns (address) {
        return msg.sender;
    }

    function _msgData() internal view virtual returns (bytes calldata) {
        return msg.data;
    }
}


// File: contracts/bases/ArcBase.sol
// SPDX-License-Identifier: BUSL-1.1
pragma solidity 0.8.19;

import {Ownable2Step} from "@openzeppelin/contracts/access/Ownable2Step.sol";
import {Pausable} from "@openzeppelin/contracts/security/Pausable.sol";
import {ReentrancyGuard} from "@openzeppelin/contracts/security/ReentrancyGuard.sol";
import {IERC20} from "@openzeppelin/contracts/token/ERC20/IERC20.sol";

/**
 * Provides set of properties, functions, and modifiers to help with 
 * security and access control of extending contracts
 */
contract ArcBase is Ownable2Step, Pausable, ReentrancyGuard
{
    function pause() public onlyOwner
    {
        _pause();
    }
    
    function unpause() public onlyOwner
    {
        _unpause();
    }

    function withdrawNative(address beneficiary) public onlyOwner {
        uint256 amount = address(this).balance;
        (bool sent, ) = beneficiary.call{value: amount}("");
        require(sent, 'Unable to withdraw');
    }

    function withdrawToken(address beneficiary, address token) public onlyOwner {
        uint256 amount = IERC20(token).balanceOf(address(this));
        IERC20(token).transfer(beneficiary, amount);
    }
}


// File: contracts/interfaces/IArc.sol
// SPDX-License-Identifier: BUSL-1.1
pragma solidity 0.8.19;

interface IArc {
    function burn(uint amount) external;
    function transfer(address, uint) external returns (bool);
    function transferFrom(address _from, address _to, uint _value) external;
}

// File: contracts/interfaces/IHandler.sol
// SPDX-License-Identifier: BUSL-1.1
pragma solidity 0.8.19;

interface IHandler {
    function handleReceive(address target, bytes calldata payload) external;
    function handleSend(address target, bytes calldata payload) external;
}

// File: contracts/interfaces/IRainbowRoad.sol
// SPDX-License-Identifier: BUSL-1.1
pragma solidity 0.8.19;

interface IRainbowRoad {
    function team() external view returns (address);
    function teamRate() external view returns (uint256);
    function feeManagers(address feeManager) external view returns (bool);
    function blockedTokens(address tokenAddress) external view returns (bool);
    function tokens(string calldata tokenSymbol) external view returns (address);
    function receiveAction(string calldata action, address to, bytes calldata payload) external;
    function sendAction(string calldata action, address from, bytes calldata payload) external;
}

// File: contracts/RainbowRoad.sol
// SPDX-License-Identifier: BUSL-1.1
pragma solidity 0.8.19;

import {IERC20Metadata} from "@openzeppelin/contracts/token/ERC20/extensions/IERC20Metadata.sol";
import {ArcBase} from "./bases/ArcBase.sol";
import {IArc} from "./interfaces/IArc.sol";
import {IRainbowRoad} from "./interfaces/IRainbowRoad.sol";
import {IHandler} from "./interfaces/IHandler.sol";

/**
 * Manages sending and receiving of tokens and NFTs between chains
 */
contract RainbowRoad is ArcBase, IRainbowRoad
{
    IArc public arc;
    address public team;
    address public pendingTeam;
    uint256 public constant MAX_TEAM_RATE = 750; // 75%
    uint256 public teamRate;
    uint256 public sendFee;
    uint256 public whitelistingFee;
    bool public chargeSendFee;
    bool public chargeWhitelistingFee;
    bool public burnSendFee;
    bool public burnWhitelistingFee;
    bool public openTokenWhitelisting;
    mapping(address => bool) public feeManagers;
    mapping(address => bool) public receivers;
    mapping(address => bool) public senders;
    mapping(address => bool) public blockedTokens;
    mapping(string => address) public tokens;
    mapping(string => address) public actionHandlers;
    mapping(string => bytes) public config;
    
    constructor(address _arc)
    {
        require(_arc != address(0), 'Arc cannot be zero address');
        arc = IArc(_arc);
        team = 0x0c5D52630c982aE81b78AB2954Ddc9EC2797bB9c;
        teamRate = 400; // 400 bps = 40%
        openTokenWhitelisting = false;
        sendFee = 100000e18;
        whitelistingFee = 1000000e18;
        chargeSendFee = true;
        chargeWhitelistingFee = true;
        burnSendFee = true;
        burnWhitelistingFee = true;
        feeManagers[msg.sender] = true;
        feeManagers[team] = true;
        feeManagers[0x726461FA6e788bd8a79986D36F1992368A3e56eA] = true;
        tokens['Arc'] = _arc;
    }
    
    function setArc(address _arc) external onlyOwner
    {
        require(_arc != address(0), 'Arc cannot be zero address');
        arc = IArc(_arc);
    }
    
    function setTeam(address _team) external onlyTeam {
        pendingTeam = _team;
    }

    function acceptTeam() external {
        require(msg.sender == pendingTeam, "Invalid pending team");
        team = pendingTeam;
    }

    function setTeamRate(uint256 _teamRate) external onlyTeam {
        require(_teamRate <= MAX_TEAM_RATE, "Team rate too high");
        teamRate = _teamRate;
    }
    
    function setSendFee(uint256 _fee) external onlyFeeManagers
    {
        require(_fee > 0, 'Fee must be greater than zero');
        sendFee = _fee;
    }
    
    function setWhitelistingFee(uint256 _fee) external onlyFeeManagers
    {
        require(_fee > 0, 'Fee must be greater than zero');
        whitelistingFee = _fee;
    }
    
    function setToken(string calldata tokenSymbol, address tokenAddress) external onlyOwner
    {
        tokens[tokenSymbol] = tokenAddress;
    }
    
    function setActionHandler(string memory action, address handler) external onlyOwner
    {
        actionHandlers[action] = handler;
    }
    
    function blockToken(address tokenAddress) external onlyOwner
    {
        require(tokenAddress != address(0), 'Token address cannot be zero address');
        require(!blockedTokens[tokenAddress], 'Token address is blocked');
        blockedTokens[tokenAddress] = true;
    }
    
    function unblockToken(address tokenAddress) external onlyOwner
    {
        require(tokenAddress != address(0), 'Token address cannot be zero address');
        require(blockedTokens[tokenAddress], 'Token address is unblocked');
        blockedTokens[tokenAddress] = false;
    }
    
    function whitelist(address tokenAddress) external
    {
        string memory tokenSymbol = IERC20Metadata(tokenAddress).symbol();
        require(openTokenWhitelisting, 'Open token whitelisting is disabled');
        require(tokenAddress != address(0), 'Token address cannot be zero address');
        require(tokens[tokenSymbol] == address(0), 'Token is already enabled');
        require(!blockedTokens[tokenAddress], 'Token address is blocked');
        
        if(chargeWhitelistingFee) {
            arc.transferFrom(msg.sender, address(this), whitelistingFee);
            
            uint256 teamFee = (teamRate * whitelistingFee) / 1000;
            require(arc.transfer(team, teamFee));
            
            if(burnWhitelistingFee) {
                arc.burn(whitelistingFee - teamFee);
            }
        }
        
        tokens[tokenSymbol] = tokenAddress;
    }
    
    function enableOpenTokenWhitelisting() external onlyOwner
    {
        require(!openTokenWhitelisting, 'Open token whitelisting is enabled');
        openTokenWhitelisting = true;
    }
    
    function disableOpenTokenWhitelisting() external onlyOwner
    {
        require(openTokenWhitelisting, 'Open token whitelisting is disabled');
        openTokenWhitelisting = false;
    }
    
    function enableSendFeeCharge() external onlyOwner
    {
        require(!chargeSendFee, 'Charge send fee is enabled');
        chargeSendFee = true;
    }
    
    function disableSendFeeCharge() external onlyOwner
    {
        require(chargeSendFee, 'Charge send fee is disabled');
        chargeSendFee = false;
    }
    
    function enableSendFeeBurn() external onlyOwner
    {
        require(!burnSendFee, 'Burn send fee is enabled');
        burnSendFee = true;
    }
    
    function disableSendFeeBurn() external onlyOwner
    {
        require(burnSendFee, 'Burn send fee is disabled');
        burnSendFee = false;
    }
    
    function enableWhitelistingFeeCharge() external onlyOwner
    {
        require(!chargeWhitelistingFee, 'Charge whitelisting fee is enabled');
        chargeWhitelistingFee = true;
    }
    
    function disableWhitelistingFeeCharge() external onlyOwner
    {
        require(chargeWhitelistingFee, 'Charge whitelisting fee is disabled');
        chargeWhitelistingFee = false;
    }
    
    function enableWhitelistingFeeBurn() external onlyOwner
    {
        require(!burnWhitelistingFee, 'Burn whitelisting fee is enabled');
        burnWhitelistingFee = true;
    }
    
    function disableWhitelistingFeeBurn() external onlyOwner
    {
        require(burnWhitelistingFee, 'Burn whitelisting fee is disabled');
        burnWhitelistingFee = false;
    }
    
    function enableFeeManager(address feeManager) external onlyOwner
    {
        require(!feeManagers[feeManager], 'Fee manager is enabled');
        feeManagers[feeManager] = true;
    }
    
    function disableFeeManager(address feeManager) external onlyOwner
    {
        require(feeManagers[feeManager], 'Fee manager is disabled');
        feeManagers[feeManager] = false;
    }
    
    function enableReceiver(address receiver) external onlyOwner
    {
        require(!receivers[receiver], 'Receiver is enabled');
        receivers[receiver] = true;
    }
    
    function disableReceiver(address receiver) external onlyOwner
    {
        require(receivers[receiver], 'Receiver is disabled');
        receivers[receiver] = false;
    }
    
    function enableSender(address sender) external onlyOwner
    {
        require(!senders[sender], 'Sender is enabled');
        senders[sender] = true;
    }
    
    function disableSender(address sender) external onlyOwner
    {
        require(senders[sender], 'Sender is disabled');
        senders[sender] = false;
    }
    
    function setConfig(string calldata configName, bytes calldata configData) external onlyOwner
    {
        config[configName] = configData;
    }
    
    function receiveAction(string calldata action, address to, bytes calldata payload) external onlyReceivers whenNotPaused nonReentrant
    {
        require(actionHandlers[action] != address(0), 'Unsupported action');
        require(to != address(0), 'To cannot be zero address');
        IHandler(actionHandlers[action]).handleReceive(to, payload);
    }
    
    function sendAction(string calldata action, address from, bytes calldata payload) external onlySenders whenNotPaused nonReentrant
    {
        require(actionHandlers[action] != address(0), 'Unsupported action');
        require(from != address(0), 'From cannot be zero address');
        
        if(chargeSendFee) {
            arc.transferFrom(from, address(this), sendFee);
            
            uint256 teamFee = (teamRate * sendFee) / 1000;
            require(arc.transfer(team, teamFee));
            
            if(burnSendFee) {
                arc.burn(sendFee - teamFee);
            }
        }
        
        IHandler(actionHandlers[action]).handleSend(from, payload);
    }
    
    receive() external payable {}
    
    /// @dev Only calls from the enabled fee managers are accepted.
    modifier onlyFeeManagers() 
    {
        require(feeManagers[msg.sender], 'Invalid fee manager');
        _;
    }
    
    /// @dev Only calls from the enabled receivers are accepted.
    modifier onlyReceivers() 
    {
        require(receivers[msg.sender], 'Invalid receiver');
        _;
    }
    
    /// @dev Only calls from the enabled senders are accepted.
    modifier onlySenders() 
    {
        require(senders[msg.sender], 'Invalid sender');
        _;
    }
    
    /// @dev Only calls from the team are accepted.
    modifier onlyTeam() 
    {
        require(msg.sender == team, "Invalid team");
        _;
    }
}

