/********************** @2022 Wizarre, All rights reserved *********************
MMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM
MMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM
MMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM
MMMMMMMMMmMMMMMMMMMMMMMNNMMMMNddNMMMMmmmmmNMMMMNddNMMMMNMMMMMMMMMMMMMMmMMMMMMMMM
MMMMMMMMdysNhoossydNMmosyyMM+ddd-NhssdNMMNdsshN-ddd+MMsysomMNdyssoohNysdMMMMMMMM
MMMMMMMMmss::NMMh/+o+/-ohhMMhhdm`:hNNmMMMMmMNy:.mdhhMNhho-/+o+/dMMN-:ssmMMMMMMMM
MMMMMMMMNoMhyhdNNNmsoyNh/:mMMMs.oNMhoNMMMMmodMNo.yMMMd:/hNyoymNNNdhyhNoNMMMMMMMM
MMMMMMMNy/:shhhhy/-syyhhy`.hho :hho-yhhhhhhy-shh: ohh.`yhhyyo./yhhhhs:/hNMMMMMMM
MMMMMMMd `+://sso:`.`---/:osoo.syhh+yyyssyyyohhyo-osso:/---`.`/oss//:o  mMMMMMMM
MMMMMMMd `s+s+:..``.-+yhmyddhhsmyoyosddyyddsoyoymsyhdhymhy+-.``..:+s+y  mMMMMMMM
MMMMMMMd  sm`  -shhy. sMNys+ho+ssymyoodmmhooydyos+oh+yyMMo -yhhs-  `ms  mMMMMMMM
MMMMMMMd `yds` `/ss+:/hddddhhodNNhdssyNMMNyssdhNNhshhddddh/:+ss/` `ydy  mMMMMMMM
MMMMMMMd  ooNh/-..`````     :sdhdssddshmmhsddsydhdo-     `````..-/dNoo  mMMMMMMM
MMMMMMMd `md+ymddysohs.   `odmmdoyNoNmohhoNNoNssdmmho`   .yhosyddmy+dm  mMMMMMMM
MMMMMMMd `Nmh+/oyo-+o` `  syMMsM/dy/NhdyhhhN:yd+MsMNyo  ` `o+-oyo:+hmm  mMMMMMMM
MMMMMMMd  -NNy+/+yy.  :../odyhohssohyhsooshshossh+hyd+/`-:  -yy+/+yNm.  mMMMMMMM
MMMMMMMd -/-::yNN/   +/`so/shhssyhyy+yy++hy+hshysyhho:oo`++   +Nms::-/. mMMMMMMM
MMMMMMMd oMy/MMh.   ss hMNNhs+:/++++/yhyyhy/++++::oshNhNy ys   .hMM:hM+ mMMMMMMM
MMMMMMMd hMooMo    ym`oMdhymhyo/    `ooo+oo`    .shhdmyhM+`ms    oM+oMy mMMMMMMM
MMMMMMMd mMsy-    yM:`Nmsdyhysys:  ` `:ys/` ` `hmMMdsshsom /My    :ysMm mMMMMMMM
MMMMMMMd.MMd.    yMh :yssMNhshymNy: /``Nm .: .yNdMN/hmMddM- dMs    .mMM`mMMMMMMM
MMMMMMMdoMh`    oMM- :omdoMMMMdhddm/ s.--.s /mhshMhydhhdoN/ :MM+    `dM+mMMMMMMM
MMMMMMMdmh`    :MMd  /yhhh+s:ysyy+hN+`d..d`+ddhyyssNNyddss+  mMM-    `ddmMMMMMMM
MMMMMMMMh`     dMMy  :hodoNoy+hysdmym-+dd/-mymhdhdyd+NNmyN-  hMMd     `hMMMMMMMM
MMMMMMMM.      .sNMh- /yddyms+NMm:mmhs`Nm yMmhdssyhs+dy+o: -dMNs`      .MMMMMMMM
MMMMMMMNd:       .sNNs.`sNNyhhoNMm+dh-`dd`:Moymhhdyhsdho`.sNNs.       /mMMMMMMMM
MMMMMMMdhmo:`      .odm+`:sddmmsmmo/o +--+ /dymddhhmms-`+md+`      `:sNymMMMMMMM
MMMMMMMd/sy:do-      `:hh/`:sdmdydm- ./  /``odmyhydo-./dy:`      -sh:yy:mMMMMMMM
MMMMMMMd`m/.+shh+-      .os/`:ohdds  - // - `hMNhs-./s+.     `-+hhs/./m mMMMMMMM
MMMMMMMd -.mho/-hNds:.    `-/-`.oo`    --    `oo-`-/-`   `./sdNy-/sdm`- mMMMMMMM
MMMMMMMd  oMoo/.mNyooss+/-`  ``       `/:`       ``  .-/+ssoohMm.+osMo  mMMMMMMM
MMMMMMMd  myssh+hN.`o++/oNmo+/-.`  `.+hddh/.`  `.-++smm+/++o`.Nhohssyd  mMMMMMMM
MMMMMMMd  ++++osys-..--:/++:/yso+/:/o+-``-+o/:/+oss::++/:--..:syso++++  mMMMMMMM
MMMMMMMMh/.`-+hdmoohdmmmmddhmmmy+ooh/ .ys` +hoo+ymmmhddmmmmdhoomdh+-`.+hMMMMMMMM
MMMMMMMMMNmhs.`::`yhymdyoshNdsddyy. .+mNNm+` -yydhsmNhsoymdyhs /:`.sdmNMMMMMMMMM
MMMMMMMMMsohh.ymhy+//+./NMNssh+::.`+dmymmymd/`.::+hosNMm/.+//+yhmy.dh+yMMMMMMMMM
MMMMMMMMMNhysmMMMMMMMMh+MNhNMMMNNMdysyNMMNssydMNNMMMNhNN+dMMMMMMMMmsydNMMMMMMMMM
MMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM
MMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM
MMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM
MMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM*/
// SPDX-License-Identifier: None

// File @openzeppelin/contracts/token/ERC20/IERC20.sol@v4.5.0

// OpenZeppelin Contracts (last updated v4.5.0) (token/ERC20/IERC20.sol)

pragma solidity ^0.8.0;

/**
 * @dev Interface of the ERC20 standard as defined in the EIP.
 */
interface IERC20 {
    /**
     * @dev Returns the amount of tokens in existence.
     */
    function totalSupply() external view returns (uint256);

    /**
     * @dev Returns the amount of tokens owned by `account`.
     */
    function balanceOf(address account) external view returns (uint256);

    /**
     * @dev Moves `amount` tokens from the caller's account to `to`.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * Emits a {Transfer} event.
     */
    function transfer(address to, uint256 amount) external returns (bool);

    /**
     * @dev Returns the remaining number of tokens that `spender` will be
     * allowed to spend on behalf of `owner` through {transferFrom}. This is
     * zero by default.
     *
     * This value changes when {approve} or {transferFrom} are called.
     */
    function allowance(address owner, address spender) external view returns (uint256);

    /**
     * @dev Sets `amount` as the allowance of `spender` over the caller's tokens.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * IMPORTANT: Beware that changing an allowance with this method brings the risk
     * that someone may use both the old and the new allowance by unfortunate
     * transaction ordering. One possible solution to mitigate this race
     * condition is to first reduce the spender's allowance to 0 and set the
     * desired value afterwards:
     * https://github.com/ethereum/EIPs/issues/20#issuecomment-263524729
     *
     * Emits an {Approval} event.
     */
    function approve(address spender, uint256 amount) external returns (bool);

    /**
     * @dev Moves `amount` tokens from `from` to `to` using the
     * allowance mechanism. `amount` is then deducted from the caller's
     * allowance.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * Emits a {Transfer} event.
     */
    function transferFrom(
        address from,
        address to,
        uint256 amount
    ) external returns (bool);

    /**
     * @dev Emitted when `value` tokens are moved from one account (`from`) to
     * another (`to`).
     *
     * Note that `value` may be zero.
     */
    event Transfer(address indexed from, address indexed to, uint256 value);

    /**
     * @dev Emitted when the allowance of a `spender` for an `owner` is set by
     * a call to {approve}. `value` is the new allowance.
     */
    event Approval(address indexed owner, address indexed spender, uint256 value);
}


// File @openzeppelin/contracts/utils/Address.sol@v4.5.0

// OpenZeppelin Contracts (last updated v4.5.0) (utils/Address.sol)

pragma solidity ^0.8.1;

/**
 * @dev Collection of functions related to the address type
 */
library Address {
    /**
     * @dev Returns true if `account` is a contract.
     *
     * [IMPORTANT]
     * ====
     * It is unsafe to assume that an address for which this function returns
     * false is an externally-owned account (EOA) and not a contract.
     *
     * Among others, `isContract` will return false for the following
     * types of addresses:
     *
     *  - an externally-owned account
     *  - a contract in construction
     *  - an address where a contract will be created
     *  - an address where a contract lived, but was destroyed
     * ====
     *
     * [IMPORTANT]
     * ====
     * You shouldn't rely on `isContract` to protect against flash loan attacks!
     *
     * Preventing calls from contracts is highly discouraged. It breaks composability, breaks support for smart wallets
     * like Gnosis Safe, and does not provide security since it can be circumvented by calling from a contract
     * constructor.
     * ====
     */
    function isContract(address account) internal view returns (bool) {
        // This method relies on extcodesize/address.code.length, which returns 0
        // for contracts in construction, since the code is only stored at the end
        // of the constructor execution.

        return account.code.length > 0;
    }

    /**
     * @dev Replacement for Solidity's `transfer`: sends `amount` wei to
     * `recipient`, forwarding all available gas and reverting on errors.
     *
     * https://eips.ethereum.org/EIPS/eip-1884[EIP1884] increases the gas cost
     * of certain opcodes, possibly making contracts go over the 2300 gas limit
     * imposed by `transfer`, making them unable to receive funds via
     * `transfer`. {sendValue} removes this limitation.
     *
     * https://diligence.consensys.net/posts/2019/09/stop-using-soliditys-transfer-now/[Learn more].
     *
     * IMPORTANT: because control is transferred to `recipient`, care must be
     * taken to not create reentrancy vulnerabilities. Consider using
     * {ReentrancyGuard} or the
     * https://solidity.readthedocs.io/en/v0.5.11/security-considerations.html#use-the-checks-effects-interactions-pattern[checks-effects-interactions pattern].
     */
    function sendValue(address payable recipient, uint256 amount) internal {
        require(address(this).balance >= amount, "Address: insufficient balance");

        (bool success, ) = recipient.call{value: amount}("");
        require(success, "Address: unable to send value, recipient may have reverted");
    }

    /**
     * @dev Performs a Solidity function call using a low level `call`. A
     * plain `call` is an unsafe replacement for a function call: use this
     * function instead.
     *
     * If `target` reverts with a revert reason, it is bubbled up by this
     * function (like regular Solidity function calls).
     *
     * Returns the raw returned data. To convert to the expected return value,
     * use https://solidity.readthedocs.io/en/latest/units-and-global-variables.html?highlight=abi.decode#abi-encoding-and-decoding-functions[`abi.decode`].
     *
     * Requirements:
     *
     * - `target` must be a contract.
     * - calling `target` with `data` must not revert.
     *
     * _Available since v3.1._
     */
    function functionCall(address target, bytes memory data) internal returns (bytes memory) {
        return functionCall(target, data, "Address: low-level call failed");
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`], but with
     * `errorMessage` as a fallback revert reason when `target` reverts.
     *
     * _Available since v3.1._
     */
    function functionCall(
        address target,
        bytes memory data,
        string memory errorMessage
    ) internal returns (bytes memory) {
        return functionCallWithValue(target, data, 0, errorMessage);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but also transferring `value` wei to `target`.
     *
     * Requirements:
     *
     * - the calling contract must have an ETH balance of at least `value`.
     * - the called Solidity function must be `payable`.
     *
     * _Available since v3.1._
     */
    function functionCallWithValue(
        address target,
        bytes memory data,
        uint256 value
    ) internal returns (bytes memory) {
        return functionCallWithValue(target, data, value, "Address: low-level call with value failed");
    }

    /**
     * @dev Same as {xref-Address-functionCallWithValue-address-bytes-uint256-}[`functionCallWithValue`], but
     * with `errorMessage` as a fallback revert reason when `target` reverts.
     *
     * _Available since v3.1._
     */
    function functionCallWithValue(
        address target,
        bytes memory data,
        uint256 value,
        string memory errorMessage
    ) internal returns (bytes memory) {
        require(address(this).balance >= value, "Address: insufficient balance for call");
        require(isContract(target), "Address: call to non-contract");

        (bool success, bytes memory returndata) = target.call{value: value}(data);
        return verifyCallResult(success, returndata, errorMessage);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but performing a static call.
     *
     * _Available since v3.3._
     */
    function functionStaticCall(address target, bytes memory data) internal view returns (bytes memory) {
        return functionStaticCall(target, data, "Address: low-level static call failed");
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-string-}[`functionCall`],
     * but performing a static call.
     *
     * _Available since v3.3._
     */
    function functionStaticCall(
        address target,
        bytes memory data,
        string memory errorMessage
    ) internal view returns (bytes memory) {
        require(isContract(target), "Address: static call to non-contract");

        (bool success, bytes memory returndata) = target.staticcall(data);
        return verifyCallResult(success, returndata, errorMessage);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but performing a delegate call.
     *
     * _Available since v3.4._
     */
    function functionDelegateCall(address target, bytes memory data) internal returns (bytes memory) {
        return functionDelegateCall(target, data, "Address: low-level delegate call failed");
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-string-}[`functionCall`],
     * but performing a delegate call.
     *
     * _Available since v3.4._
     */
    function functionDelegateCall(
        address target,
        bytes memory data,
        string memory errorMessage
    ) internal returns (bytes memory) {
        require(isContract(target), "Address: delegate call to non-contract");

        (bool success, bytes memory returndata) = target.delegatecall(data);
        return verifyCallResult(success, returndata, errorMessage);
    }

    /**
     * @dev Tool to verifies that a low level call was successful, and revert if it wasn't, either by bubbling the
     * revert reason using the provided one.
     *
     * _Available since v4.3._
     */
    function verifyCallResult(
        bool success,
        bytes memory returndata,
        string memory errorMessage
    ) internal pure returns (bytes memory) {
        if (success) {
            return returndata;
        } else {
            // Look for revert reason and bubble it up if present
            if (returndata.length > 0) {
                // The easiest way to bubble the revert reason is using memory via assembly

                assembly {
                    let returndata_size := mload(returndata)
                    revert(add(32, returndata), returndata_size)
                }
            } else {
                revert(errorMessage);
            }
        }
    }
}


// File @openzeppelin/contracts/token/ERC20/utils/SafeERC20.sol@v4.5.0

// OpenZeppelin Contracts v4.4.1 (token/ERC20/utils/SafeERC20.sol)

pragma solidity ^0.8.0;


/**
 * @title SafeERC20
 * @dev Wrappers around ERC20 operations that throw on failure (when the token
 * contract returns false). Tokens that return no value (and instead revert or
 * throw on failure) are also supported, non-reverting calls are assumed to be
 * successful.
 * To use this library you can add a `using SafeERC20 for IERC20;` statement to your contract,
 * which allows you to call the safe operations as `token.safeTransfer(...)`, etc.
 */
library SafeERC20 {
    using Address for address;

    function safeTransfer(
        IERC20 token,
        address to,
        uint256 value
    ) internal {
        _callOptionalReturn(token, abi.encodeWithSelector(token.transfer.selector, to, value));
    }

    function safeTransferFrom(
        IERC20 token,
        address from,
        address to,
        uint256 value
    ) internal {
        _callOptionalReturn(token, abi.encodeWithSelector(token.transferFrom.selector, from, to, value));
    }

    /**
     * @dev Deprecated. This function has issues similar to the ones found in
     * {IERC20-approve}, and its usage is discouraged.
     *
     * Whenever possible, use {safeIncreaseAllowance} and
     * {safeDecreaseAllowance} instead.
     */
    function safeApprove(
        IERC20 token,
        address spender,
        uint256 value
    ) internal {
        // safeApprove should only be called when setting an initial allowance,
        // or when resetting it to zero. To increase and decrease it, use
        // 'safeIncreaseAllowance' and 'safeDecreaseAllowance'
        require(
            (value == 0) || (token.allowance(address(this), spender) == 0),
            "SafeERC20: approve from non-zero to non-zero allowance"
        );
        _callOptionalReturn(token, abi.encodeWithSelector(token.approve.selector, spender, value));
    }

    function safeIncreaseAllowance(
        IERC20 token,
        address spender,
        uint256 value
    ) internal {
        uint256 newAllowance = token.allowance(address(this), spender) + value;
        _callOptionalReturn(token, abi.encodeWithSelector(token.approve.selector, spender, newAllowance));
    }

    function safeDecreaseAllowance(
        IERC20 token,
        address spender,
        uint256 value
    ) internal {
        unchecked {
            uint256 oldAllowance = token.allowance(address(this), spender);
            require(oldAllowance >= value, "SafeERC20: decreased allowance below zero");
            uint256 newAllowance = oldAllowance - value;
            _callOptionalReturn(token, abi.encodeWithSelector(token.approve.selector, spender, newAllowance));
        }
    }

    /**
     * @dev Imitates a Solidity high-level call (i.e. a regular function call to a contract), relaxing the requirement
     * on the return value: the return value is optional (but if data is returned, it must not be false).
     * @param token The token targeted by the call.
     * @param data The call data (encoded using abi.encode or one of its variants).
     */
    function _callOptionalReturn(IERC20 token, bytes memory data) private {
        // We need to perform a low level call here, to bypass Solidity's return data size checking mechanism, since
        // we're implementing it ourselves. We use {Address.functionCall} to perform this call, which verifies that
        // the target address contains contract code and also asserts for success in the low-level call.

        bytes memory returndata = address(token).functionCall(data, "SafeERC20: low-level call failed");
        if (returndata.length > 0) {
            // Return data is optional
            require(abi.decode(returndata, (bool)), "SafeERC20: ERC20 operation did not succeed");
        }
    }
}


// File @openzeppelin/contracts/access/IAccessControl.sol@v4.5.0

// OpenZeppelin Contracts v4.4.1 (access/IAccessControl.sol)

pragma solidity ^0.8.0;

/**
 * @dev External interface of AccessControl declared to support ERC165 detection.
 */
interface IAccessControl {
    /**
     * @dev Emitted when `newAdminRole` is set as ``role``'s admin role, replacing `previousAdminRole`
     *
     * `DEFAULT_ADMIN_ROLE` is the starting admin for all roles, despite
     * {RoleAdminChanged} not being emitted signaling this.
     *
     * _Available since v3.1._
     */
    event RoleAdminChanged(bytes32 indexed role, bytes32 indexed previousAdminRole, bytes32 indexed newAdminRole);

    /**
     * @dev Emitted when `account` is granted `role`.
     *
     * `sender` is the account that originated the contract call, an admin role
     * bearer except when using {AccessControl-_setupRole}.
     */
    event RoleGranted(bytes32 indexed role, address indexed account, address indexed sender);

    /**
     * @dev Emitted when `account` is revoked `role`.
     *
     * `sender` is the account that originated the contract call:
     *   - if using `revokeRole`, it is the admin role bearer
     *   - if using `renounceRole`, it is the role bearer (i.e. `account`)
     */
    event RoleRevoked(bytes32 indexed role, address indexed account, address indexed sender);

    /**
     * @dev Returns `true` if `account` has been granted `role`.
     */
    function hasRole(bytes32 role, address account) external view returns (bool);

    /**
     * @dev Returns the admin role that controls `role`. See {grantRole} and
     * {revokeRole}.
     *
     * To change a role's admin, use {AccessControl-_setRoleAdmin}.
     */
    function getRoleAdmin(bytes32 role) external view returns (bytes32);

    /**
     * @dev Grants `role` to `account`.
     *
     * If `account` had not been already granted `role`, emits a {RoleGranted}
     * event.
     *
     * Requirements:
     *
     * - the caller must have ``role``'s admin role.
     */
    function grantRole(bytes32 role, address account) external;

    /**
     * @dev Revokes `role` from `account`.
     *
     * If `account` had been granted `role`, emits a {RoleRevoked} event.
     *
     * Requirements:
     *
     * - the caller must have ``role``'s admin role.
     */
    function revokeRole(bytes32 role, address account) external;

    /**
     * @dev Revokes `role` from the calling account.
     *
     * Roles are often managed via {grantRole} and {revokeRole}: this function's
     * purpose is to provide a mechanism for accounts to lose their privileges
     * if they are compromised (such as when a trusted device is misplaced).
     *
     * If the calling account had been granted `role`, emits a {RoleRevoked}
     * event.
     *
     * Requirements:
     *
     * - the caller must be `account`.
     */
    function renounceRole(bytes32 role, address account) external;
}


// File @openzeppelin/contracts/utils/Context.sol@v4.5.0

// OpenZeppelin Contracts v4.4.1 (utils/Context.sol)

pragma solidity ^0.8.0;

/**
 * @dev Provides information about the current execution context, including the
 * sender of the transaction and its data. While these are generally available
 * via msg.sender and msg.data, they should not be accessed in such a direct
 * manner, since when dealing with meta-transactions the account sending and
 * paying for execution may not be the actual sender (as far as an application
 * is concerned).
 *
 * This contract is only required for intermediate, library-like contracts.
 */
abstract contract Context {
    function _msgSender() internal view virtual returns (address) {
        return msg.sender;
    }

    function _msgData() internal view virtual returns (bytes calldata) {
        return msg.data;
    }
}


// File @openzeppelin/contracts/utils/Strings.sol@v4.5.0

// OpenZeppelin Contracts v4.4.1 (utils/Strings.sol)

pragma solidity ^0.8.0;

/**
 * @dev String operations.
 */
library Strings {
    bytes16 private constant _HEX_SYMBOLS = "0123456789abcdef";

    /**
     * @dev Converts a `uint256` to its ASCII `string` decimal representation.
     */
    function toString(uint256 value) internal pure returns (string memory) {
        // Inspired by OraclizeAPI's implementation - MIT licence
        // https://github.com/oraclize/ethereum-api/blob/b42146b063c7d6ee1358846c198246239e9360e8/oraclizeAPI_0.4.25.sol

        if (value == 0) {
            return "0";
        }
        uint256 temp = value;
        uint256 digits;
        while (temp != 0) {
            digits++;
            temp /= 10;
        }
        bytes memory buffer = new bytes(digits);
        while (value != 0) {
            digits -= 1;
            buffer[digits] = bytes1(uint8(48 + uint256(value % 10)));
            value /= 10;
        }
        return string(buffer);
    }

    /**
     * @dev Converts a `uint256` to its ASCII `string` hexadecimal representation.
     */
    function toHexString(uint256 value) internal pure returns (string memory) {
        if (value == 0) {
            return "0x00";
        }
        uint256 temp = value;
        uint256 length = 0;
        while (temp != 0) {
            length++;
            temp >>= 8;
        }
        return toHexString(value, length);
    }

    /**
     * @dev Converts a `uint256` to its ASCII `string` hexadecimal representation with fixed length.
     */
    function toHexString(uint256 value, uint256 length) internal pure returns (string memory) {
        bytes memory buffer = new bytes(2 * length + 2);
        buffer[0] = "0";
        buffer[1] = "x";
        for (uint256 i = 2 * length + 1; i > 1; --i) {
            buffer[i] = _HEX_SYMBOLS[value & 0xf];
            value >>= 4;
        }
        require(value == 0, "Strings: hex length insufficient");
        return string(buffer);
    }
}


// File @openzeppelin/contracts/utils/introspection/IERC165.sol@v4.5.0

// OpenZeppelin Contracts v4.4.1 (utils/introspection/IERC165.sol)

pragma solidity ^0.8.0;

/**
 * @dev Interface of the ERC165 standard, as defined in the
 * https://eips.ethereum.org/EIPS/eip-165[EIP].
 *
 * Implementers can declare support of contract interfaces, which can then be
 * queried by others ({ERC165Checker}).
 *
 * For an implementation, see {ERC165}.
 */
interface IERC165 {
    /**
     * @dev Returns true if this contract implements the interface defined by
     * `interfaceId`. See the corresponding
     * https://eips.ethereum.org/EIPS/eip-165#how-interfaces-are-identified[EIP section]
     * to learn more about how these ids are created.
     *
     * This function call must use less than 30 000 gas.
     */
    function supportsInterface(bytes4 interfaceId) external view returns (bool);
}


// File @openzeppelin/contracts/utils/introspection/ERC165.sol@v4.5.0

// OpenZeppelin Contracts v4.4.1 (utils/introspection/ERC165.sol)

pragma solidity ^0.8.0;

/**
 * @dev Implementation of the {IERC165} interface.
 *
 * Contracts that want to implement ERC165 should inherit from this contract and override {supportsInterface} to check
 * for the additional interface id that will be supported. For example:
 *
 * ```solidity
 * function supportsInterface(bytes4 interfaceId) public view virtual override returns (bool) {
 *     return interfaceId == type(MyInterface).interfaceId || super.supportsInterface(interfaceId);
 * }
 * ```
 *
 * Alternatively, {ERC165Storage} provides an easier to use but more expensive implementation.
 */
abstract contract ERC165 is IERC165 {
    /**
     * @dev See {IERC165-supportsInterface}.
     */
    function supportsInterface(bytes4 interfaceId) public view virtual override returns (bool) {
        return interfaceId == type(IERC165).interfaceId;
    }
}


// File @openzeppelin/contracts/access/AccessControl.sol@v4.5.0

// OpenZeppelin Contracts (last updated v4.5.0) (access/AccessControl.sol)

pragma solidity ^0.8.0;




/**
 * @dev Contract module that allows children to implement role-based access
 * control mechanisms. This is a lightweight version that doesn't allow enumerating role
 * members except through off-chain means by accessing the contract event logs. Some
 * applications may benefit from on-chain enumerability, for those cases see
 * {AccessControlEnumerable}.
 *
 * Roles are referred to by their `bytes32` identifier. These should be exposed
 * in the external API and be unique. The best way to achieve this is by
 * using `public constant` hash digests:
 *
 * ```
 * bytes32 public constant MY_ROLE = keccak256("MY_ROLE");
 * ```
 *
 * Roles can be used to represent a set of permissions. To restrict access to a
 * function call, use {hasRole}:
 *
 * ```
 * function foo() public {
 *     require(hasRole(MY_ROLE, msg.sender));
 *     ...
 * }
 * ```
 *
 * Roles can be granted and revoked dynamically via the {grantRole} and
 * {revokeRole} functions. Each role has an associated admin role, and only
 * accounts that have a role's admin role can call {grantRole} and {revokeRole}.
 *
 * By default, the admin role for all roles is `DEFAULT_ADMIN_ROLE`, which means
 * that only accounts with this role will be able to grant or revoke other
 * roles. More complex role relationships can be created by using
 * {_setRoleAdmin}.
 *
 * WARNING: The `DEFAULT_ADMIN_ROLE` is also its own admin: it has permission to
 * grant and revoke this role. Extra precautions should be taken to secure
 * accounts that have been granted it.
 */
abstract contract AccessControl is Context, IAccessControl, ERC165 {
    struct RoleData {
        mapping(address => bool) members;
        bytes32 adminRole;
    }

    mapping(bytes32 => RoleData) private _roles;

    bytes32 public constant DEFAULT_ADMIN_ROLE = 0x00;

    /**
     * @dev Modifier that checks that an account has a specific role. Reverts
     * with a standardized message including the required role.
     *
     * The format of the revert reason is given by the following regular expression:
     *
     *  /^AccessControl: account (0x[0-9a-f]{40}) is missing role (0x[0-9a-f]{64})$/
     *
     * _Available since v4.1._
     */
    modifier onlyRole(bytes32 role) {
        _checkRole(role, _msgSender());
        _;
    }

    /**
     * @dev See {IERC165-supportsInterface}.
     */
    function supportsInterface(bytes4 interfaceId) public view virtual override returns (bool) {
        return interfaceId == type(IAccessControl).interfaceId || super.supportsInterface(interfaceId);
    }

    /**
     * @dev Returns `true` if `account` has been granted `role`.
     */
    function hasRole(bytes32 role, address account) public view virtual override returns (bool) {
        return _roles[role].members[account];
    }

    /**
     * @dev Revert with a standard message if `account` is missing `role`.
     *
     * The format of the revert reason is given by the following regular expression:
     *
     *  /^AccessControl: account (0x[0-9a-f]{40}) is missing role (0x[0-9a-f]{64})$/
     */
    function _checkRole(bytes32 role, address account) internal view virtual {
        if (!hasRole(role, account)) {
            revert(
                string(
                    abi.encodePacked(
                        "AccessControl: account ",
                        Strings.toHexString(uint160(account), 20),
                        " is missing role ",
                        Strings.toHexString(uint256(role), 32)
                    )
                )
            );
        }
    }

    /**
     * @dev Returns the admin role that controls `role`. See {grantRole} and
     * {revokeRole}.
     *
     * To change a role's admin, use {_setRoleAdmin}.
     */
    function getRoleAdmin(bytes32 role) public view virtual override returns (bytes32) {
        return _roles[role].adminRole;
    }

    /**
     * @dev Grants `role` to `account`.
     *
     * If `account` had not been already granted `role`, emits a {RoleGranted}
     * event.
     *
     * Requirements:
     *
     * - the caller must have ``role``'s admin role.
     */
    function grantRole(bytes32 role, address account) public virtual override onlyRole(getRoleAdmin(role)) {
        _grantRole(role, account);
    }

    /**
     * @dev Revokes `role` from `account`.
     *
     * If `account` had been granted `role`, emits a {RoleRevoked} event.
     *
     * Requirements:
     *
     * - the caller must have ``role``'s admin role.
     */
    function revokeRole(bytes32 role, address account) public virtual override onlyRole(getRoleAdmin(role)) {
        _revokeRole(role, account);
    }

    /**
     * @dev Revokes `role` from the calling account.
     *
     * Roles are often managed via {grantRole} and {revokeRole}: this function's
     * purpose is to provide a mechanism for accounts to lose their privileges
     * if they are compromised (such as when a trusted device is misplaced).
     *
     * If the calling account had been revoked `role`, emits a {RoleRevoked}
     * event.
     *
     * Requirements:
     *
     * - the caller must be `account`.
     */
    function renounceRole(bytes32 role, address account) public virtual override {
        require(account == _msgSender(), "AccessControl: can only renounce roles for self");

        _revokeRole(role, account);
    }

    /**
     * @dev Grants `role` to `account`.
     *
     * If `account` had not been already granted `role`, emits a {RoleGranted}
     * event. Note that unlike {grantRole}, this function doesn't perform any
     * checks on the calling account.
     *
     * [WARNING]
     * ====
     * This function should only be called from the constructor when setting
     * up the initial roles for the system.
     *
     * Using this function in any other way is effectively circumventing the admin
     * system imposed by {AccessControl}.
     * ====
     *
     * NOTE: This function is deprecated in favor of {_grantRole}.
     */
    function _setupRole(bytes32 role, address account) internal virtual {
        _grantRole(role, account);
    }

    /**
     * @dev Sets `adminRole` as ``role``'s admin role.
     *
     * Emits a {RoleAdminChanged} event.
     */
    function _setRoleAdmin(bytes32 role, bytes32 adminRole) internal virtual {
        bytes32 previousAdminRole = getRoleAdmin(role);
        _roles[role].adminRole = adminRole;
        emit RoleAdminChanged(role, previousAdminRole, adminRole);
    }

    /**
     * @dev Grants `role` to `account`.
     *
     * Internal function without access restriction.
     */
    function _grantRole(bytes32 role, address account) internal virtual {
        if (!hasRole(role, account)) {
            _roles[role].members[account] = true;
            emit RoleGranted(role, account, _msgSender());
        }
    }

    /**
     * @dev Revokes `role` from `account`.
     *
     * Internal function without access restriction.
     */
    function _revokeRole(bytes32 role, address account) internal virtual {
        if (hasRole(role, account)) {
            _roles[role].members[account] = false;
            emit RoleRevoked(role, account, _msgSender());
        }
    }
}


// File @openzeppelin/contracts/security/Pausable.sol@v4.5.0

// OpenZeppelin Contracts v4.4.1 (security/Pausable.sol)

pragma solidity ^0.8.0;

/**
 * @dev Contract module which allows children to implement an emergency stop
 * mechanism that can be triggered by an authorized account.
 *
 * This module is used through inheritance. It will make available the
 * modifiers `whenNotPaused` and `whenPaused`, which can be applied to
 * the functions of your contract. Note that they will not be pausable by
 * simply including this module, only once the modifiers are put in place.
 */
abstract contract Pausable is Context {
    /**
     * @dev Emitted when the pause is triggered by `account`.
     */
    event Paused(address account);

    /**
     * @dev Emitted when the pause is lifted by `account`.
     */
    event Unpaused(address account);

    bool private _paused;

    /**
     * @dev Initializes the contract in unpaused state.
     */
    constructor() {
        _paused = false;
    }

    /**
     * @dev Returns true if the contract is paused, and false otherwise.
     */
    function paused() public view virtual returns (bool) {
        return _paused;
    }

    /**
     * @dev Modifier to make a function callable only when the contract is not paused.
     *
     * Requirements:
     *
     * - The contract must not be paused.
     */
    modifier whenNotPaused() {
        require(!paused(), "Pausable: paused");
        _;
    }

    /**
     * @dev Modifier to make a function callable only when the contract is paused.
     *
     * Requirements:
     *
     * - The contract must be paused.
     */
    modifier whenPaused() {
        require(paused(), "Pausable: not paused");
        _;
    }

    /**
     * @dev Triggers stopped state.
     *
     * Requirements:
     *
     * - The contract must not be paused.
     */
    function _pause() internal virtual whenNotPaused {
        _paused = true;
        emit Paused(_msgSender());
    }

    /**
     * @dev Returns to normal state.
     *
     * Requirements:
     *
     * - The contract must be paused.
     */
    function _unpause() internal virtual whenPaused {
        _paused = false;
        emit Unpaused(_msgSender());
    }
}




pragma solidity ^0.8.13;



interface IERC721Mint {
    function ownerOf(uint256 tokenId) external view returns (address);

    function mint(address receiver) external returns (uint256);
}

interface IERC1155 {
    function balanceOf(address account, uint256 id) external view returns (uint256);

    function balanceOfBatch(address[] memory accounts, uint256[] memory ids) external view returns (uint256[] memory);

    function isApprovedForAll(address account, address operator) external view returns (bool);

    function burn(address account, uint256 id, uint256 value) external;

    function burnBatch(address account, uint256[] memory ids, uint256[] memory values) external;
}

contract SummonWizard is AccessControl, Pausable {
    using SafeERC20 for IERC20;

    bytes32 public constant MODIFY_ENTITIES_ROLE = keccak256("MODIFY_ENTITIES_ROLE");
    uint256 private constant SINGLE_WIZARD_MULTIPLY_COST = 2;

    IERC20 tokenContract;
    IERC721Mint landContract;
    IERC721Mint wizardContract;
    IERC1155 enhancementContract;

    address public tokenBankAddress;
    uint32 public landCooldownPeriod = 172800; // initial 48h
    uint32 public wizardMaturePeriod = 259200; // initial 72h
    uint8 public wizardSoulsDefaultAmount = 6; // amount of souls +1 because it stops at "1"
    uint256[] public summonCosts = [100000, 60000, 37500, 22500, 15000]; // per single Wizard, so for 2x Wizard it will be 2x
    uint256[] public singleUseLandEnhancementIds = [1];
    uint256[] public singleUseWizardEnhancementIds = [0];
    mapping(uint256 => LandCooldown) public landsCooldown;
    mapping(uint256 => Wizard) public wizards;

    struct Wizard {
        uint8 soulsRemainingAmount; //0=uninitialized, 1=no more souls, 6=default souls amount
        uint32 matureTimestamp;
    }

    struct LandCooldown {
        uint32 start;
        uint32 period;
    }

    struct SingleWizardCost {
        uint256 id;
        uint256 cost;
    }

    event Summoned(address receiver, uint256 newWizardId, uint256[] landIds, uint256[] wizardIds, uint256[] ingredientIds, uint256 costSum);
    event SummonedExtra(address receiver, uint256 newWizardId, uint256[] landIds, uint256[] wizardIds, uint256[] ingredientIds);
    event IncreasedWizardSoulAmount(uint256 wizardId, uint8 remainingSoulsAmount);
    event DecreasedWizardSoulAmount(uint256 wizardId, uint8 remainingSoulsAmount);
    event SetLandCooldown(uint256 landId, uint32 period);
    event SetWizardMatureTimestamp(uint256 wizardId, uint32 period);
    event SetSummonCosts(uint256[] _costs);

    constructor (
        IERC20 _tokenAddress,
        IERC721Mint _landContractAddress,
        IERC721Mint _wizardContractAddress,
        IERC1155 _enhancementContractAddress
    ) {
        tokenContract = _tokenAddress;
        landContract = _landContractAddress;
        wizardContract = _wizardContractAddress;
        enhancementContract = _enhancementContractAddress;

        tokenBankAddress = _msgSender();

        _setupRole(DEFAULT_ADMIN_ROLE, _msgSender());
        _setupRole(MODIFY_ENTITIES_ROLE, _msgSender());
    }

    function summon(
        uint256[] calldata _landIds,
        uint256[] calldata _wizardIds,
        uint256[] calldata _ingredientIds
    ) external whenNotPaused returns (uint256){
        // Input validation
        require(_landIds.length <= 1, "MAX_1_LAND");
        require(_wizardIds.length <= 2, "MAX_2_WIZARDS");
        require(_ingredientIds.length <= 3, "MAX_3_INGREDIENTS");

        uint256 _i;
        uint256 _j;

        // can be negative
        int256 _landNftAmount = 1;
        int256 _wizardNftAmount = 2;

        // Check Ingredients
        for (_i = 0; _i < _ingredientIds.length; _i++) {
            // If single-land given then reduce amount of required wizards
            for (_j = 0; _j < singleUseLandEnhancementIds.length; _j++) {
                if (_ingredientIds[_i] == singleUseLandEnhancementIds[_j]) {
                    _landNftAmount--;
                    break;
                }
            }

            // If single-wizard given then reduce amount of required wizards
            for (_j = 0; _j < singleUseWizardEnhancementIds.length; _j++) {
                if (_ingredientIds[_i] == singleUseWizardEnhancementIds[_j]) {
                    _wizardNftAmount--;
                    break;
                }
            }
        }

        // Validation
        require(_wizardNftAmount >= 0 && _landNftAmount >= 0, "TOO_MANY_SINGLE_WIZARD_OR_LAND_INGREDIENTS");
        require(_landIds.length == uint256(_landNftAmount), "WRONG_LAND_AMOUNT");
        require(_wizardIds.length == uint256(_wizardNftAmount), "WRONG_WIZARDS_AMOUNT_OR_EPHEMERAL_ELIXIRS");
        if (2 == _wizardIds.length) {
            require(_wizardIds[0] != _wizardIds[1], "WIZARDS_CAN_NOT_BE_THE_SAME");
        }

        // Check if last Land cooldown allows to make summoning & sender is Land owner
        if (_landIds.length > 0) {
            require(_msgSender() == landContract.ownerOf(_landIds[0]), "USER_IS_NOT_LAND_OWNER");
            require((landsCooldown[_landIds[0]].start + landsCooldown[_landIds[0]].period) <= uint32(block.timestamp), "LAND_DURING_COOLDOWN");

            // Set a new cooldown start for used land
            landsCooldown[_landIds[0]].start = uint32(block.timestamp);
            landsCooldown[_landIds[0]].period = landCooldownPeriod;
        }

        // Calculate token cost
        uint256 _costSum = _summonCost(_wizardIds);

        // Check if is owner of selected Wizard
        for (_i = 0; _i < _wizardIds.length; _i++) {
            require(_msgSender() == wizardContract.ownerOf(_wizardIds[_i]), "USER_IS_NOT_WIZARD_OWNER");

            // Check if Wizard is matured
            require(_isWizardMatured(_wizardIds[_i]), "WIZARD_IS_NOT_YET_MATURED");

            _decreaseWizardSoulAmount(_wizardIds[_i]);
        }

        // Transfer user's SCRL to our Bank address
        tokenContract.transferFrom(_msgSender(), tokenBankAddress, _costSum);

        // Burn all used ingredients
        if (_ingredientIds.length > 0) {
            _burn(_ingredientIds);
        }

        // Mint a new Wizard for user
        uint256 _newWizardId = wizardContract.mint(_msgSender());

        // Set Wizard mature time
        wizards[_newWizardId].matureTimestamp = uint32((block.timestamp) + uint256(wizardMaturePeriod));

        emit Summoned(_msgSender(), _newWizardId, _landIds, _wizardIds, _ingredientIds, _costSum);

        return _newWizardId;
    }

    function summonExtra(
        address receiver,
        uint256[] calldata _landIds,
        uint256[] calldata _wizardIds,
        uint256[] calldata _ingredientIds
    ) external whenNotPaused onlyRole(MODIFY_ENTITIES_ROLE) returns (uint256){
        // Input validation
        require(_landIds.length <= 1, "MAX_1_LAND");
        require(_wizardIds.length <= 2, "MAX_2_WIZARDS");
        require(_ingredientIds.length <= 3, "MAX_3_INGREDIENTS");

        uint256 _i;
        uint256 _j;

        // can be negative
        int256 _landNftAmount = 1;
        int256 _wizardNftAmount = 2;

        // Check Ingredients
        for (_i = 0; _i < _ingredientIds.length; _i++) {
            // If single-land given then reduce amount of required wizards
            for (_j = 0; _j < singleUseLandEnhancementIds.length; _j++) {
                if (_ingredientIds[_i] == singleUseLandEnhancementIds[_j]) {
                    _landNftAmount--;
                    break;
                }
            }

            // If single-wizard given then reduce amount of required wizards
            for (_j = 0; _j < singleUseWizardEnhancementIds.length; _j++) {
                if (_ingredientIds[_i] == singleUseWizardEnhancementIds[_j]) {
                    _wizardNftAmount--;
                    break;
                }
            }
        }

        // Validation
        require(_wizardNftAmount >= 0 && _landNftAmount >= 0, "TOO_MANY_SINGLE_WIZARD_OR_LAND_INGREDIENTS");
        require(_landIds.length == uint256(_landNftAmount), "WRONG_LAND_AMOUNT");
        require(_wizardIds.length == uint256(_wizardNftAmount), "WRONG_WIZARDS_AMOUNT_OR_EPHEMERAL_ELIXIRS");
        if (2 == _wizardIds.length) {
            require(_wizardIds[0] != _wizardIds[1], "WIZARDS_CAN_NOT_BE_THE_SAME");
        }

        // Mint a new Wizard for user
        uint256 _newWizardId = wizardContract.mint(receiver);

        // Set Wizard mature time
        wizards[_newWizardId].matureTimestamp = uint32((block.timestamp) + uint256(wizardMaturePeriod));

        emit SummonedExtra(receiver, _newWizardId, _landIds, _wizardIds, _ingredientIds);

        return _newWizardId;
    }

    function summonCostPerWizard(uint256[] calldata _wizardIds) external view returns (SingleWizardCost[] memory _costsStruct, uint256[] memory _costsArray){
        _costsStruct = new SingleWizardCost[](_wizardIds.length);
        _costsArray = new uint256[](_wizardIds.length);
        uint256[] memory _singleWizardIdArray = new uint256[](1);
        uint256 _singleWizardCost;
        for (uint256 _i = 0; _i < _wizardIds.length; _i++) {
            // Check if Wizard exists
            try wizardContract.ownerOf(_wizardIds[_i]){
                // Cost for only one Wizard per entry
                _singleWizardIdArray[0] = _wizardIds[_i];
                _singleWizardCost = _summonCost(_singleWizardIdArray) / SINGLE_WIZARD_MULTIPLY_COST;
            } catch {
                // No Wizard, so cost 0
                _singleWizardCost = 0;
            }

            // Put into output struct
            _costsStruct[_i] = SingleWizardCost(
                _wizardIds[_i],
                _singleWizardCost
            );
            _costsArray[_i] = _singleWizardCost;
        }
    }

    function summonCostForWizardsWithItems(uint256[] calldata _wizardIds, uint256[] calldata _ingredientIds) external view returns (uint256){
        require(_wizardIds.length <= 2, "MAX_2_WIZARDS");
        require(_ingredientIds.length <= 3, "MAX_3_INGREDIENTS");

        uint256 _i;
        uint256 _j;
        int256 _wizardNftAmount = 2;
        for (_i = 0; _i < _ingredientIds.length; _i++) {
            for (_j = 0; _j < singleUseWizardEnhancementIds.length; _j++) {
                if (_ingredientIds[_i] == singleUseWizardEnhancementIds[_j]) {
                    _wizardNftAmount--;
                    break;
                }
            }
        }

        require(_wizardNftAmount >= 0, "TOO_MANY_SINGLE_WIZARDS");
        require(_wizardIds.length == uint256(_wizardNftAmount), "WRONG_WIZARDS_AMOUNT_OR_EPHEMERAL_ELIXIRS");
        if (2 == _wizardIds.length) {
            require(_wizardIds[0] != _wizardIds[1], "WIZARDS_CAN_NOT_BE_THE_SAME");
        }

        return _summonCost(_wizardIds);
    }

    function setTokenBankAddress(address _tokenBankAddress) external onlyRole(DEFAULT_ADMIN_ROLE) {
        tokenBankAddress = _tokenBankAddress;
    }

    function setLandCooldownPeriod(uint32 _landCooldownPeriod) external onlyRole(DEFAULT_ADMIN_ROLE) {
        landCooldownPeriod = _landCooldownPeriod;
    }

    function setSingleUseLandEnhancementIds(uint256[] calldata _ids) external onlyRole(DEFAULT_ADMIN_ROLE) {
        delete singleUseLandEnhancementIds;
        for (uint256 i = 0; i < _ids.length; i++) {
            singleUseLandEnhancementIds.push(_ids[i]);
        }
    }

    function setSingleUseWizardEnhancementIds(uint256[] calldata _ids) external onlyRole(DEFAULT_ADMIN_ROLE) {
        delete singleUseWizardEnhancementIds;
        for (uint256 i = 0; i < _ids.length; i++) {
            singleUseWizardEnhancementIds.push(_ids[i]);
        }
    }

    function setWizardSoulsDefaultAmount(uint8 _souls) external onlyRole(DEFAULT_ADMIN_ROLE) {
        wizardSoulsDefaultAmount = _souls;
    }

    function setSummonCosts(uint256[] calldata _costs) external onlyRole(DEFAULT_ADMIN_ROLE) {
        delete summonCosts;
        for (uint256 i = 0; i < _costs.length; i++) {
            require(_costs[i] > 0, "COST_MUST_BE_ABOVE_0");
            summonCosts.push(_costs[i]);
        }

        emit SetSummonCosts(_costs);
    }

    function setSingleLandCooldownPeriod(uint256 _landId, uint32 _period) external whenNotPaused onlyRole(MODIFY_ENTITIES_ROLE) {
        landsCooldown[_landId].period = _period;

        emit SetLandCooldown(_landId, _period);
    }

    function setGlobalWizardMaturePeriod(uint32 _wizardMaturePeriod) external onlyRole(DEFAULT_ADMIN_ROLE) {
        wizardMaturePeriod = _wizardMaturePeriod;
    }

    function setSingleWizardMatureTimestamp(uint256 _wizardId, uint32 _timestamp) external whenNotPaused onlyRole(MODIFY_ENTITIES_ROLE) {
        wizards[_wizardId].matureTimestamp = _timestamp;

        emit SetWizardMatureTimestamp(_wizardId, _timestamp);
    }

    function isWizardMatured(uint256 _wizardId) external view returns (bool){
        return _isWizardMatured(_wizardId);
    }

    function _isWizardMatured(uint256 _wizardId) internal view returns (bool){
        return uint32(block.timestamp) >= wizards[_wizardId].matureTimestamp;
    }

    function initializeWizardSouls(uint256 _wizardId) external onlyRole(MODIFY_ENTITIES_ROLE) {
        _setupWizard(_wizardId);
    }

    // Returns in "human form", so real amount of souls (so 5x instead of 6x)
    function getWizardSoulsRemainingAmount(uint256 _wizardId) external view returns (uint8) {
        // Wizard not in mapping
        if (0 == wizards[_wizardId].soulsRemainingAmount) {
            // Check if Wizard exists
            try wizardContract.ownerOf(_wizardId){
                return wizardSoulsDefaultAmount - 1;
            } catch {
                return 0;
            }
        } else {
            // Wizard in mapping
            // Decrease by 1 (6-1=5) to return a real amount of souls externally
            return wizards[_wizardId].soulsRemainingAmount - 1;
        }
    }

    function increaseWizardSoulAmount(uint256 _wizardId) external whenNotPaused onlyRole(MODIFY_ENTITIES_ROLE) {
        // Set 5 souls if not performed any summoning yet
        if (0 == wizards[_wizardId].soulsRemainingAmount) {
            // 5 souls but we stop at 1, so 5+1=6
            wizards[_wizardId].soulsRemainingAmount = wizardSoulsDefaultAmount;
        }

        wizards[_wizardId].soulsRemainingAmount++;

        emit IncreasedWizardSoulAmount(_wizardId, wizards[_wizardId].soulsRemainingAmount);
    }

    function decreaseWizardSoulAmount(uint256 _wizardId) external whenNotPaused onlyRole(MODIFY_ENTITIES_ROLE) {
        _decreaseWizardSoulAmount(_wizardId);

        emit DecreasedWizardSoulAmount(_wizardId, wizards[_wizardId].soulsRemainingAmount);
    }

    function _decreaseWizardSoulAmount(uint256 _wizardId) internal {
        _setupWizard(_wizardId);

        require(wizards[_wizardId].soulsRemainingAmount > 1, "SOULLESS_WIZARD");
        wizards[_wizardId].soulsRemainingAmount--;
    }

    function _setupWizard(uint256 _wizardId) internal {
        // Set 5 souls if not performed any summoning yet
        if (0 == wizards[_wizardId].soulsRemainingAmount) {
            // 5 souls but we stop at 1, so 5+1=6
            wizards[_wizardId].soulsRemainingAmount = wizardSoulsDefaultAmount;
        }
    }

    function _summonCost(uint256[] memory _wizardIds) internal view returns (uint256){
        uint256 _i;
        uint256 _cost = 0;
        uint256 _costSum = 0;
        uint256 _tmpWizardSoulsRemainingAmount;

        for (_i = 0; _i < _wizardIds.length; _i++) {
            // Calculate cost
            if (0 == wizards[_wizardIds[_i]].soulsRemainingAmount) {
                // 5 souls but we stop at 1, so 5+1=6
                _tmpWizardSoulsRemainingAmount = wizardSoulsDefaultAmount;
            } else {
                _tmpWizardSoulsRemainingAmount = wizards[_wizardIds[_i]].soulsRemainingAmount;
            }

            require(_tmpWizardSoulsRemainingAmount > 1, "SOULLESS_WIZARD");
            _tmpWizardSoulsRemainingAmount --;

            if (summonCosts.length < _tmpWizardSoulsRemainingAmount) {
                // If amount of souls is out of price boundaries, e.g. more souls than usual
                // then set cost equal minimal cost for all extra souls
                _cost = summonCosts[summonCosts.length - 1];
            } else {
                _cost = summonCosts[_tmpWizardSoulsRemainingAmount - 1];
            }

            _costSum += _cost;
        }

        // Cost of single-use wizards
        if (1 == _wizardIds.length) {
            // One single-use wizard used: double used wizard cost
            _costSum = _costSum * SINGLE_WIZARD_MULTIPLY_COST;
        } else if (0 == _wizardIds.length) {
            // Two single-use wizard used: 2x minimal cost
            _costSum += summonCosts[summonCosts.length - 1] * 2;
        }

        _costSum = _costSum * 10 ** 18;

        return _costSum;
    }

    function _burn(uint256[] calldata _ingredientIds) internal {
        uint256[] memory _burnValues = new uint256[](_ingredientIds.length);
        uint256 _i;
        for (_i = 0; _i < _ingredientIds.length; _i++) {
            _burnValues[_i] = 1;
        }
        enhancementContract.burnBatch(_msgSender(), _ingredientIds, _burnValues);
    }

    function returnAccidentallySent(IERC20 _tokenToSent) external onlyRole(DEFAULT_ADMIN_ROLE) {
        uint256 _amount = _tokenToSent.balanceOf(address(this));
        _tokenToSent.safeTransfer(_msgSender(), _amount);
    }

    function pause() public onlyRole(DEFAULT_ADMIN_ROLE) {
        _pause();
    }

    function unpause() public onlyRole(DEFAULT_ADMIN_ROLE) {
        _unpause();
    }
}