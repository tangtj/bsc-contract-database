
// File: contracts/SCVFarm.sol
// SPDX-License-Identifier: MIT

pragma solidity ^0.8.4;

import '@openzeppelin/contracts/token/ERC20/ERC20.sol';
import '@openzeppelin/contracts/token/ERC20/IERC20.sol';
import '@openzeppelin/contracts/token/ERC20/utils/SafeERC20.sol';
import '@openzeppelin/contracts/token/ERC721/IERC721.sol';
import '@openzeppelin/contracts/access/Ownable.sol';
import '@openzeppelin/contracts/security/ReentrancyGuard.sol';
import '@openzeppelin/contracts/security/Pausable.sol';
import '@openzeppelin/contracts/utils/math/Math.sol';

import './libraries/OpenableNFT.sol';
import './interfaces/IStrategy.sol';
import './NFTSpecSupport.sol';
import './RewardSupport.sol';

contract SCVFarm is Ownable, ReentrancyGuard, NFTSpecSupport, Pausable, RewardSupport {
    using SafeERC20 for IERC20;

    // Info of each user.
    struct UserInfo {
        uint256 shares; // How many LP tokens the user has provided.
        uint256 rewardDebt; // Reward debt. See explanation below.
        // NFT spec
        bool hasNFT;
        uint256 nftSpecId;
        address nftInviter;
        uint256 weightedShare;
        uint256 bnbRewardDebt;
        uint256 lastRewardWindow; // last updated window
    }

    struct PoolInfo {
        IERC20 want; // Address of the want token.
        uint256 allocPoint; // How many allocation points assigned to this pool. AUTO to distribute per block.
        uint256 bnbAllocPoint;
        uint256 lastRewardBlock; // Last block number that distribution occurs.
        uint256 accRewardPerShare; // Accumulated per weighted share, times 1e12. See below.
        address strat; // Strategy address that will auto compound want tokens
        uint256 weightedShareTotal; // weightedShareTotal
        uint256 lastUpdatedWindow;
    }

    struct BNBRewardWindow {
        uint256 startBlock; // [startBlock, endBlock)
        uint256 endBlock;
        uint256 totalRewards;
        uint256 rewardPerBlock; // totalRewards / (startBlock - endBlock)
    }

    address public burnAddress = 0x000000000000000000000000000000000000dEaD;
    address public constant emptyAddress = address(0);

    address public feeWallet;

    uint256 public accReward = 0; // accumulated SCV reward
    uint256 public maxReward = 3_370_000_000 * 1e18; // max SCV reward
    uint256 public rewardPerBlock = 600 * 1e18; // scv reward per block for the whole farm

    uint256 public startBlock;

    PoolInfo[] public poolInfo; // Info of each pool.
    mapping(uint256 => mapping(address => UserInfo)) public userInfo; // poolId -> (user address -> User Info)
    BNBRewardWindow[] public bnbRewardWindows;
    mapping(uint256 => mapping(uint256 => uint256)) public accBNBRewardPerWeightShare; // poolId -> wid -> value (*1e12)

    uint256 public totalAllocPoint = 0; // Total allocation points. Must be the sum of all allocation points in all pools.
    uint256 public totalBNBAllocPoint = 0; // ditto

    uint256 public inviterSharePct = 50000; // 5%, percentage rate for inviter (scale to 1e6)
    uint256 public depositFeePct = 1000; // 0.1% percentage rate for deposit fee (scale to 1e6)
    uint256 public constant pctBase = 1e6;

    // --------------------------------
    // events

    event Deposit(
        address indexed user,
        uint256 indexed pid,
        uint256 amount,
        address token,
        uint256 tokenId
    );
    event Withdraw(address indexed user, uint256 indexed pid, uint256 amount);
    event EmergencyWithdraw(address indexed user, uint256 indexed pid, uint256 amount);
    event MigrateFarmTo(address indexed to, uint256 indexed amount);
    event BurnNFT(address indexed nft, uint256 indexed tokenId);
    event UpdateInviterShare(uint256 pct);
    event UpdateDepositFee(uint256 pct);
    event UpdateRewardPerBlock(uint256 pct);
    event AddRewardWindow(uint256 indexed startBlock, uint256 indexed endBlock, uint256 amount);
    event Claim(address indexed user, uint256 indexed pid, uint256 scvReward, uint256 bnbReward);

    constructor(
        uint256 startBlock_,
        address feeWallet_,
        address scv_,
        address specAddr_
    ) {
        startBlock = startBlock_;
        feeWallet = feeWallet_;
        scv = scv_;

        specAddress = INFTSpec(specAddr_);
    }

    receive() external payable {}

    function pause() external onlyOwner {
        _pause();
    }

    function unpause() external onlyOwner {
        _unpause();
    }

    function poolLength() external view returns (uint256) {
        return poolInfo.length;
    }

    function windowLength() external view returns (uint256) {
        return bnbRewardWindows.length;
    }

    function updateInviterSharePct(uint256 newValue) public onlyOwner {
        inviterSharePct = newValue;
        emit UpdateInviterShare(newValue);
    }

    function updateDepositFeePct(uint256 newValue) public onlyOwner {
        depositFeePct = newValue;
        emit UpdateDepositFee(newValue);
    }

    function updateRewardPerBlock(uint256 newValue, bool _withUpdate) public onlyOwner {
        if (_withUpdate) {
            massUpdatePools();
        }
        rewardPerBlock = newValue;
        emit UpdateRewardPerBlock(newValue);
    }

    function updateSpecAddress(address addr) public onlyOwner {
        _updateSpecAddress(addr);
    }

    function updateFeeWallet(address addr) public onlyOwner {
        feeWallet = addr;
    }

    function addSupportedNFT(address addr) public onlyOwner {
        _addSupportedNFT(addr);
    }

    function removeSupportedNFT(address addr) public onlyOwner {
        _removeSupportedNFT(addr);
    }

    function add(
        uint256 _allocPoint,
        uint256 _bnbAllocPoint,
        IERC20 _want,
        bool _withUpdate,
        address _strat
    ) public onlyOwner {
        if (_withUpdate) {
            massUpdatePools();
        }
        uint256 lastRewardBlock = block.number > startBlock ? block.number : startBlock;
        totalAllocPoint += _allocPoint;
        totalBNBAllocPoint += _bnbAllocPoint;
        poolInfo.push(
            PoolInfo({
                want: _want,
                allocPoint: _allocPoint,
                bnbAllocPoint: _bnbAllocPoint,
                lastRewardBlock: lastRewardBlock,
                accRewardPerShare: 0,
                strat: _strat,
                weightedShareTotal: 0,
                lastUpdatedWindow: 0
            })
        );
    }

    function set(
        uint256 _pid,
        uint256 _allocPoint,
        uint256 _bnbAllocPoint,
        bool _withUpdate
    ) public onlyOwner {
        if (_withUpdate) {
            massUpdatePools();
        }
        totalAllocPoint = totalAllocPoint - poolInfo[_pid].allocPoint + _allocPoint;
        totalBNBAllocPoint = totalBNBAllocPoint - poolInfo[_pid].bnbAllocPoint + _bnbAllocPoint;

        poolInfo[_pid].allocPoint = _allocPoint;
        poolInfo[_pid].bnbAllocPoint = _bnbAllocPoint;
    }

    // create a new sliding window for BNB rewards
    function addBNBRewardWindow(uint256 _startBlock, uint256 _endBlock) public payable onlyOwner {
        require(_endBlock > _startBlock, 'endBlock > startBlock');
        require(msg.value > 0, 'non-zero totalRewards');
        uint256 _rewardPerBlock = msg.value / (_endBlock - _startBlock);
        require(_rewardPerBlock > 0, 'non-zero rewardPerBlock');

        // also the index of the new window (to be pushed into the pool-windows)
        uint256 _windows = bnbRewardWindows.length;
        if (_windows > 0) {
            BNBRewardWindow storage prev = bnbRewardWindows[_windows - 1];
            require(_startBlock > prev.endBlock, 'startBlock overlaps with the previous block');
        }
        bnbRewardWindows.push(
            BNBRewardWindow({
                startBlock: _startBlock,
                endBlock: _endBlock,
                totalRewards: msg.value,
                rewardPerBlock: _rewardPerBlock
            })
        );
        emit AddRewardWindow(_startBlock, _endBlock, msg.value);
    }

    function getMultiplier(uint256 _from, uint256 _to) public view returns (uint256) {
        if (accReward >= maxReward) {
            return 0;
        }
        return _to - _from;
    }

    function pendingReward(uint256 _pid, address _user) external view returns (uint256) {
        PoolInfo storage pool = poolInfo[_pid];
        UserInfo storage user = userInfo[_pid][_user];
        uint256 accRewardPerShare = pool.accRewardPerShare;
        uint256 sharesTotal = IStrategy(pool.strat).sharesTotal();
        if (block.number > pool.lastRewardBlock && sharesTotal != 0) {
            uint256 multiplier = getMultiplier(pool.lastRewardBlock, block.number);
            uint256 _rewardAmt = (multiplier * rewardPerBlock * pool.allocPoint) / totalAllocPoint;
            accRewardPerShare += (_rewardAmt * 1e12) / pool.weightedShareTotal;
        }
        return (user.weightedShare * accRewardPerShare) / 1e12 - user.rewardDebt;
    }

    function pendingBNBReward(uint256 _pid, address _user) external view returns (uint256) {
        PoolInfo storage pool = poolInfo[_pid];
        UserInfo storage user = userInfo[_pid][_user];

        if (user.weightedShare > 0 && pool.weightedShareTotal > 0) {
            uint256 pending = 0;
            for (uint256 _wid = user.lastRewardWindow; _wid < bnbRewardWindows.length; _wid++) {
                BNBRewardWindow storage w = bnbRewardWindows[_wid];
                if (w.startBlock > block.number) {
                    break;
                }
                if (pool.lastRewardBlock >= w.endBlock) {
                    // already updated
                    pending += (accBNBRewardPerWeightShare[_pid][_wid] * user.weightedShare) / 1e12;
                } else {
                    uint256 accRewardPerShare = accBNBRewardPerWeightShare[_pid][_wid];
                    uint256 multiplier =
                        Math.min(w.endBlock, block.number) -
                            Math.max(w.startBlock, pool.lastRewardBlock);
                    uint256 _rewardAmt =
                        (multiplier * w.rewardPerBlock * pool.bnbAllocPoint) / totalBNBAllocPoint;
                    accRewardPerShare += (_rewardAmt * 1e12) / pool.weightedShareTotal;
                    pending += (accRewardPerShare * user.weightedShare) / 1e12;
                }
            }
            pending -= user.bnbRewardDebt;
            return pending;
        }
        return 0;
    }

    function stakedWantTokens(uint256 _pid, address _user) external view returns (uint256) {
        PoolInfo storage pool = poolInfo[_pid];
        UserInfo storage user = userInfo[_pid][_user];

        uint256 sharesTotal = IStrategy(pool.strat).sharesTotal();
        uint256 wantLockedTotal = IStrategy(poolInfo[_pid].strat).wantLockedTotal();

        if (sharesTotal == 0) {
            return 0;
        }
        return (user.shares * wantLockedTotal) / sharesTotal;
    }

    function massUpdatePools() public whenNotPaused {
        uint256 length = poolInfo.length;
        for (uint256 pid = 0; pid < length; ++pid) {
            updatePool(pid);
        }
    }

    function updatePool(uint256 _pid) public whenNotPaused {
        PoolInfo storage pool = poolInfo[_pid];
        if (block.number <= pool.lastRewardBlock) {
            return;
        }

        uint256 sharesTotal = IStrategy(pool.strat).sharesTotal();
        if (sharesTotal == 0) {
            pool.lastRewardBlock = block.number;
            return;
        }

        _updatePoolSCV(_pid);
        if (pool.bnbAllocPoint > 0) {
            _updatePoolBNB(_pid);
        }

        pool.lastRewardBlock = block.number;
    }

    // close the farm and we don't need the upgradable
    function migrateTo(address _to, bool _withUpdate) public onlyOwner {
        if (_withUpdate) {
            massUpdatePools();
        }
        uint256 rewardLeft = maxReward - accReward;
        maxReward = accReward;
        _safeTransferReward(_to, rewardLeft);
        emit MigrateFarmTo(_to, rewardLeft);
    }

    function _updatePoolSCV(uint256 _pid) internal {
        PoolInfo storage pool = poolInfo[_pid];
        uint256 multiplier = getMultiplier(pool.lastRewardBlock, block.number);
        if (multiplier > 0) {
            uint256 rewardAmt = (multiplier * rewardPerBlock * pool.allocPoint) / totalAllocPoint;
            accReward += rewardAmt;
            pool.accRewardPerShare += ((rewardAmt * 1e12) / pool.weightedShareTotal);
        }
    }

    function _updatePoolBNB(uint256 _pid) internal {
        PoolInfo storage pool = poolInfo[_pid];
        uint256 totalWindows = bnbRewardWindows.length;
        for (uint256 _wid = pool.lastUpdatedWindow; _wid < totalWindows; _wid++) {
            BNBRewardWindow storage win = bnbRewardWindows[_wid];
            if (win.startBlock > block.number) {
                // haven't been there yet
                break;
            }
            if (pool.lastRewardBlock >= win.endBlock) {
                // rewarded, shouldn't happen but
                continue;
            }
            // number of blocks to reward: endingBlock - startingBlock
            // endingBlock: the smaller one of endBlock or current Block
            // staringBlock: the larger one of startBlock or lastRewardBlock
            // would be 0 if already accumulated
            uint256 multiplier =
                Math.min(win.endBlock, block.number) -
                    Math.max(win.startBlock, pool.lastRewardBlock);
            uint256 _rewardAmt =
                (multiplier * win.rewardPerBlock * pool.bnbAllocPoint) / totalBNBAllocPoint;
            accBNBRewardPerWeightShare[_pid][_wid] += (_rewardAmt * 1e12) / pool.weightedShareTotal;

            // set to the last active window
            pool.lastUpdatedWindow = _wid;
        }
    }

    function depositNFT(
        uint256 _pid,
        IERC721 nft,
        uint256 nftTokenId
    ) internal {
        PoolInfo storage pool = poolInfo[_pid];
        UserInfo storage user = userInfo[_pid][msg.sender];

        require(
            nftUsable(address(nft), nftTokenId),
            'NFT not supported, not opened or non-exist specId'
        );
        // transfer to a address that no one own
        nft.transferFrom(msg.sender, burnAddress, nftTokenId);
        emit BurnNFT(address(nft), nftTokenId);

        user.hasNFT = true;
        user.nftSpecId = ISCVBot(address(nft)).getSpecId(nftTokenId);
        address inviter = nftOpener(nft, nftTokenId);
        user.nftInviter = emptyAddress;
        if (inviter != emptyAddress && inviter != msg.sender) {
            user.nftInviter = inviter;
        }

        uint256 _wshare = _toWeightedShares(user.shares, user.nftSpecId);
        pool.weightedShareTotal = pool.weightedShareTotal - user.weightedShare + _wshare;
        user.weightedShare = _wshare;
    }

    // returns pending SCV reward, assumes pool has been updated
    function _pendingSCV(uint256 _pid) internal view returns (uint256) {
        PoolInfo storage pool = poolInfo[_pid];
        UserInfo storage user = userInfo[_pid][msg.sender];
        return (user.weightedShare * pool.accRewardPerShare) / 1e12 - user.rewardDebt;
    }

    // returns pending BNB reward, assumes pool has been updated
    function _pendingBNB(uint256 _pid) internal view returns (uint256) {
        PoolInfo storage pool = poolInfo[_pid];
        UserInfo storage user = userInfo[_pid][msg.sender];
        uint256 amount = 0;

        for (uint256 _wid = user.lastRewardWindow; _wid <= pool.lastUpdatedWindow; _wid++) {
            amount += (user.weightedShare * accBNBRewardPerWeightShare[_pid][_wid]) / 1e12;
        }
        amount -= user.bnbRewardDebt;
        return amount;
    }

    function _rewardSCV(uint256 _pid, uint256 amount) internal {
        UserInfo storage user = userInfo[_pid][msg.sender];
        uint256 inviterAmount = 0;
        if (user.hasNFT && user.nftInviter != emptyAddress) {
            inviterAmount = (amount * inviterSharePct) / pctBase;
            _safeTransferReward(user.nftInviter, inviterAmount);
        }
        _safeTransferReward(msg.sender, amount - inviterAmount);
    }

    function _rewardBNB(uint256 _pid, uint256 amount) internal {
        UserInfo storage user = userInfo[_pid][msg.sender];
        uint256 inviterAmount = 0;
        if (user.hasNFT && user.nftInviter != emptyAddress) {
            inviterAmount = (amount * inviterSharePct) / pctBase;
            _safeTransferBNB(user.nftInviter, inviterAmount);
        }
        _safeTransferBNB(msg.sender, amount - inviterAmount);
    }

    function deposit(
        uint256 _pid,
        uint256 _wantAmt,
        IERC721 nft,
        uint256 nftTokenId
    ) public nonReentrant whenNotPaused {
        updatePool(_pid);
        PoolInfo storage pool = poolInfo[_pid];
        UserInfo storage user = userInfo[_pid][msg.sender];

        require(user.shares > 0 || _wantAmt > 0, 'show me the money');

        // distribute earning before new deposit
        if (user.shares > 0) {
            uint256 pending = _pendingSCV(_pid);
            _rewardSCV(_pid, pending);
            uint256 pendingBNB = _pendingBNB(_pid);
            _rewardBNB(_pid, pendingBNB);
            emit Claim(msg.sender, _pid, pending, pendingBNB);
        }

        // set new NFT
        if (address(nft) != emptyAddress) {
            depositNFT(_pid, nft, nftTokenId);
        }

        // deposit new tokens
        if (_wantAmt > 0) {
            pool.want.safeTransferFrom(address(msg.sender), address(this), _wantAmt);
            uint256 rateBase = specAddress.percentageBaseRate();
            uint256 reductionRate = 0;
            if (user.hasNFT) {
                reductionRate = uint256(specAddress.getDepositFeeReductionRate(user.nftSpecId));
            }

            uint256 feePct = depositFeePct - (depositFeePct * reductionRate) / rateBase;
            uint256 depositFee = (_wantAmt * feePct) / pctBase;
            if (depositFee > 0) {
                pool.want.transfer(feeWallet, depositFee);
            }

            // transfer to strategy
            uint256 _depositAmt = _wantAmt - depositFee;
            pool.want.safeIncreaseAllowance(pool.strat, _depositAmt);
            uint256 sharesAdded = IStrategy(poolInfo[_pid].strat).deposit(msg.sender, _depositAmt);
            user.shares = user.shares + sharesAdded;

            uint256 _share = user.shares;
            if (user.hasNFT) {
                _share = _toWeightedShares(user.shares, user.nftSpecId);
            }
            pool.weightedShareTotal = pool.weightedShareTotal - user.weightedShare + _share;
            user.weightedShare = _share;
        }

        user.rewardDebt = (user.weightedShare * pool.accRewardPerShare) / 1e12;
        user.bnbRewardDebt =
            (user.weightedShare * accBNBRewardPerWeightShare[_pid][pool.lastUpdatedWindow]) /
            1e12;
        user.lastRewardWindow = pool.lastUpdatedWindow;

        emit Deposit(msg.sender, _pid, _wantAmt, address(nft), nftTokenId);
    }

    function withdraw(uint256 _pid, uint256 _wantAmt) public nonReentrant whenNotPaused {
        updatePool(_pid);

        PoolInfo storage pool = poolInfo[_pid];
        UserInfo storage user = userInfo[_pid][msg.sender];

        uint256 wantLockedTotal = IStrategy(poolInfo[_pid].strat).wantLockedTotal();
        uint256 sharesTotal = IStrategy(poolInfo[_pid].strat).sharesTotal();

        require(user.shares > 0, 'user.shares is 0');
        require(sharesTotal > 0, 'sharesTotal is 0');

        uint256 pending = _pendingSCV(_pid);
        _rewardSCV(_pid, pending);
        uint256 pendingBNB = _pendingBNB(_pid);
        _rewardBNB(_pid, pendingBNB);

        emit Claim(msg.sender, _pid, pending, pendingBNB);

        // Withdraw want tokens
        uint256 amount = (user.shares * wantLockedTotal) / sharesTotal;

        if (_wantAmt > amount) {
            _wantAmt = amount;
        }

        if (_wantAmt > 0) {
            uint256 sharesRemoved = IStrategy(poolInfo[_pid].strat).withdraw(msg.sender, _wantAmt);

            if (sharesRemoved > user.shares) {
                user.shares = 0;
                pool.weightedShareTotal = pool.weightedShareTotal - user.weightedShare;
                user.weightedShare = 0;
            } else {
                // remove NFT next
                user.shares = user.shares - sharesRemoved;
                pool.weightedShareTotal =
                    pool.weightedShareTotal -
                    user.weightedShare +
                    user.shares;
                user.weightedShare = user.shares;
            }

            uint256 wantBal = IERC20(pool.want).balanceOf(address(this));

            if (wantBal < _wantAmt) {
                _wantAmt = wantBal;
            }
            pool.want.safeTransfer(address(msg.sender), _wantAmt);
        }

        user.rewardDebt = (user.weightedShare * pool.accRewardPerShare) / 1e12;
        user.bnbRewardDebt =
            (user.weightedShare * accBNBRewardPerWeightShare[_pid][pool.lastUpdatedWindow]) /
            1e12;
        user.lastRewardWindow = pool.lastUpdatedWindow;

        emit Withdraw(msg.sender, _pid, _wantAmt);

        // reset NFT
        user.hasNFT = false;
        user.nftInviter = emptyAddress;
        user.nftSpecId = 0;
    }

    function withdrawAll(uint256 _pid) public whenNotPaused {
        withdraw(_pid, type(uint256).max);
    }

    // Withdraw without caring about rewards. EMERGENCY ONLY.
    function emergencyWithdraw(uint256 _pid) public nonReentrant whenNotPaused {
        PoolInfo storage pool = poolInfo[_pid];
        UserInfo storage user = userInfo[_pid][msg.sender];

        uint256 wantLockedTotal = IStrategy(poolInfo[_pid].strat).wantLockedTotal();
        uint256 sharesTotal = IStrategy(poolInfo[_pid].strat).sharesTotal();
        uint256 amount = (user.shares * wantLockedTotal) / sharesTotal;

        IStrategy(poolInfo[_pid].strat).withdraw(msg.sender, amount);

        pool.want.safeTransfer(address(msg.sender), amount);
        emit EmergencyWithdraw(msg.sender, _pid, amount);
        user.shares = 0;

        // reset debt
        user.rewardDebt = 0;
        user.bnbRewardDebt = 0;
        user.lastRewardWindow = 0;

        // reset NFT
        user.hasNFT = false;
        pool.weightedShareTotal -= user.weightedShare;
        user.weightedShare = 0;
        user.nftInviter = emptyAddress;
    }

    function inCaseBNBGetStuck(uint256 _amount) external onlyOwner {
        payable(msg.sender).transfer(_amount);
    }

    function inCaseTokensGetStuck(address _token, uint256 _amount) public onlyOwner {
        IERC20(_token).safeTransfer(msg.sender, _amount);
    }

    function _toWeightedShares(uint256 shares, uint256 specId) public view returns (uint256) {
        uint64 boost = specAddress.getBoostRate(specId);
        uint256 base = specAddress.percentageBaseRate();
        return (shares * boost) / base;
    }
}


// File: @openzeppelin/contracts/token/ERC20/ERC20.sol
// SPDX-License-Identifier: MIT

pragma solidity ^0.8.0;

import "./IERC20.sol";
import "./extensions/IERC20Metadata.sol";
import "../../utils/Context.sol";

/**
 * @dev Implementation of the {IERC20} interface.
 *
 * This implementation is agnostic to the way tokens are created. This means
 * that a supply mechanism has to be added in a derived contract using {_mint}.
 * For a generic mechanism see {ERC20PresetMinterPauser}.
 *
 * TIP: For a detailed writeup see our guide
 * https://forum.zeppelin.solutions/t/how-to-implement-erc20-supply-mechanisms/226[How
 * to implement supply mechanisms].
 *
 * We have followed general OpenZeppelin guidelines: functions revert instead
 * of returning `false` on failure. This behavior is nonetheless conventional
 * and does not conflict with the expectations of ERC20 applications.
 *
 * Additionally, an {Approval} event is emitted on calls to {transferFrom}.
 * This allows applications to reconstruct the allowance for all accounts just
 * by listening to said events. Other implementations of the EIP may not emit
 * these events, as it isn't required by the specification.
 *
 * Finally, the non-standard {decreaseAllowance} and {increaseAllowance}
 * functions have been added to mitigate the well-known issues around setting
 * allowances. See {IERC20-approve}.
 */
contract ERC20 is Context, IERC20, IERC20Metadata {
    mapping (address => uint256) private _balances;

    mapping (address => mapping (address => uint256)) private _allowances;

    uint256 private _totalSupply;

    string private _name;
    string private _symbol;

    /**
     * @dev Sets the values for {name} and {symbol}.
     *
     * The defaut value of {decimals} is 18. To select a different value for
     * {decimals} you should overload it.
     *
     * All two of these values are immutable: they can only be set once during
     * construction.
     */
    constructor (string memory name_, string memory symbol_) {
        _name = name_;
        _symbol = symbol_;
    }

    /**
     * @dev Returns the name of the token.
     */
    function name() public view virtual override returns (string memory) {
        return _name;
    }

    /**
     * @dev Returns the symbol of the token, usually a shorter version of the
     * name.
     */
    function symbol() public view virtual override returns (string memory) {
        return _symbol;
    }

    /**
     * @dev Returns the number of decimals used to get its user representation.
     * For example, if `decimals` equals `2`, a balance of `505` tokens should
     * be displayed to a user as `5,05` (`505 / 10 ** 2`).
     *
     * Tokens usually opt for a value of 18, imitating the relationship between
     * Ether and Wei. This is the value {ERC20} uses, unless this function is
     * overridden;
     *
     * NOTE: This information is only used for _display_ purposes: it in
     * no way affects any of the arithmetic of the contract, including
     * {IERC20-balanceOf} and {IERC20-transfer}.
     */
    function decimals() public view virtual override returns (uint8) {
        return 18;
    }

    /**
     * @dev See {IERC20-totalSupply}.
     */
    function totalSupply() public view virtual override returns (uint256) {
        return _totalSupply;
    }

    /**
     * @dev See {IERC20-balanceOf}.
     */
    function balanceOf(address account) public view virtual override returns (uint256) {
        return _balances[account];
    }

    /**
     * @dev See {IERC20-transfer}.
     *
     * Requirements:
     *
     * - `recipient` cannot be the zero address.
     * - the caller must have a balance of at least `amount`.
     */
    function transfer(address recipient, uint256 amount) public virtual override returns (bool) {
        _transfer(_msgSender(), recipient, amount);
        return true;
    }

    /**
     * @dev See {IERC20-allowance}.
     */
    function allowance(address owner, address spender) public view virtual override returns (uint256) {
        return _allowances[owner][spender];
    }

    /**
     * @dev See {IERC20-approve}.
     *
     * Requirements:
     *
     * - `spender` cannot be the zero address.
     */
    function approve(address spender, uint256 amount) public virtual override returns (bool) {
        _approve(_msgSender(), spender, amount);
        return true;
    }

    /**
     * @dev See {IERC20-transferFrom}.
     *
     * Emits an {Approval} event indicating the updated allowance. This is not
     * required by the EIP. See the note at the beginning of {ERC20}.
     *
     * Requirements:
     *
     * - `sender` and `recipient` cannot be the zero address.
     * - `sender` must have a balance of at least `amount`.
     * - the caller must have allowance for ``sender``'s tokens of at least
     * `amount`.
     */
    function transferFrom(address sender, address recipient, uint256 amount) public virtual override returns (bool) {
        _transfer(sender, recipient, amount);

        uint256 currentAllowance = _allowances[sender][_msgSender()];
        require(currentAllowance >= amount, "ERC20: transfer amount exceeds allowance");
        _approve(sender, _msgSender(), currentAllowance - amount);

        return true;
    }

    /**
     * @dev Atomically increases the allowance granted to `spender` by the caller.
     *
     * This is an alternative to {approve} that can be used as a mitigation for
     * problems described in {IERC20-approve}.
     *
     * Emits an {Approval} event indicating the updated allowance.
     *
     * Requirements:
     *
     * - `spender` cannot be the zero address.
     */
    function increaseAllowance(address spender, uint256 addedValue) public virtual returns (bool) {
        _approve(_msgSender(), spender, _allowances[_msgSender()][spender] + addedValue);
        return true;
    }

    /**
     * @dev Atomically decreases the allowance granted to `spender` by the caller.
     *
     * This is an alternative to {approve} that can be used as a mitigation for
     * problems described in {IERC20-approve}.
     *
     * Emits an {Approval} event indicating the updated allowance.
     *
     * Requirements:
     *
     * - `spender` cannot be the zero address.
     * - `spender` must have allowance for the caller of at least
     * `subtractedValue`.
     */
    function decreaseAllowance(address spender, uint256 subtractedValue) public virtual returns (bool) {
        uint256 currentAllowance = _allowances[_msgSender()][spender];
        require(currentAllowance >= subtractedValue, "ERC20: decreased allowance below zero");
        _approve(_msgSender(), spender, currentAllowance - subtractedValue);

        return true;
    }

    /**
     * @dev Moves tokens `amount` from `sender` to `recipient`.
     *
     * This is internal function is equivalent to {transfer}, and can be used to
     * e.g. implement automatic token fees, slashing mechanisms, etc.
     *
     * Emits a {Transfer} event.
     *
     * Requirements:
     *
     * - `sender` cannot be the zero address.
     * - `recipient` cannot be the zero address.
     * - `sender` must have a balance of at least `amount`.
     */
    function _transfer(address sender, address recipient, uint256 amount) internal virtual {
        require(sender != address(0), "ERC20: transfer from the zero address");
        require(recipient != address(0), "ERC20: transfer to the zero address");

        _beforeTokenTransfer(sender, recipient, amount);

        uint256 senderBalance = _balances[sender];
        require(senderBalance >= amount, "ERC20: transfer amount exceeds balance");
        _balances[sender] = senderBalance - amount;
        _balances[recipient] += amount;

        emit Transfer(sender, recipient, amount);
    }

    /** @dev Creates `amount` tokens and assigns them to `account`, increasing
     * the total supply.
     *
     * Emits a {Transfer} event with `from` set to the zero address.
     *
     * Requirements:
     *
     * - `to` cannot be the zero address.
     */
    function _mint(address account, uint256 amount) internal virtual {
        require(account != address(0), "ERC20: mint to the zero address");

        _beforeTokenTransfer(address(0), account, amount);

        _totalSupply += amount;
        _balances[account] += amount;
        emit Transfer(address(0), account, amount);
    }

    /**
     * @dev Destroys `amount` tokens from `account`, reducing the
     * total supply.
     *
     * Emits a {Transfer} event with `to` set to the zero address.
     *
     * Requirements:
     *
     * - `account` cannot be the zero address.
     * - `account` must have at least `amount` tokens.
     */
    function _burn(address account, uint256 amount) internal virtual {
        require(account != address(0), "ERC20: burn from the zero address");

        _beforeTokenTransfer(account, address(0), amount);

        uint256 accountBalance = _balances[account];
        require(accountBalance >= amount, "ERC20: burn amount exceeds balance");
        _balances[account] = accountBalance - amount;
        _totalSupply -= amount;

        emit Transfer(account, address(0), amount);
    }

    /**
     * @dev Sets `amount` as the allowance of `spender` over the `owner` s tokens.
     *
     * This internal function is equivalent to `approve`, and can be used to
     * e.g. set automatic allowances for certain subsystems, etc.
     *
     * Emits an {Approval} event.
     *
     * Requirements:
     *
     * - `owner` cannot be the zero address.
     * - `spender` cannot be the zero address.
     */
    function _approve(address owner, address spender, uint256 amount) internal virtual {
        require(owner != address(0), "ERC20: approve from the zero address");
        require(spender != address(0), "ERC20: approve to the zero address");

        _allowances[owner][spender] = amount;
        emit Approval(owner, spender, amount);
    }

    /**
     * @dev Hook that is called before any transfer of tokens. This includes
     * minting and burning.
     *
     * Calling conditions:
     *
     * - when `from` and `to` are both non-zero, `amount` of ``from``'s tokens
     * will be to transferred to `to`.
     * - when `from` is zero, `amount` tokens will be minted for `to`.
     * - when `to` is zero, `amount` of ``from``'s tokens will be burned.
     * - `from` and `to` are never both zero.
     *
     * To learn more about hooks, head to xref:ROOT:extending-contracts.adoc#using-hooks[Using Hooks].
     */
    function _beforeTokenTransfer(address from, address to, uint256 amount) internal virtual { }
}


// File: @openzeppelin/contracts/token/ERC20/IERC20.sol
// SPDX-License-Identifier: MIT

pragma solidity ^0.8.0;

/**
 * @dev Interface of the ERC20 standard as defined in the EIP.
 */
interface IERC20 {
    /**
     * @dev Returns the amount of tokens in existence.
     */
    function totalSupply() external view returns (uint256);

    /**
     * @dev Returns the amount of tokens owned by `account`.
     */
    function balanceOf(address account) external view returns (uint256);

    /**
     * @dev Moves `amount` tokens from the caller's account to `recipient`.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * Emits a {Transfer} event.
     */
    function transfer(address recipient, uint256 amount) external returns (bool);

    /**
     * @dev Returns the remaining number of tokens that `spender` will be
     * allowed to spend on behalf of `owner` through {transferFrom}. This is
     * zero by default.
     *
     * This value changes when {approve} or {transferFrom} are called.
     */
    function allowance(address owner, address spender) external view returns (uint256);

    /**
     * @dev Sets `amount` as the allowance of `spender` over the caller's tokens.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * IMPORTANT: Beware that changing an allowance with this method brings the risk
     * that someone may use both the old and the new allowance by unfortunate
     * transaction ordering. One possible solution to mitigate this race
     * condition is to first reduce the spender's allowance to 0 and set the
     * desired value afterwards:
     * https://github.com/ethereum/EIPs/issues/20#issuecomment-263524729
     *
     * Emits an {Approval} event.
     */
    function approve(address spender, uint256 amount) external returns (bool);

    /**
     * @dev Moves `amount` tokens from `sender` to `recipient` using the
     * allowance mechanism. `amount` is then deducted from the caller's
     * allowance.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * Emits a {Transfer} event.
     */
    function transferFrom(address sender, address recipient, uint256 amount) external returns (bool);

    /**
     * @dev Emitted when `value` tokens are moved from one account (`from`) to
     * another (`to`).
     *
     * Note that `value` may be zero.
     */
    event Transfer(address indexed from, address indexed to, uint256 value);

    /**
     * @dev Emitted when the allowance of a `spender` for an `owner` is set by
     * a call to {approve}. `value` is the new allowance.
     */
    event Approval(address indexed owner, address indexed spender, uint256 value);
}


// File: @openzeppelin/contracts/token/ERC20/utils/SafeERC20.sol
// SPDX-License-Identifier: MIT

pragma solidity ^0.8.0;

import "../IERC20.sol";
import "../../../utils/Address.sol";

/**
 * @title SafeERC20
 * @dev Wrappers around ERC20 operations that throw on failure (when the token
 * contract returns false). Tokens that return no value (and instead revert or
 * throw on failure) are also supported, non-reverting calls are assumed to be
 * successful.
 * To use this library you can add a `using SafeERC20 for IERC20;` statement to your contract,
 * which allows you to call the safe operations as `token.safeTransfer(...)`, etc.
 */
library SafeERC20 {
    using Address for address;

    function safeTransfer(IERC20 token, address to, uint256 value) internal {
        _callOptionalReturn(token, abi.encodeWithSelector(token.transfer.selector, to, value));
    }

    function safeTransferFrom(IERC20 token, address from, address to, uint256 value) internal {
        _callOptionalReturn(token, abi.encodeWithSelector(token.transferFrom.selector, from, to, value));
    }

    /**
     * @dev Deprecated. This function has issues similar to the ones found in
     * {IERC20-approve}, and its usage is discouraged.
     *
     * Whenever possible, use {safeIncreaseAllowance} and
     * {safeDecreaseAllowance} instead.
     */
    function safeApprove(IERC20 token, address spender, uint256 value) internal {
        // safeApprove should only be called when setting an initial allowance,
        // or when resetting it to zero. To increase and decrease it, use
        // 'safeIncreaseAllowance' and 'safeDecreaseAllowance'
        // solhint-disable-next-line max-line-length
        require((value == 0) || (token.allowance(address(this), spender) == 0),
            "SafeERC20: approve from non-zero to non-zero allowance"
        );
        _callOptionalReturn(token, abi.encodeWithSelector(token.approve.selector, spender, value));
    }

    function safeIncreaseAllowance(IERC20 token, address spender, uint256 value) internal {
        uint256 newAllowance = token.allowance(address(this), spender) + value;
        _callOptionalReturn(token, abi.encodeWithSelector(token.approve.selector, spender, newAllowance));
    }

    function safeDecreaseAllowance(IERC20 token, address spender, uint256 value) internal {
        unchecked {
            uint256 oldAllowance = token.allowance(address(this), spender);
            require(oldAllowance >= value, "SafeERC20: decreased allowance below zero");
            uint256 newAllowance = oldAllowance - value;
            _callOptionalReturn(token, abi.encodeWithSelector(token.approve.selector, spender, newAllowance));
        }
    }

    /**
     * @dev Imitates a Solidity high-level call (i.e. a regular function call to a contract), relaxing the requirement
     * on the return value: the return value is optional (but if data is returned, it must not be false).
     * @param token The token targeted by the call.
     * @param data The call data (encoded using abi.encode or one of its variants).
     */
    function _callOptionalReturn(IERC20 token, bytes memory data) private {
        // We need to perform a low level call here, to bypass Solidity's return data size checking mechanism, since
        // we're implementing it ourselves. We use {Address.functionCall} to perform this call, which verifies that
        // the target address contains contract code and also asserts for success in the low-level call.

        bytes memory returndata = address(token).functionCall(data, "SafeERC20: low-level call failed");
        if (returndata.length > 0) { // Return data is optional
            // solhint-disable-next-line max-line-length
            require(abi.decode(returndata, (bool)), "SafeERC20: ERC20 operation did not succeed");
        }
    }
}


// File: @openzeppelin/contracts/token/ERC721/IERC721.sol
// SPDX-License-Identifier: MIT

pragma solidity ^0.8.0;

import "../../utils/introspection/IERC165.sol";

/**
 * @dev Required interface of an ERC721 compliant contract.
 */
interface IERC721 is IERC165 {
    /**
     * @dev Emitted when `tokenId` token is transferred from `from` to `to`.
     */
    event Transfer(address indexed from, address indexed to, uint256 indexed tokenId);

    /**
     * @dev Emitted when `owner` enables `approved` to manage the `tokenId` token.
     */
    event Approval(address indexed owner, address indexed approved, uint256 indexed tokenId);

    /**
     * @dev Emitted when `owner` enables or disables (`approved`) `operator` to manage all of its assets.
     */
    event ApprovalForAll(address indexed owner, address indexed operator, bool approved);

    /**
     * @dev Returns the number of tokens in ``owner``'s account.
     */
    function balanceOf(address owner) external view returns (uint256 balance);

    /**
     * @dev Returns the owner of the `tokenId` token.
     *
     * Requirements:
     *
     * - `tokenId` must exist.
     */
    function ownerOf(uint256 tokenId) external view returns (address owner);

    /**
     * @dev Safely transfers `tokenId` token from `from` to `to`, checking first that contract recipients
     * are aware of the ERC721 protocol to prevent tokens from being forever locked.
     *
     * Requirements:
     *
     * - `from` cannot be the zero address.
     * - `to` cannot be the zero address.
     * - `tokenId` token must exist and be owned by `from`.
     * - If the caller is not `from`, it must be have been allowed to move this token by either {approve} or {setApprovalForAll}.
     * - If `to` refers to a smart contract, it must implement {IERC721Receiver-onERC721Received}, which is called upon a safe transfer.
     *
     * Emits a {Transfer} event.
     */
    function safeTransferFrom(address from, address to, uint256 tokenId) external;

    /**
     * @dev Transfers `tokenId` token from `from` to `to`.
     *
     * WARNING: Usage of this method is discouraged, use {safeTransferFrom} whenever possible.
     *
     * Requirements:
     *
     * - `from` cannot be the zero address.
     * - `to` cannot be the zero address.
     * - `tokenId` token must be owned by `from`.
     * - If the caller is not `from`, it must be approved to move this token by either {approve} or {setApprovalForAll}.
     *
     * Emits a {Transfer} event.
     */
    function transferFrom(address from, address to, uint256 tokenId) external;

    /**
     * @dev Gives permission to `to` to transfer `tokenId` token to another account.
     * The approval is cleared when the token is transferred.
     *
     * Only a single account can be approved at a time, so approving the zero address clears previous approvals.
     *
     * Requirements:
     *
     * - The caller must own the token or be an approved operator.
     * - `tokenId` must exist.
     *
     * Emits an {Approval} event.
     */
    function approve(address to, uint256 tokenId) external;

    /**
     * @dev Returns the account approved for `tokenId` token.
     *
     * Requirements:
     *
     * - `tokenId` must exist.
     */
    function getApproved(uint256 tokenId) external view returns (address operator);

    /**
     * @dev Approve or remove `operator` as an operator for the caller.
     * Operators can call {transferFrom} or {safeTransferFrom} for any token owned by the caller.
     *
     * Requirements:
     *
     * - The `operator` cannot be the caller.
     *
     * Emits an {ApprovalForAll} event.
     */
    function setApprovalForAll(address operator, bool _approved) external;

    /**
     * @dev Returns if the `operator` is allowed to manage all of the assets of `owner`.
     *
     * See {setApprovalForAll}
     */
    function isApprovedForAll(address owner, address operator) external view returns (bool);

    /**
      * @dev Safely transfers `tokenId` token from `from` to `to`.
      *
      * Requirements:
      *
      * - `from` cannot be the zero address.
      * - `to` cannot be the zero address.
      * - `tokenId` token must exist and be owned by `from`.
      * - If the caller is not `from`, it must be approved to move this token by either {approve} or {setApprovalForAll}.
      * - If `to` refers to a smart contract, it must implement {IERC721Receiver-onERC721Received}, which is called upon a safe transfer.
      *
      * Emits a {Transfer} event.
      */
    function safeTransferFrom(address from, address to, uint256 tokenId, bytes calldata data) external;
}


// File: @openzeppelin/contracts/access/Ownable.sol
// SPDX-License-Identifier: MIT

pragma solidity ^0.8.0;

import "../utils/Context.sol";
/**
 * @dev Contract module which provides a basic access control mechanism, where
 * there is an account (an owner) that can be granted exclusive access to
 * specific functions.
 *
 * By default, the owner account will be the one that deploys the contract. This
 * can later be changed with {transferOwnership}.
 *
 * This module is used through inheritance. It will make available the modifier
 * `onlyOwner`, which can be applied to your functions to restrict their use to
 * the owner.
 */
abstract contract Ownable is Context {
    address private _owner;

    event OwnershipTransferred(address indexed previousOwner, address indexed newOwner);

    /**
     * @dev Initializes the contract setting the deployer as the initial owner.
     */
    constructor () {
        address msgSender = _msgSender();
        _owner = msgSender;
        emit OwnershipTransferred(address(0), msgSender);
    }

    /**
     * @dev Returns the address of the current owner.
     */
    function owner() public view virtual returns (address) {
        return _owner;
    }

    /**
     * @dev Throws if called by any account other than the owner.
     */
    modifier onlyOwner() {
        require(owner() == _msgSender(), "Ownable: caller is not the owner");
        _;
    }

    /**
     * @dev Leaves the contract without owner. It will not be possible to call
     * `onlyOwner` functions anymore. Can only be called by the current owner.
     *
     * NOTE: Renouncing ownership will leave the contract without an owner,
     * thereby removing any functionality that is only available to the owner.
     */
    function renounceOwnership() public virtual onlyOwner {
        emit OwnershipTransferred(_owner, address(0));
        _owner = address(0);
    }

    /**
     * @dev Transfers ownership of the contract to a new account (`newOwner`).
     * Can only be called by the current owner.
     */
    function transferOwnership(address newOwner) public virtual onlyOwner {
        require(newOwner != address(0), "Ownable: new owner is the zero address");
        emit OwnershipTransferred(_owner, newOwner);
        _owner = newOwner;
    }
}


// File: @openzeppelin/contracts/security/ReentrancyGuard.sol
// SPDX-License-Identifier: MIT

pragma solidity ^0.8.0;

/**
 * @dev Contract module that helps prevent reentrant calls to a function.
 *
 * Inheriting from `ReentrancyGuard` will make the {nonReentrant} modifier
 * available, which can be applied to functions to make sure there are no nested
 * (reentrant) calls to them.
 *
 * Note that because there is a single `nonReentrant` guard, functions marked as
 * `nonReentrant` may not call one another. This can be worked around by making
 * those functions `private`, and then adding `external` `nonReentrant` entry
 * points to them.
 *
 * TIP: If you would like to learn more about reentrancy and alternative ways
 * to protect against it, check out our blog post
 * https://blog.openzeppelin.com/reentrancy-after-istanbul/[Reentrancy After Istanbul].
 */
abstract contract ReentrancyGuard {
    // Booleans are more expensive than uint256 or any type that takes up a full
    // word because each write operation emits an extra SLOAD to first read the
    // slot's contents, replace the bits taken up by the boolean, and then write
    // back. This is the compiler's defense against contract upgrades and
    // pointer aliasing, and it cannot be disabled.

    // The values being non-zero value makes deployment a bit more expensive,
    // but in exchange the refund on every call to nonReentrant will be lower in
    // amount. Since refunds are capped to a percentage of the total
    // transaction's gas, it is best to keep them low in cases like this one, to
    // increase the likelihood of the full refund coming into effect.
    uint256 private constant _NOT_ENTERED = 1;
    uint256 private constant _ENTERED = 2;

    uint256 private _status;

    constructor () {
        _status = _NOT_ENTERED;
    }

    /**
     * @dev Prevents a contract from calling itself, directly or indirectly.
     * Calling a `nonReentrant` function from another `nonReentrant`
     * function is not supported. It is possible to prevent this from happening
     * by making the `nonReentrant` function external, and make it call a
     * `private` function that does the actual work.
     */
    modifier nonReentrant() {
        // On the first call to nonReentrant, _notEntered will be true
        require(_status != _ENTERED, "ReentrancyGuard: reentrant call");

        // Any calls to nonReentrant after this point will fail
        _status = _ENTERED;

        _;

        // By storing the original value once again, a refund is triggered (see
        // https://eips.ethereum.org/EIPS/eip-2200)
        _status = _NOT_ENTERED;
    }
}


// File: @openzeppelin/contracts/security/Pausable.sol
// SPDX-License-Identifier: MIT

pragma solidity ^0.8.0;

import "../utils/Context.sol";

/**
 * @dev Contract module which allows children to implement an emergency stop
 * mechanism that can be triggered by an authorized account.
 *
 * This module is used through inheritance. It will make available the
 * modifiers `whenNotPaused` and `whenPaused`, which can be applied to
 * the functions of your contract. Note that they will not be pausable by
 * simply including this module, only once the modifiers are put in place.
 */
abstract contract Pausable is Context {
    /**
     * @dev Emitted when the pause is triggered by `account`.
     */
    event Paused(address account);

    /**
     * @dev Emitted when the pause is lifted by `account`.
     */
    event Unpaused(address account);

    bool private _paused;

    /**
     * @dev Initializes the contract in unpaused state.
     */
    constructor () {
        _paused = false;
    }

    /**
     * @dev Returns true if the contract is paused, and false otherwise.
     */
    function paused() public view virtual returns (bool) {
        return _paused;
    }

    /**
     * @dev Modifier to make a function callable only when the contract is not paused.
     *
     * Requirements:
     *
     * - The contract must not be paused.
     */
    modifier whenNotPaused() {
        require(!paused(), "Pausable: paused");
        _;
    }

    /**
     * @dev Modifier to make a function callable only when the contract is paused.
     *
     * Requirements:
     *
     * - The contract must be paused.
     */
    modifier whenPaused() {
        require(paused(), "Pausable: not paused");
        _;
    }

    /**
     * @dev Triggers stopped state.
     *
     * Requirements:
     *
     * - The contract must not be paused.
     */
    function _pause() internal virtual whenNotPaused {
        _paused = true;
        emit Paused(_msgSender());
    }

    /**
     * @dev Returns to normal state.
     *
     * Requirements:
     *
     * - The contract must be paused.
     */
    function _unpause() internal virtual whenPaused {
        _paused = false;
        emit Unpaused(_msgSender());
    }
}


// File: @openzeppelin/contracts/utils/math/Math.sol
// SPDX-License-Identifier: MIT

pragma solidity ^0.8.0;

/**
 * @dev Standard math utilities missing in the Solidity language.
 */
library Math {
    /**
     * @dev Returns the largest of two numbers.
     */
    function max(uint256 a, uint256 b) internal pure returns (uint256) {
        return a >= b ? a : b;
    }

    /**
     * @dev Returns the smallest of two numbers.
     */
    function min(uint256 a, uint256 b) internal pure returns (uint256) {
        return a < b ? a : b;
    }

    /**
     * @dev Returns the average of two numbers. The result is rounded towards
     * zero.
     */
    function average(uint256 a, uint256 b) internal pure returns (uint256) {
        // (a + b) / 2 can overflow, so we distribute
        return (a / 2) + (b / 2) + ((a % 2 + b % 2) / 2);
    }
}


// File: contracts/libraries/OpenableNFT.sol
// SPDX-License-Identifier: MIT

pragma solidity ^0.8.4;

import '@openzeppelin/contracts/token/ERC721/IERC721.sol';

interface INFTSpec {
    function hasSpecId(uint256 specId) external view returns (bool);

    function totalSpecs() external view returns (uint256);

    function percentageBaseRate() external view returns (uint256);

    function getBoostRate(uint256 specId) external view returns (uint64);

    function getDepositFeeReductionRate(uint256 specId) external view returns (uint64);

    function isPro(uint256 specId) external view returns (bool);
}

interface ISCVBot is IERC721 {
    function getSpecId(uint256 tokenId) external view returns (uint256);

    function specAddress() external view returns (address);
}

interface ISCVOpenableNFT {
    function openerOf(uint256 tokenId) external view returns (address);

    function opened(uint256 tokenId) external view returns (bool);
}

library OpenableNFT {
    function isOpenableNFT(IERC721 nftAddress) internal view returns (bool) {
        bytes4 iopener = ISCVOpenableNFT.openerOf.selector; // bytes4(keccak256('openerOf(uint256)'));
        bytes4 iopened = ISCVOpenableNFT.opened.selector; // bytes4(keccak256('opened(uint256)'));
        return nftAddress.supportsInterface(iopener) && nftAddress.supportsInterface(iopened);
    }

    function opened(IERC721 nftAddress, uint256 tokenId) internal view returns (bool) {
        if (isOpenableNFT(nftAddress)) {
            return ISCVOpenableNFT(address(nftAddress)).opened(tokenId);
        } else {
            // defaults to 'true' for non-ISCVOpenableNFT
            return true;
        }
    }

    function openerOf(IERC721 nftAddress, uint256 tokenId) internal view returns (address) {
        if (isOpenableNFT(nftAddress)) {
            return ISCVOpenableNFT(address(nftAddress)).openerOf(tokenId);
        }
        return address(0);
    }
}


// File: contracts/interfaces/IStrategy.sol
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.4;

// For interacting with our own strategy
interface IStrategy {
    // Total want tokens managed by stratfegy
    function wantLockedTotal() external view returns (uint256);

    // Sum of all shares of users to wantLockedTotal
    function sharesTotal() external view returns (uint256);

    // Main want token compounding function
    function earn() external;

    // Transfer want tokens autoFarm -> strategy
    function deposit(address _userAddress, uint256 _wantAmt) external returns (uint256);

    // Transfer want tokens strategy -> autoFarm
    function withdraw(address _userAddress, uint256 _wantAmt) external returns (uint256);

    function inCaseTokensGetStuck(
        address _token,
        uint256 _amount,
        address _to
    ) external;
}


// File: contracts/NFTSpecSupport.sol
// SPDX-License-Identifier: MIT

pragma solidity ^0.8.4;

import './libraries/OpenableNFT.sol';

abstract contract NFTSpecSupport {
    using OpenableNFT for IERC721;

    event SpecAddressUpdate(address newAddress);
    event SpecNFTSupportUpdate(address nft, bool support);

    INFTSpec public specAddress;
    mapping(address => bool) public supportedNFTs;

    function _updateSpecAddress(address specAddress_) internal {
        specAddress = INFTSpec(specAddress_);
        emit SpecAddressUpdate(specAddress_);
    }

    function _addSupportedNFT(address specNFTAddress_) internal {
        supportedNFTs[specNFTAddress_] = true;
        emit SpecNFTSupportUpdate(specNFTAddress_, true);
    }

    function _removeSupportedNFT(address specNFTAddress_) internal {
        delete supportedNFTs[specNFTAddress_];
        emit SpecNFTSupportUpdate(specNFTAddress_, false);
    }

    function nftUsable(address nftAddress_, uint256 tokenId_) public view returns (bool) {
        return
            supportedNFTs[nftAddress_] &&
            IERC721(nftAddress_).opened(tokenId_) &&
            specAddress.hasSpecId(ISCVBot(nftAddress_).getSpecId(tokenId_));
    }

    function nftOpener(IERC721 nftAddress_, uint256 tokenId_) public view returns (address) {
        return nftAddress_.openerOf(tokenId_);
    }
}


// File: contracts/RewardSupport.sol
// SPDX-License-Identifier: MIT

pragma solidity ^0.8.4;

import '@openzeppelin/contracts/token/ERC20/IERC20.sol';
import '@openzeppelin/contracts/token/ERC20/utils/SafeERC20.sol';
import '@openzeppelin/contracts/utils/math/Math.sol';

abstract contract RewardSupport {
    using SafeERC20 for IERC20;
    address public scv;

    function _safeTransferReward(address _to, uint256 _amount) internal {
        uint256 amt = Math.min(IERC20(scv).balanceOf(address(this)), _amount);
        if (amt == 0) {
            return;
        }
        IERC20(scv).safeTransfer(_to, amt);
    }

    function _safeTransferBNB(address _to, uint256 _amount) internal {
        uint256 amt = Math.min(address(this).balance, _amount);
        if (amt == 0) {
            return;
        }
        payable(_to).transfer(amt);
    }
}


// File: @openzeppelin/contracts/token/ERC20/extensions/IERC20Metadata.sol
// SPDX-License-Identifier: MIT

pragma solidity ^0.8.0;

import "../IERC20.sol";

/**
 * @dev Interface for the optional metadata functions from the ERC20 standard.
 *
 * _Available since v4.1._
 */
interface IERC20Metadata is IERC20 {
    /**
     * @dev Returns the name of the token.
     */
    function name() external view returns (string memory);

    /**
     * @dev Returns the symbol of the token.
     */
    function symbol() external view returns (string memory);

    /**
     * @dev Returns the decimals places of the token.
     */
    function decimals() external view returns (uint8);
}


// File: @openzeppelin/contracts/utils/Context.sol
// SPDX-License-Identifier: MIT

pragma solidity ^0.8.0;

/*
 * @dev Provides information about the current execution context, including the
 * sender of the transaction and its data. While these are generally available
 * via msg.sender and msg.data, they should not be accessed in such a direct
 * manner, since when dealing with meta-transactions the account sending and
 * paying for execution may not be the actual sender (as far as an application
 * is concerned).
 *
 * This contract is only required for intermediate, library-like contracts.
 */
abstract contract Context {
    function _msgSender() internal view virtual returns (address) {
        return msg.sender;
    }

    function _msgData() internal view virtual returns (bytes calldata) {
        this; // silence state mutability warning without generating bytecode - see https://github.com/ethereum/solidity/issues/2691
        return msg.data;
    }
}


// File: @openzeppelin/contracts/utils/Address.sol
// SPDX-License-Identifier: MIT

pragma solidity ^0.8.0;

/**
 * @dev Collection of functions related to the address type
 */
library Address {
    /**
     * @dev Returns true if `account` is a contract.
     *
     * [IMPORTANT]
     * ====
     * It is unsafe to assume that an address for which this function returns
     * false is an externally-owned account (EOA) and not a contract.
     *
     * Among others, `isContract` will return false for the following
     * types of addresses:
     *
     *  - an externally-owned account
     *  - a contract in construction
     *  - an address where a contract will be created
     *  - an address where a contract lived, but was destroyed
     * ====
     */
    function isContract(address account) internal view returns (bool) {
        // This method relies on extcodesize, which returns 0 for contracts in
        // construction, since the code is only stored at the end of the
        // constructor execution.

        uint256 size;
        // solhint-disable-next-line no-inline-assembly
        assembly { size := extcodesize(account) }
        return size > 0;
    }

    /**
     * @dev Replacement for Solidity's `transfer`: sends `amount` wei to
     * `recipient`, forwarding all available gas and reverting on errors.
     *
     * https://eips.ethereum.org/EIPS/eip-1884[EIP1884] increases the gas cost
     * of certain opcodes, possibly making contracts go over the 2300 gas limit
     * imposed by `transfer`, making them unable to receive funds via
     * `transfer`. {sendValue} removes this limitation.
     *
     * https://diligence.consensys.net/posts/2019/09/stop-using-soliditys-transfer-now/[Learn more].
     *
     * IMPORTANT: because control is transferred to `recipient`, care must be
     * taken to not create reentrancy vulnerabilities. Consider using
     * {ReentrancyGuard} or the
     * https://solidity.readthedocs.io/en/v0.5.11/security-considerations.html#use-the-checks-effects-interactions-pattern[checks-effects-interactions pattern].
     */
    function sendValue(address payable recipient, uint256 amount) internal {
        require(address(this).balance >= amount, "Address: insufficient balance");

        // solhint-disable-next-line avoid-low-level-calls, avoid-call-value
        (bool success, ) = recipient.call{ value: amount }("");
        require(success, "Address: unable to send value, recipient may have reverted");
    }

    /**
     * @dev Performs a Solidity function call using a low level `call`. A
     * plain`call` is an unsafe replacement for a function call: use this
     * function instead.
     *
     * If `target` reverts with a revert reason, it is bubbled up by this
     * function (like regular Solidity function calls).
     *
     * Returns the raw returned data. To convert to the expected return value,
     * use https://solidity.readthedocs.io/en/latest/units-and-global-variables.html?highlight=abi.decode#abi-encoding-and-decoding-functions[`abi.decode`].
     *
     * Requirements:
     *
     * - `target` must be a contract.
     * - calling `target` with `data` must not revert.
     *
     * _Available since v3.1._
     */
    function functionCall(address target, bytes memory data) internal returns (bytes memory) {
      return functionCall(target, data, "Address: low-level call failed");
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`], but with
     * `errorMessage` as a fallback revert reason when `target` reverts.
     *
     * _Available since v3.1._
     */
    function functionCall(address target, bytes memory data, string memory errorMessage) internal returns (bytes memory) {
        return functionCallWithValue(target, data, 0, errorMessage);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but also transferring `value` wei to `target`.
     *
     * Requirements:
     *
     * - the calling contract must have an ETH balance of at least `value`.
     * - the called Solidity function must be `payable`.
     *
     * _Available since v3.1._
     */
    function functionCallWithValue(address target, bytes memory data, uint256 value) internal returns (bytes memory) {
        return functionCallWithValue(target, data, value, "Address: low-level call with value failed");
    }

    /**
     * @dev Same as {xref-Address-functionCallWithValue-address-bytes-uint256-}[`functionCallWithValue`], but
     * with `errorMessage` as a fallback revert reason when `target` reverts.
     *
     * _Available since v3.1._
     */
    function functionCallWithValue(address target, bytes memory data, uint256 value, string memory errorMessage) internal returns (bytes memory) {
        require(address(this).balance >= value, "Address: insufficient balance for call");
        require(isContract(target), "Address: call to non-contract");

        // solhint-disable-next-line avoid-low-level-calls
        (bool success, bytes memory returndata) = target.call{ value: value }(data);
        return _verifyCallResult(success, returndata, errorMessage);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but performing a static call.
     *
     * _Available since v3.3._
     */
    function functionStaticCall(address target, bytes memory data) internal view returns (bytes memory) {
        return functionStaticCall(target, data, "Address: low-level static call failed");
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-string-}[`functionCall`],
     * but performing a static call.
     *
     * _Available since v3.3._
     */
    function functionStaticCall(address target, bytes memory data, string memory errorMessage) internal view returns (bytes memory) {
        require(isContract(target), "Address: static call to non-contract");

        // solhint-disable-next-line avoid-low-level-calls
        (bool success, bytes memory returndata) = target.staticcall(data);
        return _verifyCallResult(success, returndata, errorMessage);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but performing a delegate call.
     *
     * _Available since v3.4._
     */
    function functionDelegateCall(address target, bytes memory data) internal returns (bytes memory) {
        return functionDelegateCall(target, data, "Address: low-level delegate call failed");
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-string-}[`functionCall`],
     * but performing a delegate call.
     *
     * _Available since v3.4._
     */
    function functionDelegateCall(address target, bytes memory data, string memory errorMessage) internal returns (bytes memory) {
        require(isContract(target), "Address: delegate call to non-contract");

        // solhint-disable-next-line avoid-low-level-calls
        (bool success, bytes memory returndata) = target.delegatecall(data);
        return _verifyCallResult(success, returndata, errorMessage);
    }

    function _verifyCallResult(bool success, bytes memory returndata, string memory errorMessage) private pure returns(bytes memory) {
        if (success) {
            return returndata;
        } else {
            // Look for revert reason and bubble it up if present
            if (returndata.length > 0) {
                // The easiest way to bubble the revert reason is using memory via assembly

                // solhint-disable-next-line no-inline-assembly
                assembly {
                    let returndata_size := mload(returndata)
                    revert(add(32, returndata), returndata_size)
                }
            } else {
                revert(errorMessage);
            }
        }
    }
}


// File: @openzeppelin/contracts/utils/introspection/IERC165.sol
// SPDX-License-Identifier: MIT

pragma solidity ^0.8.0;

/**
 * @dev Interface of the ERC165 standard, as defined in the
 * https://eips.ethereum.org/EIPS/eip-165[EIP].
 *
 * Implementers can declare support of contract interfaces, which can then be
 * queried by others ({ERC165Checker}).
 *
 * For an implementation, see {ERC165}.
 */
interface IERC165 {
    /**
     * @dev Returns true if this contract implements the interface defined by
     * `interfaceId`. See the corresponding
     * https://eips.ethereum.org/EIPS/eip-165#how-interfaces-are-identified[EIP section]
     * to learn more about how these ids are created.
     *
     * This function call must use less than 30 000 gas.
     */
    function supportsInterface(bytes4 interfaceId) external view returns (bool);
}

