/**
 *Submitted for verification at BscScan.com on 2021-05-09
*/

// SPDX-License-Identifier: MIT


pragma solidity >=0.6.0 <0.8.0;
/*
 * @dev  Provides information about the current execution context, including the
 * sender of the transaction and its data. While these are generally available
 * via msg.sender and msg.data, they should not be accessed in such a direct
 * manner, since when dealing with GSN meta-transactions the account sending and
 * paying for execution may not be the actual sender (as far as an application
 * is concerned).
 *
 * This contract is only required for intermediate, library-like contracts.
 */
abstract contract Context {
    function _msgSender() internal view virtual returns (address payable) {
        return msg.sender;
    }

    function _msgData() internal view virtual returns (bytes memory) {
        this; // silence state mutability warning without generating bytecode - see https://github.com/ethereum/solidity/issues/2691
        return msg.data;
    }
}


pragma solidity ^0.7.0;
/**
 * @dev Contract module which provides a basic access control mechanism, where
 * there is an account (an owner) that can be granted exclusive access to
 * specific functions.
 *
 * By default, the owner account will be the one that deploys the contract. This
 * can later be changed with {transferOwnership}.
 *
 * This module is used through inheritance. It will make available the modifier
 * `onlyOwner`, which can be applied to your functions to restrict their use to
 * the owner.
 */
abstract contract Ownable is Context {
    address private _owner;

    event OwnershipTransferred(address indexed previousOwner, address indexed newOwner);

    /**
     * @dev Initializes the contract setting the deployer as the initial owner.
     */
    constructor () {
        address msgSender = _msgSender();
        _owner = msgSender;
        emit OwnershipTransferred(address(0), msgSender);
    }

    /**
     * @dev Returns the address of the current owner.
     */
    function owner() public view virtual returns (address) {
        return _owner;
    }

    /**
     * @dev Throws if called by any account other than the owner.
     */
    modifier onlyOwner() {
        require(owner() == _msgSender(), "Ownable: caller is not the owner");
        _;
    }

    /**
     * @dev Leaves the contract without owner. It will not be possible to call
     * `onlyOwner` functions anymore. Can only be called by the current owner.
     *
     * NOTE: Renouncing ownership will leave the contract without an owner,
     * thereby removing any functionality that is only available to the owner.
     */
    function renounceOwnership() public virtual onlyOwner {
        emit OwnershipTransferred(_owner, address(0));
        _owner = address(0);
    }

    /**
     * @dev Transfers ownership of the contract to a new account (`newOwner`).
     * Can only be called by the current owner.
     */
    function transferOwnership(address newOwner) public virtual onlyOwner {
        require(newOwner != address(0), "Ownable: new owner is the zero address");
        emit OwnershipTransferred(_owner, newOwner);
        _owner = newOwner;
    }
}



pragma solidity ^0.7.0;
/**
 * @dev Wrappers over Solidity's arithmetic operations with added overflow
 * checks.
 *
 * Arithmetic operations in Solidity wrap on overflow. This can easily result
 * in bugs, because programmers usually assume that an overflow raises an
 * error, which is the standard behavior in high level programming languages.
 * `SafeMath` restores this intuition by reverting the transaction when an
 * operation overflows.
 *
 * Using this library instead of the unchecked operations eliminates an entire
 * class of bugs, so it's recommended to use it always.
 */
library SafeMath {
    /**
     * @dev Returns the addition of two unsigned integers, with an overflow flag.
     *
     * _Available since v3.4._
     */
    function tryAdd(uint256 a, uint256 b) internal pure returns (bool, uint256) {
        uint256 c = a + b;
        if (c < a) return (false, 0);
        return (true, c);
    }

    /**
     * @dev Returns the substraction of two unsigned integers, with an overflow flag.
     *
     * _Available since v3.4._
     */
    function trySub(uint256 a, uint256 b) internal pure returns (bool, uint256) {
        if (b > a) return (false, 0);
        return (true, a - b);
    }

    /**
     * @dev Returns the multiplication of two unsigned integers, with an overflow flag.
     *
     * _Available since v3.4._
     */
    function tryMul(uint256 a, uint256 b) internal pure returns (bool, uint256) {
        // Gas optimization: this is cheaper than requiring 'a' not being zero, but the
        // benefit is lost if 'b' is also tested.
        // See: https://github.com/OpenZeppelin/openzeppelin-contracts/pull/522
        if (a == 0) return (true, 0);
        uint256 c = a * b;
        if (c / a != b) return (false, 0);
        return (true, c);
    }

    /**
     * @dev Returns the division of two unsigned integers, with a division by zero flag.
     *
     * _Available since v3.4._
     */
    function tryDiv(uint256 a, uint256 b) internal pure returns (bool, uint256) {
        if (b == 0) return (false, 0);
        return (true, a / b);
    }

    /**
     * @dev Returns the remainder of dividing two unsigned integers, with a division by zero flag.
     *
     * _Available since v3.4._
     */
    function tryMod(uint256 a, uint256 b) internal pure returns (bool, uint256) {
        if (b == 0) return (false, 0);
        return (true, a % b);
    }

    /**
     * @dev Returns the addition of two unsigned integers, reverting on
     * overflow.
     *
     * Counterpart to Solidity's `+` operator.
     *
     * Requirements:
     *
     * - Addition cannot overflow.
     */
    function add(uint256 a, uint256 b) internal pure returns (uint256) {
        uint256 c = a + b;
        require(c >= a, "SafeMath: addition overflow");
        return c;
    }

    /**
     * @dev Returns the subtraction of two unsigned integers, reverting on
     * overflow (when the result is negative).
     *
     * Counterpart to Solidity's `-` operator.
     *
     * Requirements:
     *
     * - Subtraction cannot overflow.
     */
    function sub(uint256 a, uint256 b) internal pure returns (uint256) {
        require(b <= a, "SafeMath: subtraction overflow");
        return a - b;
    }

    /**
     * @dev Returns the multiplication of two unsigned integers, reverting on
     * overflow.
     *
     * Counterpart to Solidity's `*` operator.
     *
     * Requirements:
     *
     * - Multiplication cannot overflow.
     */
    function mul(uint256 a, uint256 b) internal pure returns (uint256) {
        if (a == 0) return 0;
        uint256 c = a * b;
        require(c / a == b, "SafeMath: multiplication overflow");
        return c;
    }

    /**
     * @dev Returns the integer division of two unsigned integers, reverting on
     * division by zero. The result is rounded towards zero.
     *
     * Counterpart to Solidity's `/` operator. Note: this function uses a
     * `revert` opcode (which leaves remaining gas untouched) while Solidity
     * uses an invalid opcode to revert (consuming all remaining gas).
     *
     * Requirements:
     *
     * - The divisor cannot be zero.
     */
    function div(uint256 a, uint256 b) internal pure returns (uint256) {
        require(b > 0, "SafeMath: division by zero");
        return a / b;
    }

    /**
     * @dev Returns the remainder of dividing two unsigned integers. (unsigned integer modulo),
     * reverting when dividing by zero.
     *
     * Counterpart to Solidity's `%` operator. This function uses a `revert`
     * opcode (which leaves remaining gas untouched) while Solidity uses an
     * invalid opcode to revert (consuming all remaining gas).
     *
     * Requirements:
     *
     * - The divisor cannot be zero.
     */
    function mod(uint256 a, uint256 b) internal pure returns (uint256) {
        require(b > 0, "SafeMath: modulo by zero");
        return a % b;
    }

    /**
     * @dev Returns the subtraction of two unsigned integers, reverting with custom message on
     * overflow (when the result is negative).
     *
     * CAUTION: This function is deprecated because it requires allocating memory for the error
     * message unnecessarily. For custom revert reasons use {trySub}.
     *
     * Counterpart to Solidity's `-` operator.
     *
     * Requirements:
     *
     * - Subtraction cannot overflow.
     */
    function sub(uint256 a, uint256 b, string memory errorMessage) internal pure returns (uint256) {
        require(b <= a, errorMessage);
        return a - b;
    }

    /**
     * @dev Returns the integer division of two unsigned integers, reverting with custom message on
     * division by zero. The result is rounded towards zero.
     *
     * CAUTION: This function is deprecated because it requires allocating memory for the error
     * message unnecessarily. For custom revert reasons use {tryDiv}.
     *
     * Counterpart to Solidity's `/` operator. Note: this function uses a
     * `revert` opcode (which leaves remaining gas untouched) while Solidity
     * uses an invalid opcode to revert (consuming all remaining gas).
     *
     * Requirements:
     *
     * - The divisor cannot be zero.
     */
    function div(uint256 a, uint256 b, string memory errorMessage) internal pure returns (uint256) {
        require(b > 0, errorMessage);
        return a / b;
    }

    /**
     * @dev Returns the remainder of dividing two unsigned integers. (unsigned integer modulo),
     * reverting with custom message when dividing by zero.
     *
     * CAUTION: This function is deprecated because it requires allocating memory for the error
     * message unnecessarily. For custom revert reasons use {tryMod}.
     *
     * Counterpart to Solidity's `%` operator. This function uses a `revert`
     * opcode (which leaves remaining gas untouched) while Solidity uses an
     * invalid opcode to revert (consuming all remaining gas).
     *
     * Requirements:
     *
     * - The divisor cannot be zero.
     */
    function mod(uint256 a, uint256 b, string memory errorMessage) internal pure returns (uint256) {
        require(b > 0, errorMessage);
        return a % b;
    }
}




//--------------------------CROWD SALE------------------------

pragma solidity ^0.7.0;


interface IERC20 {

    function totalSupply() external view returns (uint256);
    function balanceOf(address account) external view returns (uint256);
    function transfer(address recipient, uint256 amount) external returns (bool);
    function allowance(address owner, address spender) external view returns (uint256);
    function approve(address spender, uint256 amount) external returns (bool);
    function transferFrom(address sender, address recipient, uint256 amount) external returns (bool);
    event Transfer(address indexed from, address indexed to, uint256 value);
    event Approval(address indexed owner, address indexed spender, uint256 value);
}

contract PreSale is Ownable {
    using SafeMath for uint256;

    IERC20 public token;

    uint256 public _TOTAL_SOLD_TOKEN;
    uint256 public _TOTAL_DEPOSIT_BNB;
    uint256 public _RATE;
    uint256 public _MAX_BNB_CONTRIBUTION = 100 * 10**18;
    uint256 public _MIN_BNB_CONTRIBUTION = 1 * 10**18;
    uint256 private _INITIAL_UNLOCK_PERCENTAGE = 15; // 15%
    uint256 private _VESTING_DURATION = 180 days; // 6 months

    uint256 public _SOFTCAP;
    uint256 public _HARDCAP;

    uint256 public _START_TIME;
    uint256 public _END_TIME;
    uint256 public _RELEASE_TOKEN_TIME;
    
    bool public isPreSaleOpen = false;

    mapping(address => bool) public isBlacklisted;
    mapping(address => uint256) public tokensClaimed;
    mapping(address => uint256) public totalPurchased;
    mapping(address => bool) public withdrawn;

    constructor() {
        token = IERC20(0x24Ab75c6F3F627D9E046317DeF8714A8E5a55c7D);
    }

    receive() external payable {
        deposite();
    }

    function ownerWithdrawBNB() public onlyOwner{
        require(block.timestamp > _END_TIME, "Presale haven't finished yet");
        require(_TOTAL_DEPOSIT_BNB >= _SOFTCAP, "Presale didn't hit softcap");
        
        payable(msg.sender).transfer(address(this).balance);
    }

    function ownerWithdrawToken() public onlyOwner{        
        token.transfer(msg.sender, token.balanceOf(address(this)));
    }

    function setTokenForPresale(address _tokenAddress) public onlyOwner{        
        token = IERC20(_tokenAddress);
    }


    //function addWhiteListForPreSale(address[] calldata accounts, bool granted) public onlyOwner {
    //    for( uint256 i = 0; i < accounts.length; i++){
    //        isPresaleWhitelist[accounts[i]] = granted;
    //    }
    //}
    function setInitialUnlockPercentage(uint256 newPercentage) external onlyOwner {
        require(newPercentage <= 100, "Percentage cannot exceed 100");
        _INITIAL_UNLOCK_PERCENTAGE = newPercentage;
    }

    function setVestingDuration(uint256 newDuration) external onlyOwner {
        _VESTING_DURATION = newDuration;
    }
    function addToBlacklist(address user) external onlyOwner {
        isBlacklisted[user] = true;
    }

    function removeFromBlacklist(address user) external onlyOwner {
        isBlacklisted[user] = false;
    }

    function setupPresale(uint256 start, uint256 end, uint256 release, uint256 rate, uint256 softcap, uint256 hardcap, uint256 maxPerWallet, uint256 minPerWallet) public onlyOwner{
        _RATE = rate;
        _MAX_BNB_CONTRIBUTION = maxPerWallet;
        _MIN_BNB_CONTRIBUTION = minPerWallet;
        _SOFTCAP = softcap;
        _HARDCAP = hardcap;
        _START_TIME = start;
        _END_TIME = end;
        _RELEASE_TOKEN_TIME = release;
    }

    function openPresale(bool opened) public onlyOwner {
        isPreSaleOpen = opened;
    }

    function getInitialUnlockPercentage() external view returns (uint256) {
        return _INITIAL_UNLOCK_PERCENTAGE;
    }

    function getVestingDuration() external view returns (uint256) {
        return _VESTING_DURATION;
    }

    function withdrawBlacklist() public {
        require(isBlacklisted[msg.sender], "Not eligible to withdraw BNB as not blacklisted");
        require(!withdrawn[msg.sender], "Already withdrawn BNB");

        if (!withdrawn[msg.sender]) {
            withdrawn[msg.sender]= true;
            payable(msg.sender).transfer(totalPurchased[msg.sender]);
        }

    }

    function deposite() public payable {
        require(block.timestamp >= _START_TIME &&  block.timestamp <= _END_TIME, "Presale is not active this time");
        require(msg.value >= _MIN_BNB_CONTRIBUTION, "Please check minimum BNB contribution");
        require(totalPurchased[msg.sender].add(msg.value) <= _MAX_BNB_CONTRIBUTION, "Check max contribution per wallet");
        require(_TOTAL_DEPOSIT_BNB.add(msg.value) <= _HARDCAP, "Exceed HARD CAP");

        totalPurchased[msg.sender]=  totalPurchased[msg.sender].add(msg.value);
        _TOTAL_DEPOSIT_BNB = _TOTAL_DEPOSIT_BNB.add(msg.value);
        _TOTAL_SOLD_TOKEN = _TOTAL_DEPOSIT_BNB.mul(_RATE);
    }

    function withdraw() public {
        require(block.timestamp > _END_TIME, "Presale hasn't finished yet");
        require(!withdrawn[msg.sender], "Already withdrawn!");
        require(!isBlacklisted[msg.sender], "Not able to withdraw, Wallet has no KYC");

        if (_TOTAL_DEPOSIT_BNB < _SOFTCAP) {
            if (totalPurchased[msg.sender] > 0) {
                withdrawn[msg.sender] = true;
                payable(msg.sender).transfer(totalPurchased[msg.sender]);
            }
        } else {
            uint256 totalTokens = totalPurchased[msg.sender].mul(_RATE);
            uint256 initialUnlock = totalTokens.mul(_INITIAL_UNLOCK_PERCENTAGE).div(100);
            uint256 claimableAmount = 0;

            if (block.timestamp <= _RELEASE_TOKEN_TIME) {
                revert("Please check token release time");
            } else if (block.timestamp > _RELEASE_TOKEN_TIME && block.timestamp <= _RELEASE_TOKEN_TIME + _VESTING_DURATION) {
                // Initial unlock plus linear vesting for the time passed since the release
                uint256 timeSinceRelease = block.timestamp.sub(_RELEASE_TOKEN_TIME);
                uint256 linearRelease = totalTokens.sub(initialUnlock).mul(timeSinceRelease).div(_VESTING_DURATION);
                claimableAmount = initialUnlock.add(linearRelease);
            } else {
                // After vesting duration, all tokens are claimable
                claimableAmount = totalTokens;
            }

            uint256 unclaimedAmount = claimableAmount.sub(tokensClaimed[msg.sender]);
            require(token.balanceOf(address(this)) >= unclaimedAmount, "Not enough tokens in the contract");
            

            // updates withdrawn property if everything is claimed
            if (unclaimedAmount == 0) {
                withdrawn[msg.sender] = true;
            }

            tokensClaimed[msg.sender] = claimableAmount;
            token.transfer(msg.sender, unclaimedAmount);

            
        }
    }

}