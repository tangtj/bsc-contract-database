
// File: contracts/FleaDexFactory.sol
// SPDX-License-Identifier: GPL-3.0

pragma solidity =0.6.12;

import './interfaces/IFleaDexFactory.sol';
import './FleaDexPair.sol';

contract FleaDexFactory is IFleaDexFactory {
 address public override feeTo ;
 address public override feeToManager;

 mapping(address => mapping(address => address)) public override getPair;
 address[] public override allPairs;

 bool public paused = false;

 event PairCreated(address indexed token0, address indexed token1, address pair, uint256);

 event Pause();
 event Unpause();

 /**
  * @dev Modifier to make a function callable only when the contract is not paused.
  */
 modifier whenNotPaused() {
  require(!paused);
  _;
 }

 /**
  * @dev Modifier to make a function callable only when the contract is paused.
  */
 modifier whenPaused() {
  require(paused);
  _;
 }

 constructor(address _feeToManager, address _feeTo) public {
  require(_feeToManager != address(0), '_feeToManager cannot be zero address');
  require(_feeTo != address(0), '_feeTo cannot be zero address');
  feeToManager = _feeToManager;
  feeTo = _feeTo;
 }
 
 function allPairsLength() external view override returns (uint256) {
  return allPairs.length;
 }

 function pairCodeHash() external pure returns (bytes32) {
  return keccak256(type(FleaDexPair).creationCode);
 }

 function createPair(address tokenA, address tokenB) external override whenNotPaused returns (address pair) {
  require(tokenA != tokenB, 'FleaDexv2: IDENTICAL_ADDRESSES');
  (address token0, address token1) = tokenA < tokenB ? (tokenA, tokenB) : (tokenB, tokenA);
  require(token0 != address(0), 'FleaDexv2: ZERO_ADDRESS');
  require(getPair[token0][token1] == address(0), 'FleaDexv2: PAIR_EXISTS'); // single check is sufficient
  bytes memory bytecode = type(FleaDexPair).creationCode;
  bytes32 salt = keccak256(abi.encodePacked(token0, token1));
  assembly {
pair := create2(0, add(bytecode, 32), mload(bytecode), salt)
  }
  FleaDexPair(pair).initialize(token0, token1);
  getPair[token0][token1] = pair;
  getPair[token1][token0] = pair; // populate mapping in the reverse direction
  allPairs.push(pair);
  emit PairCreated(token0, token1, pair, allPairs.length);
 }

 function setFeeTo(address _feeTo) external override {
  require(msg.sender == feeToManager, 'Only FeeToManager can change the feeTo address');
  feeTo = _feeTo;
 }

 function setFeeToManager(address _feeToManager) external override {
  require(msg.sender == feeToManager, 'FleaDexV2: FORBIDDEN');
  require(_feeToManager != address(0), '_feeToManager cannot be zero address');
  feeToManager = _feeToManager;
 }

 /**
  * @dev called by the owner to pause, triggers stopped state
  */
 function pause() public whenNotPaused {
  require(msg.sender == feeToManager, 'FleaDexV2: FORBIDDEN');
  paused = true;
  emit Pause();
 }

 /**
  * @dev called by the owner to unpause, returns to normal state
  */
 function unpause() public whenPaused {
  require(msg.sender == feeToManager, 'FleaDexV2: FORBIDDEN');
  paused = false;
  emit Unpause();
 }

 function isPaused() external view override returns (bool) {
  return paused;
 }
}


// File: contracts/interfaces/IFleaDexFactory.sol
// SPDX-License-Identifier: GPL-3.0

pragma solidity >=0.5.0;

interface IFleaDexFactory {
    event PairCreated(address indexed token0, address indexed token1, address pair, uint256);

    function feeTo() external view returns (address);

    function feeToManager() external view returns (address);

    function getPair(address tokenA, address tokenB) external view returns (address pair);

    function allPairs(uint256) external view returns (address pair);

    function allPairsLength() external view returns (uint256);

    function createPair(address tokenA, address tokenB) external returns (address pair);

    function setFeeTo(address) external;

    function setFeeToManager(address) external;

    function isPaused() external view returns (bool);
}


// File: contracts/FleaDexPair.sol
// SPDX-License-Identifier: GPL-3.0

pragma solidity =0.6.12;

import './FleaDexERC20.sol';
import './libraries/Math.sol';
import './libraries/UQ112x112.sol';
import './interfaces/IERC20.sol';
import './interfaces/IFleaDexFactory.sol';
import './interfaces/IFleaDexCallee.sol';

contract FleaDexPair is FleaDexERC20 {
    using SafeMathFleaDex for uint256;
    using UQ112x112 for uint224;

    uint256 public constant MINIMUM_LIQUIDITY = 10**3;
    bytes4 private constant SELECTOR = bytes4(keccak256(bytes('transfer(address,uint256)')));

    address public factory;
    address public controllerFeeAddress = 0x139388094602Fe61CdcE68AEDF76b743C304F789; // Controller Fee Address
    address public token0;
    address public token1;

    uint256 public constant MAX_FEE_AMOUNT = 1000; // = 10%
    uint256 public constant MIN_FEE_AMOUNT = 0; // = 0.01%
    uint256 public feeAmount = 0; // default = 0.0% . LP HOLDERS
    uint256 public controllerFeeShare = 30; // 0.3% . Controller Fee

    uint112 private reserve0; // uses single storage slot, accessible via getReserves
    uint112 private reserve1; // uses single storage slot, accessible via getReserves
    uint32 private blockTimestampLast; // uses single storage slot, accessible via getReserves

    uint256 public price0CumulativeLast;
    uint256 public price1CumulativeLast;
    uint256 public kLast; // reserve0 * reserve1, as of immediately after the most recent liquidity event

    uint256 private unlocked = 1;

    modifier lock() {
        require(unlocked == 1, 'FleaDexV2: LOCKED');
        unlocked = 0;
        _;
        unlocked = 1;
    }

    /**
     * @dev Modifier to make a function callable only when the contract is not paused.
     */
    modifier whenNotPaused() {
        require(!IFleaDexFactory(factory).isPaused(), 'FleaDexV2: PAUSED');
        _;
    }

    /**
     * @dev Modifier to make a function callable only when the contract is paused.
     */
    modifier whenPaused() {
        require(IFleaDexFactory(factory).isPaused(), 'FleaDexV2: NOT PAUSED');
        _;
    }

    function getReserves()
        public
        view
        returns (
            uint112 _reserve0,
            uint112 _reserve1,
            uint32 _blockTimestampLast
        )
    {
        _reserve0 = reserve0;
        _reserve1 = reserve1;
        _blockTimestampLast = blockTimestampLast;
    }

    function _safeTransfer(
        address token,
        address to,
        uint256 value
    ) private {
        (bool success, bytes memory data) = token.call(abi.encodeWithSelector(SELECTOR, to, value));
        require(success && (data.length == 0 || abi.decode(data, (bool))), 'FleaDexV2: TRANSFER_FAILED');
    }

    event Mint(address indexed sender, uint256 amount0, uint256 amount1);
    event Burn(address indexed sender, uint256 amount0, uint256 amount1, address indexed to);
    event Swap(
        address indexed sender,
        uint256 amount0In,
        uint256 amount1In,
        uint256 amount0Out,
        uint256 amount1Out,
        address indexed to
    );
    event Sync(uint112 reserve0, uint112 reserve1);

    struct FeesAndAmounts {
        uint256 fee0;
        uint256 fee1;
        uint256 amount0OutAfterFee;
        uint256 amount1OutAfterFee;
    }

    struct AmountsIn {
        uint256 amount0In;
        uint256 amount1In;
    }

    constructor() public {
        factory = msg.sender;
    }

    // called once by the factory at time of deployment
    function initialize(address _token0, address _token1) external {
        require(msg.sender == factory, 'FleaDexV2: FORBIDDEN'); // sufficient check
        token0 = _token0;
        token1 = _token1;
    }

    function setFeeAmount(uint256 newFeeAmount) external {
        require(
            msg.sender == IFleaDexFactory(factory).feeToManager(),
            "FleaDexV2Pair: only factory's feeToManager"
        );
        require(newFeeAmount <= MAX_FEE_AMOUNT, "FleaDexV2Pair: feeAmount mustn't exceed the maximum");
        require(newFeeAmount >= MIN_FEE_AMOUNT, "FleaDexV2Pair: feeAmount mustn't exceed the minimum");
        feeAmount = newFeeAmount;
    }

    function setControllerFeeAmount(uint256 _controllerFeeShare) external {
        require(
            msg.sender == IFleaDexFactory(factory).feeToManager(),
            "FleaDexV2Pair: only factory's feeToManager"
        );
        require(
            _controllerFeeShare <= MAX_FEE_AMOUNT,
            "FleaDexV2Pair: controllerFeeShare mustn't exceed the maximum"
        );
        require(
            _controllerFeeShare >= MIN_FEE_AMOUNT,
            "FleaDexV2Pair: controllerFeeShare mustn't exceed the minimum"
        );
        controllerFeeShare = _controllerFeeShare;
    }

    function setControllerFeeAddress(address _controllerFeeAddress) external {
        require(_controllerFeeAddress != address(0), 'Cannot be zero address');
        require(
            msg.sender == IFleaDexFactory(factory).feeToManager(),
            "FleaDexV2Pair: only factory's feeToManager"
        );
        controllerFeeAddress = _controllerFeeAddress;
    }

    // update reserves and, on the first call per block, price accumulators
    function _update(
        uint256 balance0,
        uint256 balance1,
        uint112 _reserve0,
        uint112 _reserve1
    ) private {
        require(balance0 <= uint112(-1) && balance1 <= uint112(-1), 'FleaDexV2: OVERFLOW');
        uint32 blockTimestamp = uint32(block.timestamp % 2**32);
        uint32 timeElapsed = blockTimestamp - blockTimestampLast; // overflow is desired
        if (timeElapsed > 0 && _reserve0 != 0 && _reserve1 != 0) {
            // * never overflows, and + overflow is desired
            price0CumulativeLast += uint256(UQ112x112.encode(_reserve1).uqdiv(_reserve0)) * timeElapsed;
            price1CumulativeLast += uint256(UQ112x112.encode(_reserve0).uqdiv(_reserve1)) * timeElapsed;
        }
        reserve0 = uint112(balance0);
        reserve1 = uint112(balance1);
        blockTimestampLast = blockTimestamp;
        emit Sync(reserve0, reserve1);
    }

    // if fee is on, mint liquidity equivalent to 1/6th of the growth in sqrt(k)
    function _mintFee(uint112 _reserve0, uint112 _reserve1) private returns (bool feeOn) {
        address feeTo = IFleaDexFactory(factory).feeTo();
        feeOn = feeTo != address(0);
        uint256 _kLast = kLast; // gas savings
        if (feeOn) {
            if (_kLast != 0) {
                uint256 rootK = Math.sqrt(uint256(_reserve0).mul(_reserve1));
                uint256 rootKLast = Math.sqrt(_kLast);
                if (rootK > rootKLast) {
                    uint256 numerator = totalSupply.mul(rootK.sub(rootKLast));
                    uint256 denominator = rootK.mul(50).add(rootKLast);
                    uint256 liquidity = numerator / denominator;
                    if (liquidity > 0) _mint(feeTo, liquidity);
                }
            }
        } else if (_kLast != 0) {
            kLast = 0;
        }
    }

    // this low-level function should be called from a contract which performs important safety checks
    function mint(address to) external lock whenNotPaused returns (uint256 liquidity) {
        (uint112 _reserve0, uint112 _reserve1, ) = getReserves(); // gas savings
        uint256 balance0 = IERC20FleaDex(token0).balanceOf(address(this));
        uint256 balance1 = IERC20FleaDex(token1).balanceOf(address(this));
        uint256 amount0 = balance0.sub(_reserve0);
        uint256 amount1 = balance1.sub(_reserve1);

        bool feeOn = _mintFee(_reserve0, _reserve1);
        uint256 _totalSupply = totalSupply; // gas savings, must be defined here since totalSupply can update in _mintFee
        if (_totalSupply == 0) {
            liquidity = Math.sqrt(amount0.mul(amount1)).sub(MINIMUM_LIQUIDITY);
            _mint(address(0), MINIMUM_LIQUIDITY); // permanently lock the first MINIMUM_LIQUIDITY tokens
        } else {
            liquidity = Math.min(amount0.mul(_totalSupply) / _reserve0, amount1.mul(_totalSupply) / _reserve1);
        }
        require(liquidity > 0, 'FleaDexV2: INSUFFICIENT_LIQUIDITY_MINTED');
        _mint(to, liquidity);

        _update(balance0, balance1, _reserve0, _reserve1);
        if (feeOn) kLast = uint256(reserve0).mul(reserve1); // reserve0 and reserve1 are up-to-date
        emit Mint(msg.sender, amount0, amount1);
    }

    // this low-level function should be called from a contract which performs important safety checks
    function burn(address to) external lock returns (uint256 amount0, uint256 amount1) {
        (uint112 _reserve0, uint112 _reserve1, ) = getReserves(); // gas savings
        address _token0 = token0; // gas savings
        address _token1 = token1; // gas savings
        uint256 balance0 = IERC20FleaDex(_token0).balanceOf(address(this));
        uint256 balance1 = IERC20FleaDex(_token1).balanceOf(address(this));
        uint256 liquidity = balanceOf[address(this)];

        bool feeOn = _mintFee(_reserve0, _reserve1);
        uint256 _totalSupply = totalSupply; // gas savings, must be defined here since totalSupply can update in _mintFee
        amount0 = liquidity.mul(balance0) / _totalSupply; // using balances ensures pro-rata distribution
        amount1 = liquidity.mul(balance1) / _totalSupply; // using balances ensures pro-rata distribution
        require(amount0 > 0 && amount1 > 0, 'FleaDexV2: INSUFFICIENT_LIQUIDITY_BURNED');
        _burn(address(this), liquidity);
        _safeTransfer(_token0, to, amount0);
        _safeTransfer(_token1, to, amount1);
        balance0 = IERC20FleaDex(_token0).balanceOf(address(this));
        balance1 = IERC20FleaDex(_token1).balanceOf(address(this));

        _update(balance0, balance1, _reserve0, _reserve1);
        if (feeOn) kLast = uint256(reserve0).mul(reserve1); // reserve0 and reserve1 are up-to-date
        emit Burn(msg.sender, amount0, amount1, to);
    }

    // this low-level function should be called from a contract which performs important safety checks
    function swap(
        uint256 amount0Out,
        uint256 amount1Out,
        address to,
        bytes calldata data
    ) external lock whenNotPaused {
        require(amount0Out > 0 || amount1Out > 0, 'FleaDexV2: INSUFFICIENT_OUTPUT_AMOUNT');
        (uint112 _reserve0, uint112 _reserve1, ) = getReserves(); // gas savings
        require(amount0Out < _reserve0 && amount1Out < _reserve1, 'FleaDexV2: INSUFFICIENT_LIQUIDITY');

        uint256 balance0;
        uint256 balance1;
        FeesAndAmounts memory feesAndAmounts = _getFeesAndAmounts(amount0Out, amount1Out);
        {
            // scope for _token{0,1}, avoids stack too deep errors
            address _token0 = token0;
            address _token1 = token1;
            require(to != _token0 && to != _token1, 'FleaDexV2: INVALID_TO');
            if (amount0Out > 0) {
                _safeTransfer(_token0, controllerFeeAddress, feesAndAmounts.fee0);
                _safeTransfer(_token0, to, feesAndAmounts.amount0OutAfterFee); // optimistically transfer tokens
            }
            if (amount1Out > 0) {
                _safeTransfer(_token1, controllerFeeAddress, feesAndAmounts.fee1);
                _safeTransfer(_token1, to, feesAndAmounts.amount1OutAfterFee); // optimistically transfer tokens
            }
            if (data.length > 0) {
                IFleaDexCallee(to).FleaDexCall(
                    msg.sender,
                    feesAndAmounts.amount0OutAfterFee,
                    feesAndAmounts.amount1OutAfterFee,
                    data
                );
            }
            balance0 = IERC20FleaDex(_token0).balanceOf(address(this));
            balance1 = IERC20FleaDex(_token1).balanceOf(address(this));
        }
        AmountsIn memory amountsIn = AmountsIn(
            balance0 > _reserve0 - amount0Out ? balance0 - (_reserve0 - amount0Out) : 0,
            balance1 > _reserve1 - amount1Out ? balance1 - (_reserve1 - amount1Out) : 0
        );
        require(amountsIn.amount0In > 0 || amountsIn.amount1In > 0, 'FleaDexV2: INSUFFICIENT_INPUT_AMOUNT');
        {
            // scope for reserve{0,1}Adjusted, avoids stack too deep errors
            uint256 balance0Adjusted = balance0.mul(10000).sub(amountsIn.amount0In.mul(feeAmount));
            uint256 balance1Adjusted = balance1.mul(10000).sub(amountsIn.amount1In.mul(feeAmount));
            require(
                balance0Adjusted.mul(balance1Adjusted) >= uint256(_reserve0).mul(_reserve1).mul(10000**2),
                'FleaDexV2: K'
            );
        }

        _update(balance0, balance1, _reserve0, _reserve1);
        emit Swap(msg.sender, amountsIn.amount0In, amountsIn.amount1In, amount0Out, amount1Out, to);
    }

    function _getFeesAndAmounts(uint256 amount0Out, uint256 amount1Out) private view returns (FeesAndAmounts memory) {
        uint256 fee0 = amount0Out.mul(controllerFeeShare) / 10000;
        uint256 fee1 = amount1Out.mul(controllerFeeShare) / 10000;
        if (amount0Out > 0 && fee0 < 1) fee0 = 1;
        if (amount1Out > 0 && fee1 < 1) fee1 = 1;

        return FeesAndAmounts(fee0, fee1, amount0Out - fee0, amount1Out - fee1);
    }

    // force balances to match reserves
    function skim(address to) external lock whenNotPaused {
        address _token0 = token0; // gas savings
        address _token1 = token1; // gas savings
        _safeTransfer(_token0, to, IERC20FleaDex(_token0).balanceOf(address(this)).sub(reserve0));
        _safeTransfer(_token1, to, IERC20FleaDex(_token1).balanceOf(address(this)).sub(reserve1));
    }

    // force reserves to match balances
    function sync() external lock whenNotPaused {
        _update(
            IERC20FleaDex(token0).balanceOf(address(this)),
            IERC20FleaDex(token1).balanceOf(address(this)),
            reserve0,
            reserve1
        );
    }
}


// File: contracts/FleaDexERC20.sol
// SPDX-License-Identifier: GPL-3.0

pragma solidity =0.6.12;

import './libraries/SafeMath.sol';

contract FleaDexERC20 {
    using SafeMathFleaDex for uint256;

    string public constant name = 'FleaDex-LP';
    string public constant symbol = 'FLM-LP';
    uint8 public constant decimals = 18;
    uint256 public totalSupply;
    mapping(address => uint256) public balanceOf;
    mapping(address => mapping(address => uint256)) public allowance;

    bytes32 public DOMAIN_SEPARATOR;
    // keccak256("Permit(address owner,address spender,uint256 value,uint256 nonce,uint256 deadline)");
    bytes32 public constant PERMIT_TYPEHASH = 0x6e71edae12b1b97f4d1f60370fef10105fa2faae0126114a169c64845d6126c9;
    mapping(address => uint256) public nonces;

    event Approval(address indexed owner, address indexed spender, uint256 value);
    event Transfer(address indexed from, address indexed to, uint256 value);

    constructor() public {
        uint256 chainId;
        assembly {
            chainId := chainid()
        }
        DOMAIN_SEPARATOR = keccak256(
            abi.encode(
                keccak256('EIP712Domain(string name,string version,uint256 chainId,address verifyingContract)'),
                keccak256(bytes(name)),
                keccak256(bytes('1')),
                chainId,
                address(this)
            )
        );
    }

    function _mint(address to, uint256 value) internal {
        totalSupply = totalSupply.add(value);
        balanceOf[to] = balanceOf[to].add(value);
        emit Transfer(address(0), to, value);
    }

    function _burn(address from, uint256 value) internal {
        balanceOf[from] = balanceOf[from].sub(value);
        totalSupply = totalSupply.sub(value);
        emit Transfer(from, address(0), value);
    }

    function _approve(
        address owner,
        address spender,
        uint256 value
    ) private {
        allowance[owner][spender] = value;
        emit Approval(owner, spender, value);
    }

    function _transfer(
        address from,
        address to,
        uint256 value
    ) private {
        balanceOf[from] = balanceOf[from].sub(value);
        balanceOf[to] = balanceOf[to].add(value);
        emit Transfer(from, to, value);
    }

    function approve(address spender, uint256 value) external returns (bool) {
        _approve(msg.sender, spender, value);
        return true;
    }

    function transfer(address to, uint256 value) external returns (bool) {
        _transfer(msg.sender, to, value);
        return true;
    }

    function transferFrom(
        address from,
        address to,
        uint256 value
    ) external returns (bool) {
        if (allowance[from][msg.sender] != uint256(-1)) {
            allowance[from][msg.sender] = allowance[from][msg.sender].sub(value);
        }
        _transfer(from, to, value);
        return true;
    }

    function permit(
        address owner,
        address spender,
        uint256 value,
        uint256 deadline,
        uint8 v,
        bytes32 r,
        bytes32 s
    ) external {
        require(deadline >= block.timestamp, 'FleaDex: EXPIRED');
        bytes32 digest = keccak256(
            abi.encodePacked(
                '\x19\x01',
                DOMAIN_SEPARATOR,
                keccak256(abi.encode(PERMIT_TYPEHASH, owner, spender, value, nonces[owner]++, deadline))
            )
        );
        address recoveredAddress = ecrecover(digest, v, r, s);
        require(recoveredAddress != address(0) && recoveredAddress == owner, 'FleaDex: INVALID_SIGNATURE');
        _approve(owner, spender, value);
    }
}


// File: contracts/libraries/Math.sol
// SPDX-License-Identifier: GPL-3.0

pragma solidity =0.6.12;

// a library for performing various math operations

library Math {
    function min(uint256 x, uint256 y) internal pure returns (uint256 z) {
        z = x < y ? x : y;
    }

    // babylonian method (https://en.wikipedia.org/wiki/Methods_of_computing_square_roots#Babylonian_method)
    function sqrt(uint256 y) internal pure returns (uint256 z) {
        if (y > 3) {
            z = y;
            uint256 x = y / 2 + 1;
            while (x < z) {
                z = x;
                x = (y / x + x) / 2;
            }
        } else if (y != 0) {
            z = 1;
        }
    }
}


// File: contracts/libraries/UQ112x112.sol
// SPDX-License-Identifier: GPL-3.0

pragma solidity =0.6.12;

// a library for handling binary fixed point numbers (https://en.wikipedia.org/wiki/Q_(number_format))

// range: [0, 2**112 - 1]
// resolution: 1 / 2**112

library UQ112x112 {
    uint224 constant Q112 = 2**112;

    // encode a uint112 as a UQ112x112
    function encode(uint112 y) internal pure returns (uint224 z) {
        z = uint224(y) * Q112; // never overflows
    }

    // divide a UQ112x112 by a uint112, returning a UQ112x112
    function uqdiv(uint224 x, uint112 y) internal pure returns (uint224 z) {
        z = x / uint224(y);
    }
}


// File: contracts/interfaces/IERC20.sol
// SPDX-License-Identifier: GPL-3.0

pragma solidity >=0.5.0;

interface IERC20FleaDex {
    event Approval(address indexed owner, address indexed spender, uint256 value);
    event Transfer(address indexed from, address indexed to, uint256 value);

    function name() external view returns (string memory);

    function symbol() external view returns (string memory);

    function decimals() external view returns (uint8);

    function totalSupply() external view returns (uint256);

    function balanceOf(address owner) external view returns (uint256);

    function allowance(address owner, address spender) external view returns (uint256);

    function approve(address spender, uint256 value) external returns (bool);

    function transfer(address to, uint256 value) external returns (bool);

    function transferFrom(
        address from,
        address to,
        uint256 value
    ) external returns (bool);
}


// File: contracts/interfaces/IFleaDexCallee.sol
// SPDX-License-Identifier: GPL-3.0

pragma solidity >=0.5.0;

interface IFleaDexCallee {
    function FleaDexCall(
        address sender,
        uint256 amount0,
        uint256 amount1,
        bytes calldata data
    ) external;
}


// File: contracts/libraries/SafeMath.sol
// SPDX-License-Identifier: GPL-3.0

pragma solidity =0.6.12;

// a library for performing overflow-safe math, courtesy of DappHub (https://github.com/dapphub/ds-math)

library SafeMathFleaDex {
    function add(uint256 x, uint256 y) internal pure returns (uint256 z) {
        require((z = x + y) >= x, 'ds-math-add-overflow');
    }

    function sub(uint256 x, uint256 y) internal pure returns (uint256 z) {
        require((z = x - y) <= x, 'ds-math-sub-underflow');
    }

    function mul(uint256 x, uint256 y) internal pure returns (uint256 z) {
        require(y == 0 || (z = x * y) / y == x, 'ds-math-mul-overflow');
    }
}

